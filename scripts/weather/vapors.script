{
	const FREQ_CL_REFRESH 20.0
}

{ game_precache
	precachefile weather/vapors_cl
}

{ game_spawn
	blood none
	fly 1
	gravity 0
	nopush 1
	invincible 1
	setvard PLAYING_DEAD 1
}

{ game_postspawn //PARAM1 = (name|default), PARAM2 = DmgMulti, PARAM3 = HPMulti, PARAM4 = pass_params

	if !AM_SUMMONED

	setvard PARAM_TOKENS PARAM4

	if ( $get_token_amt(PARAM_TOKENS) < 2 )
	{
		setvard ERROR_MODE 1
	}
	else
	{
		setvard VAPOR_COLOR $get_token(PARAM4,0)
		setvard VAPOR_SIZE $get_token(PARAM4,1)

		dbg Vapors - col: VAPOR_COLOR siz: VAPOR_SIZE

		if ( VAPOR_SIZE > 1024 ) setvard VAPOR_SIZE 1024
		if ( $get_token_amt(PARAM_TOKENS) > 2 )
		{
			setvard VAPOR_TYPE $get_token(PARAM4,2)
			if VAPOR_TYPE isnot none
			setvard VAPOR_DMG $get_token(PARAM4,3)
			if ( VAPOR_TYPE equals fire )
			{
				name a|burning fog
				local VALID_LOOP 1
			}
			if ( VAPOR_TYPE equals poison )
			{
				name a|poisonus vapor
				local VALID_LOOP 1
			}
			if ( VAPOR_TYPE equals cold )
			{
				name an|icey cloud
				local VALID_LOOP 1
				setvard VAPOR_COLOR (128,128,255)
			}
			if VALID_LOOP
			callevent 1.0 scan_victims
		}
	}
	

	if ( ERROR_MODE ) callevent error_loop

	if !ERROR_MODE

	setvard IS_ACTIVE 1
	callevent start_fog
}

{ scan_victims
	
	if $get(ent_me,isalive)
	callevent 1.0 scan_victims
	setvard TARG_LIST $get_tsphere(player,VAPOR_SIZE,game.monster.origin)
	if TARG_LIST isnot none
	calleventloop $get_token_amt(TARG_LIST) attack_targets
}

{ attack_targets
	local CUR_TARGET $get_token(TARG_LIST,game.script.iteration)
	if ( VAPOR_TYPE equals fire ) applyeffect CUR_TARGET effects/dot_fire 5.0 $get(ent_me,id) VAPOR_DMG
	if ( VAPOR_TYPE equals poison ) applyeffect CUR_TARGET effects/dot_poison 5.0 $get(ent_me,id) VAPOR_DMG
	if ( VAPOR_TYPE equals cold ) applyeffect CUR_TARGET effects/dot_cold 5.0 $get(ent_me,id) VAPOR_DMG
}

{ start_fog
	if IS_ACTIVE
	callevent FREQ_CL_REFRESH start_fog
	local VAPOR_POS game.monster.origin
	clientevent new all weather/vapors_cl VAPOR_POS VAPOR_COLOR VAPOR_SIZE FREQ_CL_REFRESH
	setvard CL_IDX game.script.last_sent_id
}

//{ game_playerjoin
//	clientevent remove all CL_IDX
//	callevent 1.0 start_fog
//}

{ game_dynamically_created
	setvard AM_SUMMONED 1
	dbg Vapors - col: VAPOR_COLOR siz: VAPOR_SIZE
	setvard VAPOR_COLOR PARAM1
	setvard VAPOR_SIZE PARAM2
	setvard VAPOR_TYPE PARAM3
	setvard VAPOR_DMG PARAM4
	if ( VAPOR_SIZE > 1024 ) setvard VAPOR_SIZE 1024
	callevent start_fog
}

{ error_loop
	if !AM_SUMMONED
	infomsg all "ERROR: weather/vapors - wrong usage" "Use must define params property: (RRR,GGG,BBB);radius"
	callevent 10.0 error_loop
}

{ npc_suicide
	invincible 0
	race hated
	dodamage ent_me direct 30000 100 GAME_MASTER target
}

{ game_removed //not sure if this is ever actually called (might be good idea to make it so it is)
	callevent remove_cl_fx
}

{ game_death
	callevent remove_cl_fx
}

{ remove_cl_fx
	setvard IS_ACTIVE 0
	clientevent update all CL_IDX end_fx
}