{
	const MONSTER_MODEL npc/femhuman2.mdl
	const NO_RUMOR 1
	const NO_JOB 1
	const CHAT_DELAY 3.5
	const EMOTE_FREQ 20.0
	precache MONSTER_MODEL
}

#include monsters/base_chat_array

{ game_spawn

	name Ghost of|Ryza, Preistess of Felewyn

	name_unique ryza

	hp 9999 //mass hp prevents xplode bolts
	invincible 1
	roam 0
	race beloved
	setmodel MONSTER_MODEL
	setprop ent_me rendermode 5
	setprop ent_me renderamt 255
	hearingsensitivity 10
	movespeed 0.0

	setidleanim idle1
	setmoveanim idle1

	catchspeech say_atholo atholo beat defeat
	catchspeech say_felewyn felewyn

	callevent 0.1 scan_for_players

	setvard EMOTE_COUNT 0
	setvard FLICK_COUNT 0
	setvard GAVE_CRYSTALS 0
	setvard ASKED_ATHOLO 0

	setvard TRIGGER_RANGE 128

	usetrigger ryza_shield

	saytextrange 1024
	
	callevent make_speech
}

{ make_speech

	callevent chat_add_text hi "Greetings, I am the priestess Ryza..." CHAT_DELAY pull_needle
	callevent chat_add_text hi "My soul was bound here, for we knew one day Venevus would awaken Atholo." CHAT_DELAY none !do_flicker
	callevent chat_add_text hi "We also knew that one day heroes, who could defeat both Venvus and Atholo, would come!"
	callevent chat_add_text hi "I pray you are they, for my magic can only hold Atholo in his tomb for so long."
	callevent chat_add_text hi "Use the portals quickly, for there is not much time before the magic that holds me to this mortal plane fails!" CHAT_DELAY eye_wipe !do_flicker
	
	callevent chat_add_text emote1 "Atholo is protected by ancient magics, but this source of his power is sealed in that tomb with him!" CHAT_DELAY pondering2 
	callevent chat_add_text emote2 "You must hurry, my magic cannot last forever!" CHAT_DELAY retina !do_flicker
	callevent chat_add_text emote3 "Do not hesitate! Hurry through the portals!" CHAT_DELAY gluonshow 
	callevent chat_add_text emote4 "If that beast escapes, all of Daragoth is doomed!" CHAT_DELAY fear1 
	callevent chat_add_text emote5 "Hurry! I cannot hold on much longer!" CHAT_DELAY checktie !do_flicker
	callevent chat_add_text emote6 "You CAN defeat him! It has been foretold!" CHAT_DELAY lean 
	callevent chat_add_text emote7 "You are amongst the chosen ones... you must not be defeated, or all is lost!" CHAT_DELAY yes 
	callevent chat_add_text emote8 "Continue the battle! I shall hold the beast inside for as long as I can!" CHAT_DELAY quicklook 
	callevent chat_add_text emote9 "Remember! I can use some of my magic to change your Return Crystals to Crystals of Relocation!" CHAT_DELAY pondering3 
	callevent chat_add_text emote10 "Great Felewyn, I beseech thee, watch over these heroes who fight so valiantly!" CHAT_DELAY kneel 
	
	callevent chat_add_text atholo "Atholo is the one of the great lords of darkness that fought along side Lor Malgoriand, servant of the Fallen." CHAT_DELAY converse2 
	callevent chat_add_text atholo "Atholo was sealed here at the end of the war, but Venevus made his home here, and my order could not defeat the wizard." CHAT_DELAY converse2 
	callevent chat_add_text atholo "We knew what was to come, so my soul was bound here to prevent the release of Atholo. But both he, and his magic, remain within." CHAT_DELAY converse2 
	callevent chat_add_text atholo "Atholo's magic, like mine, is bound up in crystals and the elements." CHAT_DELAY converse2 
	callevent chat_add_text atholo "That is all I know of The Beast, you MUST defeat him!" CHAT_DELAY converse2 
	
	callevent chat_add_text crystal_got "This will aid you in your battle, but use it wisely. I can only enchant so many." CHAT_DELAY dryhands 
	callevent chat_add_text crystal_no "I am sorry, the last of my magic MUST be used to maintain this barrier!" CHAT_DELAY no !do_flicker
	callevent chat_add_text crystal_mention "I can use some of my magic to convert your Crystals of Return to those of Relocation." CHAT_DELAY yes 
	
	callevent chat_add_text felewyn_no "There is not enough time to explain the grandeur of my goddess, just know that she watches over you and your valiant battle!" CHAT_DELAY push_button !do_flicker
	
	callevent chat_add_text dying "I salute you all for defeating the great evil, it is indeed as foretold!" CHAT_DELAY franticbutton 
	callevent chat_add_text dying "I'm afraid, in my incorporeal form, there is only one reward I can grant you..."
	callevent chat_add_text dying "...and that is a warning..." CHAT_DELAY none !do_flicker
	callevent chat_add_text dying "This is but the first of the Great Evils you shall encounter in your lives." CHAT_DELAY no 
	callevent chat_add_text dying "It is also the least of them... all of this is written upon your destiny!" CHAT_DELAY converse1 
	callevent chat_add_text dying "One of you, perhaps more than one, WILL become an Apostle, a great messenger of The Triad!"
	callevent chat_add_text dying "And one of you shall become... something... else..." CHAT_DELAY console 
	callevent chat_add_text dying "But my time here has ended, and alas I can say no more..."
	callevent chat_add_text dying "I will pray for you, great adventurers! Take heart, for Felewyn watches over you all!" CHAT_DELAY kneel do_fade
}

{ scan_for_players

	callevent 0.7 scan_for_players

	if ( $cansee(player,TRIGGER_RANGE) )
	{
		callevent player_spotted
	}
}


{ game_heardsound

	if $get(ent_lastheard,range) < TRIGGER_RANGE
	if $get(ent_lastheard,isplayer)
	callevent player_spotted	
}

{ player_spotted //auto talk

	if !DOING_EXIT

	if ( DID_INTRO )
	{
		if !ATHOLO_DEAD
		if !EMOTE_DELAY
		setvard EMOTE_DELAY 1
		callevent EMOTE_FREQ emote_reset
		callevent say_emote
	}

	if ( !DID_INTRO )
	{
		if !ATHOLO_DEAD
		callevent say_hi
		setvard DID_INTRO 1
	}

	if ( ATHOLO_DEAD )
	{
		////dbg spotted player, doing final exit
		setvard DOING_EXIT 1
		callevent 0.1 do_exit
	}
}

{ emote_reset
	setvard EMOTE_DELAY 0
}

{ say_hi

	callevent chat_start_sequence hi
}

{ say_emote
	local L_RAND $rand(1,10)
	if ( L_RAND == 9 )
	{
		if ( GAVE_CRYSTALS >= 4 ) local L_RAND 10
	}
	
	local L_STR emote L_RAND
	callevent chat_start_sequence L_STR
}

{ game_menu_getoptions

	if DID_INTRO
	if !ATHOLO_DEAD
	
	if ( GAVE_CRYSTALS < 4 )
	{
		callevent chat_start_sequence crystal_mention
	}
	
	if $item_exists(PARAM1,item_crystal_return)
	
	local reg.mitem.title	"Enchant my crystal"
	local reg.mitem.type	payment
	local reg.mitem.data	item_crystal_return
	local reg.mitem.callback got_crystal
	menuitem.register
}

{ got_crystal

	local CRYSTAL_GIVER PARAM1

	if !ATHOLO_DEAD
	if ( GAVE_CRYSTALS >= 4 )
	{
		offer CRYSTAL_GIVER item_crystal_return
		callevent chat_start_sequence crystal_no
	}

	if ( GAVE_CRYSTALS < 4 )
	{
		offer CRYSTAL_GIVER item_crystal_reloc
		callevent chat_start_sequence crystal_got
		add GAVE_CRYSTALS 1
	}
}

{ atholo_done

	setvard TRIGGER_RANGE 64
	setvard ATHOLO_DEAD 1
	setvard EMOTES_ENABLED 0
	usetrigger ryza_shield
}

{ do_flicker

	add FLICK_COUNT 1
	if ( FLICK_COUNT < 20 )
	{
		local FLICKER_AMT $rand(10,180)
		setprop ent_me renderamt FLICKER_AMT
		callevent 0.1 do_flicker
	}
	if FLICK_COUNT == 20
	setvard FLICK_COUNT 0
	setprop ent_me renderamt 255
}


{ say_felewyn
	callevent chat_start_sequence felewyn_no
}

{ say_atholo

	callevent chat_start_sequence atholo
}

{ do_exit

	menu.autoopen 0
	callevent chat_start_sequence dying
}

{ do_fade
	setsolid none
	setvard FADE_COUNT 200
	callevent fade_loop
}

{ fade_loop
	//dbg temp FADE_COUNT
	subtract FADE_COUNT 3
	local L_FADE FADE_COUNT
	if ( FADE_COUNT >= 0 ) setprop ent_me renderamt L_FADE
	if ( FADE_COUNT <= 0 ) callevent 0.2 remove_me
	if FADE_COUNT > 0
	callevent 0.1 fade_loop
}

{ remove_me
	deleteent ent_me
}
