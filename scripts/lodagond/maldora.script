//Maldora Himself
{
	if ( $lcase(game.map.name) startswith lodagond ) setvard NPC_IS_BOSS 1

	const LIGHT_COLOR (128,128,128)
	const LIGHT_RAD 256

	const WAND_DOT 40

	const NPC_BOSS_REGEN_RATE 0.02
	const NPC_BOSS_RESTORATION 0.25

	setvard BASE_MOVESPEED 2.0

	const NUM_SPELLS 8

	const AM_UBER 1

	setvar IS_UNHOLY 1

	const SHADOW_SCRIPT ms_wicardoven/maldora_image //will be removing this sequence and replacing with dual madlora fragments
	const MINION_SCRIPT monsters/maldora_gminion_random
	const MINION_LIMIT 4

	const FREQ_SOUND 10.0

	const NO_INTRO 1
	const NPC_PROX_ACTIVATE 1
	const NOT_FRAGMENT 1
	const FIN_EXP 20000

	const NPC_PROX_ACTIVATE 1
	const NPC_PROXACT_RANGE 640
	const NPC_PROXACT_IFSEEN 1
	const NPC_PROXACT_EVENT combat_go

	const DMG_PUSH_BEAM $randf(20,60)
	const DMG_CHAIN $randf(20,60)
	const DMG_SHOCK 100.0 //effect_shock_dmg
	const DMG_ROCKS $rand(200,800)
	const DMG_WAND $randf(200,1000)

	setvard DMG_BARRIER 400
	const BARRIER_COLOR (255,0,0)
}

{ game_precache
	precachefile monsters/sorc_chief2
	precachefile ms_wicardoven/maldora_dead
	precachefile lodagond/maldora_fragment
	precachefile monsters/summon/rock_storm
	precachefile monsters/maldora_gminion_random
	precachefile ms_wicardoven/maldora_image
}

#include lodagond/maldora_fragment

{ game_death

	callexternal players ext_clear_valid_gauntlets
	setvard ME_DEAD 1
	callexternal all maldoraf_died
	svplaysound 2 0 ambience/alienvoices1.wav
	setprop ent_me renderamt 0
	if ( SORC_SPAWNED ) setvard WAND_TYPE -1
	createnpc ms_wicardoven/maldora_dead $relpos(0,0,0) crow_final WAND_TYPE
	setsolid none
	setorigin ent_me (20000,20000,20000)
}

{ npc_spawn

	name Maldora
	hp 10000
	race demon
	width 32
	height 86

	svplaysound 2 0 ambience/alienvoices1.wav

	movespeed 2.0

	setvard NPC_GIVE_EXP FIN_EXP

	roam 0

	setvard WAND_TARGET unset

	hearingsensitivity 11

	invincible 1

	setidleanim ANIM_IDLE
	setmoveanim ANIM_IDLE //remember to set back on resume
	playanim once ANIM_IDLE
	
	takedmg all 0.35

	takedmg fire 0.0
	takedmg poison 0.0
	takedmg cold 0.0
	takedmg ice 0.0
	takedmg earth 0.0
	takedmg magic 0.0
	takedmg lightning 0.0
	takedmg acid 0.0
	takedmg dark 0.65
	takedmg holy 1.0

	setvard IMMUNE_FIRE 1
	setvard IMMUNE_COLD 1
	setvard IMMUNE_LIGHTNING 1
	setvard IMMUNE_POISON 1
	setvard IMMUNE_ACID 1
	setvard IMMUNE_PUSH 1
	setvard IMMUNE_STUN 100

	setmodel MONSTER_MODEL

	setprop ent_me rendermode 2
	setprop ent_me renderamt 0

	callevent npcatk_suspend_ai

	setvard IMAGES_ALIVE 0
	setvard MINIONS_ALIVE 0

	saytextrange 2048

	playsound 0 10 magic/spawn_loud.wav
	callevent 0.1 fade_me_in

	setprop ent_me skin AXESKIN_NULL

	callevent combat_go

	if game.serverside
	
	callevent 0.1 spawn_shad
}

{ remove_me
	deleteent ent_me
}

{ fade_me_in
	
	callexternal GAME_MASTER gm_fade_in $get(ent_me,id) 2
}

{ fade_in_done
	setprop ent_me rendermode 0
	setprop ent_me renderamt 255
}

{ npc_post_spawn
	setvarg G_MALDORA_PRESENT 1
	clientevent persist all effects/sfx_motionblur_perm $get(ent_me,index) 0
}

{ [override] reset_props
	repeatdelay FREQ_SOUND
	if !ME_DEAD
	clientevent new all effects/sfx_follow_glow_cl $get(ent_me,index) LIGHT_COLOR LIGHT_RAD FREQ_SOUND
	svplaysound 2 0 ambience/alienvoices1.wav
	callevent 0.1 sound_loop
}

{ sound_loop
	svplaysound 2 8 ambience/alienvoices1.wav
}

{ reset_effect_delay
	setvard EFFECT_DELAY 0
}

{ [override] pick_spell

	setvard SPELL_CHOICE $rand(1,NUM_SPELLS)
	//setvard SPELL_CHOICE 7 //developer

	if ( BEAM_ON ) local EXIT_SUB 1
	if ( CHAIN_ON ) local EXIT_SUB 1
	if ( EXIT_SUB ) callevent 0.5 pick_spell
	if !EXIT_SUB

	if ( G_DEVELOPER > 0 )
	{
		setvard SPELL_CHOICE G_DEVELOPER
		setvard G_DEVELOPER 0
	}

	if ( $get(SPELL_TARGET,range) > 2048 ) setvard SPELL_CHOICE 0
	if ( !$get(SPELL_TARGET,isalive) ) setvard SPELL_CHOICE 0
	if ( $get(SPELL_TARGET,relationship,ent_me) equals ally ) setvard SPELL_CHOICE 0
	if ( SPELL_SUSPEND ) setvard SPELL_CHOICE 0

	if ( SPELL_CHOICE == 0 )
	{
		local EXIT_SUB 1
		if !BARRIER_ON
		if ( NO_MOVE ) callevent resume_moving
		local EXIT_SUB 1
	}
	if ( EXIT_SUB ) callevent 0.5 pick_spell
	if !EXIT_SUB

	if ( SPELL_CHOICE == 1 )
	{
		if ( BARRIER_DELAY )
		{
			setvard SPELL_CHOICE $rand(2,NUM_SPELLS)
			dbg barrier_no_go - barrier delay
			local EXIT_SUB 1
		}
		if !BARRIER_DELAY

		setvard BARRIER_DELAY 1
		callevent BARRIER_FREQ reset_barrier_delay

		callevent 0.1 raise_barrier
	}
	if ( EXIT_SUB ) callevent 0.5 pick_spell
	if !EXIT_SUB

	if ( SPELL_CHOICE == 2 )
	{
		setvard BEAM_ON 1
		callevent face_target SPELL_TARGET
		playanim critical ANIM_BOLT
		setvard BEAM_COUNT 0
		setvard APPLIED_BEAM 0
		setvard BEAM_TARGET SPELL_TARGET
		//clientevent update all CL_BEAM_IDX dbeam_color (1,1,0) 20
		
		callevent 0.1 beam_push
	}

	if ( SPELL_CHOICE == 3 )
	{
		setvard CHAIN_ON 1
		setvard CUR_CHAIN_TARGET 0
		callevent face_target SPELL_TARGET
		playanim critical ANIM_CAST
		setvard CHAIN_COUNT 0
		//clientevent update all CL_BEAM_IDX dbeam_color (1,1,1) 10
		callevent 0.1 chain_lightning
	}

	if ( SPELL_CHOICE == 4 )
	{
		if ( G_MAL_ROCK_STORMS )
		{
			//dbg MALDORA: Can't summon rock storm, still exists
			callevent 0.2 pick_spell
			local EXIT_SUB 1
		}
		if !EXIT_SUB
		setvarg G_MAL_ROCK_STORMS 1

		playanim critical ANIM_CAST
		local NUM_ROCKS game.players
		if ( NUM_ROCKS > 4 ) local NUM_RUCKS 4
		createnpc monsters/summon/rock_storm $relpos(0,0,0) $get(ent_me,id) NUM_ROCKS DMG_ROCKS 64 90 //<Owner> <#Rocks> <Dmg> <FX_Radius> <Letivtate_Height>
		//setvard ROCK_STORM_ID $get(ent_lastcreated,id)
	}
	if !EXIT_SUB

	if ( SPELL_CHOICE == 5 )
	{
		if ( BARRIER_ON )
		{
			//no summoning inside barriers as it means you have to leap out of them
			callevent 0.2 pick_spell
			local EXIT_SUB 1
		}
		if !BARRIER_ON
		if ( IMAGES_ALIVE >= 1 ) setvard SPELL_CHOICE $rand(6,NUM_SPELLS)
		if IMAGES_ALIVE == 0
		if ( $rand(1,3) == 1 )
		{
			if !AM_UBER
			saytext Shadows of my shadow...
			playsound 0 10 voices/lodagond-4/maldora_summoning.wav
		}
		callevent 2.7 laugh_it_up
		callevent stop_moving
		playanim critical ANIM_CAST
		playsound 0 10 magic/spawn.wav
		effect glow ent_me (255,255,255) 128 2 2
		//setsolid none
		createnpc SHADOW_SCRIPT $relpos(0,64,-35) $get(ent_me,id) SPELL_TARGET 45 AM_UBER
		callevent 0.1 make_shadow2
		if ( AM_UBER )
		{
			callevent 0.2 make_shadow3
			callevent 0.4 make_shadow4
		}

		setvard IMAGES_ALIVE 2
		//callevent repulse_blast //stop players from getting stuck inside of me
		callevent 1.0 resume_solid
		callevent 3.0 resume_moving		
	}

	if ( SPELL_CHOICE == 6 )
	{
		if ( BARRIER_ON )
		{
			//no summoning inside barriers as it means you have to leap out of them
			callevent 0.2 pick_spell
			local EXIT_SUB 1
		}
		if ( MINIONS_ALIVE >= MINION_LIMIT ) setvard SPELL_CHOICE $rand(7,NUM_SPELLS)
		if MINIONS_ALIVE < MINION_LIMIT
		add MINIONS_ALIVE 1
		playanim critical ANIM_CAST
		playsound 0 10 monsters/skeleton/calrain3.wav
		effect glow ent_me (255,255,255) 128 2 2
		createnpc MINION_SCRIPT $relpos(0,0,-64) $get(ent_me,id) SPELL_TARGET
		callevent stop_moving 0.9
		callevent 1.0 leap_away
	}

	if ( SPELL_CHOICE == 7 )
	{
		local L_LAST_AXE_PICK LAST_AXE_PICK
		add L_LAST_AXE_PICK 20.0 //min delay between axe changes is 20 seconds
		if ( game.time < L_LAST_AXE_PICK )
		{
			local EXIT_SUB 1
			callevent 0.2 pick_spell
		}
		if game.time > L_LAST_AXE_PICK
		callevent select_axe
		setvard LAST_AXE_PICK game.time
	}
	if !EXIT_SUB


	if ( SPELL_CHOICE == 8 ) //last spell slot means no spell selected, try again
	{
		callevent 0.2 pick_spell
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	callevent SPELL_FREQ pick_spell
}

{ select_axe

	local RND_AXE $rand(0,4)
	setprop ent_me skin AXESKIN_WHITE
	
	setvard SPELL_SUSPEND 1
	setvard NO_MOVE 1

	effect glow ent_me (255,255,255) 256 2 2
	playsound 0 10 magic/spawn_loud.wav	

	if ( RND_AXE == 0 )
	{
		local WAND_COLOR (255,0,0)
		setvard WAND_TYPE AXESKIN_FIRE
	}
	if ( RND_AXE == 1 )
	{
		local WAND_COLOR (255,255,255)
		setvard WAND_TYPE AXESKIN_DARK
	}
	if ( RND_AXE == 2 )
	{
		local WAND_COLOR (64,64,255)
		setvard WAND_TYPE AXESKIN_COLD
	}
	if ( RND_AXE == 3 )
	{
		local WAND_COLOR (0,255,0)
		setvard WAND_TYPE AXESKIN_POISON
	}
	if ( RND_AXE == 4 )
	{
		local WAND_COLOR (255,255,0)
		setvard WAND_TYPE AXESKIN_LIGHTNING
	}

	//beam where the sword will be
	local BEAM_START $get(ent_me,attachpos,1)
	vectoradd BEAM_START z 1024
	effect beam end lgtning.spr 200 BEAM_START $get(ent_me,id) 1 WAND_COLOR 255 100 2.0
	callevent 1.5 select_axe2
}

{ select_axe2
	setvard SPELL_SUSPEND 0
	setvard NO_MOVE 0
	setprop ent_me skin WAND_TYPE
}

{ [override] wand_strike_dmg //param1 = target struck (from dodamage)
	setvard WAND_ATK 0
	if ( WAND_TYPE equals 'WAND_TYPE' )
	{
		//bare fisted
		if $rand(1,5) == 1
		applyeffect PARAM1 effects/heavy_stun 3.0 $get(ent_me,id)
	}
}

{ [override] strike_wand //model event
	setvard AS_ATTACKING game.time

	if $get(WAND_TARGET,range) < ATTACK_HITRANGE
	setvard WAND_ATK 1
	if ( WAND_TYPE == AXESKIN_FIRE )
	{
		local DMG_TYPE fire
		local EFFECT_TYPE WAND_FIRE_EFFECT
	}
	if ( WAND_TYPE == AXESKIN_DARK )
	{
		local DMG_TYPE magic
		local EFFECT_TYPE WAND_DARK_EFFECT
	}
	if ( WAND_TYPE == AXESKIN_COLD )
	{
		local DMG_TYPE cold
		local EFFECT_TYPE WAND_COLD_EFFECT
	}
	if ( WAND_TYPE == AXESKIN_POISON )
	{
		local DMG_TYPE poison
		local EFFECT_TYPE WAND_POISON_EFFECT
	}
	if ( WAND_TYPE == AXESKIN_LIGHTNING )
	{
		local DMG_TYPE lightning
		local EFFECT_TYPE WAND_LIGHTNING_EFFECT
	}
	
	if ( WAND_TYPE equals 'WAND_TYPE' )
	{
		local DMG_TYPE blunt
		local EFFECT_TYPE none
	}
	dodamage WAND_TARGET ATTACK_HITRANGE DMG_WAND 80% blunt

	if ( EFFECT_TYPE equals WAND_DARK_EFFECT )
	{
		applyeffect WAND_TARGET WAND_DARK_EFFECT 10.0 0 1
	}
	else
	{
		if EFFECT_TYPE isnot none
		applyeffect WAND_TARGET EFFECT_TYPE 5.0 $get(ent_me,id) WAND_DOT
	}
}

{ spawn_shad
	setvard SORC_SPAWNED 1
	createnpc monsters/sorc_chief2 $relpos(0,64,0) $get(ent_me,id)
	callevent 4.0 spawn_shad2
}

{ spawn_shad2
	saytext It'll take more than a renegade orc and a couple of wayward humans to defeat me!
	playsound 0 10 voices/lodagond-4/maldora_orcspot.wav
	callevent 5.9 laugh_it_up
}
