{
	const SOUND_TELE "magic/teleport.wav"
}

{ game_spawn
	setmodel monsters/sorc.mdl
	width 32
	height 72
	setmodelbody 0 2 //body: 0=bare 1=bloody_shirt 2=gold_armor 3=archer
	setmodelbody 1 3 //head: 0=hat_horns 1=hat_spiked 2=lizardskin_horned 3=gold_chieften 4=lizard_cap
	setmodelbody 2 8 //weapon: 0=unarmed 1=axe 2=bow 3=steel_bow 4=sword 5=great_axe 6=sword_sheild 8=yellow_axe 9=blooddrinker

	nopush 1

	invincible 1
	race demon

	hearingsensitivity 11

	name Runegahr, Shadahar Orc Chieftain

	saytextrange 2048

	menu.autoopen 1

	catchspeech say_hi hail hi hello wassup j0 yo
	catchspeech offer_accepted yes accept
	catchspeech offer_denied no die

	callevent 0.1 say_stop
}

{ game_heardsound
	local LAST_HEARD $get(ent_lastheard,id)
	if $get(LAST_HEARD,isplayer)
	setmovedest LAST_HEARD 9999
}

{ say_hi
	callevent say_stop
	menu.open $get(ent_lastspoke,id)
}

{ say_stop
	saytext ENOUGH! Maybe you aren't the pathetic creatures I took you for...
	callevent 4.0 say_stop2
}

{ say_stop2
	saytext However, defeating me alone is not enough to prove yourselves worthy of alliance.
	callevent 4.0 say_stop3
}

{ say_stop3
	saytext For human's are tretcherous, and fall easily to temptation and despair.
	callevent 4.0 say_stop4
}

{ say_stop4
	saytext If you can reach Maldora's lair, at the heart of Lodagond, I swear upon this sword, that I will join you and aid in his defeat!
	callevent 4.0 say_stop5
}

{ say_stop5
	saytext Do you [accept] this offer? Or would you rather [die] at my hand here and now?
}

{ game_menu_getoptions
	local reg.mitem.title 	"I accept."
	local reg.mitem.type 	callback
	local reg.mitem.callback offer_accepted
	menuitem.register

	local reg.mitem.title 	"Die evil orc!"
	local reg.mitem.type 	callback
	local reg.mitem.callback offer_denied
	menuitem.register
}

{ offer_accepted
	playsound 0 10 SOUND_TELE
	createnpc monsters/summon/ibarrier $relpos(0,0,0) $get(ent_me,id) 64 2 0 0 0 1 //<owner> <rad> <duration> [invisible:0|1] [silent:0|1] [dmg] [push_allys:0|1]
	saytext Very well. When the time comes, I will be there, and in exchange for your aid, I will give one of you this sword.
	playanim critical warcry
	callevent 1.0 do_fadeout
}

{ offer_denied

	playsound 0 10 SOUND_TELE
	createnpc monsters/summon/ibarrier $relpos(0,0,0) $get(ent_me,id) 64 2 0 0 0 1 //<owner> <rad> <duration> [invisible:0|1] [silent:0|1] [dmg] [push_allys:0|1]
	playanim critical warcry
	saytext Impudent fools! You stand no chance against the charlatan without my help. So be it, die at his hands - you'll wish you'd done so at mine.
	callevent 1.0 do_fadeout
}

{ do_fadeout
	deleteent ent_me fade	
}