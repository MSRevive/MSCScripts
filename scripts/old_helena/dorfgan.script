// Dorfgan the Smith

{
	setvar ATTACK_RANGE 64
	const ANIM_ATTACK beatdoor
	const STORE_NAME dorfgans_blacksmith
}

#include monsters/base_npc
#include monsters/base_chat
#include monsters/base_npc_vendor
#include monsters/base_npc_vendor_confirm
#include old_helena/base_old_helena_npc

{ npc_spawn

	hp 350/350
	gold 25
	name Dorfgan
	//ov 120
	width 32
	height 72
	race human
	roam 0      //Don't go roaming around
	setmodel    npc/blacksmith.mdl

	setvar SOUND_PAIN player/chesthit1.wav
	setvar SOUND_PAIN2 player/armhit1.wav
	setvar ATTACK1_DAMAGE 3
	setvard ATTACK_RANGE 90

	setvar SAY_SO 0
	setvar EXPLAINING_QUEST 0
	setvar QUEST_1 0
	setvar QUEST_2 0
	setvar OFFER_ITEMS 1
	setvar CANCHAT 1
	setvar CAN_SCREAM 1
	setvar CAN_RUN 1
	setvar SEE_ENEMY 0

	callevent createmystore   

	catchspeech say_hi hi
	catchspeech say_hi hello
	catchspeech say_hi hail
	catchspeech say_hi greet

	catchspeech say_quest attack
	catchspeech say_quest orc
	catchspeech say_dagger dagger

	setvard NPCATK_TARGET unset

	menu.autoopen 1

	callevent 260.0 add_moar_stuff
}

{ say_hi

	saytext "Adventurer! We're under [attack]!"
	calleventtimed 3 say_hi2test
}

{ say_hi2test

	if SAY_SO equals 0

	saytext "I'm Dorfgan, the blacksmith."

	callevent setsayso
}

{ setsayso
	setvar SAY_SO 1
}

{ say_quest

	if EXPLAINING_QUEST equals 0

	saytext "Our village is being ransacked by those blasted Orcs!"
	setvar EXPLAINING_QUEST 1
	calleventtimed 2 say_quest_2
}

//Thothie - in this quest Dorfgan needs a hammer you would find in Thornlands to arm mercenaries
//- it uses old convention and thus will not function, in addition to the fact that the hammer no longer appears there
//- might consider some variant of this quest
//
//{
//  eventname say_quest_2
//
//  saytext Mercenaries should be coming shortly to help... that is, if I can forge weapons for them.
//  calleventtimed 3 say_quest_3
//}
//{
//  eventname say_quest_3
//
//  saytext But I've lost my hammer and can't forge anything right now.
//  calleventtimed 2 say_quest_4
//}
//{
//  eventname say_quest_4
//
//  saytext I've lost it somewhere in the caves in the southern plains... without it, I'm afraid this village won't last long...
//  setvar EXPLAINING_QUEST 0
//}
////Delivering Note from Erkold, if hammer has been returned
//{
//  eventname recvoffer_item_erkoldsnote
//
//  if QUEST_1 equals 0
//
//  DLLFunc recvoffer refuse
//  saytext I need my hammer to finish it. Perhaps if you found it for me...
//}
//
//{
//  eventname recvoffer_item_erkoldsnote
//
//  if QUEST_1 equals 1
//  if QUEST_2 equals 0
//
//  DLLFunc recvoffer accept
//  saytext I see... well, it's all done and repaired. You'll need two hands for this one, it's heavy!
//  setvar QUEST_2 1
//  calleventtimed 4 givearmor
//}
//{
//  eventname givearmor
//
//  offer ent_lastgave armor_mongol
//  offer ent_lastgave armor_helm_mongol
//}
//{
//  eventname recvoffer_blunt_rustyhammer2
//
//  if QUEST_1 equals 0
//
//  DLLFunc recvoffer accept
//  saytext That's my hammer! Thank you very much for bringing it back!
//  setvar QUEST_1 1
//  calleventtimed 4 recvhammer_2
//}
//
//{
//  eventname recvhammer_2
//
//  playanim critical pull_needle
//  saytext It will be some time before the mercenaries will arrive. Please take this gold as a reward!
//  calleventtimed 30 callmercs
//  calleventtimed 2 recvhammer_3
//}
//{
//  eventname recvhammer_3
//  
//  offer ent_lastgave gold 100
//}
//{
//  eventname callmercs
//
//  usetrigger spawnmercs
//}

//Thothie
//- Dorfgan can't currently attack due to the dated conventions used here (plus his model as no anim attack event)
//- I'd like Dorfgan to be able to fight back if you brought him a weapon (Dorfgan quest variant)
//- If he does so it'd be best if he remained within a certain distance of his spawn
//- (There maybe a function for this in base_npc_attack - if not, we should make on in base_monster_shared)
//- outpost/super - has a patrol area function that is similar
////////////////////////////////////////////////////////////

{
   //Hit by someone
   eventname struck
   
   volume 10
   playrandomsound 2 SOUND_PAIN SOUND_PAIN2 none none
   playanim once raflinch

   callevent flee
}
{
   eventname resetoffer

   setvar OFFER_ITEMS 1   
}
///////////////////////////////////////////////////////////
// 

{ playerused

	if ( HELENA_SAVED ) saytext "I've also an item or two for exclusive sale to our brave saviors here."

	if OFFER_ITEMS equals 1

	offerstore STORE_NAME buysell trade
}

{ trade_success

	if CANCHAT equals 1

	saytext "We're low on supplies, so I don't have much to offer..."
	setvar CANCHAT 0
	calleventtimed 10 resetchat
}

{ resetchat

   setvar CANCHAT 1
}

///////////////////////////////////////////////////////////
// The blacksmith doesn't have much to offer since the village is being attacked and low on supplies

{ vendor_addstoreitems

	createstore STORE_NAME

	//           |Store name|  |Item name|  |Quantity| |cost|
	addstoreitem  STORE_NAME     swords_shortsword   1         100
	addstoreitem  STORE_NAME     swords_bastardsword   1         100
	addstoreitem  STORE_NAME     axes_doubleaxe   2         100
	addstoreitem  STORE_NAME     shields_ironshield   2         100
	addstoreitem  STORE_NAME     shields_lironshield   2         100
	addstoreitem STORE_NAME proj_bolt_wooden 100 100 0 25
	addstoreitem STORE_NAME proj_bolt_fire 100 100 0 25
	addstoreitem STORE_NAME proj_bolt_iron 100 100 0 25
	addstoreitem STORE_NAME proj_bolt_steel 75 800 0 25
	addstoreitem STORE_NAME proj_bolt_silver 25 800 0 25

	callevent addrandomitems
}

{ addrandomitems
   if $rand(1,2) == 1
   addstoreitem  STORE_NAME     swords_longsword   	 1          200
}

{ addrandomitems
   if $rand(1,2) == 1
   addstoreitem  STORE_NAME     swords_scimitar   	 1          100
}

{ addrandomitems
   if $rand(1,2) == 1
   addstoreitem  STORE_NAME     axes_battleaxe   	 1          200
}

//Add winning items
{ old_helena_warboss_died

	setvard HELENA_SAVED 1
	//if ( $rand(1,2) == 1 ) addstoreitem STORE_NAME item_ore_lorel 1 200 0
	addstoreitem STORE_NAME blunt_northmaul972 1 200 0
	addstoreitem STORE_NAME bows_crossbow_heavy33 1 100 0
}

{ basevendor_offerstore

	if HELENA_SAVED
	saytext "I've got a few unusual items just for those who rescued Helena."
	callevent bchat_mouth_move
}

{ catapults_fire

	if !DID_CATA_COMMENT
	add CATAPULT_COMMENT 1
	if CATAPULT_COMMENT > 3

	saytext "Catapults!? May the gods save us! They've brought catapults!"
	setvard DID_CATA_COMMENT 1
}

{
	repeatdelay 1.5

	local TARGETS_NEAR $get_tsphere(enemy,128,game.monster.origin)

	if ( TARGETS_NEAR >= 1 )
	{
		playanim once ANIM_ATTACK
		dodamage $get_token(TARGETS_NEAR,0) ATTACK_RANGE 20.0 90% blunt
	}
}

{ say_dagger

   if $item_exists(PARAM1,smallarms_rd)
   
   if ( !HELENA_SAVED ) saytext "Is now really the time to talk about this? We're under attack!"

   if HELENA_SAVED
   if !FIXING_DAGGER
   setvard CUR_PLAYER PARAM1

   saytext "I always hate to see adventurers who can't properly take care of their equipment."
   callevent 1.0 say_dagger2
}

{ say_dagger2

   saytext "I can fix that up for you, for a nominal fee, of course."

   menu.open CUR_PLAYER

}

{ not_enough_repair_dagger
   saytext "You travellers have to learn somehow."
}

{ start_repair_dagger

	setvard REPAIR_QUEST_WINNER PARAM1

	setvard FIXING_DAGGER 1
	setvard REPAIR_STEP 0
	callevent repair_dagger

}

{ repair_dagger
	add REPAIR_STEP 1

	if ( REPAIR_STEP == 1 )
	{
		playanim once portal
		saytext "This'll be no trouble at all."
	}
	if ( REPAIR_STEP == 2 )
	{
		playanim once panic
		saytext *Crunch* Ow!
	}
	if ( REPAIR_STEP == 3 )
	{
		playanim once push_button
		saytext "Sorry, I broke your dagger. Strange thing is, I think it cut me AFTER it shattered..."
		offer REPAIR_QUEST_WINNER smallarms_eth
		setvard FIXING_DAGGER 0
	}

	if REPAIR_STEP < 3
	callevent 2.0 repair_dagger
}

{ game_playerused
	if HELENA_SAVED
	if $item_exists(PARAM1,smallarms_rd)
	setvard CHAT_STEP1 "Hey, that's a mighty interesting looking hunk of rust you have there."
	setvard CHAT_STEP2 "I might be able to clean it up into something even more interesting, for a price."
	setvard CHAT_STEPS 2
	callevent chat_loop
	setvard MENTIONED_DAGGER 1
}

{ game_menu_getoptions
	if HELENA_SAVED
	if ( $item_exists(PARAM1,smallarms_rd) )
	{
		local reg.mitem.title    "Repair rusted dagger (10,000 gold)"
		local reg.mitem.type    payment
		local reg.mitem.data    "smallarms_rd;gold:10000"
		local reg.mitem.callback  start_repair_dagger
		local reg.mitem.cb_failed not_enough_repair_dagger
		menuitem.register
	}
}