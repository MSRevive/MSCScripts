//Copied from effect_shock_damage. By greatguys1
//Main effect for the Gut Buster sword, handles the sounds, sfx, dot, and causes gibs on death

//PARAMS:
//	PARAM5 = <gut buster sword id>

#scope server

{ [server]

	const EFFECT_ID "gb"
	const EFFECT_SCRIPT $currentscript //Must be copied, as is, at the top script!
	
	const EFFECT_FLAGS nostack //<"nostack"|"player_action">
	
	const DOT_TYPE pierce_effect
	
	const DOT_IM_AFFECTED "Your flesh is defiled by the evil sword." //Player recieves affect and is afflicted
	
	const DOT_IM_RESIST "Your armor prevents the attack from piercing through your skin." //Player recieves the affect, but resists / immune it.
	const DOT_HE_IMMUNE "cannot bleed." //Target recieves the affect, but resists / immune it

	//Tracks if the gibs have been bursted
	setvard DID_PROC 0
	const NUM_GIBS $func(get_num_gibs)
}

#include effects/dot_bleed

{ game_activate //<duration> <attacker> <damage> <activeSkill> <sword_id>
//-Applies bleed fx and vars

	setvard SWORD_ID PARAM5
	setvard MY_BLOOD $get(ent_me,blood)
}

{ dot_start
	//Apply blood fx
	bleed ent_me MY_BLOOD 10000
}

{ game_death //-Causes gib burst proc

	//Cause gib burst
	callevent proc_death
}

{ proc_death //-Causes gib burst and applies relating fx

	if ( !DID_PROC )
	{
		setvard DID_PROC 1

		calleventloop NUM_GIBS create_gib

		//And apply some fx
		playsound 0 10 common/bodysplat.wav

		//Monsters with a combined height and width of greater than 230 will not go invisible on death.
		if ( !$get(ent_me,alive) ) //Some enemies come back alive, double check if they did. In those events, I still want gibs, but the enemy shouldn't turn invisible.
		{
			if ( $math(add,$get(ent_me,height),$get(ent_me,width)) < 150 )
			{
				setprop ent_me rendermode 5
				setprop ent_me renderamt 0
			}
		}
	}
}

{ create_gib //for looping

	local L_ANG $vec($randf(70,95),$rand(0,359),0)
	local L_FORCE $math(multiply,400,game.script.iteration)
	add L_FORCE 400

	local L_VEC $relvel(L_ANG,$vec(0,L_FORCE,0))
	//<owner> <velocity> <damage> <skill> <"green"|"red"> <sword_id>
	createnpc effects/swords_gb/gb_gib_explode $get(ent_me,origin) DOT_ATTACKER L_VEC 0 swordsmanship MY_BLOOD SWORD_ID
}

{ get_num_gibs

	//Size can add up to 2 additional gibs
	local L_NUM_GIBS $math(add,$get(ent_me,height),$get(ent_me,width)) //height + width
	divide L_NUM_GIBS 150 //1 gib per each 150 units
	capvar L_NUM_GIBS 0 2
	
	//Health can add up to 3 additional gibs
	add L_HP_GIBS $math(divide,$get(ent_me,maxhp),1750) //additional gib per 1750 hp
	capvar L_HP_GIBS 0 3
	
	add L_NUM_GIBS L_HP_GIBS
	add L_NUM_GIBS 1

	return **clear
	return L_NUM_GIBS
}
