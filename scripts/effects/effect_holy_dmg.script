{ 	[server]
	const EFFECT_ID			DOT_holy
	const EFFECT_NAME		"holy"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE

	const DOT_EFFECT 1
	const DOT_TYPE holy
	const DOT_DMG local.effect.damage
	const DOT_FREQ $randf(0.9,1.1)
	const DOT_ATTACKER local.effect.dmgsource
	const DOT_SKILL ACTIVE_SKILL
}

#include effects/base_dot	allowduplicate

{ 	[server] game_activate //<duration> <attacker> <dps> [skill]

	//=== Check Immunities
	callevent resist_check holy $get(PARAM2,id)
	if RESISTED_EFFECT < 2
	//=== End Check Immunities

	setvard local.effect.duration 	PARAM1
	setvard local.effect.dmgsource 	PARAM2
	setvard local.effect.damage 	PARAM3

	if ( $get(ent_me,isplayer) )
	{
		multiply local.effect.damage 4
		playermessage $get(ent_me,id) You are being burned by divine magics! BE_RESIST_STRING
		clientevent new ent_me $currentscript local.effect.duration
		setvard local.effect.clientscript game.script.last_sent_id
	}

	callevent local.effect.duration effect_die


	if ( PARAM4 startswith 'PARAM' )
	{
		setvard ACTIVE_SKILL spellcasting.divination
	}
	else
	{
		setvard ACTIVE_SKILL PARAM5
	}
}

{ 	[shared] effect_die

	if ( game.serverside )
	{
		setvard local.effect.damage 0
		if RESISTED_EFFECT < 2
		if ( $get(ent_me,isplayer) ) 
		{
			playermessage $get(ent_me,id) The holy wrath subsides.
		}
	}
}


// ===========
// Client Side
// ===========

{	[client]
	const POISON_SCREEN_DELAY		1.0
	const POISON_SCREEN_ALPHA		80
}

{	[client] client_activate

	callevent PARAM1 effect_die
}

{	[client]
	repeatdelay POISON_SCREEN_DELAY


	setvard game.cleffect.screenfade.newfade 1
	setvard game.cleffect.screenfade.alphalimit POISON_SCREEN_ALPHA	//Only use this fade if current fade is under this limit
	setvard game.cleffect.screenfade.color (255,255,255)
	setvard game.cleffect.screenfade.alpha POISON_SCREEN_ALPHA
	setvard game.cleffect.screenfade.duration POISON_SCREEN_DELAY
	setvard game.cleffect.screenfade.blendduration POISON_SCREEN_DELAY
	setvard game.cleffect.screenfade.type fadein
}

{ [server]
repeatdelay DOT_FREQ
	if BE_EFFECT_ACTIVE
	if ( $get(ent_me,isplayer) )
	{
		callevent dot_effect
	}
	else
	{
		callexternal ent_me turn_undead local.effect.damage local.effect.dmgsource
	}
	effect glow ent_me (255,255,0) 64 1 1
	playsound 2 10 fvox/hiss.wav
}