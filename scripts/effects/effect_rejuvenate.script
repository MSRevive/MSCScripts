//Rejuvenate HP
//Any npc
//PARAM1 == Duration in seconds
//PARAM2 == Percent to be healed over duration (0-100%)
{  [shared]

	const OFS_POS			16
	const OFS_NEG			-16
	const OFSZ_POS			32
	const OFSZ_NEG			-10

	const SPRITE_1			3dmflaora.spr
	precache SPRITE_1
}

{ [server]

	const EFFECT_ID			effect_rejuvenate
	const EFFECT_NAME		"Rejuvenation"
	const EFFECT_ACTIONTYPE		normal
	const EFFECT_SCRIPT		$currentscript

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE
}

#include effects/effect_base

{ [server] game_activate

	setvard local.effect.duration 		PARAM1
	setvard local.effect.healamt		PARAM2			//Total Amount to be healed [0-MaxHP]
	capvar local.effect.healamt 		0 game.monster.maxhp	//Cap it to max health
	setvard local.effect.begin		game.time
	setvard local.effect.elapsed		0
	
	clientevent new all $currentscript local.effect.duration $get(ent_me,index)
	setvard effect.clientscript game.script.last_sent_id
	//setvard local.effect.clientscript game.script.last_sent_id

	effect glow ent_me (0,100,0) 60 local.effect.duration local.effect.duration
	playsound game.sound.item game.sound.maxvol magic/heal_strike.wav

	local l.amt game.monster.maxhp
	callevent game_heal

	callevent local.effect.duration effect_die
}

{ [server] game_think

	local l.duration game.time			//Get total duration since beginning
	subtract l.duration local.effect.begin		//

	capvar l.duration 0 local.effect.duration	//Cap to max duration

	local l.frametime l.duration			//Find time delta since last frame
	subtract l.frametime local.effect.elapsed	//

	setvard local.effect.elapsed l.duration		//Save duration (elapsed time) for next frame

	if l.frametime > 0				//If time has passed since last frame 
							//(but not past max duration), then heal a
							//portion of my health

	local l.healratio l.frametime				//Heal ratio for this frame depends on 
	divide l.healratio local.effect.duration precision	//(time passed / max duration)  
								//'precision' speicifed to keep precision

	local l.heal local.effect.healamt		//Multiply heal ratio by total amount to be healed
	multiply l.heal l.healratio 			//


	givehp ent_me l.heal				//heal a percent this frame
}



{ 	[server] effect_die

	callevent game_think				//If time expired, make sure I'm healed all the way up
}


// ===========
// Client Side
// ===========

{ [client] client_activate

	setvard sfx.npcid PARAM2

	callevent PARAM1 effect_die
}

{ [client]
	//axing for effect limit
	//repeatdelay $randf(0.2,0.8)

	//callevent createsprite
}


{ [client] createsprite

	local l.pos $getcl(sfx.npcid,origin)

	vectoradd l.pos $vec($randf(OFS_NEG,OFS_POS),$randf(OFS_NEG,OFS_POS),$randf(OFSZ_POS,OFSZ_NEG))
	cleffect tempent sprite SPRITE_1 l.pos setup_sprite1_sparkle
}

{  [client] setup_sprite1_sparkle

	cleffect tempent set_current_prop death_delay 	$randf(0.4,1.0)
	cleffect tempent set_current_prop framerate	25
	cleffect tempent set_current_prop frames	20
	cleffect tempent set_current_prop scale 	0.2
	cleffect tempent set_current_prop gravity 	-0.5
	cleffect tempent set_current_prop collide 	none
}