#scope client
{
	const SPRITE_NAME fire1_fixed.spr
	const SPRITE_COLOR (255,255,255)
	const SPRITE_RENDERAMT 200
	const SPRITE_RENDERMODE add
	const SPRITE_FRAMERATE 30
	const SPRITE_NFRAMES 23	
	const SPRITE_SCALE 2.0
	const GLOW_COLOR (255,128,0)
}

{ client_activate //<start_org> <yaw>

	setvard FX_ORG PARAM1
	setvard FX_YAW PARAM2

	cleffect light new FX_ORG 128 GLOW_COLOR 4.0

	setvard SPRITE_STEP 0
	callevent setup_fire_wave
}

{ setup_fire_wave

	local N_SPRITES SPRITE_STEP
	add N_SPRITES 1

	setvard SPRITE_OFS SPRITE_STEP
	multiply SPRITE_OFS -32

	calleventloop N_SPRITES setup_fire_sprites

	add SPRITE_STEP 1

	if ( SPRITE_STEP == 12 ) callevent 5.0 remove_fx

	if SPRITE_STEP < 12
	callevent 0.25 setup_fire_wave
}

{ setup_fire_sprites
	local SPR_POS FX_ORG
	local ROW_OFS SPRITE_STEP
	multiply ROW_OFS 32

	local COL_OFS game.script.iteration
	multiply COL_OFS 32
	add SPRITE_OFS COL_OFS

	
	vectoradd SPR_POS $relpos($vec(0,FX_YAW,0),$vec(SPRITE_OFS,ROW_OFS,0))

	local GROUND_POS $get_ground_height(SPR_POS)
	vectorset SPR_POS z GROUND_POS

	cleffect tempent sprite SPRITE_NAME SPR_POS setup_fire_sprite
}

{ remove_fx
	removescript
}

{ setup_fire_sprite

	cleffect tempent set_current_prop death_delay 	4.0
	cleffect tempent set_current_prop rendercolor SPRITE_COLOR
	cleffect tempent set_current_prop renderamt	SPRITE_RENDERAMT
	cleffect tempent set_current_prop rendermode SPRITE_RENDERMODE
	cleffect tempent set_current_prop framerate	SPRITE_FRAMERATE
	cleffect tempent set_current_prop frames	SPRITE_NFRAMES
	cleffect tempent set_current_prop scale 	SPRITE_SCALE
	cleffect tempent set_current_prop gravity 	0
	cleffect tempent set_current_prop collide 	none
	cleffect tempent set_current_prop fadeout lifetime
}