//Stun
//Any npc
//Param1 - Duration
//Param2 - slowpush (?)
//Param3 - can't attack while stunned if set to 1
//Param4 - attacker


{ [server]
	const EFFECT_ID			effect_stun
	const EFFECT_NAME		"Stun"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript

	const EFFECT_BLOCK_RESISTS 1

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE

	const SOUND_RESIST body/armour3.wav
}

#include effects/base_dot	allowduplicate

{ [server] game_activate //<duration> [push] [cantattak] [attacker]

	//dbg stun game_activate

	if ( $get(ent_me,nopush) )
	{
		if !$get(ent_me,scriptvar,'NPC_CAN_BE_STUNNED') //a few monsters can't be pushed, but can still be stunned
		gplayermessage ent_me You are immune to the stun effect. //those immune to push are immune to stun
		dplayermessage PARAM4 $get(ent_me,name) is immune to stun effects.
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	if !$get(ent_me,scriptvar,IS_LURE) //do not stun lures
	//dbg stun resist_check
	callevent resist_check stun $get(PARAM4,id) //cant get attacker on standard stun, need way to pull

	if !RESISTED_EFFECT

	//playermessage ent_me You have been stunned! BE_RESIST_STRING

	setvard local.effect.duration 	PARAM1

	if ( $get(ent_me,isplayer) )
	{
		local L_LAST_STUN $get(ent_me,scriptvar,'DAT_LAST_HEAVY_STUN')
		if ( $math(add,L_LAST_STUN,10.0) > game.time ) //If it hasn't been 10 seconds since the last stun
		{
			//silent abort
			setvard EFFECT_ABORTED 1
			callevent effect_die
			exitevent			
		}
		else
		{
			callexternal ent_me ext_set_last_stun_time
		}

		if local.effect.duration > 5
		setvard local.effect.duration 5
	}

	setvard game.effect.movespeed 30%
	setvard game.effect.canjump 0
	setvard game.effect.anim.framerate 0.4

	//dbg stun apply... ( dur local.effect.duration )

	local slowduration $randf(0.2,local.effect.duration)
	if( PARAM2 equals 1 ) applyeffect ent_me effects/effect_push slowduration (0,0,0) 1 1
	if( PARAM3 equals 1 ) setvard game.effect.canattack 0
	
	if ( $get(ent_me,isplayer) )
	{
		//effect screenshake_one ent_me 16 20 local.effect.duration //It appears that this doesn't work, and effect_push only does screen shake anyways. -greatguys1 SEP2021
		hud.addstatusicon ent_me alpha_stun stun local.effect.duration
	}

	clientevent new all effects/sfx_stunring $get(ent_me,index) local.effect.duration $get(ent_me,height)


	callexternal $get(ent_me,id) ext_set_slowed 1

	callevent local.effect.duration effect_die
}

{ effect_die
	if !EFFECT_ABORTED
	callexternal $get(ent_me,id) ext_set_slowed 0
}

{ game_precache

	precachefile effects/sfx_stunring
}

{ game_death
	//deleteent script.stunring	//Delete the ring from here, because skellys stay 'alive' after death
	callevent effect_die
}

{ game_duplicated

	callevent game_death
}