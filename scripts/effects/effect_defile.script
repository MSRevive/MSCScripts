//Defiles target, and reduces its damage output (atm, monsters only) from frostbite_dmg

{ [server]
	const EFFECT_ID			DOT_defile
	const EFFECT_NAME		"Defile"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript
	const ATTACK_DAMAGE_TYPE	dark

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE

	const DOT_EFFECT 1
	const DOT_TYPE dark
	const DOT_DMG DARK_DMG
	const DOT_FREQ $randf(0.9,1.1)
	const DOT_ATTACKER MY_ATTACKER
	const DOT_SKILL ACTIVE_SKILL
}

#include effects/effect_base	allowduplicate

{ [server] game_activate //<duration> <attacker> [dmg] [ratio] [skill]

	dbg EFFECT_ID got PARAM1 PARAM2 PARAM3 PARAM4

	setvard local.effect.duration PARAM1
	setvard MY_DURATION local.effect.duration //just cuz that's ugly, but required for base
	setvard MY_ATTACKER PARAM2
	setvard DARK_DMG PARAM3
	setvard DMGMULTI_REDUCT PARAM4
	setvard ACTIVE_SKILL PARAM5
	if ( ACTIVE_SKILL startswith PARAM )
	{
		if $get(MY_ATTACKER,isplayer)
		setvard ACTIVE_SKILL spellcasting.affliction
	}
	if ( DARK_DMG startswith PARAM )
	{
		setvard BE_EFFECT_ACTIVE 0
		setvard DARK_DMG 0
	}
	if ( DMGMULTI_REDUCT startswith PARAM ) setvard DMGMULTI_REDUCT 0.25

	callevent resist_check dark $get(PARAM2,id)

	if ( $get(ent_me,isplayer) )
	{
		setvard local.effect.duration 0
		setvard BE_EFFECT_ACTIVE 0
		setvard RESISTED_EFFECT 1
		//for later:
		//playermessage You are being defiled by dark magics!
	}
	
	callevent local.effect.duration effect_die

	if RESISTED_EFFECT < 1

	setvard DMGMULTI_ORIG $get(ent_me,dmgmulti)
	if ( DMGMULTI_ORIG == 0 ) setvard DMGMULTI_ORIG 1
	local L_FINAL_DEDUCT DMGMULTI_ORIG
	multiply L_FINAL_DEDUCT DMGMULTI_REDUCT
	dmgmulti L_FINAL_DEDUCT //might be better to do a scriptflag thing, but in a rush this month

	effect glow ent_me (255,0,255) 32 MY_DURATION MY_DURATION
	clientevent new all effects/sfx_flames $get(ent_me,index) local.effect.duration $get(ent_me,height) 1 1 $get(ent_me,width)
	setvard DEFILE_CL_IDX game.script.last_sent_id
}

{ effect_die

	dbg effect_burn effect_defile PARAM1 RESISTED_EFFECT
	if RESISTED_EFFECT < 2
	if ( DEFILE_CL_IDX isnot 'DEFILE_CL_IDX' ) clientevent update all DEFILE_CL_IDX end_fx

	if ( DMGMULTI_ORIG isnot 'DMGMULTI_ORIG' )
	{
		dmgmulti DMGMULTI_ORIG
	}
}

{ game_death
	callevent effect_die
}

{ game_duplicated
	callevent effect_die
}

{
repeatdelay DOT_FREQ
	if BE_EFFECT_ACTIVE
	callevent dot_effect
}