//Thothie - minor syntax error generated, fixed
//- Added removescript to stop flames from oversticking
//Burn
//Any npc
//PARAM1 == Duration
//PARAM2 == Damge Source (entity)
//PARAM3 == Damage per second
//PARAM4 == [0: Glow][1: No glow]
//PARAM5 == [0: Light][1: No Light]
//PARAM6 == Skill to pass XP to

{ 	[server]
	const EFFECT_ID			DOT_fire
	const EFFECT_NAME		"Burn"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE

	const DOT_EFFECT 1
	const DOT_TYPE fire
	const DOT_DMG local.effect.damage
	const DOT_FREQ $randf(0.9,1.1)
	const DOT_ATTACKER local.effect.dmgsource
	const DOT_SKILL ACTIVE_SKILL

	const DOT_NO_SECONDARY 1
}

#include effects/effect_base	allowduplicate

{ 	[server] game_activate

	if $get(ent_me,isalive)

	//=== Check Immunities
	callevent resist_check fire $get(PARAM2,id)
	if RESISTED_EFFECT < 2
	//=== End Check Immunities

	playermessage $get(ent_me,id) You are on fire! BE_RESIST_STRING

	setvard local.effect.duration 	PARAM1
	setvard local.effect.dmgsource 	PARAM2
	setvard local.effect.damage 	PARAM3

	local FX_LIGHT_ON 1
	if ( PARAM5 equals 1 ) local FX_LIGHT_ON 0

	//if ( IAM_IMMUNE > 0 ) multiply local.effect.damage IAM_IMMUNE //WTF is this?

	clientevent new all effects/sfx_flames $get(ent_me,index) local.effect.duration $get(ent_me,height) FX_LIGHT_ON
	setvard CL_EFFECT_FLAME_IDX game.script.last_sent_id

	if( !PARAM4 ) 
	{
		if !$get(ent_me,scriptvar,'IS_LURE')
		effect glow ent_me (255,75,0) 72 local.effect.duration local.effect.duration
	}

	callevent local.effect.duration effect_die
	subtract local.effect.duration 1

	setvard ACTIVE_SKILL spellcasting.fire
	if ( PARAM6 isnot 'PARAM6' ) setvard ACTIVE_SKILL PARAM6

	
}

{ 	[shared] effect_die
	dbg effect_burn effect_die PARAM1 RESISTED_EFFECT
	if ( CL_EFFECT_FLAME_IDX isnot 'CL_EFFECT_FLAME_IDX' ) clientevent update all CL_EFFECT_FLAME_IDX end_fx
	if RESISTED_EFFECT < 2
}

{ game_death

	setvard local.effect.damage  0
	callevent effect_die
}

{
repeatdelay DOT_FREQ
	if BE_EFFECT_ACTIVE
	callevent dot_effect
}