//. applyeffect effects/hold_person 5 

{
	setvard ROCK_MANIFEST_RATE 0.03
	setvard ROCK_SCALE_MIN 8.0
	setvard ROCK_SCALE_MAX 10.0
	setvard ROCK_CLIMB_STEP 1.5
}

#scope client
{ client_activate //<target> <duration> <width> <height>

	setvard COIL_POS $getcl(PARAM1,origin)
	if ( $getcl(PARAM1,isplayer) )
	{
		setvard IS_PLAYER 1
		vectoradd COIL_POS z -32
		local PARAM3 32 //players don't report width proper!
		local PARAM4 $math(multiply,PARAM4,1.25) //also eye pos is a bit high, so...
	}
	setvard COIL_DURATION PARAM2
	setvard ROT_COUNT 0

	setvard COIL_WIDTH $math(multiply,PARAM3,0.75)
	setvard COIL_WIDTH_MIN $math(multiply,PARAM3,0.4) //prob 0.65
	setvard COIL_WIDTH_MAX COIL_WIDTH
	setvard COIL_HEIGHT PARAM4
	setvard COIL_Z_START $vec.z(COIL_POS)
	setvard COIL_Z_MAX COIL_Z_START
	add COIL_Z_MAX COIL_HEIGHT
	dbg *** $currentscript client_activate COIL_POS w COIL_WIDTH h COIL_HEIGHT zm COIL_Z_MAX

	if ( COIL_HEIGHT > 96 )
	{
		if !IS_PLAYER
		if ( COIL_HEIGHT > 200 ) setvard COIL_HEIGHT 200 //don't go bigger than this (too many rocks)
		local L_MAX_HEIGHT_RATIO $math(divide,COIL_HEIGHT,200)
		add ROCK_CLIMB_STEP $ratio(L_MAX_HEIGHT_RATIO,0.2,2.0)
		add ROCK_SCALE_MIN $ratio(L_MAX_HEIGHT_RATIO,1.0,2.0)
		add ROCK_SCALE_MAX $ratio(L_MAX_HEIGHT_RATIO,1.0,2.0)
	}

	setvard COIL_START_TIME game.time

	setvard FX_ACTIVE 1			
	setvard ROCK_COUNT 0
	callevent draw_coil_loop
	callevent COIL_DURATION end_fx
}

{ draw_coil_loop
	//dbg **** Draw Coil ROT_COUNT COIL_HEIGHT

	//cleffect beam_points <startpos> <endpos> <spritename> <life> <width> <amplitude> <brightness> <speed> <framerate> <color>
	if FX_ACTIVE
	local L_COIL_CUR_Z $vec.z(COIL_POS)
	if L_COIL_CUR_Z < COIL_Z_MAX
	add ROCK_COUNT 1
	callevent ROCK_MANIFEST_RATE draw_coil_loop

	local L_COMP_RATIO_MAX $math(subtract,COIL_Z_MAX,COIL_Z_START)
	local L_COMP_RATIO_CUR $math(subtract,COIL_Z_MAX,L_COIL_CUR_Z)
	local L_COMP_RATIO $math(divide,L_COMP_RATIO_CUR,L_COMP_RATIO_MAX)
	setvard COIL_WIDTH $ratio(L_COMP_RATIO,COIL_WIDTH_MIN,COIL_WIDTH_MAX)


	if ( game.time > NEXT_SOUND )
	{
		setvard NEXT_SOUND game.time
		add NEXT_SOUND 0.25
		sound.play3d debris/concrete1.wav 10 COIL_POS 0.8 0 $rand(30,80)
	}

	local BEAM_START COIL_POS
	vectoradd BEAM_START $relpos($vec(0,ROT_COUNT,0),$vec(0,COIL_WIDTH,0))
	//add COIL_HEIGHT 1
	vectoradd COIL_POS z ROCK_CLIMB_STEP
	add ROT_COUNT 20
	if ( ROT_COUNT > 359 ) subtract ROT_COUNT 359
	local BEAM_END COIL_POS
	vectoradd BEAM_END $relpos($vec(0,ROT_COUNT,0),$vec(0,COIL_WIDTH,0))
	//cleffect beam_points BEAM_START BEAM_END lgtning.spr COIL_DURATION 2 0.1 0.9 0.1 30 (255,255,255) //(1,0.5,2)
	add ROCK_IDX 1
	cleffect tempent model rockgibs.mdl BEAM_START setup_rock update_rock
}

{ end_fx
	setvard FX_ACTIVE 0
	callevent 5.0 remove_fx
}

{ remove_fx
	removescript
}

{ setup_rock
	cleffect tempent set_current_prop death_delay $math(add,COIL_DURATION,4)
	cleffect tempent set_current_prop scale $randf(ROCK_SCALE_MIN,ROCK_SCALE_MAX)
	cleffect tempent set_current_prop gravity 	0
	cleffect tempent set_current_prop collide none
	cleffect tempent set_current_prop framerate 1.0
	cleffect tempent set_current_prop frames 50
	cleffect tempent set_current_prop body $rand(1,2)
	cleffect tempent set_current_prop sequence 0
	local RND_YAW $randf(0,359.99)
	cleffect tempent set_current_prop angles $vec(0,RND_YAW,0)
	cleffect tempent set_current_prop rendermode texture
	cleffect tempent set_current_prop renderamt 255
	cleffect tempent set_current_prop iuser1 0

	local L_DECAY_START $math(add,COIL_START_TIME,COIL_DURATION)
	subtract L_DECAY_START $math(multiply,ROCK_IDX,ROCK_MANIFEST_RATE)

	cleffect tempent set_current_prop fuser1 L_DECAY_START
}

{ update_rock
	if game.time > game.tempent.fuser1
	
	if ( game.time > NEXT_SOUND )
	{
		if game.tempent.iuser1 == 0
		cleffect tempent set_current_prop iuser1 1
		setvard NEXT_SOUND game.time
		add NEXT_SOUND 0.25
		sound.play3d debris/concrete3.wav 10 game.tempent.origin 0.8 0 $rand(30,80)
		//dbg update_rock fall_start
	}

	cleffect tempent set_current_prop collide world;die

	local CUR_VEL game.tempent.velocity
	vectoradd CUR_VEL z -50
	cleffect tempent set_current_prop velocity CUR_VEL
}

//. applyeffect effects/hold_person_stone 10.0 GM 1 none 99999