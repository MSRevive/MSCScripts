#scope client
{
	const SPRITE_1 quests/manaring/vision1.spr
	const SPRITE_2 quests/manaring/vision2.spr
	const SPRITE_3 quests/manaring/vision3.spr
	
	setvar CUR_PORTAL -1
}

{ game_precache

	precache quests/manaring/vision1.spr
	precache quests/manaring/vision2.spr
	precache quests/manaring/vision3.spr
}

{ client_activate //<position> <npc_spawned_me:0|1>

	setvard PORTAL_POS PARAM1
	setvard NPC_SWAPS_PORTAL PARAM2
	
	setvard SND_CHANNEL $rand(5,255)

	sound.play3d magic/elecidlepop.wav 10 PORTAL_POS
	sound.play3d magic/chant_loop.wav 10 PORTAL_POS 0.8 SND_CHANNEL
	
	callevent spawn_a_portal
}

{ spawn_a_portal

	add CUR_PORTAL 1
	if ( CUR_PORTAL == 0 ) cleffect tempent sprite SPRITE_1 PORTAL_POS setup_portal_sprite update_portal_sprite
	if ( CUR_PORTAL == 1 ) cleffect tempent sprite SPRITE_2 PORTAL_POS setup_portal_sprite update_portal_sprite
	if ( CUR_PORTAL == 2 ) cleffect tempent sprite SPRITE_3 PORTAL_POS setup_portal_sprite update_portal_sprite
	//if ( CUR_PORTAL == 3 ) cleffect tempent sprite SPRITE_3 PORTAL_POS setup_portal_sprite
	if ( CUR_PORTAL > 3 ) callevent kill_script
	else if ( !NPC_SWAPS_PORTAL ) callevent 5 spawn_a_portal
}

{ setup_portal_sprite

	cleffect tempent set_current_prop death_delay 30
	cleffect tempent set_current_prop rendermode add
	cleffect tempent set_current_prop framerate 10
	cleffect tempent set_current_prop frames 30
	cleffect tempent set_current_prop bouncefactor 0
	cleffect tempent set_current_prop scale 0.2
	cleffect tempent set_current_prop gravity 0
	cleffect tempent set_current_prop collide none
	
	cleffect tempent set_current_prop iuser1 CUR_PORTAL
}

{ update_portal_sprite

	if ( game.tempent.iuser1 != CUR_PORTAL ) cleffect tempent set_current_prop origin $vec(10000,10000,10000)
}

{ kill_script

	sound.play3d null.wav 10 PORTAL_POS 0.8 SND_CHANNEL
	removescript
}