//Thothie - minor syntax problems generating errors, fixed
//POISON
//Any npc
//PARAM1 == Duration
//PARAM2 == Damge Source (entity)
//PARAM3 == Damage per second
//PARAM4 == Skillz

{ 	[server]
	const EFFECT_ID			DOT_gpoison
	const EFFECT_NAME		"greater poison"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE

	const GREATER_POISON 1

	const DOT_EFFECT 1
	const DOT_TYPE poison
	const DOT_DMG local.effect.damage
	const DOT_FREQ $randf(0.9,1.1)
	const DOT_ATTACKER local.effect.dmgsource
	const DOT_SKILL ACTIVE_SKILL

	const DOT_MESSAGE "You have been poisoned!"
	const DOT_RESIST_MESSAGE "You resist the poison's secondary effects."
}

#include effects/base_dot	allowduplicate
#include effects/effect_drunk

{ 	[server] game_activate

	setvard local.effect.duration 	PARAM1
	setvard local.effect.dmgsource 	PARAM2
	setvard local.effect.damage 	PARAM3

	//=== Check Immunities
	callevent resist_check poison $get(PARAM2,id)
	if RESISTED_EFFECT < 2
	//=== End Check Immunities

	local DUR_RATIO $get(PARAM2,scriptvar,'PLR_CORRODE_DURATION')
	if ( DUR_RATIO != 0 )
	{
		multiply local.effect.duration DUR_RATIO
	}

	if ( RESISTED_EFFECT < 1 )
	{
		if ( $get(ent_me,isplayer) ) clientevent new ent_me $currentscript local.effect.duration
		setvard local.effect.clientscript game.script.last_sent_id

		if ( !$get(ent_me,isplayer) )
		{
			setvard EFFECT_DMG_REDUCED 1
			setvard ORG_DMG_MULTI $get(ent_me,dmgmulti) 
			if ( ORG_DMG_MULTI == 0 ) setvard ORG_DMG_MULTI 1 //dun think this should happen, but just in case
			local L_NEW_DMG ORG_DMG_MULTI
			multiply L_NEW_DMG 0.5
			callexternal ent_me ext_dmgmulti L_NEW_DMG 
		}
	}

	if ( !$get(ent_me,scriptvar,IS_LURE) ) effect glow ent_me (75,215,0) 72 local.effect.duration local.effect.duration

	if( $get(ent_me,isplayer) ) svplaysound game.sound.body game.sound.maxvol $get(ent_me,scriptvar,'PLR_SOUND_STOMACHHIT1')

//	if ( RESISTED_EFFECT != 1 )
//	{
//		playermessage $get(ent_me,id) DOT_MESSAGE BE_RESIST_STRING
//	}
//	else
//	{
//		playermessage $get(ent_me,id) You resist the worst of the poison. BE_RESIST_STRING
//	}

	callevent local.effect.duration effect_die

	if ( PARAM4 isnot 'PARAM4' ) setvard ACTIVE_SKILL PARAM4
}

{ 	[shared] effect_die

	if ( $get(ent_me,isplayer) == 1 )
	{
		if !RESISTED
		playermessage $get(ent_me,id) The poison subsides.
	}

	if EFFECT_DMG_REDUCED
	setvard EFFECT_DMG_REDUCED 0
	callexternal ent_me ext_dmgmulti ORG_DMG_MULTI
}


// ===========
// Client Side
// ===========

{	[client]
	const POISON_SCREEN_DELAY		0.2
	const POISON_SCREEN_ALPHA		30
}

{	[client] client_activate

	callevent PARAM1 effect_die
}

{	[client]
	repeatdelay POISON_SCREEN_DELAY


	setvard game.cleffect.screenfade.newfade 1
	setvard game.cleffect.screenfade.alphalimit POISON_SCREEN_ALPHA	//Only use this fade if current fade is under this limit
	setvard game.cleffect.screenfade.color (75,215,0)
	setvard game.cleffect.screenfade.alpha POISON_SCREEN_ALPHA
	setvard game.cleffect.screenfade.duration POISON_SCREEN_DELAY
	setvard game.cleffect.screenfade.blendduration POISON_SCREEN_DELAY
	setvard game.cleffect.screenfade.type fadein
}

{
repeatdelay DOT_FREQ
	if BE_EFFECT_ACTIVE
	callevent dot_effect
}