//Freeze solid by Thothie

//Any npc
//Param1 - Freeze Duration

// . effectmestack effects/dot_cold_freeze 10

{
	const EFFECT_ID "debuff_frozen" //Also used as the name of the scriptflag
	const EFFECT_FLAGS nostack
	const EFFECT_SCRIPT $currentscript //Must be copied, as is, at the top script!

	const SOUND_FREEZE magic/freeze.wav
	
	const POOL_CAP 21 //Max number of seconds available in pool
	const POOL_REGEN 3 //Regen 1 second in the pool per number of POOL_REGEN seconds
}

#include effects/base_debuff_diminishing

{ debuff_start //<duration> <attacker> <dmg> <skill> [MAX_HP]
	
	playsound 0 10 SOUND_FREEZE

	setvard game.effect.movespeed 0
	setvard game.effect.canduck 0
	setvard game.effect.canjump 0
	setvard game.effect.canattack 0
	setvard game.effect.anim.framerate 0.01

	clientevent new all effects/sfx_icecage $get(ent_me,index) EFFECT_DURATION
	setvard CAGE_SCRIPT_IDX game.script.last_sent_id
	
	callexternal ent_me freeze_solid_start EFFECT_DURATION
	
	hud.addstatusicon ent_me hud/status/alpha_debuff_frozen EFFECT_ID EFFECT_DURATION
}

{ effect_die

	if DEBUFF_STARTED
	clientevent update all CAGE_SCRIPT_IDX end_cage_fx
	callexternal ent_me freeze_solid_end
}
