//Rejuvenate2 by Thothie

//Any npc
//PARAM1 == duration (useless)
//PARAM2 == hp to heal
//PARAM3 == Caster

//- The original rejuvinate method lagged the systme to hell and back
//- in addition, it became completely unbalanced once the ability to raise Divination skill was restored
//- finally the formula was too hard to predict
//- Thus we have this simple, one-shot method

// . effectmestack effects/effect_rejuv2 0 100 GAME_MASTER

#scope server

{
	const EFFECT_ID "effect_rejuvenate"
	const EFFECT_SCRIPT $currentscript //Must be copied, as is, at the top script!
}

#include effects/base_effect

{ game_activate

	local HEAL_AMT PARAM2
	local CASTER_ID PARAM3

	local MY_ID $get(ent_me,id)
	
	local MY_MAX_HEALTH $get(MY_ID,maxhp)
	local MY_CUR_HEALTH $get(MY_ID,hp)

	if ( MY_ID isnot CASTER_ID ) local HEALING_OTHER 1

	if ( MY_CUR_HEALTH < MY_MAX_HEALTH )
	{
		givehp HEAL_AMT
		effect glow ent_me (0,255,0) 256 1 1
		
		if ( HEALING_OTHER )
		{
			gplayermessage CASTER_ID You heal $get(MY_ID,name) for HEAL_AMT hp
			gplayermessage MY_ID $get(CASTER_ID,name) heals you for HEAL_AMT hp
		}
		else
		{
			gplayermessage CASTER_ID You heal yourself for HEAL_AMT hp
		}
	}
	else
	{
		if ( HEALING_OTHER )
		{
			gplayermessage CASTER_ID $get(MY_ID,name) is at maximum health
		}
		else
		{
			gplayermessage MY_ID You are at maximum health
		}
	}

	removescript
}
