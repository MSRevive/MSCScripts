//radial burst outwards
#scope client
{
	//so we can adjust sprite by include
	const SPRITE_NAME fire1_fixed.spr
	const SPRITE_COLOR (64,64,255)
	const SPRITE_RENDERAMT 200
	const SPRITE_RENDERMODE add
	const SPRITE_FRAMERATE 30
	const SPRITE_NFRAMES 23	
	const SPRITE_SCALE 1.5
	const SPRITE_SPEED 400

	const SOUND_BURST magic/frost_reverse.wav
	precache SOUND_BURST
}

{ client_activate //<origin> <yaw> <cone_width[degrees]>
	setvard FX_ORIGIN PARAM1
	setvard FX_YAW PARAM2
	setvard FX_WIDTH PARAM3
	setvard FX_ANGLE1 FX_YAW
	subtract FX_ANGLE1 FX_WIDTH
	if ( FX_ANGLE1 < 0 ) add FX_ANGLE1 359.99

	setvard FX_ANGLE2 FX_YAW
	add FX_ANGLE2 FX_WIDTH
	if ( FX_ANGLE2 > 359.99 ) subtract FX_ANGLE2 359.99

	setvard ANG_COUNT 0
	setvard CUR_YAW 0

	setvard ANG_INC FX_WIDTH
	multiply ANG_INC 2
	divide ANG_INC 20

	sound.play3d SOUND_BURST 10 FX_ORIGIN

	calleventloop 20 make_sprites

	callevent 4.0 remove_fx
}

{ make_sprites
	setvard CUR_YAW FX_ANGLE1
	add CUR_YAW ANG_COUNT

	local SPR_POS FX_ORIGIN
	vectoradd SPR_POS $vec($vec(0,CUR_YAW,0),$vec(0,10,0))
	cleffect tempent sprite SPRITE_NAME SPR_POS setup_ring_sprite

	add ANG_COUNT ANG_INC
}

{ remove_fx
	removescript
}

{ setup_ring_sprite
	cleffect tempent set_current_prop death_delay 	2.0
	cleffect tempent set_current_prop rendercolor SPRITE_COLOR
	cleffect tempent set_current_prop renderamt	SPRITE_RENDERAMT
	cleffect tempent set_current_prop rendermode SPRITE_RENDERMODE
	cleffect tempent set_current_prop framerate	SPRITE_FRAMERATE
	cleffect tempent set_current_prop frames	SPRITE_NFRAMES
	cleffect tempent set_current_prop scale 	SPRITE_SCALE
	cleffect tempent set_current_prop gravity 	0
	local FLAME_TURN $relvel($vec(0,CUR_YAW,0),$vec(0,SPRITE_SPEED,0))
	cleffect tempent set_current_prop velocity FLAME_TURN
	cleffect tempent set_current_prop collide 	world;die
	cleffect tempent set_current_prop fadeout lifetime
}