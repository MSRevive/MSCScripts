//Thothie SEP2009_15
//Ring of flame sprites that starts on the ground and rolls outward
#scope client
{
	//so we can adjust sprite by include
	const SPRITE_NAME fire1_fixed.spr
	const SPRITE_COLOR (255,255,255)
	const SPRITE_RENDERAMT 200
	const SPRITE_RENDERMODE add
	const SPRITE_FRAMERATE 30
	const SPRITE_NFRAMES 23	
	const SPRITE_SCALE 1.0

	const SOUND_BURST ambience/steamburst1.wav

	precache SOUND_BURST
}

{ [client] client_activate //<center_point> <radius> <light:0|1> <light_color>

	setvard FX_CENTER PARAM1
	setvard FX_RADIUS PARAM2
	local DO_GLOW PARAM3
	local GLOW_COLOR PARAM4

	if ( DO_GLOW ) cleffect light new FX_CENTER FX_RADIUS GLOW_COLOR 1.0

	callevent 3.0 remove_me
	setvard CYCLE_ANGLE 0
	calleventloop 17 create_sprites

	sound.play3d SOUND_BURST 10 FX_CENTER
}

{ remove_me
	removescript
}

{ create_sprites
	local SPR_POS FX_CENTER
	vectoradd SPR_POS $relpos($vec(0,CYCLE_ANGLE,0),$vec(0,10,0))
	cleffect tempent sprite SPRITE_NAME SPR_POS setup_ring_sprite
	add CYCLE_ANGLE 20
}

{ setup_ring_sprite
	local FADE_DEL 1.0
	local SPRITE_SPEED 100

	if ( FX_RADIUS > 128 )
	{
		local FADE_DEL 2.0
		local SPRITE_SPEED 400
	}

	cleffect tempent set_current_prop death_delay 	FADE_DEL
	cleffect tempent set_current_prop rendercolor SPRITE_COLOR
	cleffect tempent set_current_prop renderamt	SPRITE_RENDERAMT
	cleffect tempent set_current_prop rendermode SPRITE_RENDERMODE
	cleffect tempent set_current_prop framerate	SPRITE_FRAMERATE
	cleffect tempent set_current_prop frames	SPRITE_NFRAMES
	cleffect tempent set_current_prop scale 	SPRITE_SCALE
	cleffect tempent set_current_prop gravity 	0
	local FLAME_TURN $relvel($vec(0,CYCLE_ANGLE,0),$vec(0,SPRITE_SPEED,0))
	cleffect tempent set_current_prop velocity FLAME_TURN
	cleffect tempent set_current_prop collide 	none
	cleffect tempent set_current_prop fadeout lifetime
}