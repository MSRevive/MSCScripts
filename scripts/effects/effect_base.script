{ [server]
	const EFFECT_REMOVE_ON_DEATH 1
}

{ [server] game_precache

	local reg.effect.name	EFFECT_ID
	local reg.effect.flags	EFFECT_ACTIONTYPE
	local reg.effect.script	EFFECT_SCRIPT

	setvard game.effect.removeondeath EFFECT_REMOVE_ON_DEATH

	registereffect

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.flags	EFFECT_ACTIONTYPE
}

{ [server] game_activate

	//dbg effect:game_activate EFFECT_ID dot_settings : DOT_EFFECT DOT_TYPE DOT_DMG DOT_FREQ DOT_ATTACKER ACTIVE_SKILL

	//in case I died in the same hit that applied the effect
	if ( !$get(ent_me,isalive) ) local L_ABORT 1
	//if ( $get(ent_me,hp) <= 0 )  local L_ABORT 1 //this stops emote_sit&stand from being applied!
	if ( L_ABORT )
	{
		setvard RESISTED_EFFECT 2
		callevent effect_die
	}
	else
	{
		if ( DOT_EFFECT ) callevent 0.1 dot_start
	}
}

{ [shared] effect_die

	setvard BE_EFFECT_ACTIVE 0

	if ( local.effect.clientscript != 0 )
	{
		clientevent remove ent_me local.effect.clientscript
		//dbg effect_die_script_id: removecleffect# local.effect.clientscript
	}
	if ( effect.clientscript != 0 )
	{
		clientevent remove all effect.clientscript
		//dbg effect_die_script_id: removecleffect# remove all effect.clientscript
	}

//	if ( DOT_EFFECT ) 
//	{
//		callexternal ext_remove_effect EFFECT_ID
//	}
	removescript
}

{ [server] resist_check //PARAM1 = element/stun, PARAM2 = afflicter, returns RESISTED_EFFECT 0|1

	if ( PARAM1 equals 'PARAM1' ) setvard PARAM1 DOT_TYPE

	if !BE_DID_RESIST_CHECK
	setvard BE_DID_RESIST_CHECK 1

	setvard RESISTED_EFFECT 0

	if ( !$can_damage(ent_me,PARAM2) )
	{
		if $get(PARAM2,isplayer)
		setvard RESISTED_EFFECT 2
		callevent be_resist_effect $lcase(PARAM1) "immune_player"
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	//FEB2010_04 - If immune to player, do not apply affects from said
	if ( $get(ent_me,scriptvar,'NPC_NO_PLAYER_DMG') )
	{
		if $get(PARAM2,isplayer)
		setvard RESISTED_EFFECT 2
		callevent be_resist_effect $lcase(PARAM1) "immune_player"
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	//dbg resist_check entered_sub

	//if player resisted hit this effect within the last 10 seconds let the resistance carry over
	local LAST_RESIST $get(ent_me,scriptvar,'PLR_LAST_RESIST_TIME')
	local LAST_RESIST_TYPE $get(ent_me,scriptvar,'PLR_LAST_RESIST_ELM')
	add LAST_RESIST 10.0 //should be 10.0
	//dbg resist_check got LAST_RESIST & LAST_RESIST_TYPE vs game.time & PARAM1 
	if ( game.time < LAST_RESIST )
	{
		if PARAM1 equals LAST_RESIST_TYPE
		setvard RESISTED_EFFECT 2
		callevent be_resist_effect "carry_over" $lcase(PARAM1)
		local EXIT_SUB 1
		//dbg resist_check carry_over
	}
	if !EXIT_SUB

	if ( PARAM1 equals holy )
	{
		local HE_RESISTS_STRING "is not harmed by holy magic."
		local YOU_RESIST_STRING "The holy magic leaves you unharmed."
		if ( DOT_RESIST_MESSAGE isnot 'DOT_RESIST_MESSAGE' ) local YOU_RESIST_STRING DOT_RESIST_MESSAGE
		local IMMUNE_RATIO $get_takedmg(ent_me,holy)
		if ( IMMUNE_RATIO == 0 ) local IMMUNE_RATIO 1 //meh, screwy hold over
	}

	if ( PARAM1 equals cold )
	{
		local HE_RESISTS_STRING "is immune to cold attacks!"
		local YOU_RESIST_STRING "You resist being frozen."
		if ( DOT_RESIST_MESSAGE isnot 'DOT_RESIST_MESSAGE' ) local YOU_RESIST_STRING DOT_RESIST_MESSAGE
		local IMMUNE_RATIO $get(ent_me,scriptvar,'IMMUNE_COLD')
	}

	if ( PARAM1 equals lightning )
	{
		local HE_RESISTS_STRING "is immune to electrical attacks!"
		local YOU_RESIST_STRING "You resist the lightning magic's deleterious effects."
		if ( DOT_RESIST_MESSAGE isnot 'DOT_RESIST_MESSAGE' ) local YOU_RESIST_STRING DOT_RESIST_MESSAGE
		local IMMUNE_RATIO $get(ent_me,scriptvar,'IMMUNE_LIGHTNING')
	}

	if ( PARAM1 equals fire )
	{
		local HE_RESISTS_STRING "is immune to fire!"
		local YOU_RESIST_STRING "You resist the fire magic."
		if ( DOT_RESIST_MESSAGE isnot 'DOT_RESIST_MESSAGE' ) local YOU_RESIST_STRING DOT_RESIST_MESSAGE
		local IMMUNE_RATIO $get(ent_me,scriptvar,'IMMUNE_FIRE')
	}

	if ( PARAM1 equals poison )
	{
		local HE_RESISTS_STRING "is immune to poison!"
		local YOU_RESIST_STRING "You resist the poison."
		if ( DOT_RESIST_MESSAGE isnot 'DOT_RESIST_MESSAGE' ) local YOU_RESIST_STRING DOT_RESIST_MESSAGE
		local IMMUNE_RATIO $get(ent_me,scriptvar,'IMMUNE_POISON')
	}

	if ( PARAM1 equals acid )
	{
		local HE_RESISTS_STRING "is immune to acid!"
		local YOU_RESIST_STRING "You resist the acid attack."
		if ( DOT_RESIST_MESSAGE isnot 'DOT_RESIST_MESSAGE' ) local YOU_RESIST_STRING DOT_RESIST_MESSAGE
		local IMMUNE_RATIO $get(ent_me,scriptvar,'IMMUNE_ACID')
	}

	if ( !$get(ent_me,isplayer) )
	{
		if ( $lcase(game.monster.name) contains maldora )
		{
			local HE_RESISTS_STRING "is immune to magic!"
			setvard RESISTED_EFFECT 2
		}
	}
	local MY_NAME $get(ent_me,name)

	if ( PARAM1 equals stun )
	{
		local EXIT_SUB 1 //works different than other immunes, so abort afterwards

		if ( $get(ent_me,isplayer) )
		{
			if EFFECT_BLOCK_RESISTS
			local CUR_WEAPON $get(ent_me,scriptvar,'PLR_ACTIVE_WEAPON')
			if ( $get(CUR_WEAPON,scriptvar,'POLE_IN_BLOCK') == 1 ) local BLOCKED_ATTACK 1
			if ( $get(CUR_WEAPON,scriptvar,'PARRY_ON') == 1 ) local BLOCKED_ATTACK 1
			if ( BLOCKED_ATTACK )
			{
				callevent be_resist_effect "blocked"
				playermessage ent_me $get(CUR_WEAPON,name) blocked stun impact.
			}
		}
		if !RESISTED_EFFECT

		local HE_RESISTS_STRING "resists the stun attack"
		local HE_IMMUNE_STRING "is immune to stun attacks!"
		local YOU_RESIST_STRING "You resist being stunned!"
		local IMMUNE_RATIO $get(ent_me,scriptvar,'IMMUNE_STUN')

		local RESISTANCE $get(ent_me,scriptvar,'IMMUNE_STUN')
		if ( $get(ent_me,nopush) ) local RESISTANCE 100 //nopush mons/plrs are immune to stun as well

		local STUN_ROLL $rand(1,100) 

		if ( RESISTANCE isnot 'IMMUNE_STUN' )
		{
			//is immune, flag
			if ( RESISTANCE == 100 )
			{
				local RESISTED_STUN 1
				if ( !$get(ent_me,scriptvar,'IS_SOCCER_BALL') ) playermessage PARAM2 MY_NAME HE_IMMUNE_STRING
				setvard RESISTED_EFFECT 1
			}
			if !RESISTED_STUN

			//has resistance, roll
			if STUN_ROLL <= RESISTANCE
			//saytext HA HA! I RESIST! $get(PARAM2,name)
			playsound 0 10 SOUND_RESIST
			setvard BE_RESIST_STRING "( "
			strconc BE_RESIST_STRING STUN_ROLL /  $int(RESISTANCE) )
			setvard RESISTED_EFFECT 1
			gplayermessage ent_me YOU_RESIST_STRING BE_RESIST_STRING
			dplayermessage PARAM2 MY_NAME HE_RESISTS_STRING BE_RESIST_STRING
			local RESISTED_STUN 1
		}
		else
		{
			local RESISTANCE 0
		}
		//resisted
		if ( RESISTED_STUN ) callevent be_resist_effect "stun_resist_roll" stun
		if !RESISTED_STUN
		playermessage ent_me You have been stunned! ( STUN_ROLL / RESISTANCE )
	}
	if !EXIT_SUB

	//dbg resist_check IMMUNE_RATIO

	if ( IMMUNE_RATIO == 1 )
	{
		if ( $get(PARAM2,isplayer) ) playermessage PARAM2 MY_NAME HE_RESISTS_STRING
		if ( $get(ent_me,isplayer) ) playermessage ent_me YOU_RESIST_STRING
		//callevent 0.1 effect_die //handled by be_resist_effect
		setvard RESISTED_EFFECT 2
		callevent be_resist_effect "immuned" $lcase(PARAM1)
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( IMMUNE_RATIO < 1 )
	{
		if IMMUNE_RATIO > 0
		local IN_IMMUNE IMMUNE_RATIO
		multiply IN_IMMUNE 100
		local RESISTANCE 100
		subtract RESISTANCE IN_IMMUNE
		local ROLL $rand(1,100)
		setvard BE_RESIST_STRING "( "
		strconc BE_RESIST_STRING ROLL /  $int(RESISTANCE) )

		if ( ROLL <= RESISTANCE )
		{
			setvard RESISTED_EFFECT 1
			setvard RESISTED 1
			if ( !DOT_NO_SECONDARY ) gplayermessage ent_me YOU_RESIST_STRING BE_RESIST_STRING
			dplayermessage $get(PARAM2,id) MY_NAME resists the PARAM1 magic. ( $int(RESISTANCE) )
			callevent be_resist_effect "resist_roll" $lcase(PARAM1)
		}
	}
}

{ [server] dot_start

	if !DOT_ABORT

	if ( ACTIVE_SKILL equals 'ACTIVE_SKILL' )
	{
		local ACTIVE_WEAPON $get(DOT_ATTACKER,active_item)
		setvard ACTIVE_SKILL $get(ACTIVE_WEAPON,scriptvar,'WEAPON_PRIMARY_SKILL')
	}
	if ( ACTIVE_SKILL equals 'ACTIVE_SKILL' ) setvard ACTIVE_SKILL none

	setvard BE_EFFECT_ACTIVE 1

	//dbg effect:dot_start [ EFFECT_ID ] ACTIVE_SKILL BE_EFFECT_ACTIVE EFFECT_DUPLICATED

	//local NO_STACK 1
	//callevent dot_effect
}

{ game_duplicated

	add EFFECT_DUPLICATED 1

	callevent effect_die
}

{ [server] dot_effect //requires DOT_TYPE DOT_DMG DOT_FREQ DOT_ATTACKER ACTIVE_SKILL

	if game.serverside

	//dbg dot_effect pulse RESISTED_EFFECT
	if BE_EFFECT_ACTIVE //only if effect active
	
	local LAST_DMG $get(ent_me,scriptvar,'EXT_LAST_EFFECT_DMG') //find last time sent damage
	local TIME_DIFF game.time
	subtract TIME_DIFF LAST_DMG //make sure all effect damage is at least 0.3 secs appart (lag/crash prevention)
	if TIME_DIFF > 0.3
	callexternal ent_me ext_set_effect_lastdmg //set last effect time on target

	local L_DMG_TYPE DOT_TYPE
	stradd L_DMG_TYPE _effect //_effect damage can't be parried, but dmg should still be resisted as damage resist checks in the code just use msstring.contains

	//dbg effect:dot_effect xdodamage $get(ent_me,name) direct DOT_DMG 100 $get(DOT_ATTACKER,name) [ DOT_ATTACKER ] ACTIVE_SKILL L_DMG_TYPE
	local L_DOT_DMG DOT_DMG

	if ( !$get(DOT_ATTACKER,isplayer) ) multiply L_DOT_DMG 0.85

	xdodamage $get(ent_me,id) direct L_DOT_DMG 100% DOT_ATTACKER DOT_ATTACKER ACTIVE_SKILL L_DMG_TYPE
}

{ be_resist_effect //<how_resisted> <element>

	//dbg be_resist_effect PARAM1 PARAM2

	dbg be_resist_effect

	if ( RESISTED_EFFECT < 1 ) setvard RESISTED_EFFECT 1

	if ( PARAM1 isnot 'carry_over' ) callexternal $get(ent_me,id) ext_set_last_resist $lcase(PARAM2)

	//for effects that do no DOT, and only have secondary effects (such as effect_frostbite)
	if ( EFFECT_RESIST_NEGATES )
	{
		if RESISTED_EFFECT == 1
		setvard RESISTED_EFFECT 2
	}

	if ( PARAM2 equals stun )
	{
		if RESISTED_EFFECT
		setvard local.effect.duration 0
		callevent 0.1 effect_die
	}

	if RESISTED_EFFECT == 2
	setvard local.effect.damage 0
	setvard local.effect.duration 0
	setvard DOT_ABORT 1
	callevent 0.1 effect_die
}