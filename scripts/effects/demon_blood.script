#scope server

{
	const EFFECT_ID demon_blood
	const EFFECT_NAME "Demon Blood"
	const EFFECT_ACTIONTYPE nostack
	const EFFECT_SCRIPT $currentscript

	setvard game.effect.id EFFECT_ID
	setvard game.effect.displayname EFFECT_NAME
	setvard game.effect.type EFFECT_ACTIONTYPE
}

#include effects/effect_base

{ game_activate //<duration> <intensity>

	setvard FX_DURATION PARAM1
	setvard FX_INTENSITY PARAM2 //takes a negative number, is the amt of hp removed per tick

	callevent FX_DURATION end_demon_blood

	effect screenfade ent_me 0.5 3 (255,0,0) 255 fadeout
	gplayermessage ent_me "The demonic soul fills your heart with rage!"
	playsound 0 10 monsters/troll/trollidle2.wav

	setvard MAKE_NOISE 2
	setvard DEMON_BLOOD 1

	callevent 1.0 demon_blood_loop
}

{ demon_blood_loop

	if DEMON_BLOOD

	effect glow ent_me (255,0,0) 256 1.9 1.9

	callevent 2.0 demon_blood_loop

	add MAKE_NOISE 1	
	if ( MAKE_NOISE > 5 )
	{
		playrandomsound 0 10 monsters/troll/trollidle2.wav monsters/troll/trollidle.wav
		setvard MAKE_NOISE 0
	}

	givehp FX_INTENSITY
	effect screenfade ent_me 0.5 1 (255,0,0) 150 fadeout
	effect screenshake $relpos(0,0,0) 32 10 1 32
	if ( MAKE_NOISE > 0 ) playsound 0 10 player/heartbeat_noloop.wav

	if ( $get(ent_me,hp) <= 0 ) kill ent_me
}

{ game_damaged_other

	if ( DEMON_BLOOD )
	{
		if $get(PARAM1,isalive)
		if $get(PARAM1,relationship,ent_me) equals enemy

		local DEMON_BLOOD_DAMAGE $get(ent_me,skill.spellcasting)
		multiply DEMON_BLOOD_DAMAGE $math(multiply,2,$neg(FX_INTENSITY))

		if $rand(1,3) == 1

		applyeffect PARAM1 effects/generic_damage DEMON_BLOOD_DAMAGE $get(ent_me,id) 1 magic none
		playrandomsound 0 10 monsters/troll/trollpain.wav monsters/troll/trollattack.wav
	}
}

{ end_demon_blood
	if DEMON_BLOOD
	setvard DEMON_BLOOD 0
	gplayermessage ent_me "The demon blood fades."
	
	removescript
}
