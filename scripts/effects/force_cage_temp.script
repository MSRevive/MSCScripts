//same as force cage, but temporary
{ [server]
	const EFFECT_ID			effect_cage
	const EFFECT_NAME		"cage"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript
	const SOUND_FREEZE magic/bolt_start.wav

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE

	const DOT_EFFECT 1
	const DOT_TYPE lightning_effect
	const DOT_DMG EFFECT_DAMAGE
	const DOT_FREQ $randf(0.9,1.1)
	const DOT_ATTACKER MY_ATTACKER
	const DOT_SKILL ACTIVE_SKILL
}

#include effects/effect_base	allowduplicate

{ [server] game_activate //<owner> [dot]

	if !$get(ent_me,scriptvar,'IS_LURE') //do not freeze lures


	if ( $get(ent_me,width) > 48 )
	{
		dplayermessage PARAM1 Force Cage: $get(ent_me,name) too large for force cage
		callevent effect_die resisted
		local EXIT_SUB 1
	}
	if !EXIT_SUB


	playsound 0 10 SOUND_FREEZE

	setvard CAGE_OWNER PARAM1
	setvard MY_ATTACKER CAGE_OWNER
	if ( !$get(CAGE_OWNER,isplayer) ) setvard ACTIVE_SKILL none
	setvard local.effect.duration PARAM3
	callevent local.effect.duration effect_die

	//if ( $get(ent_me,isplayer) )
	//{
		setvard game.effect.movespeed 0.0001%
		setvard game.effect.canjump 0
		setvard game.effect.canattack 0
		setvard game.effect.canduck 0
		setvard game.effect.anim.framerate 0.01
		//setvard game.effect.canattack 0
		//setvard game.effect.anim.framerate 0.01
	//}

	setvard EFFECT_DAMAGE 0
	if ( PARAM2 isnot 'PARAM2' ) setvard EFFECT_DAMAGE PARAM2

	if ( EFFECT_DAMAGE == 0 ) setvard DOT_ABORT 1

	dbg game_activate force_cage $get(PARAM1,name) EFFECT_DAMAGE

	playermessage ent_me You are held in a magic field!

	if game.serverside

	setvard IS_ACTIVE 1
	callevent do_cage
	callevent sustain_cage
	
	setvelocity ent_me $vec(0,0,0)

}

{ do_cage

	local CAGE_ORG $get(ent_me,origin)
	setvard MY_HALF_HEIGHT $get(ent_me,height)
	local MAX_HEIGHT 144
	if ( MY_HALF_HEIGHT <= MAX_HEIGHT )
	{
		local HEIGHT_RATIO MY_HALF_HEIGHT
		divide HEIGHT_RATIO MAX_HEIGHT
		setvard SPHERE_SCALE $ratio(HEIGHT_RATIO,0.01,2.0)
	}
	else
	{
		setvard SPHERE_SCALE 2.0
	}
	multiply MY_HALF_HEIGHT 0.5

	if ( !$get(ent_me,isplayer) ) vectoradd CAGE_ORG z MY_HALF_HEIGHT
	clientevent new all effects/force_cage_cl CAGE_ORG SPHERE_SCALE
	setvard CL_CAGE_ID game.script.last_sent_id
	setvard NEXT_CAGE_CL_UPDATE game.time
	add NEXT_CAGE_CL_UPDATE 10.0
}

{ sustain_cage
	if IS_ACTIVE
	callevent 1.0 sustain_cage
	if ( !$get(ent_me,isalive) ) callevent effect_die
	if ( !$get(CAGE_OWNER,isalive) ) callevent effect_die

	if game.time > NEXT_CAGE_CL_UPDATE
	setvard NEXT_CAGE_CL_UPDATE game.time
	add NEXT_CAGE_CL_UPDATE 10.0
	clientevent update all CL_CAGE_ID end_fx
	local CAGE_ORG $get(ent_me,origin)
	if ( !$get(ent_me,isplayer) ) vectoradd CAGE_ORG z MY_HALF_HEIGHT
	clientevent new all effects/force_cage_cl CAGE_ORG SPHERE_SCALE
	setvard CL_CAGE_ID game.script.last_sent_id
}

{ [shared] game_death
	callevent effect_die
}

{ [shared] effect_die

	setvard IS_ACTIVE 0

	if ( game.clientside )
	{
		removescript
		playermessage The magic field dissipates.
	}

	clientevent update all CL_CAGE_ID end_fx

	setvard game.effect.movespeed 100%
	setvard game.effect.canjump 1
	setvard game.effect.canattack 1
	setvard game.effect.anim.framerate 1.0
	setvard game.effect.canduck 1

	if game.serverside
	if ( $get(CAGE_OWNER,isalive) ) callexternal CAGE_OWNER ext_end_cage

	setvard EFFECT_DAMAGE 0

	playsound 0 10 magic/energy1_loud.wav
}

{
repeatdelay DOT_FREQ

	if BE_EFFECT_ACTIVE
	callevent dot_effect
	if ( !$get(ent_me,isalive) ) callevent effect_die
}