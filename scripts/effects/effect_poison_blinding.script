//Blinding Poison - from Poison
//- Like poison but blinds
//Any npc
//PARAM1 == Duration
//PARAM2 == Damge Source (entity)
//PARAM3 == Damage per second
//PARAM4 == Flag to 1 for "diseased" instead of "poisoned"
//PARAM5 == XP Skill

{ 	[server]
	const EFFECT_ID			DOT_poison_blind
	const EFFECT_NAME		"Poison"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE

	const DOT_EFFECT 1
	const DOT_TYPE poison
	const DOT_DMG local.effect.damage
	const DOT_FREQ $randf(0.9,1.1)
	const DOT_ATTACKER local.effect.dmgsource
	const DOT_SKILL ACTIVE_SKILL

	const DOT_RESIST_MESSAGE "You resist being blinded by the poison"
}

#include effects/effect_base	allowduplicate

{ 	[server] game_activate


	//=== Check Immunities
	callevent resist_check poison $get(PARAM2,id)
	if RESISTED_EFFECT < 2
	//=== End Check Immunities

	setvard local.effect.duration 	PARAM1
	setvard local.effect.dmgsource 	PARAM2
	setvard local.effect.damage 	PARAM3

	local DUR_RATIO $get(PARAM2,scriptvar,'PLR_CORRODE_DURATION')
	if ( DUR_RATIO != 0 )
	{
		multiply local.effect.duration DUR_RATIO
	}

	if ( IAM_IMMUNE > 0 ) multiply local.effect.damage IAM_IMMUNE

	if ( RESISTED_EFFECT < 1 )
	{
		if ( $get(ent_me,isplayer) )
		{
			clientevent new ent_me $currentscript local.effect.duration
			setvard local.effect.clientscript game.script.last_sent_id
		}
	}

	if ( !$get(ent_me,scriptvar,'IS_LURE') ) effect glow ent_me (75,215,0) 72 local.effect.duration local.effect.duration

	if( $get(ent_me,isplayer) ) svplaysound game.sound.body game.sound.maxvol $get(ent_me,scriptvar,'PLR_SOUND_STOMACHHIT1')

	playermessage $get(ent_me,id) You have been struck with blinding poison! BE_RESIST_STRING

	//effect screenfade ent_me local.effect.duration local.effect.duration (75,215,0) 255 perm

	callevent local.effect.duration effect_die

	if ( PARAM5 isnot 'PARAM5' ) setvard ACTIVE_SKILL PARAM5
}

{ 	[shared] effect_die

	setvard local.effect.damage 0
    if !RESISTED_EFFECT
	playermessage $get(ent_me,id) You clear the venom from your eyes.

}

{
repeatdelay DOT_FREQ
	if BE_EFFECT_ACTIVE
	callevent dot_effect
}

// ===========
// Client Side
// ===========

{	[client]
	const POISON_SCREEN_DELAY		1.0
	const POISON_SCREEN_ALPHA		225
}

{	[client] client_activate

	setvard FADE_DURATION PARAM1
	callevent PARAM1 effect_die
}

{	[client]
	repeatdelay 3.0

	setvard game.cleffect.screenfade.newfade 1
	setvard game.cleffect.screenfade.alphalimit POISON_SCREEN_ALPHA	//Only use this fade if current fade is under this limit
	setvard game.cleffect.screenfade.color (75,215,0)
	setvard game.cleffect.screenfade.alpha POISON_SCREEN_ALPHA
	setvard game.cleffect.screenfade.duration 1.0
	setvard game.cleffect.screenfade.blendduration 1.0
	setvard game.cleffect.screenfade.type fadeout
}


