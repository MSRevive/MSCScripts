//Same as poison
//Any npc
//PARAM1 == Duration
//PARAM2 == Damge Source (entity)
//PARAM3 == Damage per second
//PARAM4 == Flag to 1 for "diseased" instead of "poisoned"
//PARAM5 == XP Skill

{ 	[server]
	const EFFECT_ID			DOT
	const EFFECT_NAME		"Disease"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE

	const DOT_EFFECT 1
	const DOT_TYPE poison
	const DOT_DMG local.effect.damage
	const DOT_FREQ $randf(0.9,1.1)
	const DOT_ATTACKER local.effect.dmgsource
	const DOT_SKILL ACTIVE_SKILL

	const DOT_RESIST_MESSAGE "You resist the disease"
}

#include effects/effect_base	allowduplicate

{ 	[server] game_activate

	setvard EFFECT_DESC "disease!"
	setvard EFFECT_DESC_ED "diseased."


	//=== Check Immunities
	callevent resist_check poison $get(PARAM2,id)
	if RESISTED_EFFECT < 2
	//=== End Check Immunities

	setvard local.effect.duration 	PARAM1
	setvard local.effect.dmgsource 	PARAM2
	setvard local.effect.damage 	PARAM3

	if ( IAM_IMMUNE > 0 ) multiply local.effect.damage IAM_IMMUNE

	if ( $get(ent_me,isplayer) )
	{
		clientevent new ent_me $currentscript local.effect.duration
		setvard local.effect.clientscript game.script.last_sent_id
	}

	if ( !$get(ent_me,scriptvar,IS_LURE) ) effect glow ent_me (75,215,0) 72 local.effect.duration local.effect.duration

	if( $get(ent_me,isplayer) ) svplaysound game.sound.body game.sound.maxvol $get(ent_me,scriptvar,'PLR_SOUND_STOMACHHIT1')

	playermessage $get(ent_me,id) You have been EFFECT_DESC_ED BE_RESIST_STRING

	callevent local.effect.duration effect_die

	if ( PARAM5 isnot 'PARAM5' ) setvard ACTIVE_SKILL PARAM5
}

{ 	[shared] effect_die

	setvard local.effect.damage 0
    if !RESISTED_EFFECT
	if ( $get(ent_me,isplayer) ) 
	{
		playermessage $get(ent_me,id) You overcome the disease.
	}
}


// ===========
// Client Side
// ===========

{	[client]
	const POISON_SCREEN_DELAY		0.2
	const POISON_SCREEN_ALPHA		30
}

{	[client] client_activate

	callevent PARAM1 effect_die
}

{	[client]
	repeatdelay POISON_SCREEN_DELAY


	setvard game.cleffect.screenfade.newfade 1
	setvard game.cleffect.screenfade.alphalimit POISON_SCREEN_ALPHA	//Only use this fade if current fade is under this limit
	setvard game.cleffect.screenfade.color (128,215,128) //different color for disease
	setvard game.cleffect.screenfade.alpha POISON_SCREEN_ALPHA
	setvard game.cleffect.screenfade.duration POISON_SCREEN_DELAY
	setvard game.cleffect.screenfade.blendduration POISON_SCREEN_DELAY
	setvard game.cleffect.screenfade.type fadein
}

{
repeatdelay DOT_FREQ
	if BE_EFFECT_ACTIVE
	callevent dot_effect
}