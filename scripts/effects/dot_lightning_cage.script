//Lightning cage by Thothie

//Any npc
//Param1 - Hold Duration
//Param2 - Who to blame
//Param3 - Dmg, if any
//Param4 - Skill to give dmg to
//Param5 - most hp it can affect (def: 1500)

// . effectme effects/dot_lightning_cage 10 GAME_MASTER 0.5 spellcasting.cold 1500

{ 
	const EFFECT_ID "+"
	const EFFECT_FLAGS nostack
	const EFFECT_SCRIPT $currentscript //Must be copied, as is, at the top script!
	
	const DOT_IM_AFFECTED "You are held in a lightning field!" //Player recieves affect and is afflicted
	
	const DOT_IM_RESIST "You resist being held in a lightning field." //Player recieves the affect, but resists / immune it.
	const DOT_HE_IMMUNE "is immune to lightning magic!" //Target recieves the affect, but resists / immune it
}

{ game_activate //This is above the include intentionally

	setvard MAX_HP PARAM5 //Sets the max_hp var before dot_check_canapply is called from the include below
	if ( !PARAM5 ) setvard MAX_HP 1500
}

#include effects/dot_lightning	allowduplicate

{ dot_check_canapply //<duration> <afflicter> <dot> <skill> <hpmax>
	
	if ( $get(ent_me,hp) > MAX_HP )
	{
		playermessage DOT_ATTACKER $get(ent_me,name) is too strong for a lightning field.
		
		setvard DOT_RESISTED 1
		removescript
		exitevent
	}
}

{ dot_start

	setvard game.effect.movespeed 0
	setvard game.effect.canjump 0
	setvard game.effect.canduck 0
	setvard game.effect.anim.framerate 0
	
	scriptflags ent_me add light_cage nopush 1 -1 none

	playsound 0 10 magic/bolt_start.wav

	local L_POS $get(ent_me,origin) 
	if ( !$get(ent_me,isplayer) ) vectoradd L_POS z $math(divide,$get(ent_me,height),2)
	
	if ( CL_CAGE ) clientevent update all CL_CAGE effect_die
	clientevent new all effects/sfx_lightning_cage 60 L_POS //It would be *shocking* if a player lasted 60 seconds in this
	setvard CL_CAGE game.script.last_sent_id
}

{ effect_die

	if !DOT_RESISTED
	playermessage ent_me "The lightning field dissipates."
	
	scriptflags ent_me remove light_cage
	
	if ( CL_CAGE ) clientevent update all CL_CAGE end_fx

	playsound 0 10 magic/energy1_loud.wav
}
