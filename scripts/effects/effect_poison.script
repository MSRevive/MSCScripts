//Thothie - minor syntax problems generating errors, fixed
//POISON
//Any npc
//PARAM1 == Duration
//PARAM2 == Damge Source (entity)
//PARAM3 == Damage per second
//PARAM4 == Flag to 1 for "diseased" instead of "poisoned"
//PARAM5 == XP Skill

{ 	[server]
	const EFFECT_ID			DOT_poison
	const EFFECT_NAME		"Poison"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE

	const DOT_EFFECT 1
	const DOT_TYPE poison
	const DOT_DMG local.effect.damage
	const DOT_FREQ $randf(0.9,1.1)
	const DOT_ATTACKER local.effect.dmgsource
	const DOT_SKILL ACTIVE_SKILL

	const DOT_NO_SECONDARY 1
}

#include effects/effect_base	allowduplicate

{ 	[server] game_activate

	setvard EFFECT_DESC "poison!"
	setvard EFFECT_DESC_ED "poisoned!"


	//=== Check Immunities
	callevent resist_check poison $get(PARAM2,id)
	if RESISTED_EFFECT < 2
	//=== End Check Immunities

	setvard local.effect.duration 	PARAM1
	setvard local.effect.dmgsource 	PARAM2
	setvard local.effect.damage 	PARAM3

	local L_DUR_RATIO $get(PARAM2,scriptvar,'PLR_CORRODE_DURATION')
	if ( L_DUR_RATIO != 0 )
	{
		local L_OLD_DUR local.effect.duration 
		multiply local.effect.duration L_DUR_RATIO
		dbg OldDuration: L_OLD_DUR NewDuration: local.effect.duration x L_DUR_RATIO
	}

	if ( IAM_IMMUNE > 0 ) multiply local.effect.damage IAM_IMMUNE

	if ( $get(ent_me,isplayer) )
	{
		clientevent new ent_me $currentscript local.effect.duration
		setvard local.effect.clientscript game.script.last_sent_id
	}

	if ( !$get(ent_me,scriptvar,IS_LURE) ) effect glow ent_me (75,215,0) 72 local.effect.duration local.effect.duration

	if( $get(ent_me,isplayer) ) svplaysound game.sound.body game.sound.maxvol $get(ent_me,scriptvar,'PLR_SOUND_STOMACHHIT1')

	playermessage $get(ent_me,id) You have been EFFECT_DESC_ED BE_RESIST_STRING

	callevent local.effect.duration effect_die


	if ( PARAM5 startswith 'PARAM' )
	{
		setvard ACTIVE_SKILL spellcasting.affliction
	}
	else
	{
		setvard ACTIVE_SKILL PARAM5
	}

}

{ 	[shared] effect_die

	setvard local.effect.damage 0
    if RESISTED_EFFECT < 1
	if ( $get(ent_me,isplayer) ) 
	{
		playermessage $get(ent_me,id) The poison subsides.
	}
}


// ===========
// Client Side
// ===========

{	[client]
	const POISON_SCREEN_DELAY		0.2
	const POISON_SCREEN_ALPHA		30
}

{	[client] client_activate

	callevent PARAM1 effect_die
}

{	[client]
	repeatdelay POISON_SCREEN_DELAY


	setvard game.cleffect.screenfade.newfade 1
	setvard game.cleffect.screenfade.alphalimit POISON_SCREEN_ALPHA	//Only use this fade if current fade is under this limit
	setvard game.cleffect.screenfade.color (75,215,0)
	setvard game.cleffect.screenfade.alpha POISON_SCREEN_ALPHA
	setvard game.cleffect.screenfade.duration POISON_SCREEN_DELAY
	setvard game.cleffect.screenfade.blendduration POISON_SCREEN_DELAY
	setvard game.cleffect.screenfade.type fadein
}

{
repeatdelay DOT_FREQ
	if BE_EFFECT_ACTIVE
	callevent dot_effect
}