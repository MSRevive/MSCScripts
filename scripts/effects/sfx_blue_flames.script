//Freeze thar blood

//Emits blue flames for cold DOTs

//Any npc
//PARAM1 == Duration in seconds
//PARAM2 == index of monster

//
// TODO: Get NPC's height/width and use this to define the area in which the freeze spurt forth
//

#scope client

{
	const OFS_POS 16
	const OFS_NEG -16
	
	const OFSZ_NEG 0

	const SPRITE_1 xsmoke3.spr
}

{ game_precache
	precache SPRITE_1
}

{ client_activate

	setvard FX_IDX PARAM2
	callevent PARAM1 effect_die
}

{ 
	repeatdelay $randf(0.1,0.3)
	
	callevent createsprite
}

{ createsprite

	local l.pos $getcl(FX_IDX,origin)
	vectoradd l.pos $vec($randf(OFS_NEG,OFS_POS),$randf(OFS_NEG,OFS_POS),$randf(0,32))
	cleffect tempent sprite SPRITE_1 l.pos setup_sprite1_flame
}

{ setup_sprite1_flame

	cleffect tempent set_current_prop death_delay 	$randf(0.4,0.6)
	cleffect tempent set_current_prop framerate	25
	cleffect tempent set_current_prop rendermode add
	cleffect tempent set_current_prop renderamt 255
	cleffect tempent set_current_prop rendercolor (64,64,255)
	cleffect tempent set_current_prop velocity	$vec($randf(-4,4),$randf(-4,4),0)
	cleffect tempent set_current_prop frames	20
	cleffect tempent set_current_prop scale 	0.5
	cleffect tempent set_current_prop gravity 	-0.5
	cleffect tempent set_current_prop collide 	none
}

{ effect_die
	removescript
}