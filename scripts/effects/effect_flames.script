//Flame Effect
//Any npc
//PARAM1 == Duration in seconds

//
// TODO: Get NPC's height/width and use this to define the area in which the flames spurt forth
//


{  [shared]

	const OFS_POS			16
	const OFS_NEG			-16
	const OFSZ_NEG			0
	const LIGHT_COLOR		(255,255,128)
	const LIGHT_RADIUS		128	

	const SPRITE_1			fire1_fixed.spr
	precache SPRITE_1
}

{ [server]

	const EFFECT_ID			effect_flames
	const EFFECT_NAME		"Flames"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE
}

#include effects/effect_base

{ [server] game_activate

	setvard local.effect.duration PARAM1
	subtract local.effect.duration 1.0
	if ( local.effect.duration < 0.1 ) setvard local.effect.duration 0.1
	clientevent new all $currentscript local.effect.duration $get(ent_me,index) $get(ent_me,height) LIGHT_ON
	setvard effect.clientscript game.script.last_sent_id
	callevent local.effect.duration effect_die
}

// ===========
// Client Side
// ===========

{ [client] client_activate

	setvard sfx.npcid PARAM2
	setvard OFSZ_POS PARAM3
	callevent PARAM1 cl_effect_die
	callevent PARAM1 effect_die

	//just getting rid of light, too many edit errors
	//if ( PARAM4 isnot nolight ) cleffect light new $getcl(sfx.npcid,origin) LIGHT_RADIUS LIGHT_COLOR PARAM1
}

{ [client]
	repeatdelay 0.4


	////dbg temp Flame still here

	callevent createsprite
}

{ [client] createsprite

	local l.pos $getcl(sfx.npcid,origin)
	vectoradd l.pos $vec($randf(OFS_NEG,OFS_POS),$randf(OFS_NEG,OFS_POS),$randf(OFSZ_POS,OFSZ_NEG))
	cleffect tempent sprite SPRITE_1 l.pos setup_sprite1_flame
}

{  [client] setup_sprite1_flame

	cleffect tempent set_current_prop death_delay 	$randf(0.4,0.6)
	cleffect tempent set_current_prop framerate	25
	cleffect tempent set_current_prop velocity	$vec($randf(-4,4),$randf(-4,4),0)
	cleffect tempent set_current_prop frames	20
	cleffect tempent set_current_prop scale 	0.75
	cleffect tempent set_current_prop gravity 	-0.5
	cleffect tempent set_current_prop collide 	none
}

{ [client] cl_effect_die

	//Thoth comment - This looks wrong - I don't think you can use clientevent statement from clientside
	clientevent remove all $currentscript
	removescript
}