//Freeze solid by Thothie (from effects_stun)
//- I think effects/effect_xxx is kinda redundant, so I've broken than convention
//Any npc
//Param1 - Freeze Duration
//Param2 - Who to blame
//Param3 - Dmg, if any
//Param4 - Skill to give dmg to (def: spellcasting.ice)
//Param5 - most hp it can affect (def: 1500)

{ [server]
	const EFFECT_ID			effect_cocoon
	const EFFECT_NAME		"cocooned"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript
	const SOUND_FREEZE debris/bustflesh2.wav

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE

	const DOT_EFFECT 1
	const DOT_TYPE magic
	const DOT_DMG EFFECT_DAMAGE
	const DOT_FREQ $randf(0.9,1.1)
	const DOT_ATTACKER MY_ATTACKER
	const DOT_SKILL ACTIVE_SKILL
}

#include effects/effect_base	allowduplicate

{ [server] game_activate //<dot> <cocoon_id|player_id>


	playsound 0 10 SOUND_FREEZE

	setvard game.effect.movespeed 0.0001%
	setvard game.effect.canjump 0
	setvard game.effect.canattack 0
	setvard game.effect.anim.framerate 0.01

	setvard MY_ATTACKER PARAM2

	if ( $get(MY_ATTACKER,isplayer) ) setvard ACTIVE_SKILL spellcasting.affliction

	setvard EFFECT_DAMAGE PARAM1

	if ( EFFECT_DAMAGE == 0 ) setvard DOT_ABORT 1

	playermessage ent_me You are trapped in a cocoon!

	if game.serverside
	//callexternal $get(ent_me,id) ext_hold_person local.effect.duration
	scriptflags ent_me add cocooned nopush 1 -1 none

	setvard IS_ACTIVE 1
}

{ [shared] game_death
	callevent effect_die
}

{ [shared] effect_die

	dbg cocooned:effect_die PARAM1

	setvard IS_ACTIVE 0
	if ( game.serverside )
	{
		playermessage ent_me You have been freed from the webs!
		scriptflags ent_me remove cocooned
	}
	setvard EFFECT_DAMAGE 0

	callexternal $get(ent_me,id) hold_person_end

	setvard game.effect.movespeed 100%
	setvard game.effect.canjump 1
	setvard game.effect.canattack 1
	setvard game.effect.anim.framerate 1.0
	removescript
}

{
repeatdelay DOT_FREQ

	dbg cocoon_pulse $get(ent_me,scriptvar,'COCOON_ATTACHED')

	if IS_ACTIVE
	if ( !$get(ent_me,scriptvar,'COCOON_ATTACHED') ) callevent effect_die "no_cocoon"

	if BE_EFFECT_ACTIVE
	callevent dot_effect
	if ( !$get(ent_me,isalive) ) callevent effect_die "targ_died"
}
