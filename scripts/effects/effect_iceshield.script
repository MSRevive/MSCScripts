//Ice Shield
//Any npc
//PARAM1 == Duration
//PARAM2 == Damage accept ratio (1.0: accept all 0.0: Accept none)

#scope server

{
	const EFFECT_ID effect_iceshield
	const EFFECT_NAME "Ice Shield"
	const EFFECT_ACTIONTYPE nostack
	const EFFECT_SCRIPT $currentscript

	setvard game.effect.id EFFECT_ID
	setvard game.effect.displayname EFFECT_NAME
	setvard game.effect.type EFFECT_ACTIONTYPE
}

#include effects/effect_base

{ game_activate //<who_applied> <FINAL_DURATION> <ICESHIELD_FORMULA>

	local WHO_CASTED PARAM1
	setvard local.effect.duration PARAM2
	setvard local.effect.damage PARAM3 //*damage reduct
	
	setvard FX_END_TIME $math(add,game.time,local.effect.duration)

	if ( $get(ent_me,isplayer) ) clientevent new ent_me $currentscript local.effect.duration //Create client script
	setvard local.effect.clientscript game.script.last_sent_id

	effect glow $get(ent_me,id) (0,0,192) 72 local.effect.duration local.effect.duration
	playsound game.sound.item game.sound.maxvol magic/heal_strike.wav

	if ( $get(ent_me,isplayer) )
	{
		hud.addstatusicon ent_me alpha_iceshield iceshield local.effect.duration
		
		gplayermessage ent_me "You are protected by a shield of ice."
		bplayermessage ent_me "Ice shield" $int(local.effect.duration) "seconds remain."
	}

	if ( WHO_CASTED isnot $get(ent_me,id) )
	{
		if $get(WHO_CASTED,isplayer)
		
		gplayermessage WHO_CASTED "You shield" $get(ent_me,name) "for" local.effect.duration "seconds." //Tell shielder that they shielded a player
		
		local L_BONUS_MSG "for shielding "
		stradd L_BONUS_MSG $get(ent_me,name)
		
		local L_DMGPOINT_ADD 1000 //Max damage points that can be added with no prior ice shield applied
		callexternal L_SHIELDER ext_dmgpoint_bonus L_DMGPOINT_ADD L_BONUS_MSG //Add bonus soda to healer
	}

	callevent local.effect.duration effect_die
}

{ ext_refresh_ice_shield //<duration> <shielder> //If a smaller duration is supplied, it will ignore it and refresh the longer duration instead.

	local L_SHIELDER PARAM2
	if ( PARAM1 > local.effect.duration ) setvard local.effect.duration PARAM1
	
	local L_TIME_DIFF FX_END_TIME
	setvard FX_END_TIME $math(add,game.time,local.effect.duration) //set new end time
	local L_TIME_DIFF $math(subtract,FX_END_TIME,L_TIME_DIFF) //Get how many seconds were added
	
	//FX
	hud.addstatusicon ent_me alpha_iceshield iceshield local.effect.duration //set ice shield icon
	
	effect glow $get(ent_me,id) (0,0,192) 72 local.effect.duration local.effect.duration
	playsound game.sound.item game.sound.maxvol magic/heal_strike.wav
	
	clientevent update ent_me local.effect.clientscript ext_refresh_ice_shield local.effect.duration //update player script
	
	//Player stuff
	if ( L_SHIELDER isnot $get(ent_me,id) ) //If not shielding self
	{
		if ( $get(ent_me,isplayer) ) //If I am a player
		{
			gplayermessage ent_me $get(L_SHIELDER,name) "has protected you with a shield of ice." //Tell me that a player so graciously shielded me (and possibly increased my duration)
			bplayermessage ent_me "Added" $int(L_TIME_DIFF) "seconds to Ice Shield."
			
			local ADD_BONUS 1
		}
		
		gplayermessage L_SHIELDER "You shield" $get(ent_me,name) "for" $int(L_TIME_DIFF) "more seconds." //Tell shielder that they shielded a player
		
		if ( $get(ent_me,scriptvar,'NPC_CRITICAL') ) local ADD_BONUS 1 //If I am a critical npc, then add damage point bonus
		
		if ADD_BONUS
		local L_BONUS_MSG "for shielding "
		stradd L_BONUS_MSG $get(ent_me,name)
		
		local L_DMGPOINT_ADD $math(multiply,$math(divide,L_TIME_DIFF,412),1000) // ( time_diff / 412 seconds ) * 1000
		
		callexternal L_SHIELDER ext_dmgpoint_bonus L_DMGPOINT_ADD L_BONUS_MSG //Add bonus soda to healer
		
		
	}
}

{ game_damaged

	if PARAM2 > 0

	clientevent update ent_me local.effect.clientscript shield_absorb
	
	playsound 0 3 player/pl_metal2.wav
	
	return local.effect.damage
}

{ [override] effect_die

	local L_TIME_LEFT $math(subtract,FX_END_TIME,game.time)
	if ( L_TIME_LEFT <= 0.10 )
	{
		hud.killstatusicon ent_me iceshield
		svplaysound 0 2 "debris/bustglass2.wav"
		
		effect tempent trail blueflare1.spr $relpos(0,0,0) $relpos(0,0,40) 10 2 5 10 20
		bplayermessage $get(ent_me,id) "Your ice shield has collapsed!"
		
		removescript
	}
	else
	{
		callevent L_TIME_LEFT effect_die
	}
}


// ===========
// Client Side
// ===========

{ [client]
	const BURN_SCREEN_DELAY		0.5
	const BURN_SCREEN_ALPHA		40
}

{ [client] client_activate

	setvard FX_DURATION PARAM1
	setvard FX_END_TIME $math(add,game.time,FX_DURATION)
	
	callevent FX_DURATION fx_die
}

{ [client] shield_absorb

	setvard game.cleffect.screenfade.newfade 1
	setvard game.cleffect.screenfade.alphalimit BURN_SCREEN_ALPHA	//Only use this fade if current fade is under this limit
	setvard game.cleffect.screenfade.color (0,0,192)
	setvard game.cleffect.screenfade.alpha BURN_SCREEN_ALPHA
	setvard game.cleffect.screenfade.duration BURN_SCREEN_DELAY
	setvard game.cleffect.screenfade.blendduration BURN_SCREEN_DELAY
	setvard game.cleffect.screenfade.type fadein
}

{ [client] ext_refresh_ice_shield //<duration>

	setvard FX_DURATION PARAM1
	setvard FX_END_TIME $math(add,game.time,FX_DURATION)
}

{ [client] fx_die

	local L_TIME_LEFT $math(subtract,FX_END_TIME,game.time)
	if ( L_TIME_LEFT <= 0.10 )
	{
		removescript
	}
	else
	{
		callevent L_TIME_LEFT fx_die
	}
}