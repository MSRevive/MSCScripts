//Cobra
//Thothie - increased ATTACK_RANGE to match ATTACK_HITRANGE 
// - increased size to 64x64
// - Added poison effect of Fangtooth (95%) - 5-10 damage
// - Also added a few more HP
// - Figure doesnt attack unless attacked first, so midas well be a bit meaner
{
	const SOUND_PAIN monsters/spider/spiderhiss.wav
	const SND_STRUCK4 SOUND_PAIN
	const SND_STRUCK5 SOUND_PAIN
	setvar SOUND_ATTACK1 monsters/spider/spiderhiss.wav
	setvar SOUND_ATTACK2 monsters/spider/spiderhiss.wav
	const SOUND_DEATH monsters/troll/trolldeath.wav
	setvar SOUND_WALK  monsters/troll/trollidle.wav
	setvar ATTACK_HITCHANCE 95%
	setvar ANIM_RUN walk
	setvar ANIM_IDLE idle1
	setvar ANIM_WALK walk
	setvard ANIM_DEATH diesimple
	setvard ANIM_ATTACK attack1
	setvar ATTACK1_DAMAGE 45
	setvard ATTACK_RANGE 230
	setvard ATTACK_HITRANGE 230
	setvard MOVE_RANGE 64

	const DMG_POISON $randf(10,15)
}

#include monsters/base_monster

{ npc_spawn

	hp 200/200
	//ov 359
	width 64
	height 64
	race demon //for ease of placement
	name a|Cobra
	roam 0
	setvard NPC_GIVE_EXP 100
	setmodel monsters/kcobra.mdl
	setidleanim idle1
	setmoveanim idle1
	playanim once idle1
	hearingsensitivity 5
	callevent npcatk_suspend_ai
}

{ attack1

   dodamage ENTITY_ENEMY ATTACK_RANGE ATTACK1_DAMAGE 75%

   if( $rand(1,3) == 1 ) applyeffect ent_lastseen effects/effect_poison 15 $get(ent_me,id) DMG_POISON
}

{ game_heardsound
	if SUSPEND_AI
	if $get(ent_lastheard,isplayer)
	setmovedest $get(ent_lastheard,id) 9999
	if ( $rand(1,10) == 1 ) playanim once idle3
	else playanim once idle1
}

{ game_struck

	if ( SUSPEND_AI )
	{
		callevent npcatk_resume_ai
		setmoveanim ANIM_RUN
		roam 1
	}
	volume 5
	playrandomsound 2 SOUND_PAIN SOUND_PAIN SOUND_STRUCK2 SOUND_STRUCK1 SOUND_STRUCK3
}