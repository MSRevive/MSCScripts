{
	setvar IS_UNHOLY 1
	setvard IMMUNE_VAMPIRE 1

	const LIGHT_RAD 64
	const STUCK_SENSITIVITY 10
	setvard MOVE_RANGE 300
	const FIN_EXP 320
	//for older betapack users
	precache monsters/animarmor_fly.mdl

	const ARROW_DAMAGE_LOW 30
	const ARROW_DAMAGE_HIGH 45

	//const BF_FLIGHT_STUCK_LIMIT 1
	//const BF_CRUISE_SPEED 50
	//const BF_BOOST_SPEED 200
}

#include monsters/anim_archer
//nclude monsters/base_flyer_agro

{ npc_spawn

	roam 1
	race demon
	width 64
	height 64
	setbbox npcsize
	hp 500
	setmodel monsters/animarmor_fly.mdl
	setidleanim ANIM_IDLE
	setmoveanim ANIM_WALK
	hearingsensitivity 8
	blood none

	fly 1

	takedmg poison 0.0
	setvard IMMUNE_POISON 1
	takedmg holy 2.0

	setmodelbody 1 1	//Pointy hat
	setmodelbody 2 3	//Steel Bow

	callevent 0.1 select_armor

	setvard ARROW_MISS_COUNT 0
}

{ select_armor

	if ( ARMOR_TYPE equals 'ARMOR_TYPE' ) setvard ARMOR_TYPE $rand(1,4)

	if ( ARMOR_TYPE == 1 )
	{
		name an|Animated Archer of Flame
		setvard DAMAGE_TYPE fire
		takedmg lightning 1.2
		takedmg fire 0.0
		takedmg cold 2.0
		setvard LIGHT_COLOR (255,0,0)

		setvard DROPS_CONTAINER 1
		setvard CONTAINER_DROP_CHANCE 10% 
		setvard CONTAINER_SCRIPT chests/quiver_of_fire
	}

	if ( ARMOR_TYPE == 2 )
	{
		name an|Animated Archer of Thunder
		setvard DAMAGE_TYPE lightning
		takedmg lightning 0.0
		takedmg acid 2.0
		setvard LIGHT_COLOR (255,255,0)

		setvard DROPS_CONTAINER 1
		setvard CONTAINER_DROP_CHANCE 10% 
		setvard CONTAINER_SCRIPT chests/quiver_of_lightning
	}

	if ( ARMOR_TYPE == 3 )
	{
		name an|Animated Archer of Frost
		setvard DAMAGE_TYPE cold
		takedmg fire 2.0
		takedmg lightning 1.2
		takedmg cold 0.0
		setvard LIGHT_COLOR (200,200,255)

		setvard DROPS_CONTAINER 1
		setvard CONTAINER_DROP_CHANCE 10% 
		setvard CONTAINER_SCRIPT chests/quiver_of_frost
	}

	if ( ARMOR_TYPE == 4 )
	{
		name an|Animated Archer of Venom
		setvard DAMAGE_TYPE poison
		takedmg lightning 2.5
		setvard LIGHT_COLOR (0,255,0)

		setvard DROPS_CONTAINER 1
		setvard CONTAINER_DROP_CHANCE 10% 
		setvard CONTAINER_SCRIPT chests/quiver_of_gpoison
	}

	effect glow ent_me LIGHT_COLOR 64 -1 0

	local GRND_LEVEL $get_ground_height(game.monster.origin)
	local MY_Z $vec.z(game.monster.origin) 
	if ( $dest(GRND_LEVEL,MY_Z) < 32 ) addvelocity ent_me $vec(0,0,110)
}

{ game_damaged_other

	setvard ARROW_MISS_COUNT 0

   if ( DAMAGE_TYPE equals fire ) applyeffect PARAM1 effects/dot_fire 30 $get(ent_me,id) $rand(5,12)
   if ( DAMAGE_TYPE equals poison ) applyeffect PARAM1 effects/dot_poison 30 $get(ent_me,id) $rand(5,12)
   if ( DAMAGE_TYPE equals cold ) applyeffect PARAM1 effects/dot_cold 10 $get(ent_me,id) $rand(5,12)
   if ( DAMAGE_TYPE equals lightning ) applyeffect PARAM1 effects/dot_lightning 10 $get(ent_me,id) $rand(5,12)
}

//===light system
{ [server] npc_post_spawn //call at spawn or cycle_up
	clientevent persist all monsters/lighted_cl $get(ent_me,index) LIGHT_COLOR LIGHT_RAD
	setvard MY_LIGHT_SCRIPT game.script.last_sent_id
}

{ [server] game_death

	clientevent remove all MY_LIGHT_SCRIPT
}
//===end light system

{ [override] bf_agrofly_boost
	setvelocity ent_me $relvel(0,BF_BOOST_SPEED,150)
}

{
repeatdelay 10.0
	effect glow ent_me LIGHT_COLOR 64 -1 0

	//stay out of ground/ceiling
	local GRND_LEVEL $get_ground_height(game.monster.origin)
	local MY_Z $vec.z(game.monster.origin) 

	if ( GRND_LEVEL >= MY_Z )
	{
		local DIST_DIFF GRND_LEVEL
		subtract DIST_DIFF MY_Z
	}

	if ( GRND_LEVEL < MY_Z )
	{
		local DIST_DIFF MY_Z
		subtract DIST_DIFF GRND_LEVEL
	}
	
	if ( DIST_DIFF < 32 )
	{
		addvelocity ent_me $vec(0,0,110)
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	local TRACE_START game.monster.origin
	local TRACE_END game.monster.origin
	vectoradd TRACE_END z 4096
	local FIND_CEILING $get_traceline(TRACE_START,TRACE_END,worldonly)
	local CEIL_Z $vec.z(FIND_CEILING)
	local SKY_Z $get_sky_height(game.monster.origin)
	if ( SKY_Z isnot none )
	{
		if SKY_Z < CEIL_Z
		local CEIL_Z equals SKY_Z
	}

	if ( CEIL_Z >= MY_Z )
	{
		local DIST_DIFF CEIL_Z
		subtract DIST_DIFF MY_Z
	}

	if ( CEIL_Z < MY_Z )
	{
		local DIST_DIFF MY_Z
		subtract DIST_DIFF CEIL_Z
	}

	if ( DIST_DIFF < 32 ) addvelocity ent_me $vec(0,0,-110)	
}

{ bf_agrofly_loop
	if $get(HUNT_LASTTARGET,isalive)
	if !$cansee(HUNT_LASTTARGET)
	setvard FLIGHT_STUCK 8
}

{ chicken_run
	movespeed 2.0
	addvelocity ent_me $relvel(0,-110,120)
}

{ chicken_end
	movespeed 1.0
}

{ do_attack
	setvelocity ent_me $vec(0,0,0)
}

{ shoot_arrow
	add ARROW_MISS_COUNT 1
	if 	ARROW_MISS_COUNT > 3
	callevent chicken_run 2.0

	setvard AS_ATTACKING game.time
	add AS_ATTACKING 5.0
}

{ npc_targetsighted
	setvard AS_ATTACKING game.time
	add AS_ATTACKING 1.0
}
