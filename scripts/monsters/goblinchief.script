//Goblin Chief

{

	if ( $lcase(game.map.name) equals goblintown )
	{
		setvard NPC_IS_BOSS 1
		setvard NPC_GIVE_EXP 400
	}
	else
	{
		if ( $lcase(game.map.name) equals gertenheld_forest )
		{
			setvard NPC_GIVE_EXP 50
		}
		else
		{
			setvard NPC_GIVE_EXP 150
		}
	}

	const NPC_BOSS_REGEN_RATE 0.1
	const NPC_BOSS_RESTORATION 1.0


	setvar ANIM_RUN run
	setvar ANIM_WALK walk
	setvard ANIM_ATTACK battleaxe_swing1_L
	setvard ANIM_DEATH die_fallback
	setvard ATTACK_RANGE 100
	setvard ATTACK_HITRANGE 130
	setvard MOVE_RANGE 50
	const ATTACK_HITCHANCE 60%
	const SOUND_STRUCK1 body/flesh1.wav
	const SOUND_STRUCK2 body/flesh2.wav
	const SOUND_STRUCK3 body/flesh3.wav
	const SOUND_HIT monsters/goblin/c_gargoyle_hit1.wav
	const SOUND_HIT1 monsters/goblin/c_gargoyle_hit1.wav
	const SOUND_HIT2 monsters/goblin/c_gargoyle_hit2.wav
	const SOUND_PAINYELL monsters/orc/pain.wav
	const SOUND_WARCRY1 monsters/goblin/c_goblin_bat1.wav
	const SOUND_ATTACK1 monsters/goblin/c_goblin_atk1.wav
	const SOUND_ATTACK2 monsters/goblin/c_goblin_atk2.wav
	const SOUND_ATTACK3 monsters/goblin/c_goblin_atk3.wav
	const SOUND_DEATH monsters/goblin/c_goblin_dead.wav
	const SOUND_DEATH2 monsters/goblin/c_goblin_dead.wav
	const SOUND_HELP monsters/goblin/c_goblin_bat2.wav
	setvard CAN_HUNT 1
	setvard HUNT_AGRO 1
	setvard RETALIATE_CHANCE 75%  //base_npc_attack - Automatically switch targets to the last damaging player
	setvard CAN_FLEE 0		//base_npc_attack - Whether I run away at low health
	setvard DROP_GOLD 		$rand(0,1)
	setvard DROP_GOLD_MIN		5
	setvard DROP_GOLD_MAX		10
 
   //Precache
   precache SOUND_DEATH //This sound must be explicitly precached since
			//the play command for it isn't in this file (it's
			//in base_monster.script)
}

#include monsters/base_monster_new

{ npc_spawn

	hp 500
	//ov 180
	width 32
	height 60
	name the|Goblin Chief
	roam 1
	hearingsensitivity 6
	race goblin
	setmodel monsters/goblin_new_boss.mdl
	setmodelbody 0 0 //body: no options currently
	setmodelbody 1 2 //head: 0-bare 1-animal 2-chief 3-slickback
	setmodelbody 2 5 //weapon: 0-Unarmed 1-SkullClub 2-DragonAxe 3-Knife 4-Bow 5-Raxe
	setmodelbody 3 0 //quiver: On/off
	setmodelbody 4 0 //arrow: On/off
	setidleanim idle1
	setmoveanim ANIM_WALK
	
	giveitem item_goblinhead
}

{ swing_axe
   // Deal damage
   setvar ATTACK_DAMAGE $randf(15,28)
   callevent npcatk_dodamage $get(ent_lastseen,id) ATTACK_HITRANGE ATTACK_DAMAGE ATTACK_HITCHANCE slash

   callevent attack_sound
}

   //Hit by someone
{ npc_struck
   
   volume 5
   playrandomsound 2 SOUND_HIT SOUND_HIT2 SOUND_STRUCK1 SOUND_STRUCK2 SOUND_STRUCK3
}

   //Attack grunts
{ attack_sound
   
	if( $rand(0,1) == 0 )
	{
		volume 5
		playrandomsound 2 SOUND_ATTACK1 SOUND_ATTACK2 SOUND_ATTACK3
	}
	else
	{
		if ( $rand(0,5) == 0 ) playsound 4 SOUND_WARCRY1
	}
}

{ npc_targetsighted		//Warcry

   local LASTSEEN_ENEMY $get(ent_lastseen,id)
   if LASTSEEN_ENEMY isnot LAST_ENEMY

   playsound game.sound.voice 5 SOUND_WARCRY1
   setvard LAST_ENEMY LASTSEEN_ENEMY
}

{ game_death
	setmodelbody 2 0
}