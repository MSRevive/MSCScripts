//This is an older script and may have some bad conventions as a result

// *** Random bandit script ***
//

{
	setvard CONTAINER_DROP_CHANCE 10% 
	setvard CONTAINER_SCRIPT chests/quiver_of_random_lesser

	const SOUND_PAIN		player/chesthit1.wav
	const SOUND_PAIN2		player/armhit1.wav
	const SOUND_BOW weapons/bow/bow.wav
	setvar ANIM_IDLE		idle
	setvar ANIM_RUN		run
	setvar ANIM_WALK		walk2
	setvard ANIM_DEATH		die_simple
	setvard CAN_HUNT 1
	setvard HUNT_AGRO		1
	setvard CAN_ATTACK	1
	setvard CAN_RETALIATE		1
	const RETALIATE_CHANGETARGET_CHANCE	75%
	setvard CAN_HEAR		1
	setvard NPC_GIVE_EXP		60
	callevent bowey
	callevent fistey
	callevent daggerey
	callevent swordey
	callevent axey
	callevent macey
	callevent magey

	setvard ATTACK_HITRANGE ATTACK_RANGE
	multiply ATTACK_HITRANGE 1.5

	setvar NO_STEP_ADJ 1 //seem to be getting stuck on ramps in prelude2

	const FIREBALL_FREQ 5.0
	const SPELL_RANGE 700
	const ANIM_PREP_SPELL prepare_fireball
	const ANIM_AIM_SPELL aim_fireball_R
	const ANIM_CAST_SPELL throw_fireball_R
}

#include monsters/base_monster

// Spawn properties
{ npc_spawn

	setvard BD_COUNT 0

	if ( WEAPON equals 'WEAPON' ) setvard WEAPON $rand(0,6)
	gold			$rand(13,27)
	//ov			180
	width		32
	height		92
	race			rogue
	takedmg		all 0.7
	hearingsensitivity	3
	roam			1
	setmodel		npc/rogue.mdl
	setidleanim		ANIM_IDLE
	setmoveanim		ANIM_WALK
}

{ bowey
	if WEAPON == 0
	name a|Bandit Archer
	hp 130
	setvard MOVE_RANGE 300
	setvard ATTACK_RANGE 800
	setvar ATTACK_SPEED 1200
	setvar ATTACK1_DAMAGE $randf(8.5,12.0)
	setvar ATTACK_COF 0
	setvard ANIM_ATTACK shootbow
	setactionanim ANIM_ATTACK
	setmodelbody 1 5
   setvard DROPS_CONTAINER 1
	setvard NO_STUCK_CHECKS 1
}
{ daggerey
	if WEAPON == 1
	name a|Bandit Rogue
	hp 130
	setvard MOVE_RANGE 50
	setvard ATTACK_RANGE 90
	setvar ATTACK1_DAMAGE $randf(8.5,10.0)
	setvar ATTACK_PERCENTAGE 80%
	setvard ANIM_ATTACK swordjab1_R
	setactionanim ANIM_ATTACK
	setmodelbody 1 1
}

{ fistey
	if WEAPON == 2
	name a|Bandit Martial Artist
	hp 160
	setvard MOVE_RANGE 50
	setvard ATTACK_RANGE 80
	setvar ATTACK1_DAMAGE $randf(6.5,8.0)
	setvar ATTACK_PERCENTAGE 80%
	setvard ANIM_ATTACK aim_punch1
	setactionanim ANIM_ATTACK
	setmodelbody 1 0
}

{ swordey
	if WEAPON == 3
	name a|Bandit Swordsman
	hp 160
	setvard MOVE_RANGE 80
	setvard ATTACK_RANGE 120
	setvar ATTACK1_DAMAGE $randf(8.5,10.0)
	setvar ATTACK_PERCENTAGE 85%
	setvard ANIM_ATTACK swordswing2_R
	setactionanim ANIM_ATTACK
	setmodelbody 1 4
}

{ axey
	if WEAPON == 4
	name a|Bandit Axeman
	hp 200
	setvard MOVE_RANGE 80
	setvard ATTACK_RANGE 120
	setvar ATTACK1_DAMAGE $randf(9.5,11.0)
	setvar ATTACK_PERCENTAGE 85%
	setvard ANIM_ATTACK battleaxe_swing1_R
	setactionanim ANIM_ATTACK
	setmodelbody 1 3
}

{ macey
	if WEAPON == 5
	name a|Bandit Berserker
	hp 200
	setvard MOVE_RANGE 80
	setvard ATTACK_RANGE 130
	setvar ATTACK1_DAMAGE $randf(9.5,11.0)
	setvar ATTACK_PERCENTAGE 85%
	setvard ANIM_ATTACK battleaxe_swing1_R
	setactionanim ANIM_ATTACK
	setmodelbody 1 2
}

{ magey
	if WEAPON == 6
	name a|Bandit Mage
	hp 100
	setvard MOVE_RANGE 300
	setvard ATTACK_RANGE 32
	setvar ATTACK1_DAMAGE $randf(20.0,40.0)
	setvar ATTACK_PERCENTAGE 85%
	setvard ANIM_ATTACK aim_punch1
	setvard CAN_FLINCH 0
	setactionanim ANIM_ATTACK
	setmodelbody 1 0
	callevent 1.0 scan_for_allies
	catchspeech debug_params debug
}

{ debug_params
	saytextrange 1024
	saytext Mov MOVE_RANGE
}

{ attack
	if( WEAPON == 0 )
	{
		//setvar AIM_ANGLE game.monster.last_seen.distance	//Aim up a bit
		//divide AIM_ANGLE GEXT_ADJ1
		setangle add_view.pitch 5 //fires in such a shallow arc, no need to adj based on distance
		setvard AS_ATTACKING game.time

		//<projectile scriptname> <"view"|(src_origin)> <target|(targ_origin)|none> <speed> <damage> <cof> <skill|none> //Converted to use new tossprojectile //greatguys1@FEB2022
		tossprojectile "proj_arrow_npc" $relpos(0,0,2) none ATTACK_SPEED ATTACK1_DAMAGE ATTACK_COF none
		callexternal $get(ent_lastprojectile,id) ext_lighten 0.4 //helps aim - thothie
		playsound 2 SOUND_BOW
	}
	else
	{
		dodamage NPCATK_TARGET ATTACK_HITRANGE ATTACK1_DAMAGE ATTACK_PERCENTAGE
	}
}

{ attack_jab
	callevent attack
}

{ npc_struck
   
	volume 5
	playrandomsound 2 SOUND_PAIN SOUND_PAIN2 none none
}

{ npc_death
   
	volume 5
	playrandomsound 2 SOUND_PAIN SOUND_PAIN2 none none
	local L_DEATHANIM $rand(0,6)
	if( L_DEATHANIM equals 0 )	setvard ANIM_DEATH die_simple
	if( L_DEATHANIM equals 1 )	setvard ANIM_DEATH die_backwards1
	if( L_DEATHANIM equals 2 )	setvard ANIM_DEATH die_backwards
	if( L_DEATHANIM equals 3 )	setvard ANIM_DEATH die_forwards
	if( L_DEATHANIM equals 4 )	setvard ANIM_DEATH headshot
	if( L_DEATHANIM equals 5 )	setvard ANIM_DEATH die_spin
	if( L_DEATHANIM equals 6 )	setvard ANIM_DEATH gutshot
}

{ npc_targetsighted
	//archer
	if ( WEAPON == 0 )
	{
		if !IS_FLEEING
		if $cansee(PARAM1)
		playanim once ANIM_ATTACK
	}

	if ( WEAPON == 6 )
	{
		if !IS_FLEEING
		if !FIREBALL_DELAY
		if !CASTING_SPELL
		if !IS_BUFFING
		setvard FIREBALL_DELAY 1
		callevent FIREBALL_FREQ reset_fireball_delay
		if $cansee(enemy,SPELL_RANGE)
		local L_SPELL_TARGET $get(ent_lastseen,id)
		callevent spell_attack L_SPELL_TARGET
		setvard CASTING_SPELL 1
	}
}
 
{ reset_fireball_delay
	setvard FIREBALL_DELAY 0
}

{ spell_attack //param1 = targ

	setvard AS_ATTACKING game.time
	playsound 0 10 magic/fireball_powerup.wav	
	setvard SPELL_TARGET PARAM1
	setmovedest SPELL_TARGET 9999
	playanim once ANIM_PREP_SPELL
	callevent 0.5 spell_attack2
}

{ spell_attack2
	setmovedest SPELL_TARGET 9999
	playanim critical ANIM_AIM_SPELL
	callevent 0.5 spell_attack3
}

{ spell_attack3
	setmovedest SPELL_TARGET 9999
	local TARG_ORG $get(SPELL_TARGET,origin)
	setangle face_origin TARG_ORG
	callevent 0.1 straighten_out

	playsound 0 10 magic/fireball_strike.wav

	playanim critical ANIM_CAST_SPELL
	
	//<projectile scriptname> <"view"|(src_origin)> <target|(targ_origin)|none> <speed> <damage> <cof> <skill|none> //Converted to use new tossprojectile //greatguys1@FEB2022
	tossprojectile "proj_fire_dart" $relpos(0,48,2) SPELL_TARGET 400 ATTACK1_DAMAGE 5 none
	callexternal $get(ent_lastprojectile,id) ext_lighten 0
	setvard CASTING_SPELL 0
}

{ straighten_out

	local MY_ANG game.monster.angles
	local MY_YAW $vec.yaw(SPELL_TARGET)
	setangle face $vec(0,MY_YAW,0)
	callevent 1.0 mage_reposition
}

{ mage_reposition
	if ( $get(HUNT_LASTTARGET,range) < SPELL_RANGE ) callevent npcatk_flee $get(HUNT_LASTTARGET,id) 700 1.0
	if ( $get(HUNT_LASTTARGET,range) >= MOVE_RANGE ) callevent chicken_run 1.0
}

{ game_struck
	
	if ( WEAPON == 6 )
	{
		if !BANDIT_FLEE_DELAY
		setvard BANDIT_FLEE_DELAY 1
		callevent 10.0 reset_bandit_flee_delay	
		callevent npcatk_flee $get(ent_laststruck,id) 2000 5.0
	}
}

{ reset_bandit_flee_delay
	setvard BANDIT_FLEE_DELAY 0
}

{ npcatk_ally_alert //param1 attacker, param2 ally under attack

	if !SUSPEND_AI

	if ( $get(PARAM1,isplayer) ) callevent cycle_up "ally_hit_by_player"

	if $get(PARAM2,id) isnot $get(ent_me,id)

	if WEAPON == 6
	if !IS_BUFFING
	if $get(PARAM2,range) < SPELL_RANGE
	//if $get(PARAM2,scriptvar,ICE_SHIELDED) != 1 //this wont work because both monsters have ICE_SHIELDED in their vars (externals)
	setvard ALLY_CHECK_ID $get(PARAM2,id)
	callevent check_can_shield
}

{ check_can_shield

	if !IS_BUFFING
	if CYCLED_UP
	if $get(ALLY_CHECK_ID,race) equals $get(ent_me,race) //switched from ally, as he'd shield beloveds and such
	callexternal ALLY_CHECK_ID ice_shield_check 
	callevent 0.1 check_can_shield2
}

{ check_can_shield2

	setvard AS_ATTACKING game.time
	
	if ICE_SHIELD_CHECK != 1
	callexternal ALLY_CHECK_ID ice_shield_on
	//yes, I can shield this guy
	setvard IS_BUFFING 1
	playanim critical ANIM_PREP_SPELL
	setmovedest ALLY_CHECK_ID 64
	callevent npcatk_suspend_ai 1.0
	callevent 0.1 shield_ally
}

{ shield_ally

	if $cansee(ALLY_CHECK_ID)

	callevent 0.5 cast_shield
}

{ cast_shield
	setvard IS_BUFFING 0
	playsound 0 10 magic/cast.wav
	applyeffect ALLY_CHECK_ID effects/effect_iceshield 20 0.2
}

{ scan_for_allies
	callevent 0.7 scan_for_allies
	if $cansee(ally)
	setvard ALLY_CHECK_ID $get(ent_lastseen,id)
	callevent check_can_shield ent_lastseen
}

{
repeatdelay 2.0

	//something in this script is causing BD periodically
	//prevention measure/hack

	if ( SUSPEND_AI ) add BD_COUNT 1
	if ( !SUSPEND_AI ) setvard BD_COUNT 0
	if ( BD_COUNT > 10 ) callevent npcatk_resume_ai
}