//Called against montsters exernally or via ms_npcscript

#include [casual] test_scripts/npc_externals
#include [casual] $currentmap_npc_externals

{
	setvard SCARABS_ATTACHED 0
	const NPC_FADE_IN_SPEED 0.1
	setvard EXT_DEMON_BLOOD_RATIO 5.0
}

{ effect_damage

	//this is not used by very many scripts anymore, hoping to remove entirely
	//now instead we send damage to targets via player/monsters external send_damage (player_main on player)

	//Effect Damage	PARAM1=Dmg PARAM2=Attacker PARAM3=Duration PARAM4=Type
	//- Called from external damaging effects in hopes of reducing crash / making XP pass proper

	////dbg Effect_damage Delay EFFECT_DAMAGE_DELAY Dmg PARAM1 Src PARAM2 Durat PARAM3 Type PARAM4

	if !EFFECT_DAMAGE_DELAY
	setvard EFFECT_DAMAGE_DELAY 1
	callevent 0.9 reset_effect_damage
	//dbg standard_effect_damage
	dodamage ent_me direct PARAM1 100% PARAM2 PARAM4 PARAM5 PARAM6 PARAM7 PARAM9
}


//FEB2009_19 - defunct - use xdodamage instead
//{ effect_skilldamage
//
//	if !EFFECT_DAMAGE_DELAY
//	setvard EFFECT_DAMAGE_DELAY 1
//	callevent 0.9 reset_effect_damage
//
//	//dbg effect_skilldamage: doskilldamage PARAM1 ent_me direct PARAM2 100% PARAM3 PARAM4
//	doskilldamage PARAM1 ent_me direct PARAM2 100% PARAM3 PARAM4
//}

{ reset_effect_damage

	setvard EFFECT_DAMAGE_DELAY 0
}

//Race changes, to be called from ms_npcscript
{ orc_race

	//dbg Got PARAM1
	race orc
}
{ demon_race

	//dbg Got PARAM1
	race demon
}
{ undead_race

	//dbg Got PARAM1
	race undead
}
{ human_race

	//dbg Got PARAM1
	race human
}
{ spider_race

	//dbg Got PARAM1
	race spider
}
{ wildanimal_race

	//dbg Got PARAM1
	race wildanimal
}
{ hated_race

	//dbg Got PARAM1
	race hated
}
{ beloved_race

	//dbg Got PARAM1
	race beloved
}
{ evil_race

	//dbg Got PARAM1
	race evil
}
{ good_race

	//dbg Got PARAM1
	race good
}
{ vermin_race

	race vermin
}
{ rogue_race

	race rogue
}
{ hguard_race

	race hguard
}
{ neutral_race

	race neutral
}


//Immune changes, to be used from ms_npcscript, item, or spell
{ lightning_immune

	takedmg lightning 0.0
	setvard IMMUNE_LIGHTNING 1
}
{ fire_immune

	takedmg fire 0.0
	setvard IMMUNE_FIRE 1
}
{ poison_immune

	takedmg poison 0.0
	setvard IMMUNE_POISON 1
}
{ cold_immune

	takedmg cold 0.0
	takedmg ice 0.0
	setvard IMMUNE_COLD 1
}
{ normal_immune

	setvard IMMUNE_COLD 0
	setvard IMMUNE_FIRE 0
	setvard IMMUNE_LIGHTNING 0
	setvard IMMUNE_POISON 0
	takedmg cold 1.0
	takedmg fire 1.0
	takedmg lightning 1.0
	takedmg poison 1.0
	takedmg ice 1.0
}


//Armor Changes (to be used by ms_npcscript)
{ fifty_armor
	takedmg all 0.5
}
{ eighty_armor
	takedmg all 0.2
}
{ no_armor
	takedmg all 1.0
}
{ weakened_armor
	takedmg all 2.0
}

//Invincible Toggle (to be used by ms_npcscript)
{ make_invulnerable
   	setvard MY_OLD_NAME $get(ent_me,name)
	local MY_NEW_NAME "Invincible "
  	stradd MY_NEW_NAME MY_OLD_NAME
	name MY_NEW_NAME
	effect glow ent_me (255,255,255) 64 -1 0
	setvard NPC_GLOW (255,255,255)
	invincible 1
}
{ make_vulnerable
	if ( MY_OLD_NAME isnot 'MY_OLD_NAME' ) name MY_OLD_NAME
	invincible 0
	effect glow ent_me (255,255,255) 128 1 1
	setvard NPC_GLOW 'NPC_GLOW'
}

//Add HP (to be used by ms_npcscript)
{ add_10_health 

	local CURRENT_HP $get(ent_me,hp)
	add CURRENT_HP 10
	hp CURRENT_HP
}
{ add_100_health 

	local CURRENT_HP $get(ent_me,hp)
	add CURRENT_HP 100
	hp CURRENT_HP
}
{ add_500_health 

	local CURRENT_HP $get(ent_me,hp)
	add CURRENT_HP 500
	hp CURRENT_HP
}
{ add_1000_health 

	local CURRENT_HP $get(ent_me,hp)
	add CURRENT_HP 1000
	hp CURRENT_HP
}

{ double_health 
   	local MY_OLDHP_NAME $get(ent_me,name)
	local MY_NEWHP_NAME "Strong "
  	stradd MY_NEWHP_NAME MY_OLDHP_NAME
	name MY_NEWHP_NAME

	local CURRENT_HP $get(ent_me,hp)
	multiply CURRENT_HP 2.0
	hp CURRENT_HP
}

{ quad_health 
   	local MY_OLDHP_NAME $get(ent_me,name)
	local MY_NEWHP_NAME "Very Strong "
  	stradd MY_NEWHP_NAME MY_OLDHP_NAME
	name MY_NEWHP_NAME

	local CURRENT_HP $get(ent_me,hp)
	multiply CURRENT_HP 4.0
	hp CURRENT_HP
}

{ half_health 
   	local MY_OLDHP_NAME $get(ent_me,name)
	local MY_NEWHP_NAME "Weakened "
  	stradd MY_NEWHP_NAME MY_OLDHP_NAME
	name MY_NEWHP_NAME

	divide NPC_GIVE_EXP 2 //Thothie FEB2009 - reduce XP value for nerfed monster
	skilllevel NPC_GIVE_EXP

	local CURRENT_HP $get(ent_me,hp)
	multiply CURRENT_HP 0.5
	hp CURRENT_HP
}

{ ice_shield_on
	//dbg Ice Shield Applied
	setvard ICE_SHIELDED 1
}

{ ice_shield_off
	//dbg Ice Shield Removed
	setvard ICE_SHIELDED 0
}

{ ice_shield_check

	if ( ICE_SHIELDED ) setvarg ICE_SHIELD_CHECK 1
	if ( !ICE_SHIELDED ) setvarg ICE_SHIELD_CHECK 0
	//dbg Ice Shield is now ( ICE_SHIELD_CHECK ) ( Was ICE_SHIELDED )
}

{ npc_fade_away

	//not always monster spawn friendly
	setalive 0
	deleteent ent_me fade
}

{ go_roam
	roam 1
}

{ no_roam
	roam 0
}

{ set_stun_prot

	setvard IMMUNE_STUN PARAM1
}

{ npc_suicide

	if $get(ent_me,isalive)

	if !I_R_COMPANION //never, ever, slay companions (if you need to, make new external)

	local SINCE_SPAWN game.time
	subtract SINCE_SPAWN NPC_SPAWN_TIME

	//if ( NPC_QUED_FOR_DEATH ) local PARAM1 dienow

	if ( SINCE_SPAWN < 2.0 )
	{
		if ( PARAM1	equals dienow )
		{
			setanim.framerate 0
			setanim.movespeed 0
			setvard NPC_OVERRIDE_DEATH 1
			deleteent ent_me fade
			local EXIT_SUB 1
		}
		else
		{
			setvard NPC_QUED_FOR_DEATH 1
			callevent 2.1 npc_suicide
			dbg npc_suicide - not_ready_to_die - qued for death
			local EXIT_SUB 1
		}
	}
	if !EXIT_SUB

	if ( PARAM1 equals no_pets )
	{
		if I_R_PET
		local EXIT_SUB 1
	}

	if ( PARAM1 equals only_bad )
	{
		if ( $get(ent_me,race) equals human ) local EXIT_SUB 1
		if ( $get(ent_me,race) equals hguard ) local EXIT_SUB 1
		if ( $get(ent_me,race) equals beloved ) local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( NPC_SILENT_SUICIDE ) setvard NPC_SILENT_DEATH 1
	if ( NPC_INVISIBLE_SUICIDE ) setorigin ent_me $vec(20000,-20000,-20000)

	invincible 0
	race hated
	setvard SKEL_RESPAWN_TIMES 99
	dodamage ent_me direct 99999 100 GAME_MASTER target
}

//newer
{ freeze_solid_start //PARAM1 = duration to freeze PARAM2 = attacker
	//this is to tell monsters that change their move/anim speeds not to do so while frozen

	if ( BASE_FRAMERATE equals 'BASE_FRAMERATE' ) setvard BASE_FRAMERATE 1.0

	local ICE_CAGE_DURATION PARAM1
	if ( NPCATK_TARGET equals unset )
	{
		setvard NPCATK_TARGET PARAM2
	}

	setvard ICE_CAGE_OLD_CANT_TURN CANT_TURN
	setvard ICE_CAGE_OLD_STUCK_CHECK NO_STUCK_CHECKS
	setvard ICE_CAGE_OLD_ROAM $get(ent_me,roam)
	dbg freeze_solid_start roamstate ICE_CAGE_OLD_ROAM
	
	setvard FREEZE_WAS_SUSPEND SUSPEND_AI
	if ( !SUSPEND_AI ) callevent npcatk_suspend_ai ICE_CAGE_DURATION

	setvard I_R_FROZEN 1
	setvard IN_ICECAGE 1
	setvard CANT_TURN 1
	setvard NO_STUCK_CHECKS 1
	roam 0

	setanim.framerate 0.0001
	//setanim.movespeed 0

	callevent ICE_CAGE_DURATION freeze_solid_end
}

{ freeze_solid_end //<unfroze_early:0|1>
	dbg thaw
	if I_R_FROZEN

	if ( PARAM1 equals 1 )
	{
		if !FREEZE_WAS_SUSPEND
		callevent npcatk_resume_ai
	}

	setvard I_R_FROZEN 0
	setvard IN_ICECAGE 0
	setvard CANT_TURN ICE_CAGE_OLD_CANT_TURN
	setvard NO_STUCK_CHECKS ICE_CAGE_OLD_STUCK_CHECK
	roam ICE_CAGE_OLD_ROAM
	setanim.framerate BASE_FRAMERATE
	//setanim.movespeed BASE_MOVESPEED
	playanim break
	if ( NEW_AI )
	{
		if ( NPCATK_TARGET isnot unset ) setmovedest NPCATK_TARGET ATTACK_MOVERANGE
	}
	else
	{
		if ( $get(HUNT_LASTTARGET,isalive) ) setmovedest HUNT_LASTTARGET MOVE_RANGE
	}
	//if ( CAN_HUNT ) playanim once ANIM_RUN
	//callevent 0.1 double_unfreeze
}

{ double_unfreeze
	dbg double_unfreeze
	playanim critical ANIM_ATTACK //cant seem to get them moving again ><
}

{ tally_race //PARAM1 = race to check (set all these G_'s 0 before calling)

	if ( game.monster.race equals orc ) add G_ORC_TALLY 1
	if ( game.monster.race equals rogue ) add G_ROGUE_TALLY 1
	if ( game.monster.race equals demon ) add G_DEMON_TALLY 1
	if ( game.monster.race equals undead ) add G_UNDEAD_TALLY 1
	if ( game.monster.race equals spider ) add G_SPIDER_TALLY 1
	if ( game.monster.race equals human ) add G_HUMAN_TALLY 1
	if ( game.monster.race equals hguard ) add G_HGUARD_TALLY 1
}

{ tally_enemy //PARAM1 = npc to check (setvarg G_ENEMY_TALLY 0 before calling)

	if $get(ent_me,relationship,PARAM1) equals enemy
	add G_ENEMY_TALLY 1
}


{ [server] send_damage //same params as do damage

	//temporary debug
//	if ( !$get(PARAM1,isalive) )
//	{
//		local OUT_PAR1 PARAM1
//		callevent debug_beam OUT_PAR1 PARAM2 (255,0,0)
//	}
	
	//if !SEND_DMG_DELAY
	//setvard SEND_DMG_DELAY 1
	//callevent 0.2 reset_send_dmg_delay
	////dbg send_damage PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7
	dodamage PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7
}

{ demon_blood //PARAM1 = ratio to increase dmg (def 5.0)

	if ( PARAM1 !startswith PARAM )
	{	
		if PARAM1 > 1
		setvard EXT_DEMON_BLOOD_RATIO PARAM1
	}

	playsound 0 10 monsters/troll/trollidle2.wav //precache?

	setvard MAKE_NOISE 2
	setvard DEMON_BLOOD 1

	if ( NPC_ADJ_FLAGS equals 'NPC_ADJ_FLAGS' ) setvard NPC_ADJ_FLAGS ''
	token.add NPC_ADJ_FLAGS demon_blood

	callevent 1.0 demon_blood_loop
}

{ demon_blood_loop

	if DEMON_BLOOD

	effect glow ent_me (255,0,0) 256 1.9 1.9

	if game.monster.hp > 11

	callevent 2.0 demon_blood_loop

	add MAKE_NOISE 1	
	if ( MAKE_NOISE > 5 )
	{
		playrandomsound 0 10 monsters/troll/trollidle2.wav monsters/troll/trollidle.wav
		setvard MAKE_NOISE 0
	}

	if ( !DEMON_NOHP_LOSS ) givehp DEMON_BLOOD_LOSS
    effect screenshake $relpos(0,0,0) 32 10 1 32
	if ( MAKE_NOISE > 0 ) playsound 0 10 player/heartbeat_noloop.wav
	setvard DEMON_BLOOD 1
}

{ game_damaged_other //PARAM1=target_hit PARAM2=dmg

	if DEMON_BLOOD
	if $get(PARAM1,relationship,ent_me) equals enemy

	return EXT_DEMON_BLOOD_RATIO

	//applyeffect ent_laststruck effects/generic_damage DEMON_LEVEL $get(ent_me,id) 1 magic
	//setdmg does not work here
	//local IN_DMG PARAM2
	//add IN_DMG DEMON_LEVEL
	//setdmg dmg IN_DMG
	//dbg game_damaged_other DEMON_LEVEL
	playrandomsound 0 10 monsters/troll/trollpain.wav monsters/troll/trollattack.wav
}

{ ext_hit_manaball
	//stop rapid damage stack from player manaballs

	if !HIT_BY_MANABALL
	setvard HIT_BY_MANABALL 1
	callevent 1.0 reset_hit_by_manaball
}

{ reset_hit_by_manaball
	setvard HIT_BY_MANABALL 0
}

{ ext_playsound_kiss
	playsound PARAM1 PARAM2 PARAM3
}

{ ext_svplaysound_kiss
	dbg ext_svplaysound_kiss PARAM1 PARAM2 PARAM3 PARAM4 PARAM5
	svplaysound PARAM1 PARAM2 PARAM3 PARAM4 PARAM5
}

{ ext_mon_playsound //<WAV> <(source)> [MaxRange] //sound must be precached

	if ( PARAM3 isnot 'PARAM3' )
	{
		local SOURCE_ORG PARAM2
		//dbg temp $dist(game.monster.origin,SOURCE_ORG) 
		if $dist(game.monster.origin,SOURCE_ORG) > PARAM3
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	setvard EXTPLAY_SOUND PARAM1
	callevent 0.1 ext_playsound2 //delay 0.1 seconds to stop flood
}

{ turn_undead //PARAM1=Dmg PARAM2=Exorcist

	//if !AM_SKELETON //base_skeleton users handle this different, should migrate

	if game.monster.isalive

	if !CUSTOM_TURN_UNDEAD

	//dbg temp got rebuke PARAM1 PARAM2

	if IMMUNE_HOLY != 1

	local EXT_INC_HOLY_DMG PARAM1
	local EXT_THE_EXCORCIST PARAM2

	effect glow ent_me (255,255,0) 512 1 1

	setvard STRUCK_BY_HOLY 1
	setvard SKELE_TURNED 1
	setvard STRUCK_HOLY 1

	//callexternal EXT_THE_EXCORCIST send_damage $get(ent_me,id) direct EXT_INC_HOLY_DMG 100 EXT_THE_EXCORCIST holy

	xdodamage $get(ent_me,id) direct EXT_INC_HOLY_DMG 100 EXT_THE_EXCORCIST EXT_THE_EXCORCIST spellcasting.divination holy_effect

	//something about the external damage on this fubars things sometimes (causes death anim to lock)
	//fade monsters killed by this as work around:
	if ( EXT_INC_HOLY_DMG > game.monster.hp ) setvard EXT_FADE_ON_DEATH 1

	if I_AM_TURNABLE

	//determined turned

	multiply TURN_STRENGTH 7

	if ( TURN_STRENGTH > 1000 ) local TURN_STRENGTH 1000 	//cap at 1000 (no turning bosses)
	
	if !IS_FLEEING			//can't turn already turned

	if TURN_STRENGTH > MY_CURRENT_HP

	local TURN_RESISTANCE MY_MAX_HP
	divide TURN_RESISTANCE 25
	local TURN_RESISTANCE $int(TURN_RESISTANCE)

	if ( TURN_RESISTANCE < 2 ) local TURN_RESISTANCE 2

	local TURNCHANCE $rand(1,TURN_RESISTANCE)

	//dbg MyTurnResistance TURN_RESISTANCE mymaxhp MY_MAX_HP Rolled TURNCHANCE
	if TURNCHANCE == 1

	//Minimum Turn Duration 5 seconds, max 20
	local TURN_DURATION $get(THE_EXCORCIST,skill.spellcasting.divination)
	if ( TURN_DURATION < 5 ) local TURN_DURATION 5
	if ( TURN_DURATION > 15 ) local TURN_DURATION 15

	//Thothie - RUN AWAY!
	setvard FLEE_DISTANCE 2048

	volume 10
	playrandomsound 2 SOUND_TURNED1 SOUND_TURNED2 SOUND_TURNED3 SOUND_TURNED4

	setvard CAN_FLEE 1
	callevent npcatk_flee THE_EXCORCIST FLEE_DISTANCE TURN_DURATION
}

#include [server] monsters/debug


{ [server] ext_speak //<"text">
	saytextrange 2048
	saytext PARAM1
}

{ [server] give_hp //PARAM1 = amont to give, can be negative
	givehp PARAM1
}

//{ [server] ext_return_relpos //used to return position relative to others
//	local MY_ORG game.monster.origin
//	vectoradd MY_ORG $relpos(PARAM1,PARAM2)
//	setvarg G_RELPOS MY_ORG
//}

{ [server] ext_invalidate //<PARAM1> 1|0 - marks target as invalid
	setvard PLAYING_DEAD PARAM1	
}

{ [server] ext_dodamage //same params as do damage

	dodamage PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7
}

{ [server] ext_set_freeze_amt
	//this is used to determine the current amount of freeze magic on the target
	setvard EFFECT_FREEZE_AMT PARAM1
}

{ ext_flash_bang //PARAM1 = origin PARAM2 = radius PARAM3 = source_owner
	if $dist(game.monster.origin,PARAM1) < PARAM2
	callevent npcatk_flee $get(PARAM3,id) 1024 5.0
}

{ speed_x2

	//increase move and atk speeds
	movespeed 1.5
	setanim.movespeed 1.5
	setanim.framerate 1.5
	setvard BASE_MOVESPEED 1.5
	setvard BASE_FRAMERATE 1.5
	if ( NPC_ADJ_FLAGS equals 'NPC_ADJ_FLAGS' ) setvard NPC_ADJ_FLAGS ''
	token.add NPC_ADJ_FLAGS speed_x2
}

{ speed_x2_5

	//increase move and atk speeds
	movespeed 1.75
	setanim.movespeed 1.75
	setanim.framerate 1.75
	setvard BASE_MOVESPEED 1.75
	setvard BASE_FRAMERATE 1.75
	if ( NPC_ADJ_FLAGS equals 'NPC_ADJ_FLAGS' ) setvard NPC_ADJ_FLAGS ''
	token.add NPC_ADJ_FLAGS speed_x2
}

{ speed_x3

	//increase move and atk speeds
	movespeed 2.0
	setanim.movespeed 2.0
	setanim.framerate 2.0
	setvard BASE_MOVESPEED 2.0
	setvard BASE_FRAMERATE 2.0
	if ( NPC_ADJ_FLAGS equals 'NPC_ADJ_FLAGS' ) setvard NPC_ADJ_FLAGS ''
	token.add NPC_ADJ_FLAGS speed_x3
}

{ speed_x4

	//increase move and atk speeds
	movespeed 3.0
	setanim.movespeed 3.0
	setanim.framerate 3.0
	setvard BASE_MOVESPEED 3.0
	setvard BASE_FRAMERATE 3.0

	if ( NPC_ADJ_FLAGS equals 'NPC_ADJ_FLAGS' ) setvard NPC_ADJ_FLAGS ''
	token.add NPC_ADJ_FLAGS speed_x4
}

{ critical_npc

	//for siege mode maps
	invincible 0
	setvard NPC_CRITICAL 1
	token.add G_CRITICAL_NPCS $get(ent_me,id)
	setvarg G_SIEGE_MAP 1
	local FIRST_TOKEN $get_token(G_CRITICAL_NPCS,0)
	if ( !$get(FIRST_TOKEN,isalive) ) token.del G_CRITICAL_NPCS 0 //fix buggy token creation
}

//prevents DOT effects from stacking into crash (see effects/effect_base)
{ ext_set_effect_lastdmg
	setvard EXT_LAST_EFFECT_DMG game.time
}

//No longer required, but still called, in case you want script to do something about it
{ ext_monsterspawn_removed
	if $get(ent_me,spawner) equals PARAM1
	local L_MAP_NAME $lcase(game.map.name)
	if ( L_MAP_NAME contains helena ) callevent npc_suicide dienow
	//if ( L_MAP_NAME equals walltest ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals aleyesu ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals b_castle ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals bloodrose ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals calruin2 ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals cleicert ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals demontemple ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals foutpost ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals gatecity ) callevent npc_suicide dienow
	if ( L_MAP_NAME contains gertenheld ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals keledrosruins ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals kfortress ) callevent npc_suicide dienow
	if ( L_MAP_NAME contains lodagond ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals ms_wicardoven ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals mscave ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals shad_palace ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals sorc_villa ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals the_keep ) callevent npc_suicide dienow
	if ( L_MAP_NAME contains islesofdread ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals the_wall ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals umulak ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals ww1 ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals ww2b ) callevent npc_suicide dienow
	if ( L_MAP_NAME equals ww3d ) callevent npc_suicide dienow

	if ( NPC_DIE_ON_SPAWN_REMOVAL ) callevent npc_suicide dienow
	
	//deleteent ent_me fade
	//callevent npc_suicide override
}

{ glow_red
	setvard NPC_GLOW (255,0,0)
}

{ glow_green
	setvard NPC_GLOW (0,255,0)
}

{ glow_blue
	setvard NPC_GLOW (0,0,255)
}

{ glow_yellow
	setvard NPC_GLOW (255,255,0)
}

{ glow_purple
	setvard NPC_GLOW (255,0,255)
}

{ glow_custom
	setvard NPC_GLOW PARAM1
}

{ glow_remove
	setvard NPC_GLOW 'NPC_GLOW'
}

{ remove_ghost
	setvard NPC_GHOST 0
}

{ make_ghost
	setvard NPC_GHOST 1
}

{ npc_post_spawn

	if ( NPC_GHOST )
	{
		setprop ent_me rendermode 5
		setprop ent_me renderamt 255
		callevent 10.1 npcatk_reset_ghost
	}


	if ( NPC_GLOW isnot 'NPC_GLOW' )
	{
		dbg npc_post_spawn [externals] initiated glow NPC_GLOW
		effect glow ent_me NPC_GLOW 64 20 0	
		callevent 20.1 npcatk_reset_glow
	}
}

//These render effects are reset priodically as some effects can remove or conflict with them
{ npcatk_reset_glow

	if NPC_GLOW isnot 'NPC_GLOW'
	effect glow ent_me NPC_GLOW 64 20 0	
	callevent 20.1 npcatk_reset_glow
}

{ npcatk_reset_ghost
	
	if NPC_GHOST
	setprop ent_me rendermode 5
	setprop ent_me renderamt 255
	callevent 10.1 npcatk_reset_ghost
}

{ set_race
	race PARAM1
}

{ set_model
	setmodel PARAM1
	setsolid box
	setbbox npcsize
}

{ ext_report_armor
	consolemsg PARAM2 $get(ent_me,name) Armor: MSC_ARMOR_ALL PARAM1 : $get_takedmg(ent_me,PARAM1)
}

{ ext_set_parry //add param SEP2009_17 - set a monster's parry skill
	setstat parry PARAM1
}

{ ext_blind
	blind 1
}

{ ext_unblind
	blind 0
}

{ game_drain_death //PARAM1 = drainer
	//dbg DRAIN DEATH
	dodamage ent_me direct PARAM1 100 100% PARAM1 target
}

//{ ext_nopush
//
//	scriptflags ent_me add ext_nopush nopush PARAM1
//	//dbg nopush set to PARAM1
//}
//
//{ ext_nopush_on
//	scriptflags ent_me add ext_nopush nopush 1 PARAM2 none
//	//if ( PARAM2 > 0 ) callevent PARAM2 ext_nopush_off //seems this should be PARAM1 though
//}
//
//{ ext_nopush_off
//	nopush 0
//}

{ ext_wink_out
	//teleport off map, return to point PARAM1 in PARAM2 seconds
	setorigin ent_me (20000,10000,-20000)
	setvard NPC_WINK_IN_POINT PARAM1
	setvard NPC_WINKED_OUT 1
	callevent PARAM2 ext_wink_in
}

{ ext_wink_in
	//return from ext_wink_out
	setorigin ent_me NPC_WINK_IN_POINT
	setvard NPC_WINKED_OUT 0
}

{ ext_set_slowed
	setvard I_R_SLOWED PARAM1
}


{ make_boss
	dbg make_boss
	setvard NPC_IS_BOSS 1
	setvard NPC_BOSS_KNOWS ''
	setvard NPC_BOSS_KNOWS_AMTS ''
}

{ ext_makepet
	setprop ent_me rendermode 5
	setprop ent_me renderamt 0
	setvard NPC_SILENT_DEATH 1
	callevent 0.01 npc_suicide
	createnpc NPC_PET_SCRIPT $relpos(0,0,0) $get(PARAM1,id) 1
}

{ ext_set_health
	hp PARAM1 PARAM2
}

//{ ext_return_relpos_vec //return an adjusted self-relative position SEP2009_17 - fixed, was useless old way as was using coord type $relpos()
//	setvard EXT_RELPOS $relpos(PARAM1,PARAM2,PARAM3)
//}

//Thothie FEB2009 - external addparam to flag monster not to hunt for critical NPCs
{ ignore_critical_npc
	setvard NPC_NO_SIEGE_HUNT 1
}

//Thothie FEB2009_18 - external for setting hit chance penalties
{ ext_hit_chance
	hitmulti ent_me PARAM1
}

//Thothie FEB2009_19 - allow frozen status from things other than freeze_solid
{ ext_set_frozen
	setvard I_R_FROZEN 1
//	setvard ORIG_NOPUSH $get(ent_me,nopush)
//	nopush 1
	scriptflags ent_me add ext_set_frozen nopush 1 PARAM1 none
	callevent PARAM1 ext_set_unfrozen
}

{ ext_set_unfrozen
	setvard I_R_FROZEN 0
	//nopush ORIG_NOPUSH
}

//Thothie APR2009 - allow unsommoning of all NPC's summons, globally
{ ext_unsummon
	if AM_SUMMONED
	setalive 0
	deleteent ent_me fade
}

{ ext_sphere_token_x //TYPE_SCAN SCAN_RANGE
	setvard PLR_SCAN_TOKEN $get_tsphere(PARAM1,PARAM2)
	//dbg ext_sphere_token_x PLR_SCAN_TOKEN
}

{ ext_sphere_token //TYPE_SCAN SCAN_RANGE SCAN_ORIGIN
	setvard PLR_SCAN_TOKEN $get_tsphere(PARAM1,PARAM2,PARAM3)
	//dbg ext_sphere_token PLR_SCAN_TOKEN
}

{ ext_box_token //TYPE_SCAN SCAN_RANGE SCAN_ORIGIN
	setvard PLR_SCAN_TOKEN $get_tbox(PARAM1,PARAM2,PARAM3)
	//dbg ext_box_token PLR_SCAN_TOKEN
}

{ ext_reduct_xp //add param for monster placements that are exploitable, etc
	if PARAM1 < 1
	if ( NPC_EXP_REDUCT equals 'NPC_EXP_REDUCT' )
	{
		setvard NPC_EXP_REDUCT PARAM1
	}
	else
	{
		subtract NPC_EXP_REDUCT PARAM1 //in case XP reduced for more than one reason
	}
	//callevent 1.5 ext_reduct_xp2 //delay to give chance for monsters to set base XP
}
//
//{ ext_reduct_xp2
//	if NPC_EXP_REDUCT < 1.0
//	multiply NPC_GIVE_EXP NPC_EXP_REDUCT
//	if ( DROP_GOLD ) multiply DROP_GOLD_AMT NPC_EXP_REDUCT
//	skilllevel NPC_GIVE_EXP
//	dbg ext_reduct_xp NPC_EXP_REDUCT NPC_GIVE_EXP
//}

{ ext_no_drops //add param to remove drops for exploitable monsters
	setvard NPC_NO_DROPS 1
	callevent 1.5 ext_no_drops2 //delay to give chance for monsters to set treasure
}

{ ext_no_drops2
	gold 0
	setvard DROP_GOLD 0
	setvard DROP_ITEM1_CHANCE 0%
	setvard DROP_ITEM2_CHANCE 0%
	setvard DROP_ITEM3_CHANCE 0%
	setvard DROP_ITEM4_CHANCE 0%
	setvard CONTAINER_DROP_CHANCE 0%
}

//Jelly's external scan+sort
{ ext_ent_list_sort //SCAN_TYPE SCAN_RANGE SCAN_ORIGIN SORT_TYPE:range|hp|maxhp|mp|maxmp
   local SCAN_TYPE PARAM1
   local SCAN_RANGE PARAM2
   local SCAN_ORIGIN PARAM3
   local SORT_TYPE PARAM4
   callevent ext_sphere_token SCAN_TYPE SCAN_RANGE SCAN_ORIGIN
   setvard PLR_SCAN_TOKEN_SORTED $sort_entlist(PLR_SCAN_TOKEN,SORT_TYPE)
}

//OCT2009 - Giving monsters resist hold over time too (also helps prevent stack crashes)
{ ext_set_last_resist
	setvard PLR_LAST_RESIST_TIME game.time
	setvard PLR_LAST_RESIST_ELM PARAM1
}

//DEC2009 - Option to make monster invulnerable to player damage (good for guards / crit NPCs)
{ ext_no_player_damage
	setvard NPC_NO_PLAYER_DMG 1
}

//Failed JAN2010 spariment
{ ext_setrender
	dbg got ext_setrender PARAM1
	setrender PARAM1
}

{ set_scale_nr //<ratio> - scale with no reach change
	local L_PASS PARAM1
	callevent ext_scale L_PASS 0 1
}

{ set_scale_nb //<ratio> - scale with no bbox change
	local L_PASS PARAM1
	callevent ext_scale L_PASS 1 0
}

{ set_scale_nbr //<ratio> - scale with no bbox or reach change
	local L_PASS PARAM1
	callevent ext_scale L_PASS 1 1
}

{ ext_scale //<ratio> [no_bounding_fix:0|1] [no_range_fix:0|1]

	//scales model, bounding box, and attack ranges
	dbg ext_scale PARAM1 PARAM2 PARAM3

	if PARAM1 > 0

	local MY_WIDTH $get(ent_me,width)
	local MY_HEIGHT $get(ent_me,height)

	if ( NPC_ORG_WIDTH equals 'NPC_ORG_WIDTH' )
	{
		setvard NPC_ORG_WIDTH MY_WIDTH
		setvard NPC_ORG_HEIGHT MY_HEIGHT
	}
	else
	{
		local MY_WIDTH NPC_ORG_WIDTH
		local MY_HEIGHT NPC_ORG_HEIGHT
	}

	setprop ent_me scale PARAM1
	multiply MY_WIDTH PARAM1
	multiply MY_HEIGHT PARAM1

	//most msc mobs can't really deal with being smaller than this
	if ( MY_WIDTH < 16 ) local MY_WIDTH 16
	if ( MY_HEIGHT < 16 ) local MY_HEIGHT 16

	width MY_WIDTH
	height MY_WIDTH
	setvard MONSTER_WIDTH MY_WIDTH

	setvard NPC_MODEL_SCALED PARAM1
	setvard NPC_USES_HANDLE_EVENTS 1

	local L_HALF_W MY_WIDTH
	multiply L_HALF_W 0.5

	//thus func tends to fubar things
	if ( PARAM2 != 1 ) setbbox $vec($neg(L_HALF_W),$neg(L_HALF_W),0) $vec(L_HALF_W,L_HALF_W,MY_HEIGHT)

	if ( PARAM1 > 1 )
	{
		//bigger = longer strides (otherwise run anims look weird)
		if ( BASE_MOVESPEED equals 'BASE_MOVESPEED' )
		{
			setvard BASE_MOVESPEED PARAM1
			setvard NPC_ORG_BASE_MOVESPEED BASE_MOVESPEED
		}
		else
		{
			if ( NPC_ORG_BASE_MOVESPEED equals 'NPC_ORG_BASE_MOVESPEED' )
			{
				setvard BASE_MOVESPEED NPC_ORG_BASE_MOVESPEED
			}
			else
			{
				setvard NPC_ORG_BASE_MOVESPEED BASE_MOVESPEED
			}
			add BASE_MOVESPEED PARAM1
		}
		setanim.movespeed BASE_MOVESPEED
		movespeed BASE_MOVESPEED
	}

	setvard NPC_MODEL_SCALED PARAM1

	if PARAM3 != 1
	if ( !NPC_RANGED )
	{
		dbg ext_scale adjusting ranges
		callevent 1.9 ext_adjust_scale_range
	}
	else
	{
		dbg ext_scale not changing reach, mob is ranged
	}
}

{ set_scale
	local L_PASS PARAM1
	callevent ext_scale L_PASS
}

{ ext_adjust_scale_range
	//local RANGE_ADJ_RATIO NPC_SCALED
	//if PARAM1 > 0
	//multiply ATTACK_RANGE RANGE_ADJ_RATIO
	//multiply ATTACK_HITRANGE RANGE_ADJ_RATIO
	//multiply MOVE_RANGE RANGE_ADJ_RATIO
	//multiply ATTACK_MOVERANGE RANGE_ADJ_RATIO

	dbg ext_adjust_scale_range entered

	local L_CUR_WIDTH $get(ent_me,width)
	local L_CUR_HEIGHT $get(ent_me,height)

	if ( NPC_ORG_RANGE equals 'NPC_ORG_RANGE' ) setvard NPC_ORG_RANGE ATTACK_RANGE
	if ( NPC_ORG_HITRANGE equals 'NPC_ORG_HITRANGE' ) setvard NPC_ORG_HITRANGE ATTACK_HITRANGE

	setvard ATTACK_RANGE NPC_ORG_RANGE
	setvard ATTACK_HITRANGE NPC_ORG_HITRANGE

	if ( ATTACK_RANGE >= 512 )
	{
		dbg ext_adjust_scale_range base range > 512 - npc ranged without flag?
	}

	if ATTACK_RANGE < 512 //else probably a ranged NPC, without NPC_RANGED flag

	local L_MIN_RANGE L_CUR_WIDTH
	if ( L_CUR_WIDTH < L_CUR_HEIGHT ) local L_MIN_RANGE L_CUR_HEIGHT
	if ( NPC_MODEL_SCALED > 1 ) multiply L_MIN_RANGE 0.85 //scaling up seems to be giving slightly insane reach
	if ( L_MIN_RANGE < 38 ) local L_MIN_RANGE 38 //min half player height
	setvard ATTACK_RANGE L_MIN_RANGE
	multiply ATTACK_RANGE 1.5
	setvard ATTACK_HITRANGE L_MIN_RANGE
	multiply ATTACK_HITRANGE 2.0

	setvard NPC_SET_RANGE ATTACK_RANGE //future use, to detect range changes
	setvard NPC_SET_RANGE_RATIO ATTACK_RANGE //future use, so we can adjust dynamic ranges
	divide NPC_SET_RANGE_RATIO NPC_ORG_ATTACK_RANGE

	dbg ext_adjust_scale_range ATTACK_RANGE ATTACK_HITRANGE
}

{ set_attack_until_spotted
	//simulates hacking on an object until spots or spotted by player
	setvard NPC_ATTACK_UNTIL_SPOTTED 1
}

{ npc_post_spawn
	if NPC_ATTACK_UNTIL_SPOTTED
	callevent npcatk_attack_till_spotted
}

{ npcatk_attack_till_spotted
	if NPCATK_TARGET equals unset
	callevent 0.25 npcatk_attack_till_spotted
	playanim once ANIM_ATTACK
	setvard AS_ATTACKING game.time
	add AS_ATTACKING 5.0
	local FACE_SPOT NPC_HOME_LOC
	local FACE_DIR $vec.yaw(NPC_HOME_ANG)
	vectoradd FACE_SPOT $relpos($vec(0,FACE_DIR,0),$vec(0,256,0))
	setvard NPC_FORCED_MOVEDEST 1
	setmovedest FACE_SPOT 9999
}

{ set_fade_in //thought I had this, guess not
	if !NPC_FADEIN_SET
	setvard NPC_FADEIN_SET 1
	setvard NPC_RENDER_AMT 0
	setvard NPC_RENDER_MODE 2
	setprop ent_me renderamt NPC_RENDER_AMT
	setprop ent_me rendermode NPC_RENDER_MODE
	
	local L_FADE_DELAY PARAM1
	if ( L_FADE_DELAY == 0 ) local L_FADE_DELAY 0.1

	setvard NPC_FADEIN_RATE PARAM1
	if ( NPC_FADEIN_RATE < 10 ) setvard NPC_FADEIN_RATE 10
	callevent L_FADE_DELAY set_fade_in_loop
}

{ set_fade_in_loop
	add NPC_RENDER_AMT 10

	if ( NPC_RENDER_AMT < 255 )
	{
		setprop ent_me renderamt NPC_RENDER_AMT
		callevent NPC_FADE_IN_SPEED set_fade_in_loop
	}
	else
	{
		setprop ent_me renderamt 255
		setprop ent_me rendermode 0
		setvard NPC_RENDER_AMT 255
		setvard NPC_RENDER_MODE 0
		callevent npc_fadein_done
	}
}

//FEB2010_06 toggle step adj on individual monsters
{ set_no_step_adj
	setvard NO_STEP_ADJ 1
}

//FEB2010_06 global set turret function
{ set_npc_turret
	movespeed 0.0
	setvard BASE_MOVESPEED 0.0
	setmoveanim ANIM_IDLE
	roam 0
	setvard NO_STUCK_CHECKS 1
	setvard ANIM_RUN ANIM_IDLE
	setvard ANIM_WALK ANIM_IDLE
	setvard NPC_IS_TURRET 1
	callevent 0.1 set_npc_turret2
}

{ set_npc_turret2
	//enforcer - case we got set_npc_turret during load time
	setvard ANIM_RUN ANIM_IDLE
	setvard ANIM_WALK ANIM_IDLE
	setmoveanim ANIM_IDLE
}

{ set_summon_circle

	callevent set_fade_in 1.0
	if ( !NPC_HANDLES_SUMMON_CIRCLES ) setanim.framerate 0
	callevent 0.1 set_summon_circle2
}

{ set_summon_circle2
	if ( !NPC_HANDLES_SUMMON_CIRCLES )
	{
		callevent npcatk_suspend_ai
		setanim.framerate 0 //case variant in npc_spawn
	}
	local CIRCLE_ORG $get(ent_me,origin)
	vectorset CIRCLE_ORG z $get_ground_height(CIRCLE_ORG)
	if ( $get(ent_me,height) < 90 )
	{
		clientevent new all effects/sfx_summon_circle CIRCLE_ORG 3
	}
	else
	{
		clientevent new all effects/sfx_summon_circle CIRCLE_ORG 5
	}
	callevent 1.0 set_summon_circle3
}

{ set_summon_circle3
	setvard AS_ATTACKING game.time
	add AS_ATTACKING 5.0
	if !NPC_HANDLES_SUMMON_CIRCLES
	callevent npcatk_resume_ai
	if ( BASE_FRAMERATE equals 'BASE_FRAMERATE' )
	{
		setanim.framerate 1.0
	}
	else
	{
		setanim.framerate BASE_FRAMERATE
	}
}

{ set_takedmg_holy //add param
	takedmg holy PARAM1
}

{ set_stepsize
	stepsize PARAM1
}

{ ext_scarab_latch
	add SCARABS_ATTACHED PARAM1
	if ( SCARABS_ATTACHED < 0 ) setvard SCARABS_ATTACHED 0
}

{ ext_hold_person //<duration>
	setvard I_R_HELD 1
	local L_HOLD_TIME PARAM1
	scriptflags ent_me add hold_person nopush 1 PARAM1 none
	callevent PARAM1 hold_person_end
}

{ hold_person_end
	setvard I_R_HELD 0
}

{ ext_playanim
	dbg ext_playanim PARAM1
	playanim critical PARAM1
}

//{ ext_sethp
//	hp PARAM1
//}

{ bs_global_command //<param1> - called globally when player dies
	if PARAM3 equals death
	if $get(ent_me,isalive)
	//clear XP from player who died
	dbg bs_global_command clearplayerhits $get(PARAM1,name)
	if !NPC_IS_BOSS
	clearplayerhits ent_me PARAM1
}

{ ext_dbg
	if ( PARAM1 isnot off )
	{
		setvard EXT_DEBUG_PARAM PARAM1
		setvard EXT_DBG_LOOP_ON 1
		callevent ext_dbg_loop
	}
	else
	{
		setvard EXT_DBG_LOOP_ON 0
	}
}

{ ext_dbg_loop
	if EXT_DBG_LOOP_ON
	callevent 0.1 ext_dbg_loop
	dbg $get(ent_me,EXT_DEBUG_PARAM)
}

{ set_blind_attack
	setvard NPC_MUST_SEE_TARGET 0
}

{ ext_spawn_sound
	setvard NPC_USES_HANDLE_EVENTS 1
	setvard NPC_DO_SPAWN_SOUND 1
}

{ setfx_spawn_sound
	//convention alias
	callevent ext_spawn_sound
}

{ setfx_summon_circle
	//convention alias
	callevent set_summon_circle
}

{ setfx_fade_in
	//convention alias
	callevent set_fade_in
}

{ setfx_tele_in

	callevent set_fade_in 20
	callevent 0.01 setfx_tele_in2
}

{ setfx_tele_in2
	local MY_POS_GROUND $get(ent_me,origin)
	vectorset MY_POS_GROUND z $get_ground_height(MY_POS_GROUND)
	clientevent new all effects/sfx_repulse_burst MY_POS_GROUND 128 1.0
}

{ set_no_player_damage
	///alias for ext_no_player_damage
	setvard NPC_NO_PLAYER_DMG 1
}

{ ext_setmodelbody
	setmodelbody PARAM1 PARAM2
}

//{ ext_anim_exists
//	dbg ext_animexists $anim_exists(PARAM1)
//}

{ set_die_on_spawn_removed
	setvard NPC_DIE_ON_SPAWN_REMOVAL 1
}

{ set_dosr
	setvard NPC_DIE_ON_SPAWN_REMOVAL 1
}

{ set_no_roam
	setvard NPC_NO_ROAM 1
	roam 0
}

{ set_roam
	if ( PARAM1 startswith PARAM )
	{
		setvard NPC_FORCE_ROAM 1
		roam 1
	}
	else
	{
		//using equals here, as addparams get streamed
		if ( PARAM1 equals 1 )
		{
			setvard NPC_FORCE_ROAM 1
			roam 1
		}
		else if ( PARAM1 equals 0 )
		{
			setvard NPC_NO_ROAM 1
			roam 0
		}
	}
}

{ trig_damage //<name>;<dmg>;<dmg_type> - called by a map brush with scriptevent
	//local TRIG_NAME PARAM1
	local TRIG_DMG PARAM2
	local TRIG_DMG_TYPE PARAM3
	stradd TRIG_DMG_TYPE _effect
	xdodamage ent_me direct TRIG_DMG 100% GAME_MASTER GAME_MASTER none TRIG_DMG_TYPE
}

{ ext_webbed //webber
	//consider this an attack
	xdodamage ent_me direct 1.0 100% PARAM1 PARAM1 spellcasting.affliction magic

	if ( WEBS_ATTACHED == 0 )
	{
		setvard WEBS_ATTACHED 0 //init
		callevent 5.0 ext_webs_decay
	}
	if ( !$get_scriptflag(ent_me,spider_resist,type_exists) ) add WEBS_ATTACHED 1
	if ( !NPC_WEB_REFRESH_LOOP )
	{
		setvard NPC_WEB_REFRESH_LOOP 1
		callevent web_update_loop
	}

	playsound 0 10 bullchicken/bc_acid2.wav
}

{ web_update_loop
	if NPC_WEB_REFRESH_LOOP
	callevent 1.0 web_update_loop

	if ( game.time > NPC_NEXT_WEB_CL_REFRESH ) local L_REFRESH 1
	if ( NPC_WEB_CL_IDX equals 'NPC_WEB_CL_IDX' ) local L_REFRESH 1

	if ( L_REFRESH )
	{
		clientevent new all effects/sfx_webbed $get(ent_me,index) WEBS_ATTACHED
		setvard NPC_WEB_CL_IDX game.script.last_sent_id
		setvard NPC_NEXT_WEB_CL_REFRESH game.time
		add NPC_NEXT_WEB_CL_REFRESH 19.0
	}
	else
	{
		if NPC_OLD_WEBS != WEBS_ATTACHED
		clientevent update all NPC_WEB_CL_IDX fx_web_set_webs WEBS_ATTACHED
		setvard NPC_OLD_WEBS WEBS_ATTACHED
	}
}

{ ext_webs_remove
	setvard WEBS_ATTACHED 0
	setvard NPC_WEB_REFRESH_LOOP 0
	if ( NPC_WEB_CL_IDX isnot 'NPC_WEB_CL_IDX' ) 
	{
		clientevent update all NPC_WEB_CL_IDX end_fx
		setvard NPC_WEB_CL_IDX 'NPC_WEB_CL_IDX'
	}
}

{ ext_webs_decay
	//dbg ext_webs_decay WEBS_ATTACHED
	subtract WEBS_ATTACHED 1
	if ( WEBS_ATTACHED > 0 )
	{
		callevent 5.0 ext_webs_decay
		clientevent update all NPC_WEB_CL_IDX fx_web_set_webs WEBS_ATTACHED
	}
	else
	{
		callevent ext_webs_remove
	}
}

{ ext_set_cocooned
	//dbg ext_set_cocooned PARAM1
	callevent ext_webs_remove
	setvard COCOON_ATTACHED PARAM1
	if ( PARAM1 )
	{
		setvard I_R_FROZEN 1
		setvard IN_ICECAGE 1
		setvard CANT_TURN 1
		setvard NO_STUCK_CHECKS 1
		setvard NPC_ORIG_ROAM $get(ent_me,roam)
		roam 0
	}
	else
	{
		setvard I_R_FROZEN 0
		setvard IN_ICECAGE 0
		setvard CANT_TURN 0
		setvard NO_STUCK_CHECKS 0
		roam NPC_ORIG_ROAM
	}
}

{ set_self_adj //[adj_down]
	dbg set_self_adj PARAM1
	if !NPC_SELF_ADJUST
	setvard NPC_SELF_ADJUST 1
	if ( PARAM1 startswith 'PARAM' ) local NO_ADJ 1
	if !NO_ADJ
	if PARAM1 > 0
	//used to cap out adjustment on higher level beasties
	setvard NPC_ADJ_DOWN PARAM1
}

{ set_no_avg
	//don't add this monster to the level averages (good for monsters encountered early in map, where not everyone may have joined yet)
	setvard NPC_SELF_ADJUST_NOAVG 1
}

{ set_poisonous
	//setvard NPC_POISONOUS 1
	//setvard NPC_GLOW (0,255,0)
	local L_PASS PARAM1
	callevent add_dot_poison L_PASS
}

{ add_dot_poison //[ratio]
	setvard NPC_DOT_POISON 1
	setvard NPC_DOT_POISON_RATIO 1.0
	if ( PARAM1 > 0 ) setvard NPC_DOT_POISON_RATIO PARAM1
	setvard NPC_GLOW (0,255,0)
}

{ add_dot_cold //[ratio]
	setvard NPC_DOT_COLD 1
	setvard NPC_DOT_COLD_RATIO 1.0
	if ( PARAM1 > 0 ) setvard NPC_DOT_COLD_RATIO PARAM1
	setvard NPC_GLOW (0,128,255)
}

{ add_dot_fire //[ratio]
	setvard NPC_DOT_FIRE 1
	setvard NPC_DOT_FIRE_RATIO 1.0
	if ( PARAM1 > 0 ) setvard NPC_DOT_FIRE_RATIO PARAM1
	setvard NPC_GLOW (255,64,0)
}

{ add_dot_lightning //[ratio]
	setvard NPC_DOT_LIGHTNING 1
	setvard NPC_DOT_LIGHTNING_RATIO 1.0
	if ( PARAM1 > 0 ) setvard NPC_DOT_LIGHTNING_RATIO PARAM1
	setvard NPC_GLOW (255,255,0)
}

{ set_no_auto_activate //don't cycle up instantly based on player prox on spawn
	setvard NPC_NO_AUTO_ACTIVATE 1
}

{ set_non_agro
	setvard NPC_NO_AGRO 1
	callevent npcatk_suspend_ai
}

{ ext_set_goblin_latched
	setvard I_R_GOBLIN_LATCHED PARAM1
}

{ setfx_sprite_in
	//make sure c-tele1.spr is precached somewhere on the map or scripts - it isn't precached by default
	setvard NPC_USES_HANDLE_EVENTS 1
	setvard NPC_SPRITE_IN 1
	callevent set_fade_in
}

{ setfx_sprite_inx
	//uses xflare1.spr as it's already cached
	setvard NPC_USES_HANDLE_EVENTS 1
	setvard NPC_SPRITE_IN 2
	callevent set_fade_in
}

{ set_summon
	//for global npc summon counting
	setvard NPC_SUMMON 1
	add G_NPC_SUMMON_COUNT 1
	dbg set_summon G_NPC_SUMMON_COUNT
}

{ ext_summon_fade
	if NPC_SUMMON
	subtract G_NPC_SUMMON_COUNT 1
	dbg ext_summon_fade G_NPC_SUMMON_COUNT
	setanim.framerate 0
	setanim.movespeed 0
	gravity 0
	setvelocity ent_me (0,0,0)
	setvard NPC_DID_DEATH 1
	setvard NPC_OVERRIDE_DEATH 1
	setvard SKEL_RESPAWN_TIMES 99
	dodamage ent_me direct 99999 100 GAME_MASTER target
	deleteent ent_me fade
}

{ set_fadein_delayed
	callevent set_fade_in 1.0
}

{ setfx_beam_in
	callevent set_fade_in
	local BEAM_START $get(ent_me,origin)
	vectorset BEAM_START z $get_ground_height(BEAM_START)
	local BEAM_END BEAM_START
	vectoradd BEAM_END z 512
	effect beam point lgtning.spr 90 BEAM_START BEAM_END (64,128,255) 200 20 2.0
	playsound 0 10 weather/Storm_exclamation.wav
}

{ set_push_resist
	setvard MSC_PUSH_RESIST PARAM1
}

//nerf powers monster's XP to reach max over time (case big xp mob close to start) [cals from map start]
//ie. if you attack this mob 10 minutes in, and <minstomax> is set to 20, you'll get 50% xp
{ set_xp_tr //<minutes to max XP>
	setvard NPC_XPTR 1
	setvard NPC_XPTR_TIME PARAM1
	multiply NPC_XPTR_TIME 60.0
	if ( NPC_XPTR_TIME > game.time )
	{
		dbg set_xp_tr NPC_XPTR_TIME
		callevent 2.0 set_xp_timeramp_start
	}
	else
	{
		dbg set_xp_tr [time elapsed]
	}
}

{ set_xp_timeramp_start
	setvard NPC_XPTR_MAXXP NPC_GIVE_EXP //all XP mods should be done by now
	callevent set_xp_timeramp_loop
	dbg set_xp_timeramp_start TIME_RATIO [ NPC_XPTR_TIME ]
}

{ set_xp_timeramp_loop
	if NPC_XPTR
	callevent 60.0 set_xp_timeramp_loop
	local L_TIME_RATIO game.time
	divide L_TIME_RATIO NPC_XPTR_TIME
	if ( L_TIME_RATIO > 1 )
	{
		setvard NPC_XPTR 0
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	local NEW_XP $ratio(L_TIME_RATIO,0,NPC_XPTR_MAXXP)
	skilllevel NEW_XP
	dbg set_xp_timeramp_loop L_TIME_RATIO = NEW_XP / NPC_XPTR_MAXXP [ NPC_XPTR_TIME ]
}

{ game_scriptflag_update //<action> <name> [type] [value] [base_expire_time]

	//handle flags
	if ( $get_scriptflag(ent_me,nopush,type_exists) )
	{
		nopush 1
	}
	else
	{
		nopush 0
	}

	//handle check for expire
	if ( PARAM1 equals add ) local L_CHECK_EXPIRE 1
	if ( PARAM1 equals edit ) local L_CHECK_EXPIRE 1 //expire time may have changed
	
	if L_CHECK_EXPIRE

	local L_CHECK_EXPTIME PARAM5

	if ( L_CHECK_EXPTIME > -1 )
	{
		add L_CHECK_EXPTIME 0.1
		callevent L_CHECK_EXPTIME check_flags_expired
	}
}

{ check_flags_expired
	scriptflags ent_me remove_expired
	callexternal ent_me ext_scriptflag_expired
}

//in case nopush was set at spawn
{ npc_post_spawn
	if $get(ent_me,nopush)
	scriptflags ent_me add npspawn nopush
}

//map side npc addparams can only get one param at a time, so we have to break this in two
{ set_cbm_file
	if !NPC_CUSTOM_COMBAT_MUSIC //this is running twice, and we REALLY need to find out why
	setvard NPC_CUSTOM_COMBAT_MUSIC 1
	setvard NPC_CMUSIC_FILE PARAM1
}

{ set_cbm //alias for set_cbm_file
	local L_PASS PARAM1
	callevent set_cbm_file L_PASS
}

{ ext_conflict
	conflictcheck
}

{ ext_showflags
	local L_TEST $get_scriptflag(ent_me,listall)
}

{ ext_dumpvars
	conflictcheck dumpvars noglobals
}

{ set_mclip
	setmonsterclip PARAM1
}

{ ext_teleportfx1
	callevent 0.1 ext_teleportfx1_delay
}

{ ext_teleportfx2
	playsound 0 10 debris/beamstart8.wav
}

{ ext_teleportfx1_delay
	local MY_FEET $get(ent_me,origin)
	vectorset MY_FEET z $get_ground_height(MY_FEET)
	clientevent update all const.localplayer.scriptID cl_tele1_fx MY_FEET
}

{ ext_beam_follow_test
	dbg beam_follow_test
	effect beam follow lgtning.spr ent_me 0 5 30.0 255 (255,0,0)
	effect beam follow lgtning.spr ent_me 1 5 30.0 255 (255,0,255)
	effect beam follow lgtning.spr ent_me 2 5 30.0 255 (255,255,0)
	effect beam follow lgtning.spr ent_me 3 5 30.0 255 (255,255,255)
}

{ set_cadj //<ratio> - reduces monsters strength and xp, but sadly, does not affect drops, so use with care
	if PARAM1 > 0
	dbg set_cadj PARAM1
	local L_ADJ_RATIO PARAM1
	if L_ADJ_RATIO < 1 //abuse prvention
	setvard NPC_DMG_MULTI L_ADJ_RATIO
	setvard NPC_HP_MULTI L_ADJ_RATIO
	dmgmulti NPC_DMG_MULTI
	multiply L_ADJ_RATIO 0.5
	setvard NPC_EXP_REDUCT L_ADJ_RATIO
}

{ ext_dmgmulti
	//dbg ext_dmgmulti PARAM1
	dmgmulti PARAM1 //this is for effects, not addparams
}

{ set_die_nt //<delay>
	//remove mob in <delay> seconds, if no target nearby
	dbg set_die_nt PARAM1

	if ( !NPC_DIE_NT_FIRST_RUN )
	{
		if ( PARAM1 <= 0 ) exitevent
		setvard NPC_DIE_NT_FIRST_RUN 1
		setvard NPC_DIE_NT_NEXT_CHECK PARAM1
		setvard NPC_DIE_NT_BASE PARAM1
		setvard NPC_LAST_SUICDE_ABORT game.time
		callevent NPC_DIE_NT_NEXT_CHECK set_die_nt
		exitevent
	}

	if ( $get(NPCATK_TARGET,isalive) )
	{
		if ( $get(NPCATK_TARGET,range) < 256 ) local L_NO_SUICIDE 1
	}

	local L_GAME_TIME game.time

	//last saw enemy < die delay, abort
	local L_LAST $math(add,NPC_LASTSEEN_ENEMY_TIME,NPC_DIE_NT_BASE)
	//add L_LAST NPC_LAST_SUICDE_ABORT
	if ( L_GAME_TIME < L_LAST ) local L_NO_SUICIDE 1
	
	//last attacked < die delay, abort
	local L_LAST $math(add,NPC_LAST_DAMAGED_TIME,NPC_DIE_NT_BASE)
	//add L_LAST NPC_LAST_SUICDE_ABORT
	if ( L_GAME_TIME < L_LAST ) local L_NO_SUICIDE 1

	//last damaged enemy < die delay, abort
	local L_LAST $math(add,NPC_LAST_DAMAGED_OTHER_TIME,NPC_DIE_NT_BASE)
	//add L_LAST NPC_LAST_SUICDE_ABORT
	if ( L_GAME_TIME < L_LAST ) local L_NO_SUICIDE 1

	if ( L_NO_SUICIDE )
	{
		setvard NPC_LAST_SUICDE_ABORT L_GAME_TIME
		callevent NPC_DIE_NT_NEXT_CHECK set_die_nt //try again in a bit
	}
	else
	{
		setvard NPC_SILENT_SUICIDE 1
		setvard NPC_INVISIBLE_SUICIDE 1
		setvard NPC_NO_COUNT 1
		callevent npc_suicide
	}
}

{ ext_rmines_clear
	//allows boss mob to tell minions to self-remove faster on the mess that is rmines
	callevent npcatk_clear_targets
	setvard NPC_DIE_NT_NEXT_CHECK 10.0
}

{ set_mspeed //<ratio> - changes the move speed without messing with the attack speed
	//adjusts xp slightly - see base_self_adjust
	dbg set_mspeed PARAM1
	if PARAM1 > 0
	if ( BASE_MOVESPEED equals 'BASE_MOVESPEED' )
	{
		setvard BASE_MOVESPEED 1.0
	}
	multiply BASE_MOVESPEED PARAM1
	setvard NPC_MOVE_SPEED_ADJ BASE_MOVESPEED
	movespeed BASE_MOVESPEED
	setanim.movespeed BASE_MOVESPEED
	dbg set_mspeed newspeed BASE_MOVESPEED
	playanim break //case we hit a trigger with this, and are moving, otherwise we'll run in place until we attack
	setvard NPC_USES_HANDLE_EVENTS 1
	if ( NPC_ADJ_FLAGS equals 'NPC_ADJ_FLAGS' ) setvard NPC_ADJ_FLAGS ''
	token.add NPC_ADJ_FLAGS mspeed
}

{ set_mspeedt
	dbg setanim.movespeed PARAM1
	setanim.movespeed PARAM1
}

{ set_mspeed2
	dbg movespeed PARAM1
	movespeed PARAM1
}

{ set_bbox
	if PARAM1 !startswith PARAM
	dbg ext_setbbox PARAM1
	if ( PARAM2 startswith PARAM )
	{
		setbbox PARAM1 //npcsize|animsize - animsize sometimes crashes
	}
	else
	{
		setbbox PARAM1 PARAM2 //<vec_mins> <vec_maxs>
	}
}

{ set_range //<units> - changes attack range, setting this lower than orig NPC will result in XP nerf
	if PARAM1 > 0
	//some NPC's change their range dynamically, in which case, this may not work
	if ( NPC_ORG_RANGE equals 'NPC_ORG_RANGE' ) setvard NPC_ORG_RANGE ATTACK_RANGE //case we do this more than once, for some reason
	setvard NPC_SET_RANGE PARAM1
	setvard NPC_SET_RANGE_RATIO NPC_ORG_RANGE //future use, so we can adjust dynamic ranges
	divide NPC_SET_RANGE_RATIO NPC_ORG_RANGE
	callevent 2.0 set_range2 //give mob chance to set his ranges
}

{ set_range2
	setvard ATTACK_RANGE NPC_SET_RANGE
	setvard ATTACK_HITRANGE NPC_SET_RANGE
	multiply ATTACK_HITRANGE 2.0
}

{ set_tele_hunter //[frequency]
	if !NPC_TELEHUNT
	dbg set_tele_hunter PARAM1
	//periodically teleport to target 
	setvard NPC_TELEHUNT 1
	setvard NPC_TELEHUNTER_FX 1
	setvard NPC_TELEHUNT_FREQ PARAM1
	if ( PARAM1 startswith PARAM ) setvard NPC_TELEHUNT_FREQ 20.0
	setvard NPC_DO_SPAWN_SOUND 1
	callevent NPC_TELEHUNT_FREQ npcatk_tele_hunter_loop
	if ( NPC_ADJ_FLAGS equals 'NPC_ADJ_FLAGS' ) setvard NPC_ADJ_FLAGS ''
	token.add NPC_ADJ_FLAGS telehunt
}

{ set_tele_hunter_random //[frequency]
	if !NPC_TELEHUNT
	dbg set_tele_hunter_random PARAM1
	//periodically teleport to random players 
	playanim break
	setvard NPC_TELEHUNT 1
	setvard NPC_TELEHUNTER_FX 1
	setvard NPC_TELEHUNT_FREQ PARAM1
	setvard NPC_TELEHUNT_RANDOM 1
	if ( PARAM1 startswith PARAM ) setvard NPC_TELEHUNT_FREQ 20.0
	setvard NPC_DO_SPAWN_SOUND 1
	callevent NPC_TELEHUNT_FREQ npcatk_tele_hunter_loop

	if ( NPC_ADJ_FLAGS equals 'NPC_ADJ_FLAGS' ) setvard NPC_ADJ_FLAGS ''
	token.add NPC_ADJ_FLAGS telehunt
}

{ set_skin //<index>
	if PARAM1 !startswith PARAM
	setvard NPC_CUSTOM_SKIN PARAM1
	setprop ent_me skin PARAM1
}

{ set_say_spawn
	if PARAM1 !startswith PARAM
	saytextrange 2048
	saytext PARAM1
}

{ set_say_spot
	if PARAM1 !startswith PARAM
	saytextrange 2048
	add NPC_DO_ON_SPOT 1
	setvard NPC_SAY_ON_SPOT PARAM1
}

{ set_say_die
	if PARAM1 !startswith PARAM
	saytextrange 2048
	add NPC_DO_ON_DIE 1
	setvard NPC_SAY_ON_DIE PARAM1
}
//todos: set_tele_hunter_in_and_out (teleports off map) set_tele_hunter_ranged (larger radious from player)

//[begin] Script Side XP: future use experiment
{ set_dump_xp
	dbg set_dump_xp set
	setvard NPC_DUMP_XP 1 //causes mob to dump hitdata to array on death code side (CMSMonster::Killed)
}

{ game_death
	if NPC_DUMP_XP
	dbg dumping xp... [ $get_array_amt(ARRAY_XP_PLAYERS) ]
	calleventloop $get_array_amt(ARRAY_XP_PLAYERS) dbg_dump_xp
}

{ dbg_dump_xp
	local CUR_HIT game.script.iteration
	local CUR_PLR $get_array(ARRAY_XP_PLAYERS,CUR_HIT)
	local CUR_PLR $get(CUR_PLR,name)
	local CUR_SKL $get_array(ARRAY_XP_SKILLS,CUR_HIT)
	local CUR_EXP $get_array(ARRAY_XP_AMTS,CUR_HIT)
	dbg dbg_dump_xp CUR_PLR CUR_SKL CUR_EXP
}

{ set_solid
	dbg set_solid PARAM1
	//this seems silly, but still have mysterious bug where this gets called twice and mystery param sometimes gets passed.
	if ( PARAM1 equals none ) setsolid PARAM1
	if ( PARAM1 equals box ) setsolid PARAM1
	if ( PARAM1 equals slidebox ) setsolid PARAM1
	if ( PARAM1 equals trigger ) setsolid PARAM1
}
//[end] Script Side XP: future use experiment

{ ext_clearfx
	dbg ext_clearfx
	clearfx
}

{ ext_remove_effect
	//. eventall ext_remove_effect freeze_solid
	//. eventall ext_remove_effect effect_iceshield
	dbg ext_remove_effect PARAM1
	removeeffect ent_me PARAM1
}

{ ext_break
	dbg ext_break
	playanim break
}

{ ext_bleed
	dbg ext_bleed col PARAM1 amt PARAM2
	bleed ent_me PARAM1 PARAM2
}

{ set_takedmg //<type:ratio>
	local L_DMG_TYPE $string_upto(PARAM1,':')
	local L_DMG_RATIO $string_from(PARAM1,':')
	takedmg L_DMG_TYPE L_DMG_RATIO
}

{ set_notarget //<0|1>
	if PARAM1 !startswith PARAM
	setvard PLAYING_DEAD PARAM1	
}

{ ext_set_next_circle_heal
	setvard NEXT_CIRCLE_HEAL PARAM1
}

{ ext_delay_target //<delay> <target|"enemy"> selects a target (after delay for init), useful for summons
	setvard NPC_DELAY_TARGET PARAM2
	if NPC_DELAY_TARGET !startswith PARAM
	callevent PARAM1 ext_delay_target2
}

{ ext_delay_target2
	if ( NPC_DELAY_TARGET equals enemy ) local L_DO_SCAN 1
	if ( !$get(NPC_DELAY_TARGET,isalive) ) local L_DO_SCAN 1

	if ( L_DO_SCAN )
	{
		local L_TARGS $get_tsphere(enemy,2048)
		local L_TARGS $sort_entlist(L_TARGS,range)
		callevent npcatk_settarget $get_token(L_TARGS,0)
	}
	else
	{
		callevent npcatk_settarget NPC_DELAY_TARGET
	}
}

{ set_world_spawn
	applyeffect $get(ent_me,id) "effects/add_dynamic_spawn" "world"
}

{ set_dyn_spawn
	applyeffect $get(ent_me,id) "effects/add_dynamic_spawn"
}

{ toggle_invincible //makes the monster invincible if they are not, or turns it off if they are.
	if ( $get(ent_me,invincible) ) invincible 0
	else invincible 1
}