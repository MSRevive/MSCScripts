//Ghoul by Thothie - From scratch
//- Fugly model - basically fast Zombie, stun/drunk attack secondary
//- Raised gold reward - came out a lil meaner than intended

#scope server
{ [shared]
	//base anims
	setvar ANIM_IDLE idle1
	setvar ANIM_WALK walk
	setvar ANIM_RUN run
	setvard ANIM_DEATH diesimple
	setvard ANIM_ATTACK claw	//alternates between claw and slash
	const ANIM_NPC_JUMP slash

	//cust anims
	const ANIM_LOOK idle_look
	const ANIM_SLASH slash
	const ANIM_CLAW claw
}

{
	if ( $lcase(game.map.name) equals the_wall ) setvard NPC_JUMPER 1
	if ( $lcase(game.map.name) equals walltest ) setvard NPC_JUMPER 1

	setvard CAN_ATTACK 1
	setvard CAN_HUNT 1
	setvard HUNT_AGRO 1
	setvard CAN_HEAR 1
	setvard CAN_WANDER 1

	setvard CAN_RETALIATE 1
	const RETALIATE_CHANGETARGET_CHANCE 80

	setvard CAN_FLINCH 0

	setvard CAN_FLEE 1
	const FLEE_HEALTH 25
	const FLEE_CHANCE 25
	setvard FLEE_DISTANCE 2048

	setvard ATTACK_RANGE 125
	setvard ATTACK_HITRANGE 200
	const MOVE_RAGE 55
	const ATTACK_ACCURACY 80%
	const ATTACK2_ACCURACY 90%
	const ATTACK_DAMAGE 80


	const SOUND_IDLE1 zombie/zo_alert10.wav
	const SOUND_IDLE2 zombie/zo_alert20.wav
	const SOUND_IDLE3 zombie/zo_alert30.wav

    const SOUND_ALERT zombie/zo_attack1.wav

	const SOUND_ATTACK1 zombie/claw_miss1.wav
	const SOUND_ATTACK2 zombie/claw_miss2.wav
	const SOUND_ATTACKHIT1 zombie/claw_strike1.wav
	const SOUND_ATTACKHIT2 zombie/claw_strike2.wav
	const SOUND_ATTACKHIT3 zombie/claw_strike3.wav

	const SOUND_DEATH bullchicken/bc_die1.wav

	const SOUND_STRUCK debris/flesh2.wav		//most common
	const SOUND_PAIN1 zombie/zo_pain2.wav	//rare > 25%
	const SOUND_PAIN2 zombie/zo_idle3.wav	//rare < 25%

   	const SOUND_TURNED1	ambience/the_horror1.wav
   	const SOUND_TURNED2	ambience/the_horror2.wav
   	const SOUND_TURNED3	ambience/the_horror3.wav
	const SOUND_TURNED4	ambience/the_horror4.wav	
	const SOUND_HOLYPAIN1 agrunt/ag_pain2.wav
	const SOUND_HOLYPAIN2 agrunt/ag_pain3.wav
	const SOUND_HOLYPAIN3 agrunt/ag_pain4.wav

	const SOUND_STEP1 player/pl_dirt1.wav
	const SOUND_STEP2 player/pl_dirt2.wav
	const SOUND_STEP3 player/pl_dirt3.wav
	const SOUND_STEP4 player/pl_dirt4.wav

	const SOUND_NPC_JUMP zombie/zo_attack1.wav

	setvard DROP_GOLD 1
	setvard DROP_GOLD_MIN 50
	setvard DROP_GOLD_MAX 75

	const I_AM_TURNABLE	0	//for base_turn_undead

	setvard MONSTER_WIDTH 32	//for anti-stuck

	const STUCK_CHECK_FREQUENCY 1.1 //I move VERY fast, reduce stuck check delays

	precache SOUND_DEATH //handled externally

	precache monsters/ghoul_greater.mdl	//for older SC.DLL users
}

#include monsters/base_monster
#include monsters/base_jumper //new base script FEB2010_25

{ [shared] npc_spawn

   name a|Greater Ghoul
   hp 450	//hi HP, no armor
   //ov 110
   width 32
   height 72
   roam 1
   race undead
   setidleanim ANIM_IDLE
   setmoveanim ANIM_WALK
   takedmg holy 1.0
   takedmg cold 0.25
	takedmg poison 0.0
   hearingsensitivity 8
   setmodel monsters/ghoul_greater.mdl
   setvard NPC_GIVE_EXP 120
   callevent 1.0 look_around_ghoul
}

{ attack1	//called by modelanim claw @frames 11 & 22

	setvard STUCK_COUNT 0
	setvard ATTACK_VOLUME 5
	dodamage ENTITY_ENEMY ATTACK_HITRANGE ATTACK_DAMAGE ATTACK_ACCURACY
	if ( $rand(1,20) == 1 ) setvard ANIM_ATTACK ANIM_SLASH
}

{ attack2 //called by modelanim slash @frame 11

	setvard STUCK_COUNT 0
	setvard ATTACK_VOLUME 10
	dodamage ENTITY_ENEMY ATTACK_HITRANGE ATTACK_DAMAGE ATTACK2_ACCURACY
	setvard ANIM_ATTACK ANIM_CLAW

	if $get(ent_laststruckbyme,range) <= ATTACK_HITRANGE
	applyeffect $get(ent_laststruckbyme,id) effects/heavy_stun $rand(2,8)
}

{ game_dodamage //1: Attack Hit (1/0)  2: Entity Attack hit  3:  Start Position  4:  End Position

	if ( !PARAM1 ) 	playrandomsound CHAN_VOICE ATTACK_VOLUME SOUND_ATTACK1 SOUND_ATTACK2
	if ( PARAM1 ) playrandomsound CHAN_VOICE ATTACK_VOLUME SOUND_ATTACKHIT1 SOUND_ATTACKHIT2 SOUND_ATTACKHIT3

	if !ALERTED_OTHERS
	//alert others to my target
	emitsound ent_laststruck $get(ent_laststruck,origin) 10 10 combat
	setvard ALERTED_OTHERS 1
}

{ npc_struck

	local MY_HEALTH $get(ent_me,hp)
	local HURT_THRESHOLD $get(ent_me,maxhp)
	multiply HURT_THRESHOLD 0.25

	if ( STRUCK_BY_HOLY )
	{
		playrandomsound CHAN_VOICE 10 SOUND_HOLYPAIN1 SOUND_HOLYPAIN2 SOUND_HOLYPAIN3
		setvard STRUCK_BY_HOLY 0
		local EXIT_SUB 1		
	}
	if !EXIT_SUB

	if ( MY_HEALTH > HURT_THRESHOLD ) playrandomsound CHAN_VOICE 8 SOUND_STRUCK SOUND_STRUCK SOUND_PAIN1	
	if ( MY_HEALTH <= HURT_THRESHOLD ) playrandomsound CHAN_VOICE 8 SOUND_STRUCK SOUND_STRUCK SOUND_PAIN2
}

{ run_step	//called by modelanim run @frames 5 and 17

	playrandomsound CHAN_VOICE 10 SOUND_STEP1 SOUND_STEP2 SOUND_STEP3 SOUND_STEP4
}

//ghoul looks around on the following external calls
{ basenpcatk_target_lost
	callevent look_around_ghoul
}
{ my_target_died
	setvard DID_ALERT 0
	callevent look_around_ghoul
}
{ give_up_advanced
	callevent look_around_ghoul
}
{ game_reached_dest
	if !$cansee(player)
	callevent look_around_ghoul
}
{ npcatk_stopflee
	if !$cansee(player)
	callevent look_around_ghoul
}

//look around temporarily increases FOV
{ look_around_ghoul

	if !$cansee(player)
	playanim once ANIM_LOOK
	playrandomsound CHAN_VOICE 10 SOUND_IDLE1 SOUND_IDLE2 SOUND_IDLE3
	callevent 1.0 //ov_up_ghoul
}
{ //ov_up_ghoul
	//ov 200
	callevent 1.0 //ov_down_ghoul
}
{ //ov_down_ghoul
	if !$cansee(player)
	//ov 90
}

//play alert sound on spotting player
{ npcatk_targetvalidate
	callevent alert_ghoul
}
{ alert_ghoul

	if !DID_ALERT
	if $cansee(player)	//play alert sound for players only
	playsound CHAN_VOICE 10 SOUND_ALERT
	setvard DID_ALERT 1
}