//Shadowform, sprite based monster
//- Shocks opponents
//- grabs/paralyzes players, until struck by another player
//- Immune to all but Holy and Dark
#scope server
{
	setvar IMMUNE_ALL_BUT_HOLY 1
	setvar IS_UNHOLY 1
	setvar NPC_HACKED_MOVE_SPEED 10
	const AS_CUSTOM_UNSTUCK 1
	setvard IMMUNE_VAMPIRE 1

	setvar NPC_MUST_SEE_TARGET 0

	setvard ANIM_ATTACK idle_standard
	setvard ANIM_WALK walk
	setvard ANIM_RUN walk
	setvard ANIM_IDLE idle_standard

	setvard ATTACK_MOVERANGE 64
	setvard ATTACK_RANGE 512

	const FREQ_CL_UPDATE 15.0

	const FREQ_IDLE_SOUND $randf(3.0,6.0)
	const FREQ_IDLE_ZAP $randf(2.0,3.0)
	const FREQ_PAIN 1.0

	const FREQ_ATTACK 5.0
	const FREQ_MINOR_STRIKE 3.0

	const DMG_MAIN_ZAP 1100
	const DOT_MAIN_ZAP 50

	const DMG_MINOR_STRIKE 300
	const DOT_MINOR_STRIKE 30
	const RANGE_MINOR_STRIKE 768

	const SOUND_ALERT monsters/shadow/shadow_alert.wav
	const SOUND_IDLE1 monsters/shadow/shadow_idle1.wav
	const SOUND_IDLE2 monsters/shadow/shadow_idle2.wav
	const SOUND_IDLE3 monsters/shadow/shadow_idle3.wav
	const SOUND_PAIN1 monsters/shadow/shadow_pain1.wav
	const SOUND_PAIN2 monsters/shadow/shadow_pain2.wav
	const SOUND_PAIN3 monsters/shadow/shadow_pain3.wav
	const SOUND_DEATH monsters/shadow/shadow_death.wav

	const SOUND_ZAP_READY magic/lightprep.wav
	const SOUND_ZAP_IDLE magic/elecidle.wav

	const SOUND_ZAP1 debris/zap1.wav
	const SOUND_ZAP2 debris/zap3.wav
	const SOUND_ZAP3 debris/zap8.wav

	const FREQ_SHOCK_SCAN $randf(3.0,5.0)

	const AS_DIST_THRESH 5

	if ( $lcase(game.map.name) contains lodagond )
	{
		setvard NPC_GIVE_EXP 2000
	}
	else
	{
		setvard NPC_GIVE_EXP 1000
	}

	setvard IMMUNE_STUN 100
}

#include monsters/base_monster_new
//#include monsters/base_propelled
#include monsters/base_flyer_grav

{ [shared] game_precache
	precache shadowfog.spr
	precachefile monsters/shadow_form_cl
}

{ [shared] npc_spawn
	name a|Shadow Form
	hp 5000
	takedmg holy 2.0
	takedmg dark 0.5

	blood none

	takedmg fire 0.0
	takedmg cold 0.0
	takedmg lightning 0.0

	race demon
	setmodel monsters/shadow_hitbox.mdl
	width 90
	height 90
	hearingsensitivity 11
	gravity 0

	if game.serverside
	setvard MISS_COUNT 0
	setvard CYCLE_IDLE_SOUND 0
	array.create ARRAY_MINOR_STRIKE
}

{ game_damaged //PARAM1=attacker PARAM2=dmg PARAM3=dmg_type
	if $get(ent_me,isalive)

	if ( !SHADOW_CL_ON ) callevent do_manifest

	if IMMUNE_ALL_BUT_HOLY
	local NO_DAMAGE 1
	if ( PARAM3 contains holy ) local NO_DAMAGE 0
	if ( PARAM3 contains dark ) local NO_DAMAGE 0
	if ( PARAM3 contains target ) local NO_DAMAGE 0
	if ( PARAM3 contains magic ) local NO_DAMAGE 0
	if ( NO_DAMAGE )
	{
		setdmg dmg 0
		setdmg hit 0
		return 0
	}
	else
	{
		if ( game.time > NEXT_STRIKE_BACK )
		{
			if PARAM1 isnot NPCATK_TARGET
			callevent add_minor_strike $get(PARAM1,id)
		}

		if game.time > NEXT_PAIN_SOUND
		setvard NEXT_PAIN_SOUND game.time
		add NEXT_PAIN_SOUND FREQ_PAIN
		playrandomsound 0 10 SOUND_PAIN1 SOUND_PAIN2 SOUND_PAIN3
		effect beam ents lgtning.spr 6 ent_me $rand(0,3) ent_me $rand(0,3) (255,0,0) 255 255 $randf(1.0,2.0)
	}
}

{ npc_targetsighted
	callevent do_manifest
}

{ my_target_died
	setvard DID_ALERT 0
}

{ do_manifest
	if ( !DID_ALERT )
	{
		setvard DID_ALERT 1
		playsound 0 10 SOUND_ALERT

		local GAME_TIME game.time

		setvard NEXT_IDLE_ZAP GAME_TIME
		add NEXT_IDLE_ZAP FREQ_IDLE_ZAP

		setvard NEXT_IDLE_SOUND GAME_TIME
		add NEXT_IDLE_SOUND FREQ_IDLE_SOUND

		setvard NEXT_ATTACK GAME_TIME
		add NEXT_ATTACK FREQ_ATTACK

		setvard NEXT_SHOCK_SCAN GAME_TIME
		add  NEXT_SHOCK_SCAN FREQ_SHOCK_SCAN

		if ( $lcase(game.map.name) equals nashalrath )
		{
			//weird shit going on here, music ends when mob dies
			callexternal NPCATK_TARGET ext_play_music all Hashalgath.mp3
		}
	}

	if !SHADOW_CL_ON
	setvard SHADOW_CL_ON 1
	callevent shadow_cl_loop
}

{ shadow_cl_loop
	if SHADOW_CL_ON
	callevent FREQ_CL_UPDATE shadow_cl_loop FREQ_CL_UPDATE
	clientevent new all monsters/shadow_form_cl $get(ent_me,index) FREQ_CL_UPDATE
	setvard CL_SCRIPT_IDX game.script.last_sent_id
}

{ npc_suicide
	setvard IMMUNE_ALL_BUT_HOLY 0
}

{ npcatk_hunt
	if $get(ent_me,isalive)
	if SHADOW_CL_ON

	if NPCATK_TARGET isnot none

	local GAME_TIME game.time

	if ( GAME_TIME > NEXT_IDLE_ZAP )
	{
		playsound 2 5 SOUND_ZAP_IDLE
		effect beam ents lgtning.spr 2 ent_me $rand(0,3) ent_me $rand(0,3) (128,128,255) 200 255 $randf(2.0,4.0)
		setvard NEXT_IDLE_ZAP GAME_TIME
		add NEXT_IDLE_ZAP FREQ_IDLE_ZAP
	}

	if ( GAME_TIME > NEXT_IDLE_SOUND )
	{
		add CYCLE_IDLE_SOUND 1
		if ( CYCLE_IDLE_SOUND > 3 ) setvard CYCLE_IDLE_SOUND 0
		if ( CYCLE_IDLE_SOUND == 1 ) playsound 4 10 SOUND_IDLE1
		if ( CYCLE_IDLE_SOUND == 2 ) playsound 3 10 SOUND_IDLE2
		if ( CYCLE_IDLE_SOUND == 3 ) playsound 4 10 SOUND_IDLE3
		setvard NEXT_IDLE_SOUND GAME_TIME
		add NEXT_IDLE_SOUND FREQ_IDLE_SOUND
	}

	if ( GAME_TIME > NEXT_SHOCK_SCAN )
	{
		setvard NEXT_SHOCK_SCAN GAME_TIME
		add  NEXT_SHOCK_SCAN FREQ_SHOCK_SCAN
		callevent shock_scan //shock random player in range, other than primary target
	}

	if ( $get_array_amt(ARRAY_MINOR_STRIKE) > 0 )
	{
		if GAME_TIME > NEXT_MINOR_STRIKE
		setvard NEXT_MINOR_STRIKE GAME_TIME
		add NEXT_MINOR_STRIKE FREQ_MINOR_STRIKE
		callevent minor_strike
	}
}

{ shock_scan
	setvard SCAN_TARGS $get_tsphere(enemy,RANGE_MINOR_STRIKE)
	if SCAN_TARGS isnot none
	calleventloop $get_token_amt(SCAN_TARGS) shock_scan_loop
}

{ shock_scan_loop
	local CUR_TARG $get_token(SCAN_TARGS,game.script.iteration)
	if $get(CUR_TARG,isalive)
	if CUR_TARG isnot NPCATK_TARGET
	callevent add_minor_strike CUR_TARG
}

{ npcatk_attack
	if ( game.time > NEXT_ATTACK )
	{
		//fake "can see"
		local MY_ORG $get(ent_me,origin)
		local TARG_ORG $get(NPCATK_TARGET,origin)
		local TRACE_LINE $get_traceline(MY_ORG,TARG_ORG,worldonly)
		if TRACE_LINE equals TARG_ORG

		setvard NEXT_ATTACK game.time
		add NEXT_ATTACK FREQ_ATTACK
		callevent zap_target1
	}
}

{ zap_target1
	if $get(ent_me,isalive)
	playsound 0 10 SOUND_ZAP_READY
	effect beam ents lgtning.spr 10 ent_me 0 ent_me 1 (128,128,128) 200 255 0.5
	effect beam ents lgtning.spr 10 ent_me 1 ent_me 2 (128,128,128) 200 255 0.5
	effect beam ents lgtning.spr 10 ent_me 2 ent_me 3 (128,128,128) 200 255 0.5
	callevent 0.5 zap_target2
}

{ zap_target2
	if $get(ent_me,isalive)
 	effect beam ents lgtning.spr 30 ent_me 0 ent_me 1 (180,180,180) 200 255 0.5
	effect beam ents lgtning.spr 30 ent_me 1 ent_me 2 (180,180,180) 200 255 0.5
	effect beam ents lgtning.spr 30 ent_me 2 ent_me 3 (180,180,180) 200 255 0.5
	callevent 0.5 zap_target3
}

{ zap_target3
	if $get(ent_me,isalive)
	if $get(NPCATK_TARGET,isalive)
	local MY_ORG $get(ent_me,origin)
	local TARG_ORG $get(NPCATK_TARGET,origin)
	local TRACE_LINE $get_traceline(MY_ORG,TARG_ORG,worldonly)
	effect beam end lgtning.spr 60 TRACE_LINE ent_me 0 (255,255,255) 200 255 3.0
	playrandomsound 0 10 SOUND_ZAP1 SOUND_ZAP2 SOUND_ZAP3
	if ( TRACE_LINE equals TARG_ORG )
	{
		setvard MISS_COUNT 0
		dodamage NPCATK_TARGET direct DMG_MAIN_ZAP 100% lightning
		applyeffect NPCATK_TARGET effects/effect_shock_dmg 5.0 $get(ent_me,id) DOT_MAIN_ZAP
		addvelocity NPCATK_TARGET $relpos($vec(0,game.monster.angles.yaw,0),$vec(0,800,110))
		setvard NEXT_ATTACK game.time
		add NEXT_ATTACK FREQ_ATTACK
	}
	else
	{
		add MISS_COUNT 1
		if ( MISS_COUNT > 2 )
		{
			callevent npc_stuck
			setvard MISS_COUNT 2
		}
	}
}

{
repeatdelay 0.2
	if $get(ent_me,isalive)

	local L_MOVE_DEST game.monster.movedest.origin

	if L_MOVE_DEST isnot $vec(0,0,0)

	addvelocity ent_me $relpos(game.monster.angles,$vec(0,NPC_HACKED_MOVE_SPEED,0))
}

{ npc_stuck
	local RND_P $randf(-359.0,359.0)
	local RND_Y $randf(-359.0,359.0)
	local RND_R $randf(-359.0,359.0)
	setvelocity ent_me $relpos($vec(RND_P,RND_Y,RND_Y),$vec(0,400,0))
}

{ game_death
	gravity 0
	setvelocity ent_me $relvel($vec(0,0,0),$vec(0,0,0))
	if ( CL_SCRIPT_IDX isnot 'CL_SCRIPT_IDX' )
	{
		clientevent update all CL_SCRIPT_IDX shadow_death
	}
}

{ add_minor_strike //<id_to_que>
	array.add ARRAY_MINOR_STRIKE PARAM1
}

{ minor_strike
	local MINOR_STRIKE_TARG $get_array(ARRAY_MINOR_STRIKE,0)
	array.del ARRAY_MINOR_STRIKE 0

	if $get(MINOR_STRIKE_TARG,range) < RANGE_MINOR_STRIKE

	local MY_ORG $get(ent_me,origin)
	local TARG_ORG $get(MINOR_STRIKE_TARG,origin)
	local TRACE_LINE $get_traceline(MY_ORG,TARG_ORG,worldonly)
	local RND_ATCH $rand(1,3)
	if ( TRACE_LINE equals TARG_ORG )
	{
		effect beam end lgtning.spr 10 TRACE_LINE ent_me RND_ATCH (128,128,255) 200 255 2.0
		playrandomsound 0 5 SOUND_ZAP1 SOUND_ZAP2 SOUND_ZAP3

		dodamage MINOR_STRIKE_TARG direct DMG_MINOR_STRIKE 100% lightning
		applyeffect MINOR_STRIKE_TARG effects/effect_shock_dmg 5.0 $get(ent_me,id) DOT_MINOR_ZAP
		local TARG_ANG $angles(MY_ORG,TARG_ORG)
		local NEW_YAW TARG_ANG
		addvelocity MINOR_STRIKE_TARG $relvel($vec(0,NEW_YAW,0),$vec(0,300,110))
	}
}