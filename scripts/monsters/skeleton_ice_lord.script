//Ice Bone Lord by Thothie from Blacksmith

{
	const NPC_NO_RAMP 1

	const NPC_BOSS_REGEN_RATE 0
	const NPC_BOSS_RESTORATION 0


	if ( $lcase(game.map.name) equals ms_snow )
	{
		setvard NPC_GIVE_EXP 4000 //pain to reach
		setvard NPC_IS_BOSS 1
	}
	else
	{
		setvard NPC_GIVE_EXP 1000
	}

	const SOUND_INTRO1 npc/ice_queen_live_beating2.wav
	const SOUND_INTRO2 npc/ice_queen_put_that_on_ice2.wav
	const SOUND_STAGETWO1 npc/ice_queen_cant_pronounce_loreldians2.wav
	const SOUND_STAGETWO2 npc/ice_queen_game2.wav
	const SOUND_FINAL1 npc/ice_queen_powah2.wav
	const SOUND_FINAL2 npc/ice_queen_die2.wav

   const SOUND_STRUCK1 controller/con_pain2.wav
   const SOUND_STRUCK2 controller/con_pain3.wav
   const SOUND_ATTACK1 zombie/claw_strike1.wav
   const SOUND_ATTACK2 zombie/claw_strike2.wav
   const SOUND_ATTACK3 zombie/claw_strike3.wav
   //const SOUND_SPAWN npc/undamael1.wav
   const SOUND_FLINCH1 npc/ice_queen_pain2.wav
   const SOUND_FLINCH2 garg/gar_pain2.wav
   const SOUND_PUSH1 npc/ice_queen_throw2.wav
   const SOUND_PUSH2 npc/ice_queen_throw3.wav
   const SOUND_FALL weapons/mortarhit.wav
   setvar ANIM_RUN walk
   setvar ANIM_IDLE idle1
   setvar ANIM_WALK walk
	const ANIM_BASERUN walk
	const ANIM_BASEIDLE idle1
	const ANIM_BASEWALK walk
   setvard ANIM_SLASH attack1
   setvard ANIM_SMASH attack2
   setvard ANIM_ATTACK ANIM_SMASH
   const ATTACK1_RANGE 160
   const ATTACK2_RANGE 160
   setvard ATTACK_RANGE ATTACK1_RANGE
   setvard MOVE_RANGE1 80
   setvard MOVE_RANGE2 80
   setvard MOVE_RANGE MOVE_RANGE1
   setvard ATTACK_HITRANGE 300
   setvard SEE_ENEMY 0
   setvard IGNORE_ENEMY 0
   setvard CAN_FLINCH 0
   setvard FLINCH_ANIM bigflinch
   setvard FLINCH_CHANCE 10
   setvard FLINCH_DELAY 1
   setvard CAN_HUNT 1
   setvard CAN_ATTACK 1
   setvard NPC_MOVE_TARGET enemy

    //Precache
   //precache SOUND_DEATH //handled externally

	setvard FLINCH_HEALTH 1000

	const MAX_HP 3000 //3000 developer
	const ATTACK_CLUB_RANGE 200
	setvard ATTACK_MODE normal

	const ANIM_THROW throw_scientist
	
	//const SOUND_STEP1 player/pl_metal1.wav
	const SOUND_STEP1 debris/glass1.wav
	const SOUND_STEP2 debris/glass2.wav
	const SOUND_STEP3 debris/glass3.wav
	//const SOUND_STEP4 debris/glass4.wav
	setvard STEP_COUNTER 0

	const SOUND_LAUGH monsters/skeleton/cal_laugh.wav
	const SOUND_FAKEDEATH1 garg/gar_die2.wav
	const SOUND_FAKEDEATH2 garg/gar_die1.wav
	const SOUND_DEATH npc/undamael2.wav	//need wave get Samantha to do speech
	const SOUND_FINAL gonarch/gon_die1.wav
	const SOUND_WHACK zombie/claw_strike1.wav
	const SOUND_REGEN x/x_laugh2.wav

	const SOUND_POWERUP ambience/particle_suck2.wav 

	const ANIM_FAKEDEATH dieforward
	setvard ANIM_DEATH dieforward
	const ANIM_DEAD dead_on_stomach
	const ANIM_ALTTHROW bigflinch
	setvard ANIM_FLINCH flinch
	const TAUNT_DELAY 2.0
	const GETUP_DELAY 8.0

	const SPECATK_FREQ_CIRCLE 20.0 //30.0
	const SPECATK_FREQ_FREEZE 20.0 //20.0

	setvard STUCK_CHECK_FREQUENCY 3.5 //plays lengthy still anims
	setvard MONSTER_WIDTH 40
	const FEIGN_THRESHOLD 500 //500 developer

	const DAMAGE_NORM1 $randf(40.0,55.0)
	const DAMAGE_NORM2 $randf(30.0,40.0)
	const DAMAGE_BALL1 $randf(50.0,55.0)
	const DAMAGE_BALL2 $randf(60.0,80.0)
	const DAMAGE_CLUBS1 $randf(60.0,85.0)
	const DAMAGE_CLUBS2 200

	//Seal Precache (crap I shouldn't have to do)
	const CIRCLE_SCRIPT monsters/summon/circle_of_ice_greater
	const XSEAL_MODEL weapons/magic/seals.mdl
	const SOUND_MANIFEST magic/spawn_loud.wav
	const XSOUND_PULSE magic/frost_forward.wav
	const XSOUND_FADE magic/frost_reverse.wav
	const XSEAL_MODEL weapons/magic/seal_fire_large.mdl
	const XFX_SPRITE firemagic.spr
	precache XSEAL_MODEL
	precache SOUND_MANIFEST
	precache XSOUND_PULSE
	precache XSOUND_FADE
	precache XFX_SPRITE


	//Ice Blast Precache (crap I shouldn't have to do)
	const ICE_BLAST_SCRIPT monsters/summon/ice_blast
	//onst XBLAST_MODEL weapons/magic/iceball.mdl
	const SOUND_FREEZE magic/freeze.wav
	const XSOUND_HOVERLOOP ambience/alienwind1.wav
	const XSOUND_HITWALL ambience/alienlaser1.wav
	const XLIGHTNING_SPRITE lgtning.spr
	const SOUND_ICEBLAST magic/temple.wav
	precache XBLAST_MODEL
	precache XSOUND_FREEZE
	precache XSOUND_HOVERLOOP
	precache XSOUND_HITWALL
	precache XLIGHTNING_SPRITE

	precache monsters/skeleton_icel.mdl //for old dll users
}

#include monsters/base_monster_new


{ npc_spawn

	setvard I_AM_TURNABLE 0

   hp MAX_HP
   gold $rand(100,300)
   //ov 359
   width 40
   height 120
   race undead
   local PICK_NAME $rand(1,2)
	if ( PICK_NAME == 1 ) name an|Ice Bone Lord
	if ( PICK_NAME == 2 ) name The|Ice Queen
   roam 0
   hearingsensitivity 6
   precache monsters/skeleton_icel.mdl //for old dll users
   setmodel monsters/skeleton_icel.mdl
   setidleanim ANIM_IDLE
   setmoveanim ANIM_WALK
   playanim once ANIM_IDLE
   setactionanim ANIM_ATTACK
   takedmg all 0.6

	takedmg fire 0.0
   	takedmg poison 0.0
	takedmg cold 0.0
	takedmg ice 0.0
	takedmg slash 0.5
   	takedmg bash 0.5
	takedmg pierce 0.5
	takedmg ice 0.0
	takedmg lightning 1.5
	takedmg holy 1.25


	setvard IMMUNE_COLD 1
	setvard IMMUNE_FIRE 1
	setvard IMMUNE_LIGHTNING 1.5

	if ( G_CURRENT_WEATHER equals snow ) setvard WAS_SNOWING 1

	setvard SPEC_ATK_FREQUENCY SPECATK_FREQ_CIRCLE
	callevent SPEC_ATK_FREQUENCY spec_attack

	//needs a manifest effect here
	playsound 0 10 SOUND_FREEZE
	//effect glow <target> <(r,g,b)> <amount> <duration> <fadeduration>
	effect glow ent_me (255,255,255) 512 5 5
	callevent 0.1 finish_manifest
}

{ finish_manifest

	playsound 0 10 SOUND_ICEBLAST
}

{ [override] attack_1	//called by model anim attack2

	if ( ATTACK_MODE equals normal ) 
	{
		if ( $get(NPCATK_TARGET,range) < ATTACK_HITRANGE ) callevent npcatk_dodamage NPCATK_TARGET direct DAMAGE_NORM1 75% $get(ent_me,id) slash

   		if ( $rand(1,3) == 1 ) setvard ANIM_ATTACK ANIM_SMASH
	}

	if ( ATTACK_MODE equals freeze_ball ) 
	{
		if ( $get(NPCATK_TARGET,range) < ATTACK_HITRANGE ) callevent npcatk_dodamage NPCATK_TARGET direct DAMAGE_BALL1 80% $get(ent_me,id) slash

   		if ( $rand(1,10) == 1 ) setvard ANIM_ATTACK ANIM_SMASH
	}

	if ( ATTACK_MODE equals final ) 
	{
		if ( $get(NPCATK_TARGET,range) < ATTACK_HITRANGE )
		{
			callevent npcatk_dodamage NPCATK_TARGET direct DAMAGE_CLUBS1 85% $get(ent_me,id) blunt
			if ( $rand(1,10) == 1 ) applyeffect NPCATK_TARGET effects/heavy_stun 2
		}

		add ALT_ATTACK 1
		if ALT_ATTACK >= 16
        setvard ANIM_ATTACK ANIM_SMASH
		setvard ALT_ATTACK 0
	}
}

{ [override] attack_2	//called by model anim attack1

	if ( ATTACK_MODE equals normal ) 
	{
	   setvard ANIM_ATTACK ANIM_SLASH
	   if get$(NPCATK_TARGET,range) < ATTACK2_RANGE
	   callevent npcatk_dodamage NPCATK_TARGET ATTACK2_RANGE DAMAGE_NORM2 100%
	   applyeffect NPCATK_TARGET effects/effect_push 1 $relvel(0,200,30) 0
	   playsound CHAN_VOICE 10 SOUND_ATTACK1 SOUND_ATTACK2 SOUND_ATTACK3
	}

	if ( ATTACK_MODE equals freeze_ball ) 
	{
	   setvard ANIM_ATTACK ANIM_SLASH
	   if get$(NPCATK_TARGET,range) < ATTACK2_RANGE
	   callevent npcatk_dodamage NPCATK_TARGET ATTACK2_RANGE DAMAGE_BALL2 100%
	   applyeffectstack NPCATK_TARGET effects/effect_frost 5 $get(ent_me,id) $rand(3,5)
	   playsound CHAN_VOICE 10 SOUND_ATTACK1 SOUND_ATTACK2 SOUND_ATTACK3
	}

	if ( ATTACK_MODE equals final ) 
	{
	   setvard ANIM_ATTACK ANIM_SLASH
	   if get$(NPCATK_TARGET,range) < ATTACK2_RANGE
	   callevent npcatk_dodamage NPCATK_TARGET ATTACK2_RANGE DAMAGE_CLUBS2 100%
	   addvelocity NPCATK_TARGET $relvel(0,300,60)
	   applyeffect NPCATK_TARGET effects/heavy_stun 5
	   playsound CHAN_VOICE 10 SOUND_ATTACK1 SOUND_ATTACK2 SOUND_ATTACK3
	}
   
}

//Hit by someone
{ game_struck

	local MY_HP $get(ent_me,hp)
	local OUT_HP MY_HP
	subtract OUT_HP PARAM1

	//dbg MyHp MY_HP

	if ( ATTACK_MODE isnot final )

	if ( OUT_HP <= FEIGN_THRESHOLD ) callevent fall_down

   local MY_CURRENT_HP $get(ent_me,hp)

   if ( MY_CURRENT_HP < 1000 ) setvard CAN_FLINCH 1

   volume 5
   playrandomsound CHAN_BODY 10 SOUND_STRUCK1 SOUND_STRUCK2
}

//{ npc_attack
//
//   if( $rand(0,10) > 9 )
//   {
//      setvard ANIM_ATTACK ANIM_SMASH
//      setvard ATTACK_RANGE ATTACK1_RANGE
//      setvard MOVE_RANGE MOVE_RANGE1
//  }
//   else
//   {
//      setvard ANIM_ATTACK ANIM_SLASH
//      setvard ATTACK_RANGE ATTACK2_RANGE
//      setvard MOVE_RANGE MOVE_RANGE2
//   }
//}

{ npc_flinch

	playrandomsound CHAN_BODY 10 SOUND_FLINCH1 SOUND_FLINCH2
}

{ game_dodamage //1: Attack Hit (1/0)  2: Entity Attack hit  3:  Start Position  4:  End Position

	if PARAM1

	if ( ATTACK_MODE equals normal ) 
	{
		   if ( $rand(1,20) == 1 )
		   {
				//playanim critical ANIM_THROW
				playrandomsound 0 10 SOUND_PUSH1 SOUND_PUSH2
				playanim once ANIM_ALTTHROW
				setvard PASS_THIS_POS PARAM2
				callevent 0.2 throw_chummer $get(PARAM2,id)
				local DOING_SECONDARY 1
		   }
	}

	if ( ATTACK_MODE equals freeze_ball ) 
	{
		   if ( $rand(1,15) == 1 )
		   {
				//playanim critical ANIM_THROW
				playrandomsound 0 10 SOUND_PUSH1 SOUND_PUSH2
				playanim once ANIM_ALTTHROW
				setvard PASS_THIS_POS PARAM2
				callevent 0.2 throw_chummer $get(PARAM2,id)
				local DOING_SECONDARY 1
		   }
	}

	if ( ATTACK_MODE equals final ) 
	{
		   if ( $rand(1,20) == 1 )
		   {
				//playanim critical ANIM_THROW
				playrandomsound 0 10 SOUND_PUSH1 SOUND_PUSH2
				playanim once ANIM_ALTTHROW
				setvard PASS_THIS_POS PARAM2
				callevent 0.2 throw_chummer $get(PARAM2,id)
				local DOING_SECONDARY 1
		   }
	}

	if !DOING_SECONDARY
	if ( ANIM_ATTACK equals ANIM_SMASH ) playsound 0 10 SOUND_WHACK
}

{ throw_chummer	

	//PARAM1 ent to be thrown

	local MY_NME_POS $get(ent_laststruckbyme,origin)
	local MY_ME_POS $get(ent_me,origin)
	local NME_DIST $dist(MY_NME_POS,MY_ME_POS)

	////dbg ThrowChummerGot p1 PARAM1 range NME_DIST vs ATTACK2_RANGE
	if NME_DIST < ATTACK2_RANGE
	applyeffect PASS_THIS_POS effects/effect_push 3 $relvel(0,800,800) 0
}

{ [override] game_death

	if ( !WAS_SNOWING )	callexternal players ext_weather_change clear

	if ( !G_CHRISTMAS_MODE ) playsound 0 10 debris/bustglass3.wav
	if ( G_CHRISTMAS_MODE ) playsound 0 10 npc/happy_hogswatch.wav
	effect tempent gibs glassgibs.mdl $relpos(0,0,256) 1 40 10 100 30
	createnpc monsters/summon/sfx_glassmaker $relpos(0,0,0)

	setmodel none
	setbbox none
	setsolid none

	usetrigger ice_lord_died icedungeon_exit

	//	tempent gibs <modelname/spritename> <position> <size> <velocity> <randomness> <amount> <duration>
	callevent 0.1 final_death
}

{ walk_step //called by anim walk @ frames 2,12,22,31,41,51

	add STEP_COUNTER 1
	if ( STEP_COUNTER == 1 ) playsound 0 5 SOUND_STEP1
	if ( STEP_COUNTER == 2 ) playsound 0 5 SOUND_STEP2
	if ( STEP_COUNTER == 3 )
	{
		playsound 0 5 SOUND_STEP3
		setvard STEP_COUNTER 0
	}
}

{ fall_down

	if ATTACK_MODE isnot final

	callevent npcatk_suspend_ai

	setvard CANT_TURN 1

   	setbbox npcsize

	//setvard STORED_TARGET HUNT_LASTTARGET

	setmovedest none

	setvard CAN_FLINCH 0
	hp MAX_HP
	//dbg Hitpoints Reset
	invincible 2
	setmovedest none
	//race beloved //failed attempt to make playing dead skele a invalid target (destroys AI)

	playanim critical ANIM_FAKEDEATH

	setidleanim ANIM_DEAD
	setmoveanim ANIM_DEAD
	setactionanim ANIM_DEAD

	setvard ANIM_ATTACK ANIM_DEAD
	setvar ANIM_RUN ANIM_DEAD
   	setvar ANIM_IDLE ANIM_DEAD
   	setvar ANIM_WALK ANIM_DEAD

	setvard IS_FLEEING 1
	setvard PURE_FLEE 1
	setvard PLAYING_DEAD 1

	callevent 1.0 stay_down_damnit

	//setvard CAN_ATTACK 0
	//setvard CAN_HUNT 	0
	//setvard CAN_HEAR	0

	movespeed 0.0

	//dbg temp atk mode before ATTACK_MODE


	if ( ATTACK_MODE equals freeze_ball )
	{
		setvard ATTACK_MODE final
		playsound 0 10 SOUND_FAKEDEATH2
		callevent TAUNT_DELAY taunt2
		callevent GETUP_DELAY get_up
	}

	if ( ATTACK_MODE equals normal )
	{
		setvard ATTACK_MODE freeze_ball
		playsound 0 10 SOUND_FAKEDEATH1
		callevent TAUNT_DELAY taunt1
		callevent GETUP_DELAY get_up
	}

	//dbg temp atk mode after ATTACK_MODE

   	setbbox npcsize

}

{  [override] npcatk_faceattacker 			//PARAM1: Entity to face

	//some wierd stuff going on here, he's doing stuff in flee mode he shouldn't be able to
   	if !IS_FLEEING
	if !PLAYING_DEAD

   	setmovedest $get(PARAM1,id) 9999
   	look 1024	//Thothie - I can't find documentation on this command

}

{ stay_down_damnit //can't get him to stay down for the life of me second time around
	
	if PLAYING_DEAD
	setidleanim ANIM_DEAD
	setmoveanim ANIM_DEAD
	setactionanim ANIM_DEAD
	setanim ANIM_DEAD
   	setbbox npcsize
	callevent 1.0 stay_down_damnit
}

{ taunt1

	playsound 0 10 SOUND_STAGETWO1
	saytextrange 2048
	saytext Hahaaa! I've not had this much fun since the Loreldians were here!
}

{ taunt2
	playsound 0 10 SOUND_FINAL1
	saytextrange 2048
	saytext Good, gooood, maybe you are worthy of my full power...
}

{ get_up

	setvard CANT_TURN 0
   	setbbox npcsize
	if ( ATTACK_MODE equals freeze_ball )
	{
		saytextrange 2048
		playsound 0 10 SOUND_STAGETWO2
		saytext Shall we continue this little game?
	}
	setidleanim	ANIM_IDLE
	effect glow ent_me (128,128,255) 512 3 3
	playanim critical getup
}

{ getup_done //called by anim getup @ frame 29


   	setbbox npcsize
	setsolid box
	//race undead

	callevent npcatk_resume_ai

	//dbg Getup_done anim played
	
	//setmovedest	HUNT_LASTTARGET 9999

	setvar ANIM_RUN ANIM_BASERUN
   	setvar ANIM_IDLE ANIM_BASEIDLE
   	setvar ANIM_WALK ANIM_BASEWALK
	setvard ANIM_ATTACK ANIM_SMASH

	setidleanim ANIM_IDLE
	setmoveanim ANIM_RUN
	setactionanim ANIM_SMASH
	roam 			1
	invincible		0
	setvard IS_FLEEING 0
	setvard PURE_FLEE 0
	setvard PLAYING_DEAD 0

	//setvard CAN_ATTACK 1
	//setvard CAN_HUNT 	1
	//setvard CAN_RETALIATE	1	
	//setvard CAN_HEAR	1

	if ( ATTACK_MODE isnot final ) movespeed 1.0

	if ATTACK_MODE equals final

	//playanim critical ANIM_THROW

	//screenfade <target/"all"> <duration> <holdtime> <(r,g,b)> <alpha> <fadin|fadeout|noblend|perm>
	effect screenfade all 3 1 (255,255,255) 255 fadein

	//screenshake <position> <amplitude> <frequency> <duration> <radius>
	effect screenshake $relpos(0,0,0) 190 30 3.0 2048

	effect glow ent_me (255,255,255) 512 5 5

	saytextrange 2048
	playsound 0 10 SOUND_FINAL2
	saytext NOW YOU WILL ALL DIE!!!

	callevent 1.0 give_clubs
}

{ give_clubs

	setvard STUCK_CHECK_FREQUENCY 2.5 //movin faster now, and no more long anims
	playsound 0 10 SOUND_POWERUP
 	setmodelbody 0 1
	movespeed 3.0
	animspeed 2.0
	setprop ent_me animtime 3.0
	effect screenfade all 3 1 (255,255,255) 128 fadeout
	//effect screenfade all 1 0 0 0 fadein
	setvard ALT_ATTACK 1
}

{ npcatk_targetvalidate

	if ( !DID_WARCRY )
	{
		callevent 1.0 pre_taunt		
		callevent 5.0 taunt0
		playanim once ANIM_THROW
	   	setvard DID_WARCRY 1
	}
}

{ pre_taunt

		saytextrange 2048
		playsound 0 10 SOUND_INTRO1
		saytext Ah, the warm fire of live beating heart...
}

{ taunt0

		saytextrange 2048
		playsound 0 10 SOUND_INTRO2
		saytext We'll just have to put that on ice.
}

{ spec_attack

	if ( ATTACK_MODE equals normal )
	{
		//circle of ice
		if !PLAYING_DEAD
		if ( $cansee(enemy) )
		{
			playanim critical ANIM_THROW
			setvard STUCK_COUNT 0
			playsound 0 10 SOUND_MANIFEST
			effect glow ent_me (128,128,255) 80 3 0
			callevent 1.5 circle_of_ice
		}
		setvard SPEC_ATK_FREQUENCY SPECATK_FREQ_CIRCLE
	}

	if ( ATTACK_MODE equals freeze_ball )
	{
		//freeze ball
		if !PLAYING_DEAD
		if ( $cansee(enemy) )
		{
			effect glow ent_me (128,128,255) 80 3 2
			playsound 0 10 SOUND_MANIFEST
			callevent 2.0 toss_iceball
			setvard SPEC_ATK_FREQUENCY SPECATK_FREQ_FREEZE
		}
	}

	callevent SPEC_ATK_FREQUENCY spec_attack
}

{ circle_of_ice

	if !PLAYING_DEAD

	setvard STUCK_COUNT 0


	createnpc CIRCLE_SCRIPT $relpos(0,0,0) $get(ent_me,id) 10.0 10.0
}

{ toss_iceball

	if !PLAYING_DEAD

	local BALL_DEST $relpos(0,2000,0)
	//dbg Foward BALL_DEST
	playsound 0 10 SOUND_ICEBLAST
	createnpc ICE_BLAST_SCRIPT $relpos(0,64,32) $get(ent_me,id) 10.0 BALL_DEST //$get(ent_lastseen,origin) - works but goes into ground
}

{ my_target_died //PARAM1 equals target slain

	//regenerate on kill in mode 3
	if ATTACK_MODE equals final
	if HIT_RECENT equals PARAM1
	hp MAX_HP
	effect glow ent_me (0,255,0) 255 5 5
	playsound 0 10 SOUND_REGEN	
}

{ game_damaged_other

	if ATTACK_MODE equals final
	if HIT_RECENT == 0
	setvard HIT_RECENT PARAM1
	callevent 0.5 hit_recent_reset
}

{ hit_recent_reset

	setvard HIT_RECENT 0
}