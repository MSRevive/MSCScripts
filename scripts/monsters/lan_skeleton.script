//Thothie - for MS 1.35 compatitbility
//Sets you on fire, has clubs

//Greater Maldora Minion

{
   setvar ANIM_RUN walk
   setvar ANIM_WALK walk
   setvar ANIM_IDLE idle1
   setvard CAN_HUNT 1
   setvard ANIM_ATTACK attack1
   const ATTACK_DAMAGE 30
   setvard ATTACK_RANGE 100
   setvard ATTACK_HITRANGE 200
   const ATTACK_HITCHANCE 85%
   const SOUND_STRUCK1 controller/con_pain3.wav
   const SOUND_STRUCK2 controller/con_pain3.wav
   const SOUND_STRUCK3 zombie/zo_pain2.wav
   const SOUND_PAIN zombie/zo_pain2.wav
   const SOUND_ATTACK1 controller/con_attack1.wav
   const SOUND_ATTACK2 controller/con_attack2.wav
   const SOUND_DEATH controller/con_die2.wav
   const SOUND_IDLE1 controller/con_attack3.wav
   setvard MY_ENEMY enemy	//base_npc_attack - Premptive attack
   setvard RETALIATE_CHANCE 75%  //base_npc_attack - Automatically switch targets to the last damaging player
   setvard CAN_FLEE 0		//base_npc_attack - Whether I run away at low health
   setvard DROP_GOLD 		1
   setvard DROP_GOLD_MIN		15
   setvard DROP_GOLD_MAX		240

   //Precache
   precache SOUND_DEATH //This sound must be explicitly precached since
			//the play command for it isn't in this file (it's
			//in base_monster.script)
}

#include monsters/base_monster


{
   eventname npc_spawn

	setvard I_AM_TURNABLE 0

   hp 400
   //ov 359
   width 32
   height 80
   name Maldora's Minion
   roam 0
   hearingsensitivity 8
   setvard NPC_GIVE_EXP 125
   race undead
   setmodel monsters/skeleton2.mdl
   setmodelbody	0 1
   takedmg all 0.65
   takedmg fire 0.1
	takedmg holy 3.0

   setidleanim idle1
   setmoveanim ANIM_WALK
   giveitem item_torch
}

{ attack_1

   dodamage ent_lastseen ATTACK_HITRANGE ATTACK_DAMAGE ATTACK_HITCHANCE

   if( $rand(0,1) == 0 )
   {
   	volume 5
   	applyeffect ent_laststruckbyme effects/effect_burn 5 $get(ent_me,id) $rand(10,20)
   	playrandomsound 2 SOUND_ATTACK1 SOUND_ATTACK2
   }
}

{ npc_struck
   
   volume 5
   playrandomsound 2 SOUND_PAIN SOUND_PAIN SOUND_STRUCK1 SOUND_STRUCK2 SOUND_STRUCK3
}

{ npc_death
	removeitem item_torch
	setvard ANIM_DEATH dieforward
	local TAUNT_COMMENT $rand(1,3)
	if( TAUNT_COMMENT == 1 ) saytext Maldora shall destroy you!
	if( TAUNT_COMMENT == 2 ) saytext I die - yet Maldora is beyond death... Are you?
	if( TAUNT_COMMENT == 3 ) saytext Rest I will, until my master Maldora returns.
}

{ turn_undead

	//Can be damaged by Holy Light, but not turned
 
	//PARAM1 - incoming Holy damage
	//PARAM2 - exorcist

	local INC_HOLY_DMG PARAM1
	local THE_EXCORCIST PARAM2
	local ME_ME $get(ent_me,id)

	dodamage ME_ME direct INC_HOLY_DMG 100 THE_EXCORCIST

	effect glow ent_me (255,255,0) 512 1 1
}