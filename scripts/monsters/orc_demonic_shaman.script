#scope server
{
	setvard DROP_GOLD 1			//Gold Drop
	setvard DROP_GOLD_AMT $rand(50,100)	//
	setvard NPC_GIVE_EXP 300

	const FIRE_BALL_DAMAGE_NORM '$rand(100,150)'
	const FIRE_BALL_DAMAGE_ALT '$rand(5,10)'
	const DOT_DMG 50
	const DMG_DEATH_BURST 400

	const ORC_SHAMAN_CUSTOM_DEATH 1
	const ORC_SHAMAN_CUSTOM_FIREBALL 1

	const PROJECTILE_SCRIPT proj_elemental_guided
	setvard PROJ_ELEMENT_TYPE fire_jet 

	const SOUND_DEATH weapons/explode3.wav

}

{ game_precache
	precache xfireball3.spr
	precache fleshgibs.mdl //apparently not precached by default anymore
	precache magic/sps_fogfire.wav
	precache weapons/explode3.wav
	precachefile effects/sfx_explode
}

#include monsters/orc_shaman_fire

{ [override] orc_spawn
   	hp 420
   	//ov 180
   	name a|Demonic Orc Fire Shaman
	setprop ent_me skin 2
	width 32
  	height 60 
   	hearingsensitivity 8
   	takedmg all .8
	takedmg fire 0.0
	takedmg cold 1.5
	takedmg holy 0.5
	setvard IS_UNHOLY 1

	setvard FIRE_BALL_DAMAGE FIRE_BALL_DAMAGE_NORM

	clientevent persist all FIRE_FIST_SCRIPT $get(ent_me,index) 19
	setvard MY_CL_SCRIPT_IDX game.script.last_sent_id

	//giveitem item_torch

   	setmodelbody	0 0		//Body 0=naked 1=leather 2=plate 3=green-hide
   	setmodelbody	1 4		//Head 0=cap_shorthorn 1=cap_nohorn 2=buffo_skin 3=cheif_ramhorns 4=green_hood
   	setmodelbody	2 0		//Weapon 0=fist 1=axe 2=bow_weak 3=bow_strng 4=sword 5=bloodyaxe 6=sword&sheild
}

{ ext_proj_elemental_hit //<origin> <type>
	xdodamage PARAM1 128 FIRE_BALL_DAMAGE 0 ent_me ent_me none fire_effect dmgevent:fireburst
}

{ fireburst_dodamage
	if PARAM1
	if $get(PARAM2,relationship,ent_me) equals enemy
	applyeffect PARAM2 effects/dot_fire 5.0 $get(ent_me,id) DOT_DMG

	local TARG_ORG $get(PARAM2,origin)
	local MY_ORG ARROW_ORG
	local TARG_ANG $angles(MY_ORG,TARG_ORG)
	local NEW_YAW TARG_ANG
	addvelocity PARAM2 $relvel($vec(0,NEW_YAW,0),$vec(0,500,110))
}

{ orc_shaman_death
	setprop ent_me rendermode 5
	setprop ent_me renderamt 0

	clientevent remove all MY_CL_SCRIPT_IDX

	xdodamage $relpos(0,0,0) 256 DMG_DEATH_BURST 0.1 ent_me ent_me none generic
	clientevent new all effects/sfx_explode $get(ent_me,origin) 256
	effect tempent gibs fleshgibs.mdl $relpos(0,0,0) 1.0 50 50 15 10.0
	setvard SPLODIE_TARGS $get_tsphere(enemy,256)
	if SPLODIE_TARGS isnot none
	calleventloop $get_token_amt(SPLODIE_TARGS) splodie_affect_targets
}

{ splodie_affect_targets
	local CUR_TARG $get_token(SPLODIE_TARGS,game.script.iteration)
	if ( $get(CUR_TARG,relationship,ent_me) equals enemy ) applyeffect CUR_TARG effects/dot_fire 5.0 $get(ent_me,id) DOT_DMG
	local TARG_ORG $get(CUR_TARG,origin)
	local TARG_ANG $angles(game.monster.origin,TARG_ORG)
	setvelocity CUR_TARG $relvel($vec(0,TARG_ANG,0),$vec(10,1000,0))
}

{ throw_fireball
	callevent FIRE_BALL_FREQ reset_fireball
	setvard FIRE_BALL_DELAY 1
	playsound 0 10 SOUND_FIRESHOOT
	setvard PROJ_ELEMENT_TARGET NPCATK_TARGET
	//<projectile scriptname> <"view"|(src_origin)> <target|(targ_origin)|none> <speed> <damage> <cof> <skill|none> //Converted to use new tossprojectile //greatguys1@FEB2022
	tossprojectile PROJECTILE_SCRIPT $relpos(0,48,18) NPCATK_TARGET ATTACK_SPEED FIRE_BALL_DAMAGE ATTACK_CONE_OF_FIRE none
}
