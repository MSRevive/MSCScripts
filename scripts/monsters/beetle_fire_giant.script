//Poisonus explosion on death
{
	setvard NPC_GIVE_EXP 5000
	const BBET_SIZE 2
	const BBET_CAN_FLY 0
	const BBET_CAN_LEAP 0
	const BBET_CAN_SLAM 1
	const BBET_GORE_PUSH_STR 300
	const BBET_FAKE_DEATH 0

	const DMG_SLASH 160
	const DMG_GORE 240
	const DOT_POISON 100.0
	const DMG_BURST 2000
	const DMG_SLAM 1000
	const BBET_HORN 1 //I think the bone attach0 is set to on this model does not exist in this submodel

	const ANIM_SPECIAL bug_conjure //frame_spell

	const FREQ_SPIT $randf(10.0,20.0)

	const SOUND_POISON_BURST weapons/explode3.wav

	setvard FLAME_JET_DMG 200
	setvard FLAME_JET_DOT 200
}

{ game_precache
	precache monsters/beetles_giant.mdl
	precache cactusgibs.mdl
	precache explode1.spr
	precache xfireball3.spr
}

#include monsters/beetle_base

{ beetle_spawn
	name a|Giant Fire Beetle
	hp 10000
	takedmg all 0.5
	takedmg fire 0.0
	takedmg cold 1.25
	setmodelbody 0 2 //0=Horned 1=Venom 2=Fire
}

//================ Slam attack
{ beetle_slam
	setvard SLAM_POS $get(ent_me,attachpos,1)
	vectorset SLAM_POS z $get_ground_height(SLAM_POS)
	dodamage SLAM_POS BBET_SLAM_RADIUS DMG_SLAM 100% 0 reflective blunt
	clientevent new all effects/sfx_stun_burst SLAM_POS 256 0
	setvard STUN_TARGS $get_tsphere(enemy,256,SLAM_POS)
	if STUN_TARGS isnot none
	calleventloop $get_token_amt(STUN_TARGS) affect_targets
}

{ affect_targets
	local CUR_TARG $get_token(STUN_TARGS,game.script.iteration)
	if $get(CUR_TARG,onground)
	applyeffect CUR_TARG effects/debuff_stun 8.0 $get(ent_me,id)
	local TARG_ORG $get(CUR_TARG,origin)
	local TARG_ANG $angles(SLAM_POS,TARG_ORG)
	setvelocity CUR_TARG $relvel($vec(0,TARG_ANG,0),$vec(10,1000,0))
}

//================ Splodie Death (from Venom Beetle)
{ game_death
	setprop ent_me rendermode 5
	setprop ent_me renderamt 0

	playsound 0 10 SOUND_POISON_BURST
	xdodamage $relpos(0,0,0) 256 DMG_BURST 0 ent_me ent_me none generic
	clientevent new all effects/sfx_explode $get(ent_me,origin) 512
	effect tempent gibs cactusgibs.mdl $relpos(0,0,0) 1.0 50 50 15 2.0
	setvard POISON_TARGS $get_tsphere(enemy,512)
	playsound 0 10 SOUND_POISON_BURST
	if POISON_TARGS isnot none
	calleventloop $get_token_amt(POISON_TARGS) poison_affect_targets
}

{ poison_affect_targets
	local CUR_TARG $get_token(POISON_TARGS,game.script.iteration)
	applyeffect CUR_TARG effects/dot_fire 5.0 $get(ent_me,id) DOT_POISON
	local TARG_ORG $get(CUR_TARG,origin)
	local TARG_ANG $angles(game.monster.origin,TARG_ORG)
	setvelocity CUR_TARG $relvel($vec(0,TARG_ANG,0),$vec(10,1000,0))
}

//================ Spit Fire (multi_target)
{ npcatk_hunt
	if NPCATK_TARGET isnot unset
	if game.time > NEXT_SPIT
	setvard NEXT_SPIT game.time
	add NEXT_SPIT FREQ_SPIT
	setvard AS_ATTACKING game.time
	playanim critical ANIM_SPECIAL
}

{ frame_spell
	setvard SPIT_LIST $get_tsphere(enemy,1024)

	if SPIT_LIST isnot none
	setvard SPIT_TARG_IDX 0
	callevent spit_targets
}

{ spit_targets
	local CUR_SPIT_TARG $get_token(SPIT_LIST,SPIT_TARG_IDX)

	//<projectile scriptname> <"view"|(src_origin)> <target|(targ_origin)|none> <speed> <damage> <cof> <skill|none> //Converted to use new tossprojectile //greatguys1@FEB2022
	tossprojectile "proj_flame_jet" $relpos(0,64,28) CUR_SPIT_TARG 300 DMG_SPIT 2 none 

	add SPIT_TARG_IDX 1
	local N_SPIT_TARGS $get_token_amt(SPIT_LIST)
	subtract N_SPIT_TARGS 1
	if SPIT_TARG_IDX < N_SPIT_TARGS
	add SPIT_TARG_IDX 1
	callevent 0.1 spit_targets
}