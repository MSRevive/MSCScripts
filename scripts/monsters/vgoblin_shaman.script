//Vile Goblin Shaman
//- Lots o spit bile
//- Poison cloud
//- Poison nova (defensive blast)
//- Horrors
//- Elder Horror Death script
#scope server
{
	const NEW_MODEL 1

	const NPC_BASE_EXP 800

	//not very agile
	const GOB_JUMPER 0
	const GOB_CHARGER 0

	const DMG_FIST 10
	const DMG_FIREBALL 75
	const DOT_POISON $rand(20,40)

	setvard CAN_FIREBALL 1
	const FREQ_FIREBALL 2.0
	const DMG_FIREBALL 150
	const DMG_FIREBALL_DOT 25
	const DMG_NOVA 200
	const DOT_FIST 50

	const FREQ_NOVA 10.0
	const FREQ_CLOUD $randf(10.0,20.0)
	const FREQ_SUMMON $randf(10.0,20.0)

	setvard ATTACK_MOVERANGE 800
	setvard MOVE_RANGE 800
	setvard ATTACK_HITCHANCE 75%
	setvard ANIM_ATTACK swordswing1_L

	const SOUND_FIREBALL bullchicken/bc_attack2.wav

	const SUMMON_SCRIPT monsters/summon/horror_egg
	const DEATH_SCRIPT monsters/horror2
	const CLOUD_SCRIPT monsters/summon/npc_poison_cloud2
	const NOVA_SCRIPT monsters/summon/poison_burst
	const FIST_SCRIPT monsters/poison_fist_cl
}

#include monsters/bgoblin

{ [shared] game_precache
	precachefile SUMMON_SCRIPT
	precachefile DEATH_SCRIPT
	precachefile CLOUD_SCRIPT
	precachefile NOVA_SCRIPT
	precachefile FIST_SCRIPT
}


{ [override] [shared] goblin_spawn

	name a|Vile Goblin Poisoner
	race goblin
	blood green
	hp 1000
	width 32
	height 60
	roam 1

	hearingsensitivity 2

	if ( !NEW_MODEL )
	{
		setmodel monsters/goblin2.mdl
		width 32
		height 60

		setmodelbody 0 0 //body: 0-Runed 1-Archer 2-Leathers 3-Sniper
		setmodelbody 1 4 //head: 0-Horns* 1-Spikes* 2-Animal 3-Chief 4-Cowl *redcap
		setmodelbody 2 0 //weapon: 0-Unarmed 1-Axe 2-wbow 3-sbow 4-sword 5-rAxe 6-sword&shield 7-club 8-knife
		setmodelbody 3 0 //arrow: on/off

		setprop ent_me skin 3
	}
	else
	{
		setmodel monsters/goblin_new.mdl
		width 24
		height 50

		setmodelbody 0 0 //body: 0-Bare 1-Quiver&Strap
		setmodelbody 1 4 //head: 0-Bare 1-Horns 2-Animal 3-Chief 4-Rag
		setmodelbody 2 0 //weapon: 0-Unarmed 1-Axe 2-wbow 3-sbow 4-sword 5-rAxe 6-sword&shield 7-club 8-knife
		setmodelbody 3 0 //arrow: On/off
		
		setprop ent_me skin 2 //0-norm 1-blood 2-vile
	}

	setidleanim ANIM_IDLE
	setmoveanim ANIM_WALK

	takedmg poison 0.0
	takedmg lightning 1.5

	clientevent persist all FIST_SCRIPT $get(ent_me,index) 19
	setvard MY_CL_SCRIPT_IDX game.script.last_sent_id
}


{ [override] toss_fireball

	setvard TOSS_FIREBALL 0
	//<projectile scriptname> <"view"|(src_origin)> <target|(targ_origin)|none> <speed> <damage> <cof> <skill|none> //Converted to use new tossprojectile //greatguys1@FEB2022
	tossprojectile "proj_poison_spit2" $relpos(0,48,0) NPCATK_TARGET 400 DMG_FIREBALL 0.5 none
	playsound 0 10 SOUND_FIREBALL
}

{ game_death
	clientevent remove all MY_CL_SCRIPT_IDX
	if !NO_DEATH_HORROR
	createnpc DEATH_SCRIPT $relpos(0,0,0)
	callexternal GAME_MASTER gm_fade $get(ent_me,id) 5
}

{ game_struck

	if ( $get(NPCATK_TARGET,range) < 96 )
	{
		if game.time > NEXT_NOVA
		setvard NEXT_NOVA game.time
		playsound 0 10 SOUND_SHAM_ALERT
		playanim critical ANIM_WARCRY
		setvard AS_ATTACKING game.time
		add NEXT_NOVA FREQ_NOVA
		callevent do_nova
	}
}

{ do_nova
	createnpc NOVA_SCRIPT $relpos(0,0,0) $get(ent_me,id) 256 1 DMG_NOVA DOT_POISON
}

{ [override] swing_sword

	if ( TOSS_FIREBALL ) callevent toss_fireball
	if !TOSS_FIREBALL
	playrandomsound 0 10 SOUND_ATTACK1 SOUND_ATTACK2 SOUND_ATTACK3
	setvard POISON_FIST 1
	dodamage NPCATK_TARGET ATTACK_HITRANGE DMG_FIST ATTACK_HITCHANCE blunt
}

{ game_dodamage //PARAM1=hit:0|1 PARAM2=ent_hit PARAM3=(start) PARAM4=(end)

	//fire axe effect
	if ( PARAM1 )
	{
		if POISON_FIST
		applyeffect PARAM2 effects/dot_poison 5.0 $get(ent_me,id) DOT_FIST
	}
	setvard POISON_FIST 0
}

{ [override] gob_cycle_up

	setvard NEXT_FIREBALL game.time
	add NEXT_FIREBALL 3.0

	setvard NEXT_CLOUD game.time
	add NEXT_CLOUD $randf(5.0,10.0)

	setvard NEXT_SUMMON game.time
	add NEXT_SUMMON $randf(15.0,20.0)

	if !GOB_JUMP_SCANNING
	setvard GOB_JUMP_SCANNING 1
	playsound 0 10 SOUND_SHAM_ALERT
}

{ [override] gob_hunt

	if $get(NPCATK_TARGET,isalive)
	if !SUSPEND_AI
	if !IS_FLEEING

	if ( game.time > NEXT_CLOUD )
	{
		if $cansee(NPCATK_TARGET)
		setvard NEXT_CLOUD game.time
		add NEXT_CLOUD FREQ_CLOUD
		playanim critical ANIM_WARCRY
		callevent npcatk_suspend_ai 1.5
		playsound 0 10 SOUND_SHAM_ALERT
		local CLOUD_POS $get(NPCATK_TARGET,origin)
		if ( $get(NPCATK_TARGET,isplayer) ) vectoradd CLOUD_POS z -32 //player center compensate
		createnpc CLOUD_SCRIPT CLOUD_POS $get(ent_me,id) DMG_FIREBALL_DOT 10.0 1
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( !SUMMON_ALIVE )
	{
		if game.time > NEXT_SUMMON
		setvard NEXT_SUMMON game.time
		add NEXT_SUMMON FREQ_SUMMON
		playanim critical ANIM_SMASH
		setvard AS_ATTACKING game.time
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( CAN_FIREBALL )
	{
		if game.time > NEXT_FIREBALL
		if $get(NPCATK_TARGET,range) > MIN_FIREBALL_DIST
		if $cansee(NPCATK_TARGET)
		setvard NEXT_FIREBALL game.time
		add NEXT_FIREBALL FREQ_FIREBALL
		setvard TOSS_FIREBALL 1
		playanim once ANIM_SWIPE
		setvard AS_ATTACKING game.time
		add AS_ATTACKING 10.0
		playsound 0 10 SOUND_FIREBALL_CAST
	}

	//idle in place if can see target but not recently hurt
	if ( $cansee(NPCATK_TARGET) )
	{

		local STRUCK_CHECK game.time
		subtract STRUCK_CHECK 5.0 
		if ( STRUCK_CHECK > LAST_STRUCK )
		{
			if !IS_FLEEING
			setmoveanim ANIM_IDLE
			setvard AS_ATTACKING game.time
			add AS_ATTACKING 10.0
		}
		else
		{
			setmoveanim ANIM_RUN
		}
	}
	else
	{
		setmoveanim ANIM_RUN
	}
}

{ [override] swing_axe

	callevent summon_horror_egg
}

{ summon_horror_egg
	setvard SUMMON_ALIVE 1
	createnpc SUMMON_SCRIPT $relpos(0,64,16) $get(ent_me,id)
	setvard LAST_EGG $get(ent_lastcreated,id)
	callevent 0.1 boost_egg
}

{ boost_egg
	addvelocity LAST_EGG $relvel(0,130,120)
}

{ horror_died
	setvard NEXT_SUMMON game.time
	add NEXT_SUMMON FREQ_SUMMON
	setvard SUMMON_ALIVE 0
}

{ no_death_event
	setvard NO_DEATH_HORROR 1
}