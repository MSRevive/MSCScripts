//This is an older script and may have some bad conventions as a result.

//Zombie boss with a twist
//. tele -3000 2300 -256
{
	if ( $lcase(game.map.name) equals lostcaverns ) setvard NPC_IS_BOSS 1

	const NPC_BOSS_REGEN_RATE 0.1
	const NPC_BOSS_RESTORATION 0.5

	const FREQ_TBEAM 30.0
	const BEAM_DUR 15.0

	const EYE_HP 1000
	const EYE_POWER 50

	setvar ANIM_WALK walk
	setvar ANIM_RUN walk
	const ANIM_SWIPE attack1
	const ANIM_DISEASE attack2
	setvard ANIM_ATTACK ANIM_SWIPE
	setvard ANIM_DEATH diesimple
	setvar ANIM_IDLE idle1
	setvard ANIM_DEATH1 diesimple
	setvard ANIM_DEATH2 diebackward
	setvard ANIM_DEATH3 dieheadshot
	setvard ANIM_DEATH4 dieheadshot2
	setvard ANIM_DEATH5 dieforward
	setvard ANIM_DEATH ANIM_DEATH1

	setvard CAN_FLINCH 1
	setvard FLINCH_CHANCE 30
	
	const SOUND_IDLE1 garg/gar_breathe1.wav
	const SOUND_IDLE2 garg/gar_breathe2.wav
	const SOUND_IDLE3 garg/gar_breathe3.wav
	const SOUND_IDLE4 garg/gar_idle4.wav
	const SOUND_PAIN1 garg/gar_idle1.wav
	const SOUND_PAIN2 garg/gar_idle2.wav
	const SOUND_PAIN3 garg/gar_idle3.wav
	const SOUND_STRUCK1 weapons/cbar_hitbod1.wav
	const SOUND_STRUCK2 weapons/cbar_hitbod2.wav
	const SOUND_STRUCK3 weapons/cbar_hitbod3.wav
	const SOUND_WARCRY1 garg/gar_alert1.wav
	const SOUND_WARCRY2 garg/gar_alert2.wav
	const SOUND_WARCRY3 garg/gar_alert3.wav
	const SOUND_RAGE1 garg/gar_attack1.wav
	const SOUND_RAGE2 garg/gar_attack2.wav
	const SOUND_DEATH garg/gar_die1.wav
	const SOUND_MISS1 zombie/claw_miss1.wav
	const SOUND_MISS2 zombie/claw_miss2.wav
	const SOUND_HIT1 zombie/claw_strike1.wav
	const SOUND_HIT2 zombie/claw_strike2.wav
	precache SOUND_DEATH //external

	const ATTACK_DAMAGE $randf(100,200)
	setvard ATTACK_RANGE 120
	setvard ATTACK_HITRANGE 150
	const ATTACK_HITCHANCE 80
	setvard ATTACK_MOVERANGE 80

	const DISEASE_FREQ 5.0
	const DISEASE_DMG $randf(13,16)
	const DISEASE_DUR $rand(20,25)

	setvard NPC_GIVE_EXP 1000

	const MONSTER_MODEL monsters/zombie_huge.mdl
	precache MONSTER_MODEL //upload new model

	const ME_NO_WANDER 1

	setvard HEAR_RANGE_MAX 200
	setvard HEAR_RANGE_PLAYER 200
}

#include monsters/base_monster_new

{ npc_spawn

	name Zygoli, The All Seeing

	setmodel MONSTER_MODEL
	hp 6000
	width 50
	height 110
	setidleanim ANIM_IDLE
	setmoveanim ANIM_WALK
	playanim once ANIM_IDLE //stop the shakes
	race undead
	hearingsensitivity 3

	roam 1

	takedmg holy 1.0
	takedmg poison 0.0
	takedmg fire 2.0
	takedmg ice 0.25
	takedmg cold 0.25
	takedmg pierce 0.5
	takedmg blunt 1.0
	takedmg slash 1.0

	setvard BASE_MOVERATE 1.5
	setvard BASE_FRAMERATE 1.5
	movespeed 1.5
	setanim.movespeed 1.5
	setanim.framerate 1.5

	callevent 1.0 idle_sounds
	callevent 0.1 setup_eyes
}

{ setup_eyes
	setvard N_EYES 4
	callevent update_eyes

	local EYE_POS $get(ent_me,origin)
	vectoradd EYE_POS $relpos($vec(0,0,0),$vec(0,64,100))
	createnpc monsters/summon/zy_eye EYE_POS $get(ent_me,id) EYE_HP EYE_POWER
	callevent 0.1 setup_eye2
}

{ setup_eye2
	local EYE_POS $get(ent_me,origin)
	vectoradd EYE_POS $relpos($vec(0,90,0),$vec(0,64,100))
	createnpc monsters/summon/zy_eye EYE_POS $get(ent_me,id) EYE_HP EYE_POWER
	callevent 0.1 setup_eye3
}

{ setup_eye3
	local EYE_POS $get(ent_me,origin)
	vectoradd EYE_POS $relpos($vec(0,180,0),$vec(0,64,100))
	createnpc monsters/summon/zy_eye EYE_POS $get(ent_me,id) EYE_HP EYE_POWER
	callevent 0.1 setup_eye4
}

{ setup_eye4
	local EYE_POS $get(ent_me,origin)
	vectoradd EYE_POS $relpos($vec(0,270,0),$vec(0,64,100))
	createnpc monsters/summon/zy_eye EYE_POS $get(ent_me,id) EYE_HP EYE_POWER
}

{ respawn_eye
	infomsg all "ZYGOLI THE ALL SEEING" "Has summoned a new eye."
	add N_EYES 1	
	createnpc monsters/summon/zy_eye $relpos(0,0,150) $get(ent_me,id) EYE_HP EYE_POWER
	callevent 0.1 update_eyes
}

{ ext_eye_died
	subtract N_EYES 1
	callevent npc_flinch
	effect glow ent_me (255,255,255) 200 1.0 1.0
	callevent 0.1 update_eyes
	callevent 120.0 respawn_eye
}

{ update_eyes
	if ( N_EYES == 0 ) takedmg all 1.0
	if ( N_EYES == 1 ) takedmg all 0.5
	if ( N_EYES == 2 ) takedmg all 0.3
	if ( N_EYES == 3 ) takedmg all 0.1
	if ( N_EYES < 4 ) invincible 0
	if ( N_EYES == 4 )
	{
		takedmg all 0.0
		invincible 1
	}
}

{ idle_sounds

	if ( NPCATK_TARGET equals unset ) playrandomsound 0 10 SOUND_IDLE1 SOUND_IDLE2 SOUND_IDLE3 SOUND_IDLE4

	if ( NPCATK_TARGET isnot unset ) playrandomsound 0 5 SOUND_RAGE1 SOUND_RAGE2

	local NEXT_SOUND $randf(4,15)

	callevent NEXT_SOUND idle_sounds
}

{ walk_step1
	playsound 0 10 garg/gar_step1.wav
}
{ walk_step2
	playsound 0 10 garg/gar_step2.wav
}
{ walk_step3
	playsound 0 10 garg/gar_step1.wav
}
{ walk_step4
	playsound 0 10 garg/gar_step2.wav
}
{ walk_step5
	playsound 0 10 garg/gar_step1.wav
}
{ walk_step6
	playsound 0 10 garg/gar_step2.wav
}

{ npc_death

	local PICK_DEATH $rand(1,5)
	
	if ( PICK_DEATH == 1 ) setvard ANIM_DEATH ANIM_DEATH1
	if ( PICK_DEATH == 2 ) setvard ANIM_DEATH ANIM_DEATH2
	if ( PICK_DEATH == 3 ) setvard ANIM_DEATH ANIM_DEATH3
	if ( PICK_DEATH == 4 ) setvard ANIM_DEATH ANIM_DEATH4
	if ( PICK_DEATH == 5 ) setvard ANIM_DEATH ANIM_DEATH5
}

{ game_struck
	playrandomsound 0 10 SOUND_STRUCK1 SOUND_STRUCK2 SOUND_STRUCK3
	if ( !PAIN_DELAY ) callevent 0.1 pain_sound
}

{ pain_sound

	setvard PAIN_DELAY 1
	local $randf(5,10) NEXT_PAIN
	playrandomsound 0 10 SOUND_PAIN1 SOUND_PAIN2 SOUND_PAIN3
	callevent NEXT_PAIN pain_delay_reset
}

{ pain_delay_reset
	setvard PAIN_DELAY 0
}

{ npc_flinch

	local R_FLINCH $rand(1,4)
	if ( R_FLINCH == 1 ) setvard FLINCH_ANIM flinchsmall
	if ( R_FLINCH == 2 ) setvard FLINCH_ANIM flinch
	if ( R_FLINCH == 3 ) setvard FLINCH_ANIM bigflinch
	if ( R_FLINCH == 4 ) setvard FLINCH_ANIM llflinch
	playrandomsound 0 10 SOUND_PAIN1 SOUND_PAIN2 SOUND_PAIN3
}

{ attack_1
	////dbg temp using swipe ANIM_ATTACK
	setvard I_ATTACKING 1
	local F_ATTACK_DAMAGE ATTACK_DAMAGE
	multiply F_ATTACK_DAMAGE N_EYES
	callevent npcatk_dodamage NPCATK_TARGET ATTACK_HITRANGE F_ATTACK_DAMAGE ATTACK_HITCHANCE blunt
}

{ attack_2
	////dbg temp using disease ANIM_ATTACK
	setvard I_ATTACKING 1
	local F_ATTACK_DAMAGE ATTACK_DAMAGE
	multiply F_ATTACK_DAMAGE N_EYES
	callevent npcatk_dodamage NPCATK_TARGET ATTACK_HITRANGE F_ATTACK_DAMAGE ATTACK_HITCHANCE blunt
	setvard I_DISEASE 1
	setvard ANIM_ATTACK ANIM_SWIPE
}

{ npcatk_targetvalidate

	if !DID_WARCRY
	setvard DID_WARCRY 1
	setvard DISEASE_DELAY 1
	callevent DISEASE_FREQ reset_disease_delay
	playrandomsound 0 10 SOUND_WARCRY1 SOUND_WARCRY2 SOUND_WARCRY3
	callevent FREQ_TBEAM do_tbeam
}

{ npc_selectattack


	////dbg temp selecting attack ( del DISEASE_DELAY vs DISEASE_FREQ )
	if !DISEASE_DELAY
	setvard DISEASE_DELAY 1
	callevent DISEASE_FREQ reset_disease_delay
	setvard ANIM_ATTACK ANIM_DISEASE
}

{ game_dodamage //1:AttackHit(1/0)  2:EntityHit  3:StartOrigin  4:EndOrigin

	if I_ATTACKING
	setvard I_ATTACKING 0
	if ( PARAM1 ) playrandomsound 0 10 SOUND_HIT1 SOUND_HIT2
	if ( !PARAM1 ) playrandomsound 0 10 SOUND_MISS1 SOUND_MISS2

	if I_DISEASE
	setvard I_DISEASE 0
	if PARAM1
	applyeffect PARAM2 effects/effect_disease DISEASE_DUR $get(ent_me,id) DISEASE_DMG 1
}

{ reset_tbeam_delay
	setvard TBEAM_DELAY 0
}

{ do_tbeam

	callevent FREQ_TBEAM do_tbeam

	if $cansee(enemy)
	setvard TBEAM_TARG $get(ent_lastseen,id)
	setvard TBEAM_ON 1
	setvard TBEAM_VIS 0
	callevent tbeam_loop
	callevent BEAM_DUR end_tbeam
}

{ end_tbeam
	clientevent update all TBEAM_IDX dbeam_off
	setvard TBEAM_ON 0
	setvard TBEAM_VIS 0
}

{ tbeam_loop

	if TBEAM_ON
	callevent 0.2 tbeam_loop

	local CAN_SEE_BTARG $cansee(TBEAM_TARG)

	if ( CAN_SEE_BTARG )
	{
		addvelocity TBEAM_TARG $relvel(10,-300,10)
		
		if ( $get(TBEAM_TARG,range) > ATTACK_RANGE )
		{
			setvard NPC_FORCED_MOVEDEST 1
			setmovedest TBEAM_TARG 100
		}

		if ( !TBEAM_VIS )
		{
			setvard TBEAM_VIS 1
			clientevent update all TBEAM_IDX dbeam_target $get(TBEAM_TARG,index)
		}
	
		//fx re-establish every 5 secs
		local DIFF_TIME game.time
		subtract DIFF_TIME TBEAM_LASTFX

		if DIFF_TIME > 5.0
		setvard TBEAM_LASTFX game.time
		dodamage TBEAM_TARG direct 10.0 100% ent_me magic

		playsound 0 5 magic/pulsemachine_noloop.wav

		//effect beam ents lgtning.spr 90 TBEAM_TARG 0 (255,0,0) 200 0 5
		//local BEAM_START game.monster.origin
		//vectoradd BEAM_START z 100
		//effect beam end lgtning.spr 120 BEAM_START TBEAM_TARG 0 (255,0,0) 200 5 5
		//effect screenfade TBEAM_TARG 4 1 (255,0,0) 128
	}

	if ( !CAN_SEE_BTARG )
	{
		setvard TBEAM_VIS 0
		clientevent update all TBEAM_IDX dbeam_off
	}
}

{ npc_post_spawn
	clientevent new all effects/dynamic_beam_cl $get(ent_me,index) (255,0,0) 100
	setvard TBEAM_IDX game.script.last_sent_id
}

{ game_death
	clientevent remove all TBEAM_IDX
	callexternal all zygoli_died
}