{
	setvard NPC_PET_TYPE wolf_shadow //tells charm that this can be tamed
	const NPC_PET_SCRIPT monsters/companion/pet_wolf_shadow

	const NPC_BASE_EXP 300

	setvar IS_UNHOLY 1

	const CUSTOM_WOLF 1
	const AM_ALPHA 1
	const DOT_BURN 40

	const DMG_BITE $randf(20,50)
	const DMG_CLAW $randf(10,30)

	const FREQ_COMBAT_HOWL $randf(10,15)

	const CHANCE_BURN 30%

	const SOUND_BURN ambience/steamburst1.wav

	setvard NPC_GIVE_EXP 150
}

#include [server] monsters/wolf_base

{ [shared] npc_spawn

	name a|Shadow Wolf
	race demon
	setmodel MONSTER_MODEL
	width 36
	height 48
	roam 1
	hp 900
	takedmg fire 0.0
	takedmg cold 1.25
	takedmg holy 1.0
	setvard NPC_GIVE_EXP 30
	setmodelbody 0 3
	hearingsensitivity 8

	setvard NEXT_HOWL 0
	setvarg G_ALPHA $get(ent_me,id) //lesser wolves consider me alpha
}

{ do_combat_howl
	//stunning howl
	if !I_R_FROZEN
	setvard DOING_HOWL 1
	playanim critical ANIM_HOWL
	playrandomsound 0 10 SOUND_HOWL1 SOUND_HOWL2
	dodamage $relpos(0,0,0) 200 0 100% 0.0 reflective target
	callevent 0.1 end_combat_howl
}

{ end_combat_howl
	setvard DOING_HOWL 0
	callevent FREQ_COMBAT_HOWL do_combat_howl
}

{ game_dodamage

	if ( PARAM1 )
	{
		if !DOING_HOWL
		if $rand(1,100) < CHANCE_BURN
		if $get(PARAM2,relationship,ent_me) equals enemy
		playermessage PARAM2 You hear a stunning howl!
		applyeffect PARAM2 effects/dot_fire 10 $get(ent_me,id) DOT_BURN
		playsound 0 10 SOUND_BURN
		effect glow ent_me (255,75,0) 128 1 1
	}

	if DOING_HOWL
	if $get(PARAM2,relationship,ent_me) equals enemy
	applyeffect PARAM2 effects/debuff_stun 5 $get(ent_me,id)
}

{ cycle_up
	if !CYCLES_STARTED
	setvard CYCLES_STARTED 1
	callevent FREQ_COMBAT_HOWL do_combat_howl
}

{ sfor_extra_wolf //extra param
	setvard DROPS_CONTAINER 1
	setvard CONTAINER_DROP_CHANCE 100%
	setvard CONTAINER_SCRIPT chests/sfor_wolf
}