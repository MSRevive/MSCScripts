#scope server
{ [shared]
	setvar ANIM_WALK fly1
	setvar ANIM_IDLE idle1
	setvar ANIM_RUN fly1
	const ANIM_FLY fly1
	const ANIM_ROLL fly2
	setvard ANIM_ATTACK bite1 //attack1
	const ANIM_BITE1 bite1 //attack1
	const ANIM_BITE2 bite2 //attack2
	setvard ANIM_DEATH die
	const ANIM_BREATH breath //breath_attack
	const ANIM_HOVER hover
}

{
	setvar AS_SUMMON_TELE_CHECK 1

	const DMG_PROJECTILE 100
	const PROJ_SCRIPT proj_lightning_ball
	const PROJ_SPEED 200
	const PROJ_FOV 0.5

	const FX_BURST_SCRIPT effects/sfx_shock_burst
	const EFFECT_DOT effects/dot_lightning
	const DUR_DOT 5.0
	const DMG_DOT 30
	const DMG_BITE 100
	const DMG_BLAST 200
	const ELEMENT_COLOR (255,255,0)
	const BURST_ELEMENT lightning_effect

	const DMG_PROJECTILE 100
	const PROJ_SCRIPT proj_lightning_ball
	const PROJ_SPEED 500
	const PROJ_FOV 0.5

	setvard DROP_GOLD 1
	setvard DROP_GOLD_MIN 40
	setvard DROP_GOLD_MAX 200
	setvard NPC_GIVE_EXP 400

	setvar IS_UNHOLY 1

	setvard SPIT_AMMO 3
	const MAX_SPIT_AMMO 3
	setvard BREATH_AMMO 1

	setvard BALL_SIZE 5
	setvard BALL_DMG 100

	const FREQ_HORROR_BOOST $randf(5.0,10.0)

	setvard ATTACK_RANGE 60
	setvard ATTACK_HITRANGE 60
	const ATTACK_HITCHANCE 80%
	const DMG_BITE 100


	//sounds
	const SOUND_IDLE1 controller/con_idle1.wav
	const SOUND_IDLE2 controller/con_idle2.wav
	const SOUND_IDLE3 controller/con_idle3.wav

	const SOUND_SPIT1 bullchicken/bc_attack3.wav
	const SOUND_SPIT2 bullchicken/bc_attack2.wav

	const SOUND_ATTACK1 controller/con_attack1.wav
	const SOUND_ATTACK2 controller/con_attack2.wav
	const SOUND_ATTACK3 controller/con_attack3.wav

	const SOUND_DEATH controller/con_die1.wav

	const SOUND_PAIN0 debris/bustflesh2.wav		//most common
	const SOUND_PAIN1 controller/con_pain1.wav	//rare > 50%
	const SOUND_PAIN2 controller/con_die2.wav	//rare < 50%

	const SOUND_SPRAY controller/con_attack3.wav

	const SOUND_FLAP1 monsters/bat/flap_big1.wav
	const SOUND_FLAP2 monsters/bat/flap_big2.wav

	const SOUND_SPRAY debris/beamstart1.wav
	const SOUND_SHOCK1 debris/zap8.wav
	const SOUND_SHOCK2 debris/zap3.wav
	const SOUND_SHOCK3 debris/zap4.wav
}

#include monsters/base_monster_new
#include monsters/base_flyer_grav

{ npc_spawn
	callevent horror_spawn
}

{ horror_spawn
	name an|Enraged Electrical Horror

	hp 1000	//hi HP, no armor
	width 32
	height 32
	setbbox npcsize
	roam 1
	race demon
	setidleanim ANIM_IDLE
	setmoveanim ANIM_WALK
	hearingsensitivity 11
	setmodel monsters/edwardgorey2.mdl
	setmodelbody 0 2
	movespeed 3.0
	setvard BASE_MOVESPEED 3.0

	playanim once ANIM_WALK

	if game.serverside
	callevent 1.0 idle_sounds
	takedmg poison 2.0
	takedmg holy 2.0
	takedmg lightning 0.0

	setvard SPIT_AMMO 3
	setvard BREATH_AMMO 1
}

{ npc_post_spawn
	setvard NPC_HALF_HEALTH $get(ent_me,maxhp)
	multiply NPC_HALF_HEALTH 0.5
}

{ idle_sounds

	if $get(ent_me,isalive)

	local NEXT_SOUND $randf(3,10)

	callevent NEXT_SOUND idle_sounds

	if NPCATK_TARGET equals none
	playrandomsound 0 10 SOUND_IDLE1 SOUND_IDLE2 SOUND_IDLE3
}

{ npc_targetsighted
	if ( SPIT_AMMO > 0 )
	{
		setvard ANIM_ATTACK ANIM_BREATH
		setvard PROJECTILE_ATTACK 1
		setvard BREATH_ATTACK 0
		setvard ANIM_WALK ANIM_FLY
		setvard ANIM_RUN ANIM_FLY
		playanim once ANIM_ATTACK
		setvard BFLY_AGRESSIVE 0
		local EXIT_SUB 1
	}
	else
	{
		//consider adding to base_flyer_grav under setvard BF_AGRESSIVE 1|0
		setvard BFLY_AGRESSIVE 1
		if ( BREATH_AMMO > 0 )
		{
			setvard ANIM_ATTACK ANIM_BREATH
			setvard ANIM_WALK ANIM_ROLL
			setvard ANIM_RUN ANIM_ROLL
			setvard PROJECTILE_ATTACK 0
			setvard BREATH_ATTACK 1
			local EXIT_SUB 1
		}
		else
		{
			if ( ANIM_ATTACK isnot ANIM_BITE1 )
			{
				if ANIM_ATTACK isnot ANIM_BITE2
				setvard ANIM_ATTACK ANIM_BITE1
			}
			setvard ANIM_WALK ANIM_FLY
			setvard ANIM_RUN ANIM_FLY
			setvard BREATH_ATTACK 0
			setvard PROJECTILE_ATTACK 0
		}
	}


	if !EXIT_SUB

	if game.time > NEXT_HORROR_BOOST
	setvard NEXT_HORROR_BOOST game.time
	add NEXT_HORROR_BOOST FREQ_HORROR_BOOST
	playsound 0 10 SOUND_FLAP
	playanim once ANIM_ROLL
	addvelocity ent_me $relvel(0,800,0)
}

{ [server] breath_attack //called by model anim: breath
	if ( PROJECTILE_ATTACK )
	{
		//lightning ball
		//<projectile scriptname> <"view"|(src_origin)> <target|(targ_origin)|none> <speed> <damage> <cof> <skill|none> //Converted to use new tossprojectile //greatguys1@FEB2022
		tossprojectile PROJ_SCRIPT view none PROJ_SPEED DMG_PROJECTILE PROJ_FOV none
		playrandomsound 0 10 SOUND_SHOCK1 SOUND_SHOCK2 SOUND_SHOCK3
		subtract SPIT_AMMO 1
		if ( SPIT_AMMO <= 0 ) callevent 30.0 reload_spit
	}

	if ( BREATH_ATTACK )
	{
		//lightning burst
		callevent 15.0 reload_breath
		effect glow ent_me ELEMENT_COLOR 768 2 2
		playsound 0 10 SOUND_SPRAY
		setvard BURST_POS $get(ent_me,origin)
		vectorset BURST_POS z $get_ground_height(BURST_POS)
		clientevent new all FX_BURST_SCRIPT BURST_POS 256 1 ELEMENT_COLOR
		dodamage BURST_POS 256 DMG_BLAST 100% 0 reflective BURST_ELEMENT
		subtract BREATH_AMMO 1
		setvard BURST_TARGS $get_tsphere(enemy,256,BURST_POS)
		if BURST_TARGS isnot none
		calleventloop $get_token_amt(BURST_TARGS) burst_affect_targets
	}
}

{ reload_breath
	setvard BREATH_AMMO 1
}

{ burst_affect_targets
	local CUR_TARG $get_token(BURST_TARGS,game.script.iteration)
	applyeffect CUR_TARG EFFECT_DOT DUR_DOT $get(ent_me,id) DMG_DOT
	if BURST_PUSH
	callevent repel_target CUR_TARG $vec(0,1000,110) BURST_POS
}

{ repel_target //<target> <strength> <from_origin>
	local L_TARG_ORG $get(PARAM1,origin)
	local L_MY_ORG PARAM3
	local L_TARG_ANG $angles(L_MY_ORG,L_TARG_ORG)
	local L_NEW_YAW L_TARG_ANG
	setvelocity PARAM1 $relvel($vec(0,L_NEW_YAW,0),PARAM2)
}

{ [server] attack1	//called by model anim: bite1

	dodamage NPCATK_TARGET ATTACK_HITRANGE DMG_BITE ATTACK_HITCHANCE
	playrandomsound 0 10 SOUND_ATTACK1 SOUND_ATTACK2 SOUND_ATTACK3
	if ( $rand(1,2) == 1 ) setvard ANIM_ATTACK ANIM_BITE2
}

{ [server] attack2	//called by model anim: bite2
	dodamage NPCATK_TARGET ATTACK_HITRANGE DMG_BITE ATTACK_HITCHANCE

	playrandomsound 0 10 SOUND_ATTACK1 SOUND_ATTACK2 SOUND_ATTACK3

	setvard ANIM_ATTACK ANIM_BITE1

	applyeffect NPCATK_TARGET EFFECT_DOT DUR_DOT $get(ent_me,id) DMG_DOT

	if $get(NPCATK_TARGET,range) <= ATTACK_HITRANGE
    addvelocity NPCATK_TARGET $relvel(-550,50,10)
}

{ [server] spiral_charge //called by model anim: fly2

	playrandomsound 0 10 SOUND_FLAP1 SOUND_FLAP2
}

{ [server] turret_horror //called by model anim: hover

	playrandomsound 0 10 SOUND_FLAP1 SOUND_FLAP2
}

{ npc_struck

	local MY_HEALTH $get(ent_me,hp)

	if ( MY_HEALTH >= NPC_HALF_HEALTH ) playrandomsound 0 10 SOUND_PAIN0 SOUND_PAIN0 SOUND_PAIN1
	if ( MY_HEALTH < NPC_HALF_HEALTH ) playrandomsound 0 10 SOUND_PAIN0 SOUND_PAIN0 SOUND_PAIN2
}

{ my_target_died
	setvard SPIT_AMMO 3
	setvard BREATH_AMMO 1
}

{ reload_spit
	if SPIT_AMMO < MAX_SPIT_AMMO
	add SPIT_AMMO 1
	if NPCATK_TARGET isnot unset
	if $get(NPCATK_TARGET,range) < 200
	callevent npcatk_flee NPCATK_TARGET 512 1.0
}

