#scope server

{ [shared]
	const ANIM_SEARCH idle_look
	setvar ANIM_IDLE idle1
	//const ANIM_JUMPING_JACKS idle2
	const ANIM_SWIPE attack1
	const ANIM_HEADBUTT attack2
	const ANIM_JUMP jump
	const ANIM_LEAP jump //leap
	setvar ANIM_WALK_DEFAULT walk
	setvar ANIM_RUN_DEFAULT run1
	setvard ANIM_DEATH dieforward
	const ANIM_WARCRY warcry
	setvard ANIM_FLINCH bigflinch
	const ANIM_BEAM beam

	setvar ANIM_WALK ANIM_WALK_DEFAULT
	setvar ANIM_RUN ANIM_RUN_DEFAULT
	setvard ANIM_ATTACK ANIM_SWIPE
}

{
	const ORC_HOP_DELAY 1.0 //how often the orc jumps
	const ORC_JUMP_THRESH 80 //how much higher the player must be for the orc to want to jump

	if ( $lcase(game.map.name) equals underpath )
	{
		setvard NPC_GIVE_EXP 2000 //300
	}
	else
	{
		setvard NPC_GIVE_EXP 300
	}


	const SOUND_IDLE1 bullchicken/bc_idle1.wav
	const SOUND_IDLE2 bullchicken/bc_idle2.wav
	const SOUND_IDLE3 bullchicken/bc_idle3.wav
	const SOUND_IDLE4 bullchicken/bc_idle4.wav
	const SOUND_IDLE5 bullchicken/bc_idle5.wav
	const SOUND_DEATH bullchicken/bc_die1.wav
	const SOUND_HEADBUTT bullchicken/bc_spithit1.wav
	const SOUND_SWIPEHIT1 zombie/claw_strike1.wav
	const SOUND_SWIPEHIT2 zombie/claw_strike2.wav
 	const SOUND_SWIPEMISS1 zombie/claw_miss1.wav
	const SOUND_SWIPEMISS2 zombie/claw_miss2.wav
	const SOUND_STRUCK1 debris/flesh1.wav
	const SOUND_STRUCK2 debris/flesh2.wav
	const SOUND_STEP1 player/pl_snow1.wav
	const SOUND_STEP2 player/pl_snow2.wav
	const SOUND_PAIN_WEAK bullchicken/bc_pain2.wav
	const SOUND_PAIN_STRONG bullchicken/bc_pain1.wav
	const SOUND_WARCRY bullchicken/bc_attackgrowl3.wav
	const SOUND_LEAP bullchicken/bc_attackgrowl2.wav
	const SOUND_LEAP_LAND weapons/g_bounce2.wav
	const SOUND_FLINCH bullchicken/bc_pain3.wav

	precache SOUND_DEATH

	const SWIPE_DAMAGE '$rand(50,75)'
	const HEADBUTT_CHANCE 100%
	const HEADBUTT_FREQ 7.0
	const HEADBUTT_DAMAGE '$rand(50,75)'
	const LEAP_DAMAGE '$rand(10,30)'
	const LEAP_STUNCHANCE 30%
	const LEAP_RANGE_TOOFAR 512 //leap if cloesr than this
	const LEAP_RANGE_TOOCLOSE 128 //but not if closer than this
	setvard LEAP_AWAY_INTERVAL 500 //leap away every time I loose this many HP (1/4th hp)
	const FREQ_LEAP_AWAY $randf(10.0,20.0) //but only this often*
	const NEXT_LEAP_AWAY 1800

	setvard DROP_GOLD 1
	setvard DROP_GOLD_MIN 30
	setvard DROP_GOLD_MAX 50

	//setvard ATTACK_HITRANGE
	//setvard ATTACK_MOVERANGE
	const ATTACK_HITCHANCE 95%

	setvard CAN_FLINCH 1
	setvard FLINCH_CHANCE 20%
	setvard FLINCH_DELAY 10.0
	setvar FLINCH_HEALTH 1500
}

#include monsters/base_monster_new

{ [shared] npc_spawn

	name a|Cave Ogre
	hp 1000
	race orc
	if ( !START_SUSPEND ) roam 1
	setmodel monsters/ogre_cave.mdl
	setmoveanim ANIM_WALK
	height 64
	width 32
	hearingsensitivity 2
	blood green
	setidleanim ANIM_IDLE

	takedmg poison 1.5
	takedmg acid 1.5

	setvard RUN_STEP 0

	if game.serverside

	if ( !START_SUSPEND ) callevent 1.0 idle_sounds
	callevent 2.0 final_postspawn
}

{ final_postspawn
	setvard WEAK_THRESHOLD $get(ent_me,maxhp)
	multiply WEAK_THRESHOLD 0.5

	setvard DMG_LEAP_THRESH $get(ent_me,maxhp)
	multiply DMG_LEAP_THRESH 0.1
}

{ npcatk_validatetarget //first spot

	if $get(PARAM1,isplayer)
	if !DID_WARCRY
	playanim critical ANIM_WARCRY
	playsound 0 10 SOUND_WARCRY
	setvard DID_WARCRY 1
}

{ my_target_died

	if ( !$cansee(enemy) )
	{
		playanim critical ANIM_WARCRY
		playsound 0 10 SOUND_WARCRY
		setvard DID_WARCRY 0
		if !LEAP_ENABLED
		callevent 1.0 enable_leap
	}
}

{ enable_leap

	setvard LEAP_ENABLED 1
}

{ npcatk_hunt

	//regenerate
	if ( game.monster.hp < game.monster.maxhp ) givehp 0.1

	if NPCATK_TARGET isnot unset
	if $get(NPCATK_TARGET,range) <= LEAP_RANGE_TOOFAR
	if $get(NPCATK_TARGET,range) > LEAP_RANGE_TOOCLOSE
	if LEAP_ENABLED

	setvard LEAP_ENABLED 0
	callevent 5.0 enable_leap
	callevent script_leap //do not confused with model vent leap_attack
}

{ script_leap

	playanim critical ANIM_LEAP
	callevent 0.1 leap_boost	
}

{ leap_boost

	if !I_R_FROZEN
	addvelocity ent_me $relvel(0,600,100)	
}
	
{ npc_selectattack

	if ( HEADBUTT_DELAY )
	{
		setvard ANIM_ATTACK ANIM_SWIPE
	}
	else
	{
		if ( $rand(1,100) < HEADBUTT_CHANCE )
		{
			setvard ANIM_ATTACK ANIM_HEADBUTT
			setvard HEADBUTT_DELAY 1
			callevent HEADBUTT_FREQ headbutt_reset
		}
	}
}

{ headbutt_reset
	
	setvard HEADBUTT_DELAY 0
}

{ attack1

	setvard SWIPE_ATTACK 1
	callevent npcatk_dodamage NPCATK_TARGET ATTACK_HITRANGE SWIPE_DAMAGE ATTACK_HITCHANCE
}

{ attack2 //headbut on frame 6 of attack2

	playrandomsound 0 10 SOUND_SWIPEMISS1 SOUND_SWIPEMISS2
	callevent npcatk_dodamage NPCATK_TARGET ATTACK_HITRANGE HEADBUTT_DAMAGE ATTACK_HITCHANCE
	setvard HEADBUTT_ON 1
}

{ game_dodamage //1: Attack Hit (1/0)  2: Entity Attack hit  3:  Start Position  4:  End Position

	if ( HEADBUTT_ON )
	{
		if ( PARAM1 )
		{
			playsound 0 10 SOUND_HEADBUTT
			applyeffect NPCATK_TARGET effects/debuff_stun 5 $get(ent_me,id)
		}
		if ( !PARAM1 ) playrandomsound 0 10 SOUND_SWIPEMISS1 SOUND_SWIPEMISS2
		setvard ANIM_ATTACK ANIM_SWIPE
		local EXIT_SUB 1
	}
	setvard HEADBUTT_ON 0
	if !EXIT_SUB

	//assumes swipe
	if SWIPE_ATTACK
	setvard SWIPE_ATTACK 0
	if ( PARAM1 )
	{
		playrandomsound 0 10 SOUND_SWIPEHIT1 SOUND_SWIPEHIT2
		addvelocity ent_laststruckbyme $relvel(-100,130,120)
	}
	if ( !PARAM1 ) playrandomsound 0 10 SOUND_SWIPEMISS1 SOUND_SWIPEMISS2
}

{ game_struck //PARAM1 = damage

	if ( game.monster.hp > WEAK_THRESHOLD )	playrandomsound 0 10 SOUND_STRUCK1 SOUND_STRUCK2 SOUND_PAIN_STRONG
	if ( game.monster.hp <= WEAK_THRESHOLD ) playrandomsound 0 10 SOUND_STRUCK1 SOUND_STRUCK2 SOUND_PAIN_WEAK

	if ( PARAM1 > DMG_LEAP_THRESH )
	{
		//dbg leaping away due to damage
		if game.time > NEXT_DMG_LEAP_AWAY
		setvard NEXT_DMG_LEAP_AWAY game.time
		add NEXT_DMG_LEAP_AWAY FREQ_LEAP_AWAY
		callevent leap_away
	}

//	local HP_AFTER game.monster.hp
//	subtract HP_AFTER PARAM1

//	if ( HP_AFTER < NEXT_LEAP_AWAY )
//	{
//		//dbg leaping away due to interval
//		subtract NEXT_LEAP_AWAY LEAP_AWAY_INTERVAL
//		if game.time > NEXT_DMG_LEAP_AWAY
//		setvard NEXT_DMG_LEAP_AWAY game.time
//		add NEXT_LEAP_AWAY FREQ_LEAP_AWAY
//		callevent leap_away
//	}
}

{ leap_away

	playanim critical ANIM_LEAP
	setmovedest NPCATK_TARGET 99999 flee
	callevent 0.1 leap_boost
	callevent npcatk_suspend_ai 3.0
}

{ leap_attack //called from ANIM_LEAP at frame 28

	playsound 0 10 SOUND_LEAP

	if ( $cansee(enemy,128) )
	{
		callevent npcatk_dodamage $get(ent_lastseen,id) ATTACK_HITRANGE LEAP_DAMAGE ATTACK_HITCHANCE
		if ( $rand(1,100) < LEAP_STUNCHANCE ) applyeffect $get(ent_lastseen,id) effects/debuff_stun 5 $get(ent_me,id)
	}
}

{ leap_done

	playsound 0 10 SOUND_LEAP_LAND
	setmoveanim ANIM_RUN
}

{ npc_flinch

	playsound 0 10 SOUND_FLINCH
}

{ idle_sounds
	if $get(ent_me,isalive)
	callevent $randf(3,10) idle_sounds
	if NPCATK_TARGET equals unset
	playrandomsound 0 10 SOUND_IDLE1 SOUND_IDLE2 SOUND_IDLE3 SOUND_IDLE4 SOUND_IDLE5
}

{ run_step1 //called from model during ANIM_RUN

	playsound 0 5 SOUND_STEP1
}

{ run_step2 //called from model during ANIM_RUN

	playsound 0 5 SOUND_STEP2
}

{ npcatk_targetvalidate

	//if spotted player, start jump checks
	if !ORC_JUMPING
	if $get(NPCATK_TARGET,isplayer)
	setvard ORC_JUMPING 1
	callevent 1.0 orc_jump_check
	
}

{ orc_jump_check

	if ORC_JUMPING
	callevent ORC_HOP_DELAY orc_jump_check

	if !IS_FLEEING
	if NPCATK_TARGET isnot unset
	if $cansee(NPCATK_TARGET)

	//jump at player if he's higher than I am
	local ME_POS game.monster.origin
	local MY_Z $vec.z(ME_POS)
	local TARGET_POS $get(NPCATK_TARGET,origin)
	local TARGET_Z $vec.z(TARGET_POS)
	local TARGET_Z_DIFFERENCE TARGET_Z
	subtract TARGET_Z_DIFFERENCE MY_Z

	if ( TARGET_Z_DIFFERENCE > ORC_JUMP_THRESH ) callevent orc_hop
}

{ orc_hop

	setvard NPC_FORCED_MOVEDEST 1 
	setmovedest NPCATK_TARGET 1
	playanim critical ANIM_LEAP
	playsound 0 10 SOUND_LEAP
	local JUMP_HEIGHT $rand(550,650)
	addvelocity ent_me $relvel(0,250,JUMP_HEIGHT)
}

{ my_target_died

	if !$cansee(enemy)
	//stop jumping (saves CPU when monster idle)
	setvard ORC_JUMPING 0
}
