//Da Chief
//Only extra ability is fire breath, so needs a heavy escort

#scope server
{
	const NEW_MODEL 1

	const NPC_BASE_EXP 3000

	const SOUND_ATTACK1 monsters/goblin/c_gargoyle_atk1.wav
	const SOUND_ATTACK2 monsters/goblin/c_gargoyle_atk2.wav
	const SOUND_ATTACK3 monsters/goblin/c_gargoyle_atk3.wav
	const SOUND_BREATH monsters/goblin/sps_fogfire.wav
	const STEP_SIZE_NORM 36

	const CAN_STUN 1

	setvard DROP_GOLD 0

	setvard ANIM_ATTACK battleaxe_swing1_L

	const DMG_AXE $rand(100,300)
	const DOT_FIRE 75
	const DMG_CHARGE 50

	const FREQ_BREATH $randf(10.0,30.0)
	const FIRE_BREATH_SCRIPT monsters/bgoblin_chief_cl
	const SOUND_REPEL ambience/alien_humongo.wav

	setvard ATTACK_HITCHANCE 90%
}

#include monsters/bgoblin

{ [shared] game_precache
	precachefile FIRE_BREATH_SCRIPT
}

{ [override] [shared] goblin_spawn

	name a|Blood Goblin Chieftain
	race goblin
	blood red //green for v_goblin
	hp 5000
	roam 1

	setanim.framerate 1.5

	hearingsensitivity 4


	if ( !NEW_MODEL )
	{
		setmodel monsters/goblin2_boss.mdl
		width 32
		height 72

		setmodelbody 0 2 //body: 0-Bare 1-Archer 2-Armor 3-Sniper
		setmodelbody 1 3 //head: 0-Horns 1-Spikes 2-Animal 3-Chief 4-Cowl
		setmodelbody 2 2 //weapon: 0-Unarmed 1-SkullClub 2-DragonAxe 3-Knife 4-Bow 5-Raxe 6-SW&SH
		setmodelbody 3 0 //arrow: on/off

		setprop ent_me skin 2
	}
	else
	{
		setmodel monsters/goblin_new_boss.mdl
		width 32
		height 72

		setmodelbody 0 0 //body: 0-Bare 1-Quiver&Strap
		setmodelbody 1 3 //head: 0-Bare 1-Horns 2-Animal 3-Chief 4-Rag
		setmodelbody 2 2 //weapon: 0-Unarmed 1-SkullClub 2-DragonAxe 3-Knife 4-Bow 5-Raxe 6-SW&SH
		setmodelbody 3 0 //arrow: On/off
		

		setprop ent_me skin 1 //0-norm 1-blood 2-vile
	}

	setidleanim ANIM_IDLE
	setmoveanim ANIM_WALK

	takedmg all 0.5
	takedmg fire 0.0
	takedmg cold 1.25
}

{ game_death

	callexternal GAME_MASTER bgoblin_chief_died $get(ent_me,origin)
	if ( FIRE_BREATH_ON ) clientevent update all CL_IDX end_fx
}

{ [override] swing_axe //ANIM_SMASH

	setvard AXE_SWING 1
	playrandomsound 0 10 SOUND_ATTACK1 SOUND_ATTACK2 SOUND_ATTACK3
	dodamage NPCATK_TARGET ATTACK_HITRANGE DMG_AXE ATTACK_HITCHANCE slash
}

{ cycle_up
	if !STARTED_CYCLES
	setvard STARTED_CYCLES 1
	callevent cl_effect_loop
	callevent FREQ_BREATH do_fire_breath
}

{ game_dodamage //PARAM1=hit:0|1 PARAM2=ent_hit PARAM3=(start) PARAM4=(end)

	//fire axe effect
	if ( PARAM1 )
	{
		if AXE_SWING
		applyeffect PARAM2 effects/dot_fire 10.0 $get(ent_me,id) DOT_FIRE
	}
	setvard AXE_SWING 0
}

{ do_fire_breath

	callevent FREQ_BREATH do_fire_breath
	if NPCATK_TARGET isnot unset

	callevent npcatk_suspend_ai
	setvard BREATH_ANG $get(ent_me,angles.yaw)
	subtract BREATH_ANG 30
	if ( BREATH_ANG < 0 ) add BREATH_ANG 359
	setvard BREATH_COUNT 0
	setmoveanim ANIM_WARCRY
	setidleanim ANIM_WARCRY
	playanim critical ANIM_WARCRY
	callevent 0.01 fire_breath_loop
	setvard FIRE_BREATH_ON 1
	svplaysound 1 10 SOUND_BREATH

	clientevent new all monsters/bgoblin_chief_cl $get(ent_me,index)
	setvard CL_IDX game.script.last_sent_id
}

{ fire_breath_loop

	add BREATH_COUNT 1
	if ( BREATH_COUNT == 60 ) callevent end_fire_breath

	if BREATH_COUNT < 60
	callevent 0.05 fire_breath_loop

	add BREATH_ANG 1
	if ( BREATH_ANG > 359 ) subtract BREATH_ANG 359

	local FACE_POS game.monster.origin
	vectoradd FACE_POS $relpos($vec(0,BREATH_ANG,0),$vec(0,500,0))
	setmovedest FACE_POS 9999

	local CLOUD_START $get(ent_me,svbonepos,20) //see if you can move this client side with $getcl(SKEL_IDX,bonepos,BONE_IDX)
	vectoradd CLOUD_START $relpos($vec(0,game.monster.angles.yaw,0),$vec(0,16,0))
	//clientevent update all CL_IDX make_cloud CLOUD_START $get(ent_me,angles.yaw)

	if ( CLOUD_TARGS isnot none ) calleventloop $get_token_amt(CLOUD_TARGS) burn_targets

	if game.time > NEXT_SCAN
	setvard NEXT_SCAN game.time
	add NEXT_SCAN 0.5
	local SCAN_POINT $relpos(0,128,0)
	setvard CLOUD_TARGS $get_tbox(enemy,128,SCAN_POINT)
	playanim once ANIM_WARCRY
}

{ burn_targets
	local CUR_TARGET $get_token(CLOUD_TARGS,game.script.iteration)
	if $get(CUR_TARGET,isalive)
	local TARG_ORG $get(CUR_TARGET,origin)
	if $within_cone2D(TARG_ORG,game.monster.origin,game.monster.angles,30)
	if $get(CUR_TARGET,range) < 256
	applyeffect CUR_TARGET effects/dot_fire 10.0 $get(ent_me,id) DOT_FIRE
	addvelocity CUR_TARGET $relvel(0,300,120)
}

{ end_fire_breath

	clientevent update all CL_IDX end_fx

	callevent npcatk_resume_ai
	setidleanim ANIM_IDLE
	setmoveanim ANIM_RUN
	svplaysound 1 0 SOUND_BREATH
	setvard FIRE_BREATH_ON 0
}

{ [override] leap_stun
	//createnpc monsters/summon/stun_burst $relpos(0,64,0) $get(ent_me,id) 64 0 DMG_CHARGE
	//optimization: doing stun burst local to save an NPC creation
	local BURST_POS $relpos(0,64,0)
	callevent stunburst_go BURST_POS 64 0 DMG_CHARGE
}

{ game_struck
	if ( FIRE_BREATH_ON )
	{
		if $get(ent_laststruck,range) < 128
		local TARGET_ORG $get(ent_laststruck,origin)
		local TARG_ANG $angles(game.monster.origin,TARGET_ORG)
		setvelocity ent_laststruck $relvel($vec(0,TARG_ANG,0),$vec(10,1000,0))
		playsound 0 10 SOUND_REPEL
		effect glow ent_me (255,0,0) 60 2.0 2.0
	}
}

//less likey to jump, but jumps higher, if needed to reach target
{ [override] gob_jump_check

	if GOB_JUMPER
	if GOB_JUMP_SCANNING

	local GOB_HOP_DELAY $randf(2,4)

	callevent GOB_HOP_DELAY gob_jump_check

	if NPCATK_TARGET isnot unset
	if !IS_FLEEING
	if !SUSPEND_AI
	if $get(NPCATK_TARGET,range) < GOBLIN_JUMPRANGE

	//jump at target if he's higher than I am
	local ME_Z game.monster.origin.z
	local TARG_Z $get(NPCATK_TARGET,origin.z)
	if ( $get(NPCATK_TARGET,isplayer) ) subtract TARG_Z 38 //player center compensation

	if ( ME_Z > TARG_Z )
	{
		local Z_DIFF ME_Z
		subtract ME_Z TARG_Z
	}
	else
	{
		local Z_DIFF TARG_Z
		subtract Z_DIFF ME_Z
	}

	if ( Z_DIFF > ATTACK_RANGE )
	{
		playanim critical ANIM_SMASH
		callevent 0.1 do_jump //bludgeon style
	}
}

{ do_jump
	stepsize 1000
	addvelocity ent_me $relvel(0,-200,800)
	callevent 0.5 push_forward
	callevent 1.0 jump_done
}

{ push_forward
	addvelocity ent_me $relvel(0,400,100)
}

{ jump_done
	stepsize STEP_SIZE_NORM
	setmoveanim ANIM_RUN
}

{ my_target_died
	callevent 1.0 npcatk_go_home
}

//localized stun burst to save NPC (since he already has his own client effects)
{ stunburst_go //<pos> <radius> <repel:0|1> <dmg>
	setvard STUN_BURST_POS PARAM1
	setvard STUN_BURST_RAD PARAM2
	setvard STUN_BURST_REPEL PARAM3
	setvard STUN_BURST_DMG PARAM4
	dbg stunburst_go pos: STUN_BURST_POS rad: STUN_BURST_RAD repel: STUN_BURST_REPEL dmg: STUN_BURST_DMG

	clientevent new all effects/sfx_stun_burst STUN_BURST_POS STUN_BURST_RAD 0
	playsound 0 10 magic/boom.wav
	callevent 0.25 stun_targets
}

{ stun_targets
	setvard STUN_LIST $get_tsphere(enemy,STUN_BURST_RAD,STUN_BURST_POS)
	dbg stun_targets STUN_LIST
	if STUN_LIST isnot none
	if $get_token_amt(STUN_LIST) > 0
	calleventloop $get_token_amt(STUN_LIST) stunburst_affect_targets
}

{ stunburst_affect_targets

	local CHECK_ENT $get_token(STUN_LIST,game.script.iteration)

	if $get(CHECK_ENT,onground)

	applyeffect CHECK_ENT effects/debuff_stun 5.0 $get(ent_me,id)

	if ( STUN_BURST_DMG > 0 ) dodamage CHECK_ENT direct STUN_BURST_DMG 100% ent_me magic

	if STUN_BURST_REPEL
	local TARGET_ORG $get(CHECK_ENT,origin)
	local TARG_ANG $angles(STUN_BURST_POS,TARGET_ORG)
	local NEW_YAW TARG_ANG
	setvelocity CHECK_ENT $relvel($vec(0,NEW_YAW,0),$vec(0,1000,0))
}

{ goblin_pre_spawn
	setvard ATTACK_RANGE 96
	setvard ATTACK_HITRANGE 128
	setvard ATTACK_MOVERANGE 48 //a bit huggie :)
	setvard MOVE_RANGE 48
}