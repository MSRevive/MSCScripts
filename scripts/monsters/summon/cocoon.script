#scope server
{
	const SOUND_STRUCK1 debris/flesh1.wav
	const SOUND_STRUCK2 debris/flesh2.wav
	const SOUND_STRUCK3 debris/flesh3.wav

	const SOUND_DEATH debris/bustflesh1.wav
}

{ [shared] game_spawn
	name a|Spider's Cocoon
	setmodel misc/treasure.mdl
	setmodelbody 0 5
	race spider
	invincible 1
	takedmg poison 0.0
	takedmg holy 0.0
	blood white
	hp 100
	width 40 //38
	height 96 //74
	callevent 1.0 remove_invuln
	setprop ent_me rendermode 5
	setprop ent_me renderamt 255

	setvard COCOON_ATTACHED 1 //so webs dont try to attach to me
}

{ remove_invuln
	hp MY_HP
	invincible 0
}

{ game_dynamically_created //<target> <hp of creator> <duration> [dot] [webber]
	setvard WEB_TARGET PARAM1
	setvard WEB_STR PARAM2
	setvard MY_HP WEB_STR
	multiply MY_HP 0.25
	hp MY_HP

	setvard MY_DOT MY_HP

	capvar MY_DOT 5 30
	capvar MY_HP 25 300

	if ( PARAM3 != -1 ) callevent PARAM3 web_suicide

	if ( PARAM4 != -1 ) setvard MY_DOT PARAM4

	if ( PARAM5 !startswith PARAM )
	{
		setvard MY_OWNER PARAM5
		if ( $get(MY_OWNER,isplayer) ) setvard OWNER_IS_PLAYER 1
	}

	if ( OWNER_IS_PLAYER ) race evil

	if ( $get(WEB_TARGET,isplayer) )
	{
		local OUT_TITLE $get(WEB_TARGET,name)
		local OUT_MSG "Is trapped in a cocoon of webs!"
		infomsg all OUT_TITLE OUT_MSG
	}

	callexternal WEB_TARGET ext_set_cocooned 1
	if ( !OWNER_IS_PLAYER )
	{
		applyeffect WEB_TARGET effects/cocooned MY_DOT $get(ent_me,id)
	}
	else
	{
		setvard MY_HP 50
		setvard WEB_STR 50
		applyeffect WEB_TARGET effects/cocooned MY_DOT MY_OWNER
	}

	dbg game_dynamically_created targ $get(WEB_TARGET,name) str WEB_STR hp MY_HP dot MY_DOT ownr $get(MY_OWNER,name)
}

{ game_struck
	playsound 0 10 SOUND_STRUCK1 SOUND_STRUCK2 SOUND_STRUCK3
}

{ game_death
	playsound 0 10 SOUND_DEATH
	callexternal WEB_TARGET ext_set_cocooned 0

	local THE_RESCUER $get(ent_laststruck,id)
	if ( THE_RESCUER isnot WEB_TARGET )
	{
		if $get(THE_RESCUER,isplayer)
		if $get(WEB_TARGET,isplayer)
		local BONUS_MSG "for freeing "
		stradd BONUS_MSG $get(WEB_TARGET,name)
		callexternal THE_RESCUER ext_dmgpoint_bonus 1000 BONUS_MSG
	}

	deleteent ent_me
}

{
repeatdelay 1.0
	if $get(ent_me,isalive)
	//in case I get a glowshell
	setprop ent_me rendermode 5
	setprop ent_me renderamt 255
	if !$get(WEB_TARGET,isalive)
	deleteent ent_me
}

{ web_suicide

	if $get(ent_me,isalive)

	invincible 0
	race hated
	dodamage ent_me direct 99999 100 GAME_MASTER target
}

