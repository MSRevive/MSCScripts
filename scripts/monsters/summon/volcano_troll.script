//Trollcano

#scope server

{
	setvard ANIM_DEATH down	//for base_npc
	const SOUND_START	magic/volcano_start.wav
	const SOUND_LOOP	magic/volcano_loop.wav
	const MODEL_WORLD	misc/volcano.mdl
	setvard LOOP_DELAY 0

  	precache misc/volcano.mdl
	precache SOUND_START
	precache SOUND_LOOP

	const AOE_FREQ 2
	const AOE_RADIUS 32
}

{ [shared]
	const ROCK_START_HEIGHT 66
}

#include monsters/summon/base_aoe

{ game_spawn

	hp 1500
	invincible 1 //screw it, it has 1500pts anyways - and ya don't want it to report attacks against itself
	setsolid none //doesnt trap monsters/players inside of it
	width 32
	height 64
	name a|Trollcano!
	roam 0
	skilllevel 0
	hearingsensitivity 0
	setmodel MODEL_WORLD
	blood none
	playanim hold up
	setvard PLAYING_DEAD 1
	callevent 2 volcano_start  		//Start erupting after a delay
	effect screenshake $relpos(0,0,0) 50 10 6 512
	playsound 0 7 SOUND_START
}

{ game_dynamically_created //PARAM1=owner

	setvard CAST_BY_PLAYER $get(PARAM1,isplayer) 

	setvard MY_OWNER PARAM1

	//setduration
	setvard EFFECT_DURATION 25
	setvard CLIENT_DURATION EFFECT_DURATION
	subtract CLIENT_DURATION 1 //ensures clients are done before fx is deleted

	callevent EFFECT_DURATION volcano_death  	//Die after a delay
	clientevent persist all $currentscript $get(ent_me,origin) CLIENT_DURATION $get(MY_OWNER,index)
}

// Start shooting
{ volcano_start
   setvard ERUPTING 1
   //emitsound $get(ent_me,id) game.monster.origin 64 EFFECT_DURATION danger 128
}

{
   [client] repeatdelay 6

	playsound CHAN_BODY 7 SOUND_LOOP
}

// Fade out
{ volcano_death

   setvard ERUPTING 0

   playanim hold down
   callevent 3 volcano_fadeout
}

{ volcano_fadeout
   playsound CHAN_BODY game.sound.silentvol SOUND_LOOP
   deleteent ent_me fade
}

//Client-side
//===========

{  [client]
  	const MODEL_WORLD      	weapons/projectiles.mdl
  	const SPRITE_BURN 	fire1_fixed.spr
	const LIGHT_RADIUS	64
	const LIGHT_COLOR	(255,0,0)
	const LIGHT_DURATION	0.8
}

{  [client] client_activate

	setvard local.cl.origin PARAM1
	vectoradd local.cl.origin z ROCK_START_HEIGHT
	local DIE_TIME PARAM2
	callevent DIE_TIME volcano_done
	incvar DIE_TIME 10	//Die 10 seconds late, so all rocks have chance to fall
	callevent DIE_TIME volcano_die

	setvard FX_ACTIVE 1
	setvard FX_ORIGIN local.cl.origin
	callevent volcano_shoot_loop
}

{ [client] volcano_shoot_loop

	if FX_ACTIVE
	callevent 0.25 volcano_shoot_loop

	local xangle $randf(-50,-90)
	local yangle $randf(-180,180)
	local ROCK_ANGS $vec(xangle,yangle,0)

	local ROCK_VEL $relvel(ROCK_ANGS,$vec(0,500,0)) 
	callevent volcono_shoot_rock ROCK_VEL $randf(0.5,0.8)
}

{ [client] volcano_done
	setvard FX_ACTIVE 0
}

{  [client] volcano_die

	setvard FX_ACTIVE 0
	playsound CHAN_BODY game.sound.silentvol SOUND_LOOP
	removescript
}

{  [client] volcono_shoot_rock

	setvard local.cl.velocity PARAM1
	setvard local.cl.gravity PARAM2
	cleffect tempent model MODEL_WORLD local.cl.origin volcano_rock_create volcano_rock_update volcano_rock_collide
	calleventloop 4 makefire_loop local.cl.origin
}

{  [client] volcano_rock_create

	cleffect tempent set_current_prop death_delay 10
	cleffect tempent set_current_prop body 69
	cleffect tempent set_current_prop scale 2.0
	cleffect tempent set_current_prop velocity local.cl.velocity
	cleffect tempent set_current_prop bouncefactor 0.4
	cleffect tempent set_current_prop gravity local.cl.gravity
	cleffect tempent set_current_prop collide all
	
	local L_ANG $vec($randf(0,359),$randf(0,359),180)
	cleffect tempent set_current_prop angles L_ANG //A bug appears to select a random angle again after hitting the ground. Still works out ok though.
	
	cleffect tempent set_current_prop framerate 0.6
	cleffect tempent set_current_prop frames 16
	
	cleffect tempent set_current_prop sequence 8
}

{  [client] volcano_rock_collide

	cleffect tempent set_current_prop framerate 0
}

{  [client] makefire_loop

	cleffect tempent sprite SPRITE_BURN PARAM1 volcano_fire_create
}

{  [client] volcano_fire_create

	cleffect tempent set_current_prop death_delay 2
	cleffect tempent set_current_prop velocity $vec($randf(-200,200),$randf(-200,200),$randf(-200,200))
	cleffect tempent set_current_prop bouncefactor 5
	cleffect tempent set_current_prop scale 0.5
	cleffect tempent set_current_prop gravity $randf(0.6,1.0)
	cleffect tempent set_current_prop collide all;die
	cleffect tempent set_current_prop framerate 30
	cleffect tempent set_current_prop frames 23
}