//Lightning Storm
//Does friendly damage, but only to players

#scope server

//Shared
//======

{ [shared]
	const STORM_HEIGHT 260
	const SMOKE_SPRITE "bigsmoke.spr"
	const LIGHTNING_SPRITE "lgtning.spr"
	const LIGHTNING_SPRITE_SPARKS "3dmflaora.spr"
	const STORM_WIDTH 75

	const AOE_FREQ 1.0
	const AOE_RADIUS 150
}

{ game_precache
	precache LIGHTNING_SPRITE_SPARKS	//Precache for the effect
	precache SMOKE_SPRITE
	precache LIGHTNING_SPRITE		//Precache for the effect
}

#include monsters/summon/base_aoe

{ game_spawn
	name a|Lightning Storm
	invincible 1
	nopush 1
	gravity 0
	
	setvard PLAYING_DEAD 1 //not valid target

	setvard LOOP_SOUND 0
}

{ game_dynamically_created

	setvard AOE_OWNER PARAM1
	setvard MY_OWNER PARAM1 //For base_aoe support
	if ( $get(AOE_OWNER,is_item) )
	{
		setvard MY_ITEM AOE_OWNER
		setvard AOE_OWNER $get(MY_ITEM,owner)
		setvard MY_OWNER AOE_OWNER
	}
	
	setvard NEG_WIDTH $neg(STORM_WIDTH)

	local L_ANGLE PARAM2
	setangle face.y L_ANGLE
	
	setvard MY_BASE_DAMAGE PARAM3
	setvard BASE_DURATION PARAM4
	setvard STORM_DURATION $math(add,game.time,PARAM4)
	setvard ACTIVE_SKILL PARAM5

	setvard CAST_BY_PLAYER $get(MY_ITEM,is_item)

	local F_GROUND_Z $get_ground_height(game.monster.origin)
	dbg GroundLevelSentToClient: F_GROUND_Z

	clientevent new all $currentscript $get(ent_me,origin) L_ANGLE F_GROUND_Z
	setvard STORM_CLID game.script.last_sent_id
	
	callevent do_storm
	callevent check_death
}

{ do_storm //Deal damage

	callevent 0.3 do_storm	

	add LOOP_SOUND 1
	if ( LOOP_SOUND >= 3 )
	{
		playsound 0 6 weather/lightning.wav
		setvard LOOP_SOUND 0
	}
}

{ apply_aoe_effect //<target>

	if !$get(PARAM1,haseffect,DOT_lightning)
	
	local EFFECT_DUR 5.0
	
	local L_ENT MY_ITEM
	if ( $get(L_ENT,is_item) )
	{
		multiply EFFECT_DUR 2
		local L_ENT $get(L_ENT,owner)
	}

	applyeffect PARAM1 effects/effect_shock_dmg EFFECT_DUR L_ENT MY_BASE_DAMAGE ACTIVE_SKILL
}

{ sustain_storm //<position> //called externally from magic hand

	local L_POS PARAM1
	
	setorigin ent_me L_POS
	clientevent update all STORM_CLID cl_pos_update L_POS
	setvard STORM_DURATION $math(add,game.time,BASE_DURATION)
}

{ check_death
	repeatdelay 0.5
	
	local L_REMOVE 0
	
	if ( game.time > STORM_DURATION ) local L_REMOVE 1
	
	if ( L_REMOVE )
	{
		clientevent remove all STORM_CLID
		callexternal MY_ITEM storm_ended
		deleteent ent_me
	}
}

//Client (smokes, from original)
//======

{ [client] client_activate

	setvard SMOKE_POSITION PARAM1
	setvard SMOKE_ANGLE $vec(0,PARAM2,0)
	setvard GROUND_Z PARAM3
}

// Throw clouds
{ [client] smokes_shoot
	repeatdelay 0.25
	
	local x $rand(-64,64)
	local y $rand(-64,64)
	
	local L_POS $relpos(SMOKE_ANGLE,$vec(x,y,250))
	vectoradd L_POS SMOKE_POSITION

	callevent cl_beam L_POS

	cleffect tempent sprite SMOKE_SPRITE L_POS setup_smokes
}

{ [client] cl_pos_update

	setvard SMOKE_POSITION PARAM1
	setvard GROUND_Z $vec.z(PARAM1)
	
	subtract GROUND_Z 100
}

{ [client] cl_beam

	local CL_BEAM_START PARAM1
	local CL_BEAM_END CL_BEAM_START
	vectorset CL_BEAM_END z GROUND_Z
	
	cleffect beam_points CL_BEAM_START CL_BEAM_END LIGHTNING_SPRITE 1.0 60 0.4 0.5 1 2 (255,255,0)
}

{  [client] setup_smokes

   cleffect tempent set_current_prop death_delay 	2.5
   cleffect tempent set_current_prop framerate		10
   cleffect tempent set_current_prop frames			17
   cleffect tempent set_current_prop bouncefactor 	0
   cleffect tempent set_current_prop scale 			$randf(0.5,1.0)
   cleffect tempent set_current_prop rendermode		alpha
   cleffect tempent set_current_prop renderamt		255
   cleffect tempent set_current_prop rendercolor	(0,0,0)
   cleffect tempent set_current_prop gravity 		.005
   cleffect tempent set_current_prop collide 		none
}
