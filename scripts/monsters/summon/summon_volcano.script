//Rewriting a good portion of this. T'is an old script and thus was fucked in strange ways. //greatguys1@FEB2022

//Thothie - Reduced level of Volcano to something obtainable
//- (This script has been  effed with waaay too much. ^_^
//Volcano

#scope server

{
	//const VOLCANO_EXT_EVENT ext_volcano_hit

	const MODEL_WORLD	misc/volcano.mdl
	setvard ANIM_DEATH down	//for base_npc
	
	const SOUND_SHOOT magic/flamelick_cast.wav
	
	const FIREBALL_FREQ 0.27

	const AOE_FREQ 1
	const AOE_RADIUS 32
	//setvard AOE_DMG $math(multiply,FIREBALL_DMG,0.12) //Now set in game_dynamically_created 
	
	const ANGLE_OFFSET 60 //Max angle offset the fireball can shoot
	const FORCE_OFFSET 70 //Force of the projectile +/- a random range of this amount
}

{ [shared]

	const ROCK_START_HEIGHT 40
}

{ game_precache

	precache misc/volcano.mdl
}

{ game_spawn

	name "A|volcano"
	hp 1500
	invincible 1 //screw it, it has 1500pts anyways - and ya don't want it to report attacks against itself
	race beloved
	
	width 32
	height 64
	setsolid none //doesnt trap monsters/players inside of it
	
	roam 0
	hearingsensitivity 0
	
	setmodel MODEL_WORLD
	playanim hold up
	
	callevent 2 volcano_start  		//Start erupting after a delay
	effect screenshake $relpos(0,0,0) 50 10 6 512
}

{ game_dynamically_created //PARAM1=owner PARAM2=IntendedDmg PARAM3=Duration

	//please balance from magic hand end
	
	setvard MY_OWNER PARAM1
	setvard FIREBALL_DMG PARAM2
	setvard AOE_DMG $math(multiply,FIREBALL_DMG,0.12) //Damage is based on a ratio of FIREBALL_DMG, which is got from a dynamic spawn param
	setvard EFFECT_DURATION PARAM3
	
	setvard CAST_BY_PLAYER $get(MY_OWNER,isplayer) 

	
	//Setup client fx
	clientevent new all $currentscript $get(ent_me,index) EFFECT_DURATION
	setvard FX_IDX game.script.last_sent_id
	
	callevent EFFECT_DURATION volcano_off  	//Die after a delay
}

// Start shooting
{ volcano_start

	setvard ERUPTING 1
	//emitsound ent_me $get(ent_me,origin) 64 EFFECT_DURATION danger 128 //This causes the fear effect and is not fun.
	
	clientevent update all FX_IDX fx_start
	
	callevent aoe_loop
	callevent fireball_loop
}

{ aoe_loop

	if ERUPTING
	
	callevent func_rockspawn //return var: FUNC_ROCKSPAWN_ORIGIN
	//<target|(src_origin)> <range|aoe|(dest_origin)|direct> <damage> <cth|fall_off> <attacker> <inflciter> <skill|none> <dmg_type> [flag_string]
	xdodamage FUNC_ROCKSPAWN_ORIGIN 128 AOE_DMG 0.01 MY_OWNER $get(ent_me,id) spellcasting.fire fire "dmgevent:*volcano"
	callevent AOE_FREQ aoe_loop
}

{ fireball_loop

	if ERUPTING
	
	callevent func_rockspawn //return var: FUNC_ROCKSPAWN_ORIGIN
	local L_ORIGIN FUNC_ROCKSPAWN_ORIGIN
	
	local L_DIR L_ORIGIN
	vectoradd L_DIR z 50
	
	//The "cone of fire" of the projectile works better for view angles. Distribution of the fireballs using "cof" is not even, so I made my own angle offsets.
	vectoradd L_DIR x $math(multiply,ANGLE_OFFSET,$randf(-1,1))
	vectoradd L_DIR y $math(multiply,ANGLE_OFFSET,$randf(-1,1))
	
	local L_FORCE 500
	add L_FORCE $math(multiply,FORCE_OFFSET,$randf(-1,1))
	
	//<projectile scriptname> <view|(src_origin)> <target|(targ_origin)> <speed> <damage> <cof> <skill|none>
	callexternal MY_OWNER ext_tossprojectile "proj_volcano" L_ORIGIN L_DIR L_FORCE FIREBALL_DMG 0 spellcasting.fire
	
	//<channel> <volume> <filename> [Attenuation:0.8] [Pitch:100] - plays a wave, use channel 0 for auto
	playsound CHAN_WEAPON 4 SOUND_SHOOT 0.8
	
	callevent FIREBALL_FREQ fireball_loop
}

{ volcano_dodamage //PARAM1=hit:0|1 PARAM2=ent_hit PARAM3=(start) PARAM4=(end) PARAM5=DmgType PARAM6=DmgAmt

	local L_TARGET PARAM2
	local L_DMG PARAM6
	
	if PARAM1
	if $get(L_TARGET,isalive)
	if L_DMG > 0
	
	//Should match dot in items/proj_volcano
	local L_DMG_FIRE $get(MY_OWNER,skill.spellcasting.fire)
	multiply L_DMG_FIRE 0.5
	applyeffect L_TARGET effects/effect_burn 5 MY_OWNER L_DMG_FIRE 1 0 spellcasting.fire
}

{ volcano_off

	setvard ERUPTING 0
	playanim hold down
	callevent 4 volcano_die
}

// Fade out
{ volcano_die

	clientevent update all FX_IDX fx_die
	deleteent ent_me fade
}


//Client-side
//===========

{ [client]
  	const MODEL_WORLD "weapons/projectiles.mdl"
  	const SPRITE_BURN "fire1_fixed.spr"
	const SPRITE_SMOKE "rain_mist.spr"
	const FLAME_CIRCLE_BODY 51
	
	const CHAN_VOLCANO 7
	const SOUND_START "magic/volcano_start.wav"
	const SOUND_LOOP "magic/volcano_loop.wav"
	
	const LIGHT_RADIUS 250
	const LIGHT_COLOR "(255,100,100)"
}

{ [client] client_activate //PARAM1 = idx, PARAM2 = duration

	setvard MODEL_IDX PARAM1
	setvard FX_DURATION PARAM2
	setvard FX_DELAY 0.5
	
	callevent FX_DURATION fx_end
	
	setvard FX_ACTIVE 0
	
	callevent func_rockspawn //return var: FUNC_ROCKSPAWN_ORIGIN
	//Plays when volcano grows
	sound.play3d SOUND_START 7 FUNC_ROCKSPAWN_ORIGIN 0.8 CHAN_VOLCANO
}

{ [client] fx_start //volcano started to shoot fireballs

	setvard FX_ACTIVE 1
	setvard FX_DELAY 0.2
	
	cleffect tempent model MODEL_WORLD $getcl(MODEL_IDX,origin) setup_flame_circle update_flame_circle
	
	callevent func_rockspawn //return var: FUNC_ROCKSPAWN_ORIGIN
	//- cleffect light <new|lightid> <origin> <radius> <(r,g,b)> <duration> ["entity"|"dark"]
	cleffect light new FUNC_ROCKSPAWN_ORIGIN LIGHT_RADIUS LIGHT_COLOR FX_DURATION
	
	callevent 6 loop_sfx
}

{ [client] loop_sfx
	//repeatdelay 6
	if FX_ACTIVE
	callevent func_rockspawn //return var: FUNC_ROCKSPAWN_ORIGIN
	sound.play3d SOUND_LOOP 7 FUNC_ROCKSPAWN_ORIGIN 0.8 CHAN_VOLCANO
	callevent 6 loop_sfx
}

{ [client]
	repeatdelay FX_DELAY
	if ( FX_ACTIVE )
	{
		callevent func_rockspawn //return var: FUNC_ROCKSPAWN_ORIGIN
		cleffect tempent sprite SPRITE_BURN FUNC_ROCKSPAWN_ORIGIN volcano_fire_create
	}
	else
	{
		callevent func_rockspawn //return var: FUNC_ROCKSPAWN_ORIGIN
		cleffect tempent sprite SPRITE_SMOKE FUNC_ROCKSPAWN_ORIGIN setup_smoke update_smoke
	}
}

{  [client] volcano_fire_create

	cleffect tempent set_current_prop death_delay 	1.7
	cleffect tempent set_current_prop velocity 	$vec($randf(-40,40),$randf(-40,40),$randf(230,270))
	cleffect tempent set_current_prop bouncefactor 	5
	cleffect tempent set_current_prop scale 	0.5
	cleffect tempent set_current_prop gravity 	0.5
	cleffect tempent set_current_prop collide 	all;die
	cleffect tempent set_current_prop framerate	30
	cleffect tempent set_current_prop frames	23
}

{ [client] setup_flame_circle
	cleffect tempent set_current_prop death_delay FX_DURATION
	cleffect tempent set_current_prop body FLAME_CIRCLE_BODY
	cleffect tempent set_current_prop framerate 0.5
	cleffect tempent set_current_prop sequence 8
	cleffect tempent set_current_prop rendermode add
	cleffect tempent set_current_prop renderamt 100
	cleffect tempent set_current_prop scale 7
	cleffect tempent set_current_prop gravity 0
	cleffect tempent set_current_prop collide none
	cleffect tempent set_current_prop frames 11
	cleffect tempent set_current_prop angles $vec(0,90,0)
	cleffect tempent set_current_prop velocity $vec(0,0,0)
}

{ [client] update_flame_circle
	if ( FX_ACTIVE )
	{
		cleffect tempent set_current_prop origin $getcl(MODEL_IDX,origin)
	}
	else
	{
		cleffect tempent set_current_prop origin $vec(10000,10000,10000)
	}
}

{ [client] setup_smoke
	cleffect tempent set_current_prop death_delay 4
	//cleffect tempent set_current_prop fadeout lifetime
	cleffect tempent set_current_prop framerate $rand(1,2)
	cleffect tempent set_current_prop frames 17
	cleffect tempent set_current_prop bouncefactor 0
	cleffect tempent set_current_prop scale 0.1
	cleffect tempent set_current_prop rendermode add
	cleffect tempent set_current_prop renderamt 240
	cleffect tempent set_current_prop rendercolor (255,170,170)
	cleffect tempent set_current_prop gravity -0.055
	cleffect tempent set_current_prop collide "world;die"
}

{ [client] update_smoke

	if ( $math(subtract,game.time,game.tempent.fuser1) >= 0.1 ) //seconds
	{
		local L_SCALE $math(add,game.tempent.scale,0.04)
		local L_VEL game.tempent.velocity
		vectoradd L_VEL y $math(subtract,game.tempent.velocity,1)
		vectoradd L_VEL x $math(add,game.tempent.velocity,0.3)
		
		
		cleffect tempent set_current_prop scale L_SCALE
		cleffect tempent set_current_prop velocity L_VEL
		cleffect tempent set_current_prop fuser1 game.time
	}
}

{ [client] fx_end //Stopped shooting fireballs

	setvard FX_ACTIVE 0
	setvard FX_DELAY 0.5
	callevent func_rockspawn //return var: FUNC_ROCKSPAWN_ORIGIN
	sound.play3d SOUND_LOOP 0 FUNC_ROCKSPAWN_ORIGIN 0.8 CHAN_VOLCANO
	callevent 6.0 fx_die
}

{ [client] fx_die

	removescript
}

{ [shared] func_rockspawn //returns the origin of fireball spawn

	//As of the alpha currently, $func is broken for clients. This is a workaround until I get back to making this a func.
	if ( game.serverside )
	{
		local L_ORIGIN $get(ent_me,origin)
	}
	else
	{
		local L_ORIGIN $getcl(MODEL_IDX,origin)
	}
	
	vectoradd L_ORIGIN z ROCK_START_HEIGHT
	
	setvard FUNC_ROCKSPAWN_ORIGIN L_ORIGIN
	
	//return **clear
	//return L_ORIGIN
}
