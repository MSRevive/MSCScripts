{
	const SEAL_MODEL weapons/magic/seals.mdl
	const SEAL_OFS 8
	const SOUND_MANIFEST magic/spawn_loud.wav
	const SOUND_PULSE magic/frost_forward.wav
	const SOUND_FADE magic/frost_reverse.wav
	const PULSE_PLAYTIME 1.0
	const FX_SPRITE firemagic.spr
	const CIRCLE_RADIUS 172
	const APPLY_EFFECT effects/freeze_solid
}	

#include monsters/base_npc

{ game_dynamically_created //PARAM1 = owner PARAM2 = CircleDuration PARAM3 = FreezeDuration

	setvard MY_OWNER PARAM1
	setvard MY_DURATION PARAM2
	setvard FREEZE_DURATION PARAM3

	setvard MY_OWNER_RACE $get(PARAM1,race)

	callevent MY_DURATION circle_end

	//dbg Circle got: Owner MY_OWNER Dur MY_DURATION Dmg FREEZE_DURATION

     //tempent trail <modelname/spritename> <start> <dest> <count> <life> <scale> <speed> <randomness>
	//effect tempent trail SEAL_MODEL $relpos(0,0,96) $get(ent_me,origin) 1 MY_DURATION 1 1 0
	//effect tempent spray <modelname/spritename> <position> <direction> <count> <speed> <noise>
	//effect tempent trail <modelname/spritename> <start> <dest> <count> <life> <scale> <speed> <randomness>
	//effect tempent gibs <modelname/spritename> <position> <size> <velocity> <randomness> <amount> <duration>
	//effect tempent gibs SEAL_MODEL $relpos(0,0,0) 1.0 1 0 1 MY_DURATION
}

{ npc_spawn

	name a|Greater Circle of Ice
	hp 1
	fov 359
	invincible 1
	race beloved
	height 2
	width 2
	fly 1
	float 1
	gravity 0.0
	blood none
	setmodel none
	setsolid none

	setvard CIRCLE_ON 1

	callevent 0.1 make_seal

	callevent 1.0 circle_go

	callevent 0.1 circle_hum
}

{ make_seal

	local SEAL_POS $get(ent_me,origin)
	local GROUND_Z $get_ground_height(SEAL_POS)
	local SEAL_Z $vec.z(SEAL_POS)
	local GROUND_DIST GROUND_Z
	subtract GROUND_DIST SEAL_Z
	//dbg MyBasePos SEAL_POS GroundZ GROUND_Z
	subract GROUND_DIST 2
	vectoradd SEAL_POS $relpos($vec(0,0,0),$vec(0,0,GROUND_DIST)) 
	//dbg MyGroundPos SEAL_POS Ground GROUND_DIST
	createnpc monsters/summon/seal_maker SEAL_POS SEAL_MODEL MY_DURATION SEAL_OFS
}

{ circle_hum

	//drop_to_floor ent_me

   	if ( !CIRCLE_UP )
	{
		//clientevent persist all $currentscript effect.duration $get(ent_me,index) PARAM2 GLOW_COLOR
		local CLIENT_DURATION MY_DURATION
		subtract CLIENT_DURATION 0.2 //effort to prevent client side fx from sticking
		clientevent new all_in_sight $currentscript CLIENT_DURATION $get(ent_me,index) CIRCLE_RADIUS
		local MY_ORIGIN $get(ent_me,origin)
		cleffect light new MY_ORIGIN CIRCLE_RADIUS (128,128,255) MY_DURATION
		setvard THIS_SCRIPT_CLIENT_ID game.script.last_sent_id
		setvard CIRCLE_UP 1
	}

	if ( DID_WINDUP ) playsound 0 10 SOUND_PULSE
	if ( !DID_WINDUP )
	{
		playsound 0 10 SOUND_MANIFEST
		setvard DID_WINDUP 1
	}

	if CIRCLE_ON
	callevent PULSE_PLAYTIME circle_hum
}

{ circle_go

	if CIRCLE_ON

	callevent 0.25 circle_go

	if ( $get(ent_lastseen,id) equals MY_OWNER )
	{
		look 1024
	}

	dodamage $relpos(0,0,0) CIRCLE_RADIUS 0 100% 0 reflective

	if $cansee(ally)
	if $get(ent_lastseen,range) <= CIRCLE_RADIUS
	if $get(ent_lastseen,relationship,MY_OWNER) equals enemy
	////dbg Saw $get(ent_lastseen,name)
	local DMG_SOURCE MY_OWNER
	//if ( $get(MY_OWNER,isplayer) == 0 ) local DMG_SOURCE $get(ent_me,id)
	if !$get(ent_lastseen,haseffect,DOT_cold)
	applyeffect ent_lastseen APPLY_EFFECT FREEZE_DURATION DMG_SOURCE
	//dbg Saw $get(ent_lastseen,name) is $get(PARAM2,relationship,MY_OWNER)
}

{  game_dodamage //1: Attack Hit (1/0)  2: Entity Attack hit  3:  Start Position  4:  End Position
	
	if $get(PARAM2,relationship,MY_OWNER) equals enemy
	////dbg Hit $get(PARAM2,name)
	local DMG_SOURCE MY_OWNER
	//if ( $get(MY_OWNER,isplayer) == 0 ) local DMG_SOURCE $get(ent_me,id)
	if !$get(ent_lastseen,haseffect,DOT_cold)
	applyeffect ent_lastseen APPLY_EFFECT FREEZE_DURATION DMG_SOURCE
	//dbg Hit $get(ent_lastseen,name) is $get(PARAM2,relationship,MY_OWNER)
}

{ circle_end

	clientevent remove all THIS_SCRIPT_CLIENT_ID
	setvard CIRCLE_ON 0

	clientevent remove all $currentscript	//there can be only one
	playsound 0 10 SOUND_FADE
	callevent 0.5 circle_remove
}

{ circle_remove

	deleteent ent_me
}

// ===========
// Client Side
// ===========

{ [client]

	const OFS_POS			128
	const OFS_NEG			-128
	const OFSZ_POS			256
	const OFSZ_NEG			-10
	
	const LIGHT_PLAYER_SCALE	0.3
	const LIGHT_DROPPED_SCALE	0.5
}

{ [client] client_activate

	setvard sfx.duration PARAM1
	setvard sfx.npcid PARAM2
	setvard sfx.radius PARAM3

	setcallback render enable

	setvard RAIN_SPRITES 1

	setvard SEAL_DROP_COUNTER 0

	setvard ROT_COUNT 0
	
	callevent 0.1 rain_go

	callevent PARAM1 effect_die
}

//Sprite

{ [client] rain_go
	//repeatdelay 0.1

	if RAIN_SPRITES
	callevent createsprite
	callevent 0.1 rain_go
}

{ [client] createsprite

	local l.pos $getcl(sfx.npcid,origin)

	if ( !MAIN_SEAL )	//lazy h4x!
	{
		local g.pos $getcl(sfx.npcid,origin)
		cleffect tempent model SEAL_MODEL g.pos setup_floatup_model
		setvard MAIN_SEAL 1
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( !SEAL_DOWN )
	{
		local g.pos $getcl(sfx.npcid,origin)
		cleffect tempent model SEAL_MODEL g.pos setup_floatdown_model
		setvard SEAL_DOWN 1
	}

	if ( ROT_COUNT >= 360 ) setvard ROT_COUNT 0

	vectoradd l.pos $relpos($vec(0,ROT_COUNT,0),$vec(0,CIRCLE_RADIUS,256))

	add ROT_COUNT 40

	cleffect tempent sprite FX_SPRITE l.pos setup_flame
}

{ [client] setup_flame

	cleffect tempent set_current_prop death_delay 	1.0
    //cleffect tempent set_current_prop bouncefactor 	0
	//cleffect tempent set_current_prop scale 	1.0
	//cleffect tempent set_current_prop framerate	30
	//cleffect tempent set_current_prop frames	8
	cleffect tempent set_current_prop rendercolor 	(128,128,255)
	//cleffect tempent set_current_prop gravity 	800
	//cleffect tempent set_current_prop collide 	none
    //cleffect tempent set_current_prop velocity 	(0,0,100)
}

{ [client] setup_floatup_model

	cleffect tempent set_current_prop death_delay sfx.duration
    cleffect tempent set_current_prop bouncefactor 	0
	cleffect tempent set_current_prop framerate	1
	cleffect tempent set_current_prop scale 	2.0
	cleffect tempent set_current_prop gravity 	-0.01
	cleffect tempent set_current_prop rendermode normal
	cleffect tempent set_current_prop collide none
    cleffect tempent set_current_prop velocity 	(0,0,-0.1)
    cleffect tempent set_current_prop body SEAL_OFS
}

{ [client] setup_floatdown_model

	cleffect tempent set_current_prop death_delay sfx.duration
    cleffect tempent set_current_prop bouncefactor 	0
	cleffect tempent set_current_prop framerate	1
	cleffect tempent set_current_prop scale 	2.0
	cleffect tempent set_current_prop gravity 	0.01
	cleffect tempent set_current_prop rendermode normal
	cleffect tempent set_current_prop collide none
    cleffect tempent set_current_prop velocity 	(0,0,0.1)
    cleffect tempent set_current_prop body SEAL_OFS
}


{ [client] effect_die

	setvard RAIN_SPRITES 0
	removescript
}