//Thothie MAR2010_15
//- Yet another attempt to make a dependable base_aoe script
//- These vars MUST be set in game_dynamically_created
//- AOE_OWNER - id of owner (will use value of MY_OWNER if set)
//- AOE_RADIUS - radius of AOE effect (can also be const)
//- AOE_DURATION - duration of AOE effect (can also be const)
//
//- Constants (default)
//- AOE_SCAN_TYPE dodamage (scan via dodamage - maybe blocked by other entities) | tsphere (scan via sphere [goes through walls]) | rsphere (scans area freely, but does not go through walls) | noscan (does no scan, but still calls aoe_scan_loop)
//- AOE_SCAN_FREQ 0.5 - how often to scan
//- AOE_FRIENDLY 0 (set 1 for friendly effects)
//- AOE_VULNERABLE 0 (set to 1 to allow AOE generator to be attacked)
//- AOE_VADJ 0 (vertically adjust scan point for dodamage scans)
//
//- Set by base
//- AOE_ISPLAYER - 1 if owner is player
//- AOE_PVP - 1 if PVP is on
//- AOE_ACTIVE - 1 while active
//
//- Calls to be handled by top script
//- aoe_affect_target <id> //note: checks PVP before call, unless AOE_FRIENDLY
//- aoe_end - called when AOE_DURATION ends (does not remove script)

#scope server
{
	const AOE_SCAN_TYPE dodamage
	const AOE_SCAN_FREQ 0.5
	const AOE_FRIENDLY 0
	const AOE_VULNERABLE 0
	const AOE_VADJ 0
}

{ game_dynamically_created
	callevent 0.01 aoe_start
}

{ game_spawn
	if !AOE_VULNERABLE
	invincible 1
	setvard PLAYING_DEAD 1
	nopush 1
	gravity 0
}

{ aoe_start
	if ( $get(MY_OWNER,isalive) ) setvard AOE_OWNER MY_OWNER
	race $get(AOE_OWNER,race)
	setvard AOE_PVP game.pvp
	setvard AOE_ISPLAYER $get(AOE_OWNER,isplayer)
	setvard AOE_ACTIVE 1
	callevent aoe_scan_loop

	local L_AOE_DURATION AOE_DURATION
	subtract AOE_DURATION 0.01 //for accurate end time

	callevent L_AOE_DURATION aoe_end
}

{ aoe_scan_loop
	if AOE_ACTIVE
	callevent AOE_SCAN_FREQ aoe_scan_loop
	if ( AOE_SCAN_TYPE equals dodamage )
	{
		dodamage $relpos(0,0,AOE_VADJ) AOE_RADIUS 0 100% 0 reflective target
	}

	if ( AOE_SCAN_TYPE contains sphere )
	{
		if ( !AOE_FRIENDLY )
		{
			setvard AOE_TARGET_LIST $get_tsphere(enemy,AOE_RADIUS)
		}
		else
		{
			setvard AOE_TARGET_LIST $get_tsphere(ally,AOE_RADIUS)
		}
		if AOE_TARGET_LIST isnot none
		calleventloop $get_token_amt(AOE_TARGET_LIST) aoe_affect_targets
	}
}

{ aoe_end
	setvard AOE_ACTIVE 0
}

{ aoe_affect_targets
	local CURAOE_TARG $get_token(AOE_TARGET_LIST,game.script.iteration)

	local DO_EFFECT 1

	if ( AOE_ISPLAYER )
	{
		if ( !AOE_FRIENDLY )
		{
			if ( !AOE_PVP )
			{
				if $get(CURAOE_TARG,isplayer)
				local DO_EFFECT 0
			}
		}
	}

	if ( AOE_SCAN_TYPE equals rsphere )
	{
		//uses tsphere, but require no world brushes be blocking
		local TRACE_START $get(ent_me,origin)
		local TRACE_END $get(CURAOE_TARG,origin)
		local TRACE_LINE $get_traceline(TRACE_START,TRACE_END,worldonly)
		if TRACE_LINE isnot TRACE_END
		local DO_EFFECT 0
	}

	if DO_EFFECT
	callevent aoe_affect_target CURAOE_TARG	
}

{ game_dodamage //PARAM1=hit:0|1 PARAM2=ent_hit PARAM3=(start) PARAM4=(end)
	if $get(PARAM2,exists)

	if ( !AOE_FRIENDLY )
	{
		if ( $get(PARAM2,relationship,AOE_OWNER) equals enemy ) local DO_EFFECT 1
		if ( !AOE_PVP )
		{
			if ( AOE_ISPLAYER )
			{
				if $get(PARAM2,isplayer)
				local DO_EFFECT 0
			}
		}
	}

	if DO_EFFECT
	callevent aoe_affect_target $get(PARAM2,id)
}