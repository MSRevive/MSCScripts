//Left mouse button phys guns objects around
//Right mouse button teleports the player in the direction they are looking

//Holding use and clicking right mouse button will copy the target and overwrite the lmb to paste it
//Trying to copy no target clears the selection and returns the lmb to normal control

//Holding use and double clicking right mouse will open a menu with more options

{
	//anims
	const ANIM_USE 6
}

{ 
	setvar NPC_CLIPBOARD none
	setvar NPC_PHYSGUN none
	
	const PHYS_KEEP_DIST 300
	setvar PHYS_LAST_ANIM 0
	
	const CLIPBOARD_KEEP_DIST 300
	setvar CLIPBOARD_ADD_DIST 0
}

{ game_spawn

	name "Jester's Staff"
	desc "For the real clowns who spend days troubleshooting simple problems."

	weight 0
	size 5
	value 0
	sethudsprite hand item
	sethudsprite trade 21

	setviewmodel viewmodels/v_1hblunts.mdl
	setviewmodelprop ent_me submodel 1 6

	sethand both
}

{ game_pickup

	setvard PICKING_UP
	playviewanim 0
}

{ game_attack1 //Paste npc

	if ( NPC_CLIPBOARD isnot none )
	{
		local L_KEEP_DIST CLIPBOARD_KEEP_DIST
		add L_KEEP_DIST CLIPBOARD_ADD_DIST
		
		local L_POS $relpos($get(ent_owner,viewangles),$vec(0,L_KEEP_DIST,0))
		vectoradd L_POS $get(ent_owner,eyepos)
		
		local L_SPAWN_POINT $get_traceline($get(ent_owner,eyepos),L_POS,worldonly)
		createnpc NPC_CLIPBOARD L_SPAWN_POINT
	}
}

{ game_attack1_down //Physgun

	if ( NPC_CLIPBOARD equals none )
	{
		callevent try_physgun_anim
		
		if ( NPC_PHYSGUN equals none )
		{
			callevent find_physgun_target
		}
		else
		{
			if ( $get(NPC_PHYSGUN,isalive) ) callevent update_physgun_target
			else setvard NPC_PHYSGUN none
		}
	}
}

{ game_-attack1
	setvard NPC_PHYSGUN none
}

{ try_physgun_anim

	if ( $math(subtract,game.time,PHYS_LAST_ANIM) > 0.8 )
	{
		playviewanim ANIM_USE
		setvard PHYS_LAST_ANIM game.time
	}
}

{ find_physgun_target

	local L_START $get(ent_owner,eyepos)
	
	local L_END $relpos($get(ent_owner,viewangles),$vec(0,10000,0))
	vectoradd L_END L_START
	
	local L_TARGET $get_traceline(L_START,L_END,ent,$get(ent_owner,id))
	if ( $get(L_TARGET,exists) )
	{
		if ( $get(L_TARGET,isalive) )
		{
			if ( !$get(L_TARGET,isplayer) )
			{
				if ( $get(L_TARGET,origin) isnot '(0,0,0)' )
				{
					setvard NPC_PHYSGUN L_TARGET
					bplayermessage ent_owner "Linked physgun to" $get(L_TARGET,name)
				}
			}
		}
	}
}

{ update_physgun_target

	local L_KEEP_DIST PHYS_KEEP_DIST
	add L_KEEP_DIST $math(divide,$get(NPC_PHYSGUN,width),2)
	
	local L_TARG_POS $relpos($get(ent_owner,viewangles),$vec(0,L_KEEP_DIST,0))
	vectoradd L_TARG_POS $get(ent_owner,eyepos)
	
	setorigin NPC_PHYSGUN L_TARG_POS
	setvelocity NPC_PHYSGUN $vec(0,0,0)
	
	effect glow NPC_PHYSGUN (50,50,255) 72 0.2 0.2
}

{ game_attack2

	if ( $get(ent_owner,keydown,use) )
	{
		callevent find_copy_target
	}
	else
	{
		//No keydown = teleport forward
		local L_TELE_POS $relpos($get(ent_owner,viewangles),$vec(0,400,0))
		vectoradd L_TELE_POS $get(ent_owner,origin)
		
		setorigin ent_owner L_TELE_POS
	}
}

{ find_copy_target

	local L_START $get(ent_owner,eyepos)
	
	local L_END $relpos($get(ent_owner,viewangles),$vec(0,10000,0))
	vectoradd L_END L_START
	
	local L_TARGET $get_traceline(L_START,L_END,ent,$get(ent_owner,id))
	if ( $get(L_TARGET,exists) )
	{
		if ( $get(L_TARGET,isalive) )
		{
			if ( !$get(L_TARGET,isplayer) )
			{
				if ( $get(L_TARGET,origin) isnot '(0,0,0)' )
				{
					setvard NPC_CLIPBOARD $get(L_TARGET,scriptname)
					setvard CLIPBOARD_ADD_DIST $math(divide,$get(L_TARGET,width),2)
					bplayermessage ent_owner "Copied" $get(L_TARGET,name)
				}
			}
		}
	}
}



{ game_viewanimdone

	//if ( PICKING_UP ) playviewanim 1
	playviewanim 1
}