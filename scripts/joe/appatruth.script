#scope server
{
	const CHAT_NEVER_INTERRUPT 1
}

#include monsters/base_npc
#include monsters/base_chat_array

{ [shared] npc_spawn
	
	name Apparition of Atruth
	setmodel npc/dwarf_lantern.mdl
	hp 9001
	race beloved
	setvard PLAYING_DEAD 1
	invincible 1
	nopush 1
	roam 0

	setprop ent_me rendermode 5
	setprop ent_me renderamt 100
	setprop ent_me renderfx 16

	setidleanim idle

	width 32
	height 54

	setmodelbody 0 2
	saytextrange 2048

	blood none

	setsolid none

	usetrigger candle
	callevent 2.0 intro
}

// Not really much of an intro.
{ intro

	callevent chat_face_speaker
	callevent chat_add_text intro_chat "Aye! Could ya be helpin' out a poor old ghost of a once good dwarf?" 3.5 nod speak_help
	callevent chat_add_text intro_chat "My fellows and I were working these here mines, but something... Unholy... Happened to them." 6.0
	callevent chat_add_text intro_chat "If ya could put their souls to rest, I would be forever grateful, as then I could be movin' on me self." 7.0
	callevent chat_add_text intro_chat "There's also some explosives in the caves we were hoping to use to connect the other mines." 6.5 nod access
	callevent chat_add_text intro_chat "They be dwarven explosives, so they'll still be good, despite the time and the damp." 6.0
	callevent chat_add_text intro_chat "So take these torches, and keep your eye out for a fuse. They'll be useful for that as well as fer seein'." 6.0

	callevent chat_start_sequence intro_chat add_to_que
}

{ speak_help
	playsound 0 10 voices/dwarf/vs_nx0drogm_heal.wav
}

{ game_postspawn //PARAM1 = (name|default), PARAM2 = DmgMulti, PARAM3 = HPMulti, PARAM4 = pass_params
	setvard TORCH_TRIGGER PARAM4
}

{ access
	
	usetrigger TORCH_TRIGGER
	usetrigger wave1

	callevent chat_add_text end_chat "I think I'll be fading away fer a bit. After all, I can't really be helpin' ye like this, now can I?" 6.5 none remove_me
	callevent chat_start_sequence end_chat add_to_que

}

{ remove_me

	usetrigger candle
	deleteent ent_me fade
}
