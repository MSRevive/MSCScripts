//Armourer

{
	const SOUND_DEATH none			//base_npc

	//base_vendor
	setvar STORE_CLOSED 0
	const STORE_NAME edana_armory
	const STORE_TRIGGERTEXT "store trade buy sell purchase sale offer"
	const STORE_SELLMENU 1
	const SELL_RATIO 0.75
	const NO_RUMOR 1

	const VEND_ARMORER 1
	const NPC_REACTS 1
	setvard VENDOR_NOT_ON_USE 1
}

#include monsters/base_npc
#include monsters/base_chat
#include monsters/base_npc_vendor
#include monsters/base_npc_vendor_confirm
#include helena/helena_npc

// Spawn properties
{ npc_spawn

	hp 25
	gold 25
	name "Iron Fist Ike, the Armourer"
	//ov 30
	width 32
	height 72
	race human
	roam 0 //Don't go roaming around
	setmodel npc/blacksmith.mdl
	invincible  1

	setvard JOB 0
	setvard CANCHAT 1
	setvard STORE_CLOSED 0

	callevent createmystore
	if ( $lcase(game.map.name) equals helena ) 
	{
		name "Galhad, Dorfgan's Sales Rep"
		setvard NO_JOB 1
	}

	if $lcase(game.map.name) isnot helena

	catchspeech say_hi hi hello hail greet hey
	catchspeech say_job job work money gold
	catchspeech say_yes yes sure "why not" ok sho whatever "of course" "tell me more"
	catchspeech say_where abulurd where
	catchspeech say_key key broken piece
}

///////////////////////////////////////////////////////////

// Greeting players

{ npcreact_targetsighted		//Respond to seeing player

	if !GOT_SCARED
	if !STORE_CLOSED
	if $get(PARAM1,dist) <= 90
	if JOB.TARGET isnot PARAM1

	if ( JOB.WINNER isnot PARAM1 )
	{
		saytext Howdy! How about a trade?
	}
	else
	{
		saytext Howdy $stradd($get(JOB.WINNER,name),'!') What can I do for you?
	}	
}

{ say_hi

  	if $get(ent_lastspoke,dist) <= 90
  	if !STORE_CLOSED

  	saytext "'Ello there.  Would you like to [buy] some fine armor, traveler?"
}

{ vendor_addstoreitems

	//           |Store name|   |Item name|	       |Quantity|	|cost| |Sell ratio|
	addstoreitem  STORE_NAME     armor_leather_torn 	$rand(1,2)	100	SELL_RATIO
	addstoreitem  STORE_NAME     armor_leather		$rand(1,2)	100	SELL_RATIO
	addstoreitem  STORE_NAME     armor_leather_studded	$rand(0,1)	100	SELL_RATIO
	addstoreitem  STORE_NAME     armor_plate   	 	$rand(0,2)	125	SELL_RATIO
	addstoreitem  STORE_NAME     armor_helm_plate 	$rand(0,2)	125	SELL_RATIO
	
	addstoreitem  STORE_NAME     skin_boar 0 150 0.75
	addstoreitem  STORE_NAME     skin_ratpelt 0 250 0.75
	addstoreitem  STORE_NAME     skin_bear 0 150 1
	addstoreitem  STORE_NAME     skin_boar_heavy	0 150 1

	addstoreitem  STORE_NAME     shields_ironshield  	$rand(0,3)	100	0.25
	addstoreitem  STORE_NAME     shields_buckler  	$rand(0,3)	100	SELL_RATIO

	addstoreitem  STORE_NAME     gown_edana		10		100	SELL_RATIO
}

{ trade_success

   if CANCHAT == 1

   playsound game.sound.voice game.sound.maxvol npc/goods.wav
   say [.34] [.24] [.35] [.40]
   setvard CANCHAT 0
   callevent 10 resetchat
}

{ resetchat
	setvard CANCHAT 1
}

{ say_job

  	if ( JOB == 0 )
	{
  		setvard JOB 1
  		saytext "Well you do look strong enough, I could always use a short-term errand-runner. Are ye interested?"
		menu.open ent_lastspoke
  		callevent 300 reset_job
	}
	else if ( JOB.TARGET isnot $get(ent_lastspoke,id) )
	{
		if $get(JOB.TARGET,id) isnot PARAM1
		playanim once no
  		saytext "Sorry, Ive already got me an errand boy. If you see" $get(JOB.TARGET,name) "tell him to hurry it up!"
	}

	if ( JOB >= 3 ) saytext "Sorry, think I've got all the help I need for today."
}

{ reset_job
  	setvard JOB 0
}

{ say_yes

 	if JOB equals 1
	if !JOB.SPEECH1 

   	setvard JOB.SPEECH1 1
  	setvard JOB.TARGET $get(ent_lastspoke,id)
  	saytext Excellent! I need you to go to Abulurd, give him this letter. 
  	offer ent_lastspoke item_ikeletter
	callexternal $get_by_name(abulurd) global_quest_letter
}

{ say_where

  	if JOB.SPEECH1 equals 1
  	saytext "Abulurd? Why he's outside of town in his house. On the path between here and the Temple."
}

{ quest_log_done

	saytext "Hmmm... this does look like a good set. I'll have him send over the rest."
	setvard script.questlog.target PARAM1
	callevent 4 quest_log_done_2
}
{ quest_log_done_2

  	saytext "Well here's something for your trouble."
  	offer script.questlog.target gold 6
 	callevent 4 quest_ledger_start
}

{ quest_ledger_start
	
  	saytext "Head back over to Abulurd and ask if he has the ledger I asked him about yesterday."
  	setvard JOB 2
	callexternal $get_by_name(abulurd) global_quest_ledger
}
{ quest_ledger_done

	saytext "Yep, this is it. Thanks for your help" $get(PARAM1,name)
	offer PARAM1 gold 6
	setvard JOB 3
	setvard JOB.TARGET ''
	setvard JOB.WINNER PARAM1
}

{ quest_boar_done

	//Continuing the Boar quest from Abulurd, the old man
	saytext "Aah! What's this?"
	setvard questboar.target PARAM1
	playanim critical studycart
	callevent 1 quest_boar_done_2
	callexternal $get_by_name(abulurd) global_quest_boars_done
}
{ quest_boar_done_2

	saytext This pelt is of excellent quality, my friend! 
	callevent 2.3 quest_boar_done_3
}
{ quest_boar_done_3

	saytext "Stay here for a second and I'll make something out of it."
	setvard questboar.angle game.monster.angles.yaw
	callevent 1 quest_boar_done_4
}
{ quest_boar_done_4

	local newang questboar.angle
	add newang 180
	setangle face.yaw newang
	callevent 5 quest_boar_done_5
}
{ quest_boar_done_5

  	saytext Here you go! Hope you will enjoy it.
	setangle face.yaw questboar.angle
  	offer questboar.target armor_leather_torn
}

{ say_closed

   saytext "Sorry, I'm closed. I will reopen at seven in the morning"
}

{  [override] vendor_offerstore		//1: Offer Target

  	if $get(PARAM1,dist) <= 90

	if ( STORE_CLOSED )	saytext "Sorry, I'm closed. I will reopen at seven in the morning"
	else callevent basevendor_offerstore PARAM1
}

{ game_menu_getoptions

	if $lcase(game.map.name) isnot helena

	if( JOB == 1 )
	{
		if( !JOB.SPEECH1 )
		{
			local reg.mitem.title	"Accept Job"
			local reg.mitem.type	say

			local l.say $rand(1,6)
			if( l.say == 1 )	local reg.mitem.data 'Yes'
			else if( l.say == 2 )	local reg.mitem.data 'Sure'
			else if( l.say == 3 )	local reg.mitem.data "Why not" 
			else if( l.say == 4 )	local reg.mitem.data 'Ok'
			else if( l.say == 5 )	local reg.mitem.data "Of course"
			else if( l.say == 6 )	local reg.mitem.data "Tell me more"

			menuitem.register
		}
		else if( $item_exists(PARAM1,item_ikelog) )
		{
			local reg.mitem.title 	"Give Log"
			local reg.mitem.type 	payment
			local reg.mitem.data 	item_ikelog
			local reg.mitem.callback quest_log_done
			menuitem.register
		}
		else callevent menuitem_where
	}
	else if ( JOB == 2 )
	{
		if( $item_exists(PARAM1,item_ledger) )
		{
			local reg.mitem.title 	"Give Ledger"
			local reg.mitem.type 	payment
			local reg.mitem.data 	item_ledger
			local reg.mitem.callback quest_ledger_done
			menuitem.register
		}
		else callevent menuitem_where
	}

	if( $item_exists(PARAM1,skin_boar_heavy) )
	{
		local reg.mitem.title 	"Give Pelt"
		local reg.mitem.type 	payment
		local reg.mitem.data 	skin_boar_heavy
		local reg.mitem.callback quest_boar_done
		menuitem.register
	}

	//============ OUTPOST KEY
	setvard HAS_KEYPARTS 0
	if ( $item_exists(PARAM1,brokenkey_1) ) 
	{
		local reg.mitem.id	payment
		local reg.mitem.title 	"Give Key Hilt"
		local reg.mitem.type 	payment
		local reg.mitem.data 	brokenkey_1
		local reg.mitem.callback  got_part_1
		menuitem.register
		setvard HAS_KEYPARTS 1
	}
	
	if ( $item_exists(PARAM1,brokenkey_2) )
	{
		local reg.mitem.id	payment
		local reg.mitem.title 	"Give Key Middle"
		local reg.mitem.type 	payment
		local reg.mitem.data 	brokenkey_2
		local reg.mitem.callback  got_part_2
		menuitem.register
		setvard HAS_KEYPARTS 1
	}

	if ( $item_exists(PARAM1,brokenkey_3) )
	{
		local reg.mitem.id	payment
		local reg.mitem.title 	"Give Key End"
		local reg.mitem.type 	payment
		local reg.mitem.data 	brokenkey_3
		local reg.mitem.callback  got_part_3
		menuitem.register
		setvard HAS_KEYPARTS 1
	}


	if ( HAS_KEYPARTS )
	{
		setvard KEY_STEP 0
		callevent key_intro
	}

	if ( GOT_KEYPART_1 )
	{
		if ( GOT_KEYPART_2 )
		{
			if ( GOT_KEYPART_3 )
			{
				setvard FORGE_KEY_STEP 0
				local reg.mitem.id	payment
				local reg.mitem.title 	"Pay 10,000 to reforge key"
				local reg.mitem.type 	payment
				local reg.mitem.data 	"gold:10000"
				local reg.mitem.callback  start_forge_key
				local reg.mitem.cb_failed not_enough_forge_key
				menuitem.register
			}
		}
	} //endif GOT_KEYPART_1

}

{ menuitem_where

	local reg.mitem.title	"I can't find Abulurd"
	local reg.mitem.type	say

	local l.say $rand(1,2)
	if( l.say == 1 )	local reg.mitem.data "Where is abulurd?"
	else if( l.say == 2 )	local reg.mitem.data "I can't find abulurd"

	menuitem.register	
}

{ key_intro

	if !KEY_INTRO_DELAY	//stops him from spamming key intro with each part

	add	KEY_STEP 1

	if ( KEY_STEP == 1 ) 
	{
		playanim once converse1
		saytext "Thats a mighty interestin lookin piece of a key you have there..."
	}

	if ( KEY_STEP == 2 ) saytext "If ya find all the parts, I maybe able to put it back together for ya."

	if ( KEY_STEP == 3 ) saytext "Best not give me any parts lest ya have them all, as youll not be gettin any back."

	if ( KEY_STEP == 4 ) 
	{
		setvard KEY_INTRO_DELAY 1
		callevent 60.0 reset_keyintro
		saytext "Oh... And 10,000 gold for the labor. Delicate work, key forging is."
	}

	if KEY_STEP < 4

	callevent 2.0 key_intro
}

{ start_forge_key
	recvoffer accept
	setvard QUEST_WINNER PARAM1

	setvard GOT_KEYPART_1 0
	setvard GOT_KEYPART_2 0
	setvard	GOT_KEYPART_3 0

	callevent 0.1 forge_key
}

{ forge_key

	add FORGE_KEY_STEP 1

	if ( FORGE_KEY_STEP == 1 )
	{
		playanim once portal
		saytext "Yup, just as I thought, gonna be a tricky little thing..."
	}
	if ( FORGE_KEY_STEP == 2 )
	{
		//playanim once portal
		saytext "But dont ya worry, Ill get her done alright..."
	}
	if ( FORGE_KEY_STEP == 3 ) saytext ...almost got it....

	if ( FORGE_KEY_STEP == 4 )
	{
		playanim once push_button2
		look 1024
		offer QUEST_WINNER key_forged
		saytext "See, there ya go, one re-forged key."
	}

	if FORGE_KEY_STEP < 4
	callevent 2.0 forge_key
}

{ not_enough_forge_key

	playanim once checktie
	saytext "Reforging an ornate key like this is very delicate work! I will need more gold."
}

{ got_part_1

	playanim once push_button
	setvard GOT_KEYPART_1 1
	saytext "Mmm hmm... Fancy lookin key this hilt belongs to, no doubt."
	if ( GOT_KEYPART_2 )
	{
		if ( GOT_KEYPART_3 )
		{
			saytext "Alright, got all the pieces, now I just need that labor charge..."
		}
	}
}

{ got_part_2

	playanim once push_button
	setvard GOT_KEYPART_2 1
	saytext "Yeah, I bet this key is supposed to open somethin really valuable..."
	if ( GOT_KEYPART_1 )
	{
		if ( GOT_KEYPART_3 )
		{
			saytext "Alright, got all the pieces, now I just need that labor charge..."
		}
	}
}

{ got_part_3

	playanim once push_button
	setvard GOT_KEYPART_3 1
	saytext "These are about the fanciest lookin key bits Ive ever seen..."
	if ( GOT_KEYPART_1 )
	{
		if ( GOT_KEYPART_2 )
		{
			saytext "Alright, got all the pieces, now I just need that labor charge..."
		}
	}
}

{ say_key

	setvard KEY_STEP 0
	callevent key_intro
}

{ reset_key_intro

	setvard KEY_INTRO_DELAY 0
}