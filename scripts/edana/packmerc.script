//Pack Merchant

{
	const SOUND_DEATH none			//base_npc

	//base_vendor
   	const STORE_NAME foglund_shop
   	setvar STORE_TRIGGERTEXT "store trade buy sell purchase sale offer"
   	const STORE_SELLMENU 1
	const SELL_RATIO 0.75
	setvard SELL_WEAPON_LEVEL 0,3

	setvard VEND_NEWBIE 1
	const VEND_WEAPONS 1
	const VEND_CONTAINERS 1

	const NO_CHAT 1
}

#include monsters/base_npc
#include monsters/base_chat
#include monsters/base_npc_vendor
#include monsters/base_npc_vendor_confirm
#include monsters/base_xmass

// Spawn properties
{ npc_spawn

	hp 25
	gold 25
	name "Foglund the Merchant"
	//ov 30
	width 32
	height 72
	race human
	roam 0      //Don't go roaming around
	setmodel    npc/human1.mdl
	setmodelbody 1 2
	invincible  1

	setvard CHAT_JOB 0
	setvard CHAT_GOODS 0
	setvard	CANCHAT 1

	if ( game.map.name isnot lowlands ) setvard AD_TEXT "Backpacks! Sheaths! Torches!"
	if ( game.map.name equals lowlands ) setvard AD_TEXT "I do hope you can help me with my little bear problem..."

	catchspeech say_hi hi hello hail greet hey
	catchspeech say_store buy trade sell purchase sale
	catchspeech say_rumour heard rumor rumour news happening
	catchspeech say_job work job
	catchspeech say_bear bear
}

{ say_rumor
	callevent say_rumour
}

{ say_bear
	saytext "I hate bears, I really do, I've an insatiable desire to buy bear parts. Got any?"
	callevent 4.0 say_bear2
}

{ say_bear2
	if game.map.name equals lowlands
	saytext "Granted, I suspect this has gotten me into a bit of trouble."
	callevent 4.0 say_bear3
}


{ say_bear3
	saytext "It is said if you kill 100 bears, the Curse of the Bear Gods will befall you."
	callevent 4.0 say_bear4
}

{ say_bear4
	saytext "Looks as though it has befallen me... I can't get outside."
	callevent 4.0 say_bear5
}

{ say_bear5
	saytext "I've no clue how I'm going to get back to Edana."
	callevent 4.0 say_bear6
}

{ say_bear6
	saytext "I'll give you access to my special stock, if you think you can break this curse!"
}

{ say_hi

  saytext Hello, I have many things that you will need!
  calleventtimed 2 say_store
  callevent no_chat
}

///////////////////////////////////////////////////////////

// Greeting players
{
   repeatdelay 1

   if CANCHAT
   if $cansee(player,128) 

   saytext AD_TEXT
   say [.6] [.6] [.6]
   callevent no_chat
   callevent 90 reset_chat
}


{  no_chat			//Make sure I don't idle chat while player is interacting with me
   	setvard CANCHAT 0
   	callevent 20 reset_chat
}
{  reset_chat
   	setvard CANCHAT 1
}

{ vendor_addstoreitems

	//           |Store name|  |Item name|    		|Q|		|cost%| |Sell ratio| |Bundle amt|

	local L_MAP_NAME $lcase(game.map.name)

	if ( L_MAP_NAME equals lowlands )
	{
		addstoreitem STORE_NAME item_crystal_reloc 1 300 0.1
		addstoreitem STORE_NAME item_crystal_return 1 300 0.1
		addstoreitem STORE_NAME sheath_spellbook 1 80 SELL_RATIO
		addstoreitem STORE_NAME skin_bear 20 100 0.9
		addstoreitem STORE_NAME health_spotion 10 80 0.5
		addstoreitem STORE_NAME mana_protection 1 $rand(100,150) 0.2
	}

	if ( L_MAP_NAME isnot lowlands ) addstoreitem STORE_NAME skin_bear 0 100 2.0
	addstoreitem STORE_NAME		item_bearclaw 0 100 0.9
	addstoreitem  STORE_NAME     swords_rsword		$rand(1,5)	100	SELL_RATIO
	addstoreitem  STORE_NAME     smallarms_rknife	$rand(1,5)	100	SELL_RATIO
	addstoreitem  STORE_NAME     smallarms_knife		$rand(1,3)	100	SELL_RATIO
	addstoreitem  STORE_NAME     bows_treebow		$rand(1,5)	100	SELL_RATIO
	addstoreitem  STORE_NAME     axes_rsmallaxe		$rand(1,5)	100	SELL_RATIO
	addstoreitem  STORE_NAME     blunt_hammer1		$rand(1,5)	100	SELL_RATIO
	addstoreitem  STORE_NAME     polearms_qs	$rand(1,5)  	100	SELL_RATIO
	addstoreitem  STORE_NAME     item_torch		$rand(5,10)	100	SELL_RATIO
	addstoreitem  STORE_NAME     pack_bigsack  		$rand(1,5)      100	SELL_RATIO
	addstoreitem  STORE_NAME     pack_heavybackpack  	$rand(1,4)    	100	SELL_RATIO
	addstoreitem  STORE_NAME     sheath_back_holster 	$rand(1,5)  	100	SELL_RATIO
	addstoreitem  STORE_NAME     blunt_club		0  		100	SELL_RATIO

	if ( $rand(1,5) == 1 ) addstoreitem  STORE_NAME     blunt_club	    	1   		100	SELL_RATIO
}

{ trade_success

   if CHAT_GOODS

   say goods[.34] [.24] [.35] [.40]
   callevent no_chat
   setvard CHAT_GOODS 0
   callevent 6 reset_chat_goods
}

{ reset_chat_goods
	setvard CHAT_GOODS 1
}

{ say_job

  	saytext "Well I'm just a small shop owner with no need for help, maybe Ike could help ye out?"
  	say     [.5] [.4] [.2] [.1] [.4] [.4] [.2] [.2] [.4] [.1][.2] [.2] [.8] [.2] [.2] [.4] [.6] [.4] [.2]
  	callevent no_chat
  	callevent 15 reset_chat_job
}

{ reset_chat_job
	setvard CHAT_JOB 1
}

{ say_rumour
	callevent no_chat
	playanim once no
	saytext "I don't know much. Speak to Krythos. He've been all over the world."
	say     [.2] [.2] [.2] [.5] [.3] [.1] [.15] [.6] [.4] [.4][.2] [.2] [.2] [.3] [1]
}
