//Old man

{
   	const	SOUND_IDLE1 voices/human/male_oldidle.wav
	const	SOUND_IDLE2 voices/human/male_oldidle2.wav
	const	SOUND_IDLE3 voices/human/male_oldidle3.wav
	const	SOUND_POTION voices/human/male_oldpotion.wav
	const	NO_RUMOR 1

	const   SOUND_DEATH none			//base_npc

	//base_vendor
   	const STORE_NAME edana_merchant_2
   	setvar STORE_TRIGGERTEXT "store trade buy sell purchase sale offer"

	const NO_RUMOR 1
	const VENDOR_NOT_ON_USE 1
}

#include monsters/base_npc
#include monsters/base_chat
#include monsters/base_npc_vendor
#include monsters/base_npc_vendor_confirm
#include monsters/base_xmass

// Spawn properties
{ npc_spawn

   hp 25
   gold 25
   name Old man //A name doesn't get set until Ike mentions it
   name_unique abulurd
   //ov 30
   width 32
   height 72
   race human
   roam 0      //Don't go roaming around
   setmodel	npc/human1.mdl
   invincible  1

   setvard	CANCHAT 1
   setvard	STORENAME edanaoldmans_shop
   setvard	letter 0
   setvard	QUEST_BOAR 0
   setvard 	CAN_ASKPOTION 1

   catchspeech say_hi hi hello hail greet 
   catchspeech npc_say_store buy store heal potion buy trade
   catchspeech say_ledger ledger
   catchspeech say_job boar garden kill
   catchspeech say_shutup "shut up" quiet stop
}

   //Idling
{
	repeatdelay $rand(30,45)
   
   	if CAN_ASKPOTION
	playrandomsound game.sound.voice 6 SOUND_IDLE1 SOUND_IDLE2 SOUND_IDLE3
	say [.3] [.3] [.3] [.3]
}


{ say_hi

  	playanim once talkleft
  	saytext New folk around here, eh? And the adventerous kind, I see.
	say [.3] [.1] [.2] [.3] [.4] [.2] [.2] [.3] [.1] 
  	callevent 3 say_hi2
}
{ say_hi2
  	saytext Well, then, you are free to use my garden for practicing your skills. 
	say [.2] [.2] [.3] [.1] [.2] [.2] [.2] [.3] [.1] [.3] [.1] [.4]
  	callevent 3 say_hi3
}
{ say_hi3
  	saytext If you're lucky, the real big one'll be there. Real tough one, aye...
	say [.10] [.10] [.10] [.20] [.7] [.7] [.10] [.20] [.1] [.1] [.1] [.3]
}

{
   repeatdelay $rand(60,120)

   if CAN_ASKPOTION

   local POTION_CHAT $rand(1,3)
   if( POTION_CHAT == 1 )
   {
      saytext How about a [potion]?
      playsound game.sound.voice 6 SOUND_POTION
   }
   else if( POTION_CHAT == 2 ) saytext Did you need some [potions], lad?.
   else if( POTION_CHAT == 3 ) saytext I've got something that'll heal you up.
}

///////////////////////////////////////////////////////////

{
   eventname vendor_addstoreitems

 //	        |Store name|  |Item name|              |Quantity|  |cost%|	
   addstoreitem  STORE_NAME     health_mpotion    4   	       80
}

{  trade_success

   	if CANCHAT == 1

   	setvard CANCHAT 0
   	callevent 10 resetchat
}

{ resetchat
  	setvard CANCHAT 1
}

{ reset
  	setvard letter 0
}

{ game_recvoffer_item

  	if PARAM1 equals 'item_ikeletter'
  	if letter equals 0
  	recvoffer accept
	callevent quest_letter_done $get(ent_lastgave,id)
} 

{ global_quest_letter

	//Player doen't know the old man's name until Ike mentions it
	name Abulurd
}

{ quest_letter_done

  	playanim once pondering3
  	saytext So Ike's looking for some good firewood eh? My son does some good work, give him this sample.
  	setvard letter 1
	setvard quest.letter.target PARAM1
  	callevent 300 reset
  	callevent 3 quest_letter_done_2
}

{ quest_letter_done_2
 
  	offer quest.letter.target item_ikelog
}

{ global_quest_ledger

	setvard quest.ledger 1
}

{ say_ledger

	if quest.ledger == 1

	setvard quest.ledger 2
	setvard quest.ledger.target $get(ent_lastspoke,id)
  	playanim once pondering2
  	saytext Hmmm... I think I have that ledger around here somewhere...
  	callevent 4 quest_ledger_done
}
{ quest_ledger_done

  	playanim critical pull_needle
  	saytext Ah! Here it is! Now hurry and take it to Ike.
  	callevent 2 quest_ledger_done_2
}

{ quest_ledger_done_2

  offer quest.ledger.target item_ledger
}


//------- boars

{  trig_boarsdead
	setvard QUEST_BOAR 2
}
{ global_quest_boars_done

	setvard QUEST_BOAR 3
}

{ say_job

  	if( QUEST_BOAR == 0 )
	{
  		setvard QUEST_BOAR 1

  		saytext They're trapped in the backyard!
		playanim once converse2
		say [.30] [.30] [.30] [.40] [.20]
		callevent 2.3 say_boar2
	}
	else if( QUEST_BOAR == 1 )
	{
  		saytext Head out back and train up. I don' mind.
		//playanim once yes
		say [.20] [.10] [.10] [.10] [.10] [.10] [.15] [.10] 
	}
	else if( QUEST_BOAR == 2 )
	{
  		saytext Did'ya get all the little rascals?  A fine job lad.
		playanim once yes
		say [.20] [.10] [.10] [.30] [.10] [.10] [.15] [.10] 
		callevent 3 say_boar_done2
	}
}
{ say_boar2
  	saytext I just send people there so they can train their skills.
	say [.30] [.20] [.30] [.40] [.20] [.20] [.30] [.10] 
	callevent 4 say_boar3
}
{ say_boar3
  	saytext Nobody can afford a mentor these days...
	say [.20] [.20] [.30] [.10] [.20] [.20] [.20] 

	callevent 3 say_boar4
}

{ say_boar4
	saytext Oh, and Ike the Armourer might have a job for ye when you're done.
	say [.20] [.20] [.30] [.10] [.20] [.20] [.20] [.10] [.10] [.10] [.10] [.10]
	callevent 5 say_boar5
}

{ say_boar5
	saytext ...Care for a [potion], by the way?
	say [.10] [.10] [.10] [.10] [.10]
}

{ say_boar_done2
  	saytext Head to town and Ike can use the skins
	say [.20] [.20] [.30] [.10] [.20] [.10] [.10] [.10] [.10]
}


{ say_shutup
  	if CAN_ASKPOTION
  	setvard CAN_ASKPOTION 0

  	saytext I'm sorry, was I bothering you?
	playanim once panic
	say [.3] [.1] [.4] [.1] [.1] [.7] [.7] [.7] [.4]
}

{ game_menu_getoptions

	if( !letter )
	{
		if $item_exists(PARAM1,item_ikeletter)
		
		local reg.mitem.title 	"Give Letter"
		local reg.mitem.type 	payment
		local reg.mitem.data 	item_ikeletter
		local reg.mitem.callback quest_letter_done
		menuitem.register
	}
	
	if( quest.ledger == 1 )
	{
		local reg.mitem.title 	"Ask About Ledger"
		local reg.mitem.type 	say
		local reg.mitem.data 	"Do you have Ike's Ledger?"
		menuitem.register
	}

	if( $item_exists(PARAM1,skin_boar_heavy) )
	{
		local reg.mitem.title 	"Give Pelt"
		local reg.mitem.type 	callback
		local reg.mitem.callback quest_heavypelt_done
		menuitem.register
	}
	
}
