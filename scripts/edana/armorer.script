//Armourer

{
	const SOUND_DEATH none			//base_npc

	//base_vendor
	setvar STORE_CLOSED 0
   	const STORE_NAME gatecity_armory
   	const STORE_TRIGGERTEXT "store trade buy sell purchase sale offer"
	const STORE_SELLMENU 1
	const SELL_RATIO 0.75
	const OVER_CHARGE 150%
	const NO_RUMOR 1
   	setvard SELL_WEAPON_LEVEL 3,6,9,12

	const VEND_ARMORER 1
	setvard VEND_NEWBIE 1
	const VEND_CONTAINERS 1
	const VEND_WEAPONS 1
	const VEND_SPEC_SHEATHS 1

	const NPC_REACTS 1
}

#include monsters/base_npc
#include monsters/base_chat
#include monsters/base_npc_vendor
#include monsters/base_npc_vendor_confirm

// Spawn properties
{ npc_spawn

	name_unique	Roland
	hp	25
	gold	25
	name	Roland the Blacksmith
	fov	30
	width	32
	height	72
	race	human
	roam	0
    setmodel	dwarf/male1.mdl
    setmodelbody 0 0	//0-Elder 1-Youngin 2-Zombie
    setmodelbody 1 2 //0-Unarmed 1-smallaxe 2-battleaxe 3-bastardsword
	invincible	1

	setvard	JOB 0
	setvard	CANCHAT 1
	setvard	STORE_CLOSED 0

	catchspeech say_hi hi hello hail greet hey
   	catchspeech say_job job work money gold
   	catchspeech say_rumor rumour news happenings rumor
	catchspeech say_axe axe golden broken mayor
	catchspeech say_failed_pay no give back

	callevent createmystore
}

///////////////////////////////////////////////////////////

// Greeting players

{ npcreact_targetsighted		//Respond to seeing player
  	
	if ( !STORE_CLOSED )
	{
		if( $get(PARAM1,dist) <= 90 )
		{
			saytext Would you like to [purchase] one of my fine weapons?	
		}
	}
}

{ say_hi

	if ( !STORE_CLOSED )
	{
		if( $get(ent_lastspoke,dist) <= 90 )
		{
			saytext Would you like [buy] some fine armor or weapons?
		}
	}
}

{ say_job

	saytext Sorry, but with the Undermountains closed, I have no work to be done.

}

{ say_rumor

	saytext I hear the mayor is looking for some help.
}

{ vendor_addstoreitems

	//           |Store name|   |Item name|	       |Quantity|	|cost| |Sell ratio|
	addstoreitem  STORE_NAME     armor_leather_torn 	$rand(1,3)	125	SELL_RATIO
	addstoreitem  STORE_NAME     armor_leather		$rand(1,3)	125	SELL_RATIO
	addstoreitem  STORE_NAME     armor_leather_studded	$rand(1,3)	125	SELL_RATIO
	addstoreitem  STORE_NAME     armor_plate   	 	$rand(1,2)	90	SELL_RATIO
	addstoreitem  STORE_NAME     armor_helm_plate 	$rand(1,2)	90	SELL_RATIO
	addstoreitem  STORE_NAME     armor_helm_bronze 	$rand(1,2)	90	SELL_RATIO

	callevent mongol_gear
	callevent knight_gear

	addstoreitem  STORE_NAME     armor_golden		0		100	SELL_RATIO
	addstoreitem  STORE_NAME     armor_helm_golden 	0		100	SELL_RATIO
	addstoreitem  STORE_NAME     armor_dark		0		100	SELL_RATIO
	addstoreitem  STORE_NAME     armor_helm_dark		0		100	SELL_RATIO

	addstoreitem STORE_NAME sheath_belt_snakeskin 1 100 SELL_RATIO
	addstoreitem STORE_NAME sheath_back_snakeskin 1 100 SELL_RATIO
	addstoreitem STORE_NAME sheath_dagger_snakeskin 1 100 SELL_RATIO
	addstoreitem STORE_NAME sheath_axe_snakeskin 1 100 SELL_RATIO
	addstoreitem STORE_NAME sheath_blunt_snakeskin	1 100 SELL_RATIO
	addstoreitem STORE_NAME sheath_holster_snakeskin 1 100 SELL_RATIO
	addstoreitem STORE_NAME sheath_back_holster 5 100 SELL_RATIO

	addstoreitem  STORE_NAME     shields_buckler  	$rand(1,2)	100	SELL_RATIO
	addstoreitem  STORE_NAME     shields_ironshield  	$rand(1,2)	100	0.25
	addstoreitem  STORE_NAME     shields_lironshield  	$rand(1,2)	100	SELL_RATIO

	addstoreitem  STORE_NAME     swords_rsword		$rand(1,5)    	90	SELL_RATIO
	addstoreitem  STORE_NAME     swords_shortsword	$rand(1,5)    	90	SELL_RATIO
	addstoreitem  STORE_NAME     swords_nkatana		$rand(1,5)    	90	SELL_RATIO
	addstoreitem  STORE_NAME     swords_longsword 	$rand(1,5)    	90	SELL_RATIO
	addstoreitem  STORE_NAME     axes_doubleaxe 		$rand(1,5)    	90	SELL_RATIO
	addstoreitem  STORE_NAME     blunt_gauntlets_leather 		$rand(1,2)    	90	SELL_RATIO
	addstoreitem  STORE_NAME     sheath_belt_holster	1		OVERCHARGE	SELL_RATIO

	addstoreitem  STORE_NAME    smallarms_knife		4		100		SELL_RATIO
	addstoreitem  STORE_NAME    smallarms_dagger		3		100		SELL_RATIO
	addstoreitem  STORE_NAME    smallarms_rknife		0	   	100		SELL_RATIO

	addstoreitem  STORE_NAME     swords_rsword		0	   	100		SELL_RATIO
	addstoreitem  STORE_NAME     swords_shortsword		2	 	100		SELL_RATIO
	addstoreitem  STORE_NAME     swords_scimitar 		2		100		SELL_RATIO
	addstoreitem  STORE_NAME     swords_longsword 		2	   	100		SELL_RATIO
	addstoreitem  STORE_NAME     swords_bastardsword 	2	   	100		SELL_RATIO

	addstoreitem  STORE_NAME     axes_rsmallaxe		0	 	100		SELL_RATIO
	addstoreitem  STORE_NAME     axes_smallaxe		3	    	100		SELL_RATIO
	addstoreitem  STORE_NAME     axes_axe			2	    	100		SELL_RATIO
	addstoreitem  STORE_NAME     axes_2haxe			2	    	100		SELL_RATIO
	addstoreitem  STORE_NAME     axes_battleaxe		2	    	100		SELL_RATIO

	addstoreitem  STORE_NAME     blunt_club	    		1   	    	100		SELL_RATIO
	addstoreitem  STORE_NAME     blunt_hammer1		0	    	100		SELL_RATIO
	addstoreitem  STORE_NAME     blunt_hammer2		2	    	100		SELL_RATIO
	addstoreitem  STORE_NAME     blunt_mace			2	    	100		SELL_RATIO
	addstoreitem  STORE_NAME     blunt_warhammer		2	    	100		SELL_RATIO
	addstoreitem  STORE_NAME     blunt_hammer3		2	    	100		SELL_RATIO
	addstoreitem  STORE_NAME     blunt_maul			2	    	100		SELL_RATIO
}

{ mongol_gear

   if( $rand(1,10) == 1 )
   {
		addstoreitem  STORE_NAME     armor_mongol 		1		100	SELL_RATIO
		addstoreitem  STORE_NAME     armor_helm_mongol 	1		100	SELL_RATIO
   }
}

{ knight_gear

	if( $rand(1,25) == 1 )
	{
		addstoreitem  STORE_NAME     armor_knight 		1		100	SELL_RATIO
		addstoreitem  STORE_NAME     armor_helm_knight 	1		100	SELL_RATIO
	}
}

{  trade_success

   if CANCHAT == 1

   playsound game.sound.voice game.sound.maxvol npc/goods.wav
   say [.34] [.24] [.35] [.40]
   setvard CANCHAT 0
   callevent 10 resetchat
}

{ resetchat

  setvard CANCHAT 1
}

{ vendor_say_closed

   saytext "Sorry, I'm closed. I will reopen at seven in the morning."
}

{ game_menu_getoptions

	if ( OFFER_SET )
	{
		if $item_exists(PARAM1,item_gaxe_handle)
		local reg.mitem.title 	"Pay 10,000 Gold"
		local reg.mitem.type 	payment
		local reg.mitem.data 	"gold:10000;item_gaxe_handle;item_ore_lorel"
		local reg.mitem.callback  say_gotgold
		local reg.mitem.cb_failed say_failed_pay
		menuitem.register
	}

	if !OFFER_SET
	if ( $item_exists(PARAM1,item_gaxe_handle) )
	{
		local reg.mitem.title 	"Ask about broken axe"
		local reg.mitem.type 	callback
		local reg.mitem.callback say_axe
		menuitem.register

		if $item_exists(PARAM1,item_ore_lorel)
		local reg.mitem.title 	"Show Loreldian Ore"
		local reg.mitem.type 	callback
		local reg.mitem.callback say_ore
		menuitem.register
	}
}

{ say_axe

	if ( PARAM1 equals 'PARAM1' )
	{
		//callled from chat, not from menu, make sure he has axe
		local SPEAKER_ID $get(ent_lastspoke,id)
		if !$item_exists(SPEAKER_ID,item_gaxe_handle)
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	saytext Wow, this here was a real piece of work, before it broke.
	callevent 4.0 say_axe2
}

{ say_axe2

	saytext "Don't make them like this anymore, that's fer sure."
	callevent 4.0 say_axe3
}

{ say_axe3
	saytext "I can still see the bits of the blade end, I can tell it was made of Loreldian ore."
	callevent 4.0 say_axe4
}

{ say_axe4
	saytext "That's where the thing gets its magic, and why it only harms the unholy."
	callevent 4.0 say_axe5
}

{ say_axe5
	saytext "Most such cursed things are imbued with the power of The Fallen, either directly, or indirectly."
	callevent 4.0 say_axe6
}

{ say_axe6

	saytext "This stuff don't grow on trees, ya know. Even if it did, it's very difficult to work with."
	callevent 4.0 say_axe7
}

{ say_axe7

	saytext "But if ya find some, bring it back to me, together with this handle, and maybe I can fix ya up."
}

{ say_ore
	setvard ORE_TARGET PARAM1
	saytext "Wow, you actually found some of the stuff! Hmm... Let's see here..."
	callevent 4.0 say_ore2
}

{ say_ore2
	saytext "Okay, I think I can do this, but it'll cost ya."
	callevent 4.0 say_ore3
}

{ say_ore3
	saytext "I'll need 10,000 gold. Plus the hilt, of course."
	callevent 2.0 say_ore4
	setvard OFFER_SET 1
}

{ say_ore4
	saytext "Just tell me no, if yer not interested."
}

{ say_gotgold
	setvard GOLD_TARGET PARAM1
	saytext Alright, just give me a few moments here.
	playanim critical attack
	callevent 1.0 smith_sound
	callevent 2.0 say_gotgold2
}

{ say_gotgold2
	playanim critical attack
	callevent 1.0 smith_sound
	callevent 2.0 say_gotgold3
}

{ say_gotgold3
	playanim critical attack2
	callevent 1.0 smith_sound2
	callevent 1.0 say_gotgold4
}

{ say_gotgold4
	saytext "There ya go! Good as new... Sorta..."
	offer GOLD_TARGET axes_golden
	setvard GAVE_AXE2 1
	callevent 3.0 say_gotgold5
}

{ say_gotgold5
	saytext "Now, if you want to make sure it won't break AGAIN, well..."
	callevent 4.0 say_gotgold6
}

{ say_gotgold6
	saytext "To be honest, you'd need a better smith than I. But I happen to know one- Thordac!"
	callevent 4.0 say_gotgold7
}

{ say_gotgold7
	saytext "Go over to Deralia, give him yer axe and my instructions here, telling him what ya need."
	offer GOLD_TARGET item_roland_letter
	callevent 4.0 say_gotgold8
}

{ say_gotgold8
	saytext "I warn ye though, he'll charge you up the nose for what you'll need done!"
}

{ say_failed_pay

	if OFFER_SET
	if !GAVE_AXE2
	setvard OFFER_SET 0
	saytext "Well, if ya ain't got the money now, I can wait."
	//ffer ORE_TARGET item_ore_lorel
}

{ smith_sound
	playrandomsound 0 10 debris/metal3.wav debris/metal2.wav debris/metal1.wav
}

{ smith_sound2
	playsound 0 10 debris/bustmetal2.wav
}