{
	const STUN_BURST_MP_DRAIN 20
	const SPECIAL1_OVERRIDE 1
	const CUSTOM_REGISTER_CHARGE1 1

	const ANIM_HANDS_DOWN 3
	const ANIM_LIFT1 2
	const ANIM_LOWER 3
	const ANIM_IDLE1 1
	const ANIM_IDLE_TOTAL 1
	const ANIM_ATTACK1 4
	const ANIM_SHEATH 3
	const ANIM_SLAM 14
	const MODEL_VIEW viewmodels/v_martialarts.mdl
	const MODEL_VIEW_IDX 8

	const MELEE_RANGE 80
	const MELEE_DMG_DELAY 0.3
	const MELEE_ATK_DURATION 0.9
	const MELEE_ENERGY 1
	const MELEE_DMG 200
	const MELEE_DMG_RANGE 0
	const MELEE_DMG_TYPE blunt
	const MELEE_ACCURACY 70%
	const MELEE_STAT martialarts
	const MELEE_VIEWANIM_ATK 4

	const MELEE_SOUND weapons/swingsmall.wav
	const MELEE_SOUND_DELAY 0.3
	const MELEE_PARRY_CHANCE 30%

	const PLAYERANIM_AIM		fists

	const MODEL_HANDS weapons/p_weapons3.mdl
	const MODEL_WORLD weapons/p_weapons3.mdl
	const MODEL_BODY_OFS 114 //%% [needs p_model, if not world model]

	const SOUND_SWIPE weapons/swingsmall.wav //%% large wing sound
	const SOUND_HITWALL1 weapons/cbar_hitbod1.wav //%% hitstone sound
	const SOUND_HITWALL2 weapons/cbar_hitbod2.wav //%% hitstone sound
	const SOUND_SWING weapons/swingsmall.wav //%% large wing sound
	const SOUND_STRIKE monsters/troll/trollattack.wav
	const SOUND_BOOM magic/boom.wav
}

#include items/base_melee

{  weapon_spawn

	name 	Stone Fists
	desc 	Fists of Stone
	weight 	0
	size 	0
	value	0
	sethand 	both
	sethudsprite hand gauntlets
	sethudsprite trade gauntlets
}

{ register_charge1

	//thothie made seperate so can override

   //Default charge up attack - Double damage for holding to 100%
   //============================================================

   local reg.attack.type		strike-land
   local reg.attack.keys		+attack1
   local reg.attack.range		MELEE_RANGE
   local reg.attack.dmg			MELEE_DMG
   local reg.attack.dmg.range		MELEE_DMG_RANGE
   local reg.attack.dmg.type		MELEE_DMG_TYPE
   local reg.attack.energydrain		MELEE_ENERGY
   local reg.attack.stat		MELEE_STAT
   local reg.attack.hitchance		MELEE_ACCURACY
   local reg.attack.priority		0
   local reg.attack.delay.strike	MELEE_DMG_DELAY
   local reg.attack.delay.end		MELEE_ATK_DURATION
   local reg.attack.ofs.startpos	MELEE_STARTPOS
   local reg.attack.ofs.aimang		MELEE_AIMANGLE
   local reg.attack.callback		melee
   local reg.attack.noise		MELEE_NOISE

   local reg.attack.priority	1
   local reg.attack.keys	-attack1
   local reg.attack.callback	slam
   multiply reg.attack.dmg	4
   local reg.attack.chargeamt	100%
   local reg.attack.reqskill	20

   registerattack
}

{ weapon_deploy

	setvard GAME_PVP game.pvp

	playviewanim  ANIM_HANDS_DOWN
}

{ remove_fists
	sethand both
	deleteent ent_me
}

{ melee_strike //PARAM1: "none"/"npc"/"world"   2: Attack end Position   3: Entity hit (if any)

	if game.serverside

	if PARAM1 equals npc
	local HIT_TARG PARAM3
	if ( $get(HIT_TARG,isalive) )
	{
		if $get(HIT_TARG,height) < 200
		if $get(HIT_TARG,relationship,ent_me) equals enemy
		if ( $get(HIT_TARG,isplayer) )
		{
			if !GAME_PVP
			local EXIT_SUB 1
		}
		if !EXIT_SUB
		local RND_LR $randf(-30,30)
		local OWNER_YAW $get(ent_owner,angles.yaw)
		local OUT_VEL $get(ent_owner,velocity)
		vectoradd OUT_VEL $relvel($vec(0,OWNER_YAW,0),$vec(RND_LR,200,50))	
		addvelocity HIT_TARG OUT_VEL
		if ( $rand(1,3) == 1 ) applyeffect HIT_TARG effects/debuff_stun 5.0 $get(ent_me,id)	
		//clientevent new all effects/sfx_hit_shield PARAM2 $get(ent_owner,angles.yaw) (255,255,255) 2.0 2.0
		playsound 0 10 SOUND_STRIKE
	}
}

{ slam_start
	playviewanim ANIM_SLAM
}

{ slam_strike //PARAM1: "none"/"npc"/"world"   2: Attack end Position   3: Entity hit (if any)
	if game.serverside

	if ( PARAM1 equals world )
	{
		local ATTACK_POS PARAM2
		local ATTACK_Z $vec.z(ATTACK_POS)
		local GROUND_Z $get_ground_height(ATTACK_POS)
		if ATTACK_Z == GROUND_Z
		callevent do_stun_burst ATTACK_POS
	}


	if PARAM1 equals npc
	local HIT_TARG PARAM3
	if ( $get(HIT_TARG,isalive) )
	{
		if $get(HIT_TARG,height) < 200
		if $get(HIT_TARG,relationship,ent_me) equals enemy
		if ( $get(HIT_TARG,isplayer) )
		{
			if !GAME_PVP
			local EXIT_SUB 1
		}
		if !EXIT_SUB

		clientevent new all effects/sfx_hit_shield PARAM2 $get(ent_owner,angles.yaw) (255,255,255) 2.0 2.0
		playsound 2 10 SOUND_STRIKE
		playsound 1 5 SOUND_BOOM

		local RND_CHECK $rand(1500,10000)
		if RND_CHECK >= $get(HIT_TARG,hp)
		local RND_LR $randf(-30,30)
		local OWNER_YAW $get(ent_owner,angles.yaw)
		local OUT_VEL $get(ent_owner,velocity)
		vectoradd OUT_VEL $relvel($vec(0,OWNER_YAW,0),$vec(RND_LR,800,110))	
		addvelocity HIT_TARG OUT_VEL
		applyeffect HIT_TARG effects/debuff_stun 5.0 $get(ent_me,id)	

	}
}

{ do_stun_burst //<pos>
	if ( $get(ent_owner,mp) < STUN_BURST_MP_DRAIN )
	{
		dplayermessage ent_owner Fists of Stone: Insufficient mana for Stun Burst.
	}
	if $get(ent_owner,mp) >= STUN_BURST_MP_DRAIN

	setvard STUN_POS PARAM1

	givemp ent_owner $neg(STUN_BURST_MP_DRAIN)

	xdodamage STUN_POS 256 0 ent_owner ent_owner MELEE_STAT blunt
	clientevent new all effects/sfx_stun_burst STUN_POS 256 0
	callexternal ent_owner ext_sphere_token enemy 256 STUN_POS
	setvard STUN_TARGS $get(ent_owner,scriptvar,'PLR_SCAN_TOKEN')
	if STUN_TARGS isnot none
	calleventloop $get_token_amt(STUN_TARGS) stun_targets
}

{ stun_targets
	local CUR_TARG $get_token(STUN_TARGS,game.script.iteration)
	if ( $get(CUR_TARG,isplayer) )
	{
		if !GAME_PVP
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	applyeffect CUR_TARG effects/debuff_stun 10.0 $get(ent_me,id)
	local TARG_ORG $get(CUR_TARG,origin)
	local TARG_ANG $angles(STUN_POS,TARG_ORG)
	setvelocity CUR_TARG $relvel($vec(0,TARG_ANG,0),$vec(10,500,0))
}

{
repeatdelay 1.0

	if game.serverside
	if ( $get(ent_owner,hp) < $get(ent_owner,maxhp) ) givehp ent_owner 5
	if ( $get(ent_owner,mp) < $get(ent_owner,maxmp) ) givehp ent_owner 1

	if !$get(ent_owner,isalive)
	//callevent remove_fists
}

{ game_drop
	//callevent remove_fists
}

{ game_putinpack
	//callevent remove_fists
}