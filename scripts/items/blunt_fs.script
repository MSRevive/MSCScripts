{
	const BASE_LEVEL_REQ     20
	const FIRE_BURST_MP 30

  //const MODEL_VIEW        weapons/blunt/calrainmace_rview.mdl
	const MODEL_VIEW        viewmodels/v_1hblunts.mdl
	const MODEL_VIEW_IDX 8
  const SOUND_SWIPE       weapons/swingsmall.wav

const MODEL_WORLD      		weapons/p_weapons3.mdl
const MODEL_BODY_OFS 90
  const ANIM_PREFIX  standard

  //Used in base_melee
  //------------------
  const MELEE_RANGE  60
  const MELEE_DMG_DELAY  0.6
  const MELEE_ATK_DURATION 1.1
  const MELEE_ENERGY  8
  const MELEE_DMG  250
  const MELEE_DMG_RANGE      140
  const MELEE_DMG_TYPE  fire
  const MELEE_ACCURACY  80%
  const MELEE_STAT  bluntarms

  const MELEE_SOUND      SOUND_SWIPE
  const MELEE_SOUND_DELAY      MELEE_DMG_DELAY
  const MELEE_PARRY_AUGMENT     0%

}

#include items/blunt_base_onehanded

{  weapon_spawn

   name  a|Fire Star
   desc  A morning star forged with elemental fire
   weight  35
   size  6
   value 2000
   sethudsprite hand mace
   sethudsprite trade 188
}

{ game_dodamage //1: Attack Hit (1/0)  2: Entity Attack hit  3:  Start Position  4:  End Position

	if PARAM1

	if $rand(1,5) == 1

	local BURN_DAMAGE $get(ent_owner,skill.spellcasting.fire)
	divide BURN_DAMAGE 2
	add BURN_DAMAGE $randf(1,3)
	if ( BURN_DAMAGE < 5 ) local BURN_DAMAGE 5

	applyeffect PARAM2 effects/dot_fire 5 $get(ent_owner,id) BURN_DAMAGE bluntarms
}

{ special_02_damaged_other

	local BURN_DAMAGE $get(ent_owner,skill.spellcasting.fire)
	divide BURN_DAMAGE 2
	add BURN_DAMAGE $randf(1,3)
	applyeffect PARAM1 effects/dot_fire 5 $get(ent_owner,id) BURN_DAMAGE bluntarms
}

{ special_02_strike
	local ATTACK_END_POS PARAM2
	if $vec.z(ATTACK_END_POS) equals $get_ground_height(ATTACK_END_POS)
	if ( $get(ent_owner,mp) < FIRE_BURST_MP ) local NO_GO_MSG "Fire Star: Insufficient Mana for Flame Burst"
	if ( $get(ent_owner,skill.spellcasting.fire) < 20 ) local NO_GO_MSG "Fire Star: Insufficient Fire Skill for Flame Burst"
	if ( NO_GO_MSG isnot 'NO_GO_MSG' ) dplayermessage ent_owner NO_GO_MSG
	if NO_GO_MSG equals 'NO_GO_MSG'
	playsound 0 10 ambience/steamburst1.wav
	givemp ent_owner $neg(FIRE_BURST_MP)
	clientevent new all effects/sfx_fire_burst ATTACK_END_POS 128 1 (255,0,0)
	callexternal ent_owner ext_sphere_token enemy 128 ATTACK_END_POS
	setvard FIRE_BURST_TARGS $get(ent_owner,scriptvar,'PLR_SCAN_TOKEN')
	dbg FIRE_BURST_TARGS
	setvard BURST_POS ATTACK_END_POS
	if FIRE_BURST_TARGS isnot none
	setvard BURST_DOT_BURN $get(ent_owner,skill.spellcasting.fire)
	multiply BURST_DOT_BURN 0.5
	calleventloop $get_token_amt(FIRE_BURST_TARGS) fire_burst_affect_targs	
}

{ fire_burst_affect_targs
	local CUR_TARG $get_token(FIRE_BURST_TARGS,game.script.iteration)

	if ( $get(CUR_TARG,isplayer) )
	{
		if !GAME_PVP
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	local TARG_ORG $get(CUR_TARG,origin)
	local TRACE_START BURST_POS
	local TRACE_END TARG_ORG
	local TRACE_LINE $get_traceline(TRACE_START,TRACE_END,worldonly)
	if TRACE_LINE equals TRACE_END

	if ( $get(CUR_TARG,hp) < 1500 )
	{
		local TARG_ORG $get(CUR_TARG,origin)
		local MY_ORG $get(ent_owner,origin)
		local TARG_ANG $angles(MY_ORG,TARG_ORG)
		local NEW_YAW TARG_ANG
		addvelocity CUR_TARG $relvel($vec(0,NEW_YAW,0),$vec(0,800,0))
	}

	applyeffect CUR_TARG effects/dot_fire 5.0 $get(ent_owner,id) BURST_DOT_BURN bluntarms
}