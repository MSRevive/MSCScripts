{
	const BASE_LEVEL_REQ 35

	const STUN_BURST_MP 40

	const MELEE_RANGE 90
	const MELEE_DMG_DELAY  0.5
	const MELEE_ATK_DURATION 1.1
	const DEMON_DMG_DELAY  0.25
	const DEMON_ATK_DURATION 0.7
	const MELEE_ENERGY 2
	const MELEE_DMG 600
	const MELEE_DMG_RANGE 40
	const MELEE_ACCURACY 80%
	const MELEE_PARRY_AUGMENT 20%
	const MELEE_DMG_TYPE blunt

	const MODEL_VIEW viewmodels/v_2hblunts.mdl
	const MODEL_VIEW_IDX 10

	const MODEL_WORLD weapons/p_weapons4.mdl
	const MODEL_BODY_OFS 22
	const ANIM_PREFIX standard
}

#include items/blunt_base_twohanded

{  weapon_spawn

   name  The|Earth Breaker
   desc  A gigantic hammer of ground breaking proportions
   weight  80
   size  10
   value 7000
   sethudsprite hand hammer
   sethudsprite trade 51
}

{ game_deploy
	setvard GAME_PVP game.pvp
}

{ special_02_strike
	local ATTACK_END_POS PARAM2
	if $vec.z(ATTACK_END_POS) equals $get_ground_height(ATTACK_END_POS)
	if ( $get(ent_owner,mp) < STUN_BURST_MP )
	{
		dplayermessage ent_owner "Earth Breaker: Insufficient Mana for Stun Burst"
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	givemp ent_owner $neg(STUN_BURST_MP)
	clientevent new all effects/sfx_stun_burst ATTACK_END_POS 250 1 (255,0,0)
	setvard BURST_POS ATTACK_END_POS
	vectoradd BURST_POS 32
	callexternal ent_owner ext_sphere_token enemy 128 BURST_POS
	setvard BURST_TARGS $get(ent_owner,scriptvar,'PLR_SCAN_TOKEN')
	setvard BURST_POS ATTACK_END_POS
	if BURST_TARGS isnot none
	setvard BURST_STUN_DURATION $get(ent_owner,skill.bluntarms)
	setvard BURST_DMG BURST_STUN_DURATION
	multiply BURST_STUN_DURATION 0.5
	multiply BURST_DMG 4.0
	dbg special_02_strike stundur BURST_STUN_DURATION
	calleventloop $get_token_amt(BURST_TARGS) burst_affect_targs
	//callevent 0.1 aoe_damage
}

//{ aoe_damage
//	local DMG_AMT BURST_STUN_DURATION
//	multiply DMG_AMT 2
//	xdodamage BURST_POS 256 DMG_AMT 0.1 ent_owner ent_me bluntarms blunt
//}

{ burst_affect_targs
	local CUR_TARG $get_token(BURST_TARGS,game.script.iteration)
	if ( $get(CUR_TARG,isplayer) )
	{
		if !GAME_PVP
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	applyeffect CUR_TARG effects/effect_stun BURST_STUN_DURATION 0 1 $get(ent_owner,id)
	local TARG_ORG $get(CUR_TARG,origin)
	local MY_ORG BURST_POS
	local NEW_YAW $angles(MY_ORG,TARG_ORG)
	addvelocity CUR_TARG $relvel($vec(0,NEW_YAW,0),$vec(0,800,110))
	xdodamage CUR_TARG 256 BURST_DMG 100% ent_owner ent_me bluntarms blunt
}