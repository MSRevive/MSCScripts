//Ice Shield
//Level req: 5
//50% damage reduction

{
	const NO_REGISTER 1
	const SOUND_SHOOT      	magic/cast.wav

	const MELEE_ATK_DURATION 	0.65

	const MANA_COST	50 //special internal handling
	const ICESHIELD_FORMULA 	0.5 //50% damage reduction

	const SPELL_SCRIPT effects/effect_iceshield
}

#include items/magic_hand_base

{ spell_spawn

   name "Ice Shield"
   desc "Provides 50% damage reduction for you or allies, for a time."
   
   setvard LAST_ATTACK $math(add,game.time,MELEE_ATK_DURATION)
}

{ game_attack1

	local TIME_DIFF game.time
	subtract TIME_DIFF LAST_ATTACK
	
	if TIME_DIFF > MELEE_ATK_DURATION

	setvard LAST_ATTACK game.time

	playviewanim ANIM_CAST
	playowneranim critical PLAYERANIM_PREPARE squatwalk1

	if ( $get(ent_owner,mp) < MANA_COST ) dplayermessage ent_owner "Insufficient mana."
	if $get(ent_owner,mp) >= MANA_COST

	local SPELL_TARGET $get(ent_owner,target)

	//is this target valid, if not, make owner target
	if ( !$get(SPELL_TARGET,isalive) ) local SPELL_TARGET $get(ent_owner,id)
	if ( !$get(SPELL_TARGET,isplayer) )
	{
		if $get(ent_owner,relationship,SPELL_TARGET) isnot ally
		local SPELL_TARGET $get(ent_owner,id)
	}

	local FINAL_DURATION 150
	if ( $get(SPELL_TARGET,id) isnot $get(ent_owner,id) ) local FINAL_DURATION 412

	givemp $neg(MANA_COST)
	callexternal ent_owner mana_drain //update mana on player hud readout, if using hud feedback
	
	local ALREADY_SHIELDED $get(SPELL_TARGET,haseffect,effect_iceshield)
	if ( ALREADY_SHIELDED ) //If target is already shielded, refresh their duration.
	{
		callexternal SPELL_TARGET ext_refresh_ice_shield FINAL_DURATION $get(ent_owner,id) //<duration> <shielder>
	}
	else
	{
		applyeffect SPELL_TARGET SPELL_SCRIPT $get(ent_owner,id) FINAL_DURATION ICESHIELD_FORMULA //<duration> <damage_reduction> <shielder>
	}
}

{ fake_precache
	svplaysound 0 0 debris/bustglass2.wav //For effect_iceshield
}