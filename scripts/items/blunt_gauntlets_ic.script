{
	const BASE_LEVEL_REQ 25

	const CUSTOM_CLAWS 1

	const FIRE_WAVE_MP 20

	const MELEE_DMG  200

	const DEMON_STRIKE_RATIO 6.0

	const MODEL_VIEW viewmodels/v_martialarts_claws.mdl
	const MODEL_VIEW_IDX 2
	const MODEL_HANDS weapons/p_weapons3.mdl
	const MODEL_WORLD weapons/p_weapons3.mdl
	const MODEL_BODY_OFS 69

	const ANIM_PREFIX standard
}

#include items/blunt_gauntlets_demon

{  weapon_spawn

   name  Infernal Claws
   desc  Demonic claws imbued with flame
   weight  3
   size  1
   value 6000
   sethand  both
   sethudsprite hand 143
   sethudsprite trade 143

	callevent register_demon_toggle
	setvard DEMON_MODE 0
}

{ game_deploy
	setvard GAME_PVP game.pvp
}

{ demon_strike

	if game.serverside

	if PARAM1 equals world

	if ( $get(ent_owner,skill.spellcasting.fire) < 25 )
	{
		dplayermessage ent_owner Infernal Claws: Insufficient skill for Fire Wave.
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( $get(ent_owner,mp) < FIRE_WAVE_MP )
	{
		dplayermessage ent_owner Infernal Claws: Insufficient mana for Fire Wave.
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	givemp ent_owner $neg(FIRE_WAVE_MP)

	local ATTACK_END_POS PARAM2
	//vectorset ATTACK_END_POS z $get_ground_height(ATTACK_END_POS)
	callevent do_fire_wave ATTACK_END_POS
}

{ do_fire_wave
	playsound 0 7 magic/flame_loop_start.wav

	setvard FIRE_WAVE_START_POS PARAM1
	setvard FIRE_WAVE_YAW $get(ent_owner,angles.yaw)
	clientevent new all effects/sfx_fire_wave FIRE_WAVE_START_POS FIRE_WAVE_YAW

	setvard OWNER_ORG FIRE_WAVE_START_POS
	setvard OWNER_ANG $vec(0,FIRE_WAVE_YAW,0)

	setvard FIRE_WAVE_SCAN_POS FIRE_WAVE_START_POS
	vectoradd FIRE_WAVE_SCAN_POS $relpos($vec(0,FIRE_WAVE_YAW,0),$vec(0,128,32))

	callevent 1.25 fire_wave2 //simulates time flames remain better

	callexternal ent_owner ext_sphere_token enemy 768 FIRE_WAVE_SCAN_POS
	setvard FIRE_WAVE_TARGS $get(ent_owner,scriptvar,'PLR_SCAN_TOKEN')

	if FIRE_WAVE_TARGS isnot none
	calleventloop $get_token_amt(FIRE_WAVE_TARGS) fire_wave_affect_targs
}

{ fire_wave2
	callexternal ent_owner ext_sphere_token enemy 1024 FIRE_WAVE_SCAN_POS
	setvard FIRE_WAVE_TARGS $get(ent_owner,scriptvar,'PLR_SCAN_TOKEN')

	if FIRE_WAVE_TARGS isnot none
	calleventloop $get_token_amt(FIRE_WAVE_TARGS) fire_wave_affect_targs

}

{ fire_wave_affect_targs

	local CUR_TARG $get_token(FIRE_WAVE_TARGS,game.script.iteration)

	if ( $get(CUR_TARG,isplayer) )
	{
		if !GAME_PVP
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	dbg fire_wave_affect_targs $within_cone2D(TARG_ORG,OWNER_ORG,OWNER_ANG,120) $get(CUR_TARG,name)

	local TARG_ORG $get(CUR_TARG,origin)

	if $within_cone2D(TARG_ORG,OWNER_ORG,OWNER_ANG,120)

	local TRACE_START FIRE_WAVE_START_POS
	local TRACE_END TARG_ORG
	local TRACE_LINE $get_traceline(TRACE_START,TRACE_END,worldonly)
	if TRACE_LINE equals TRACE_END

	local DOT_BURN $get(ent_owner,skill.spellcasting.fire)
	applyeffect CUR_TARG effects/effect_burn 5.0 $get(ent_owner,id) DOT_BURN
}

{ game_dodamage
	if $rand(1,3) == 1

	if PARAM1
	if ( $get(PARAM2,isplayer) )
	{
		if !GAME_PVP
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	local DOT_BURN $get(ent_owner,skill.spellcasting.fire)
	multiply DOT_BURN 0.5
	applyeffect PARAM2 effects/effect_burn 5.0 $get(ent_owner,id) DOT_BURN
}