{
	const MODEL_HANDS      	none
	const MODEL_WORLD      	weapons/projectiles.mdl
	const MODEL_BODY_OFS 70
	const ARROW_BODY_OFS 70
	const ARROW_STICK_DURATION 0
	const ARROW_EXPIRE_DELAY 0
	const SOUND_HITWALL1		weapons/xbow_hit1.wav
	const SOUND_HITWALL2		weapons/xbow_hit1.wav
	const ARROW_SOLIDIFY_ON_WALL	0  	//base_arrow: Turns solid when a wall is hit
	const ARROW_BREAK_CHANCE	100%	//base_arrow: Chance of breaking upon hit
	const ITEM_NAME		watermana
	const PROJ_DAMAGE_TYPE	acid
	const PROJ_DAMAGESTAT		spellcasting.ice //this does not determin xp stat
	const PROJ_ANIM_IDLE		idle_icebolt
	const PROJ_MOTIONBLUR 0

	const PROJ_DAMAGE		0 //via tossprojectile only
	const PROJ_AOE_RANGE		0
	const PROJ_AOE_FALLOFF	0
	const PROJ_STICK_DURATION	0  //Don't stick in enemies
	const PROJ_COLLIDE	1
	
	const PROJ_CLOUD_DURATION 20.0
}

#include items/proj_arrow_base

{ game_precache
	precachefile monsters/summon/affliction_lance
}

{ arrow_spawn

	name 		a|Affliction Lance
	desc 		You have been afflicted by this lance
	weight 		0.1
	size 		1
	value		0
	gravity		0.8
	groupable    	25
}

{ game_tossprojectile
	setvard GAME_PVP game.pvp
	clientevent new all effects/sfx_follow_glow_cl $get(ent_me,index) (0,255,0) 128 1.5
}

{ game_projectile_hitnpc

	setvard HIT_NPC 1

	if ( $get(PARAM1,isplayer) )
	{
		if !GAME_PVP
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if $get(PARAM1,relationship,ent_expowner) equals enemy

	local L_DMG 800
	local OWNER_SKILL_RATIO $get(ent_expowner,skill.polearms)
	multiply OWNER_SKILL_RATIO 0.01
	multiply L_DMG OWNER_SKILL_RATIO

	local PUSH_STR 800

	local TARG_ORG $get(PARAM1,origin)
	local MY_ORG $get(ent_expowner,origin)
	local TARG_ANG $angles(MY_ORG,TARG_ORG)
	local NEW_YAW TARG_ANG
	setvelocity PARAM1 $relvel($vec(0,NEW_YAW,0),$vec(0,PUSH_STR,0))

	local POISON_DOT $get(ent_expowner,skill.spellcasting.affliction)
	multiply POISON_DOT 0.5
	applyeffect PARAM1 effects/effect_acid 5.0 $get(ent_expowner,id) POISON_DOT
	

	if $get(PARAM1,isalive)
	xdodamage PARAM1 direct L_DMG 100% ent_expowner ent_me polearms acid
}

{ game_projectile_landed

	if !HIT_NPC
	if !DID_SPAWN_CHECK
	setvard DID_SPAWN_CHECK 1

	local MY_PITCH $get(ent_me,angles.pitch)

	//dbg game_projectile_landed MY_PITCH

	if ( MY_PITCH > 175 )
	{
		if MY_PITCH < 360
		local DO_PROJ 1
	}
	if DO_PROJ

	if ( $get(ent_expowner,mp) < 20 ) dplayermessage ent_expowner Insuffcient mana for Afflictor.

	if $get(ent_expowner,mp) >= 20
	givemp ent_expowner -20

	local MY_ORG $get(ent_me,origin)
	local GROUND_Z $get_ground_height(MY_ORG)
	local MY_Z $vec.z(MY_ORG)
	//dbg game_projectile_landed MY_ORG vs GROUND_Z
	add GROUND_Z 64

	//somewhere near the ground
	if MY_Z < GROUND_Z
	subtract GROUND_Z 128
	if MY_Z > GROUND_Z

	local MY_ORG $get(ent_me,origin)
	dbg game_projectile_landed MY_ORG
	local GROUND_Z $get_ground_height(MY_ORG)
	vectorset MY_ORG z GROUND_Z
	
	//ent_expowner
	//
	//scriptflags <target> add <name> <type> [value:1] [expiretime:-1] [expiremsg:none]
	//$get_scriptflag(<target>,<type>,type_exists) - returns 1 if <type> found
	
	local L_FX_ID $get_scriptflag(ent_expowner,pole_a,name_value)
	if ( L_FX_ID isnot 'none' )
	{
		callexternal L_FX_ID transfer_location MY_ORG
	}
	else
	{
		createnpc monsters/summon/affliction_lance MY_ORG $get(ent_expowner,id) MY_ORG $get(ent_me,angles) PROJ_CLOUD_DURATION //<ownerId> <origin> <angles> <duration>
	}
}