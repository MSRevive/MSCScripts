//Thothie - Base for new spell scrolls

//VARS:
//	const SKILL_REQ <skillname>
//	const SPELL_LEVEL_REQ <#>

{
	const MODEL_VIEW_BOOK		misc/p_misc.mdl
	const MODEL_HANDS      	misc/p_misc.mdl
	const MODEL_WORLD     	misc/p_misc.mdl
	const MODEL_BODY_OFS		6
	const ANIM_PREFIX		evilbook

	//h4x to get rid of aim_ errors
	const AIM_ANIM blunt
	const ANIM_AIM blunt
	const PLAYERANIM_AIM blunt

	const SKILL_REQ skill.spellcasting
}

#include items/base_item_extras

{ game_spawn

	setanimext	PLAYERANIM_AIM //h4x to get rid of aim_ errors
	setidleanim evilbook_floor_idle
	sethand both

	weight 	0
	size 	0
	sethudsprite hand item
	sethudsprite trade letter

	callevent scroll_spawn

	if game.serverside
	setvard SCROLL_TIME game.time
	add SCROLL_TIME 3
}

{ game_pickup

	setvar JUST_GOT 1	//stop from activating on pick-up
}

{ game_deploy

	setviewmodel	MODEL_VIEW_BOOK
	setmodel	MODEL_HANDS
	local		L_SUBMODEL MODEL_BODY_OFS
	add		L_SUBMODEL game.item.hand_index
	setmodelbody	0 L_SUBMODEL
}

{ game_fall

	setmodel MODEL_WORLD

	local		L_SUBMODEL MODEL_BODY_OFS
	add		L_SUBMODEL 2
	setmodelbody	0 L_SUBMODEL

	local		L_ANIM ANIM_PREFIX
	stradd		L_ANIM _floor_idle
	playanim	L_ANIM
}

{ game_attack1

	if ( SCROLL_TIME > game.time )
	{
		//don't allow use of scroll for 3 seconds after pulling from chest
		//this is more to stop accidental casting than anything else
		playermessage "The magic needs a moment to recharge."
		exitevent
	}

	if ( $get(ent_owner,scriptvar,PLR_NEXT_SCROLL) > game.time )
	{
		//only can use one scroll every 5 seconds (or so)
		//this is to stop 'chaining' of scrolls, resulting in server crash due to multiple firewalls or volcanos
		playermessage "The magic needs a moment to recharge."
		exitevent
	}

	if ( $get(ent_owner,numitems) >= G_MAX_ITEMS )
	{
		dplayermessage ent_owner Cannot activate scrolls when inventory is full.
		exitevent
	}

	callexternal ent_owner ext_set_next_scroll

	local OWNER_SKILL $get(ent_owner,SKILL_REQ)

	if ( OWNER_SKILL < SPELL_LEVEL_REQ ) playermessage "You lack the arcane skills to control this scroll's magic."
	if ( OWNER_SKILL >= SPELL_LEVEL_REQ ) callevent grant_spell
}

{ grant_spell

	local SPELL_SPAWNER_LOC $relpos(0,0,SPELL_MAKER_HEIGHT)

	if ( SPELL_MAKER_SCRIPT isnot 'SPELL_MAKER_SCRIPT' ) createnpc SPELL_MAKER_SCRIPT SPELL_SPAWNER_LOC $get(ent_owner,id) SPELL_TO_CREATE THIS_ITEM SPELL_MAKER_HEIGHT $get(ent_me,id)
	//let spell maker choose when to remove scroll
	//callevent 0.1 clear_hands
}

{ clear_hands

	callevent game_wear	//This doesn't do anything currently
	deleteme //Clear item to make way for spell (otherwise spell drops to floor)
}
