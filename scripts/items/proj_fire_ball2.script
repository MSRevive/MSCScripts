//Thothie - fireball from Firedart
//fireball projectile

{
	const MODEL_HANDS      		none
	//may make own over-sized model via scale QC
	const MODEL_WORLD      		weapons/projectiles.mdl
	const SOUND_BURN		items/torch1.wav
	const SPRITE_FIRE		3dmflaora.spr //fireball1.spr
	const SPRITE_BURN 		fire1_fixed.spr
	const ITEM_NAME			firemana
	const PROJ_DAMAGE_TYPE		fire
	const PROJ_DAMAGESTAT		spellcasting

	const ARROW_BODY_OFS		4

	const PROJ_DAMAGE		$rand(400,500)
	const PROJ_AOE_RANGE		250
	const PROJ_AOE_FALLOFF		0
	const PROJ_STICK_DURATION	0  //Don't stick in enemies
	const PROJ_SOLIDIFY_ON_WALL	0  //Don't go solid on wall hit
	const PROJ_COLLIDEHITBOX	64 //entirely experimental

	const SOUND_START	magic/volcano_start.wav
	const SOUND_LOOP	magic/volcano_loop.wav

	precache SPRITE_BURN

	const SCRIPT_1			items/proj_fire_dart_cl
	precachefile			SCRIPT_1
	precache rjet1.spr
	precache MODEL_WORLD
}

#include items/proj_arrow_base

{ arrow_spawn

	name 		a|Meteor
	weight 		500
	size 		10	//attempting adding size for larger collision box
	value		5
	gravity		0.0
   	setmonsterclip 0

	setmodelbody 0 0

	sethudsprite hand arrows
	sethudsprite trade ITEM_NAME
	sethand any
	effect glow ent_me (255,75,0) 128 5 5

	callevent 0.1 pulsate_loop
}

{ pulsate_loop
	setprop ent_me rendermode 5
	local PULSE_VALUE $rand(100,255)
	setprop ent_me renderamt PULSE_VALUE
	callevent 0.1 pulsate_loop
}

{  game_tossprojectile

	playsound 0 10 SOUND_START

	setvard SOUND_ON 1
	callevent 1.0 loop_sound

	clientevent new all_in_sight SCRIPT_1 $get(ent_me,index)
   	setvard FB_SCRIPT_INDEX game.script.last_sent_id
}

{ loop_sound
	if SOUND_ON
	playsound CHAN_BODY 7 SOUND_LOOP
	callevent 6.0 loop_sound
}

{  projectile_landed
	
	setvard SOUND_ON 0

	playsound const.snd.body const.snd.fullvol fire.wav

	effect screenshake $relpos(0,0,0) 50 20 DURATION 256
	effect tempent trail rjet1.spr $relpos(0,0,0) $relpos(0,0,10) 20 2 9 10 20
	clientevent remove all FB_SCRIPT_INDEX
}

{  game_hitnpc

	setvard MY_OWNER $get(ent_expowner,id)
	if ( LOCK_BURN_DAMAGE equals 'LOCK_BURN_DAMAGE' )
	{
		local BURN_DAMAGE $get(ent_expowner,skill.spellcasting.fire)
		multiply BURN_DAMAGE 0.4
	}
	if ( LOCK_BURN_DAMAGE isnot 'LOCK_BURN_DAMAGE' ) local BURN_DAMAGE LOCK_BURN_DAMAGE
	//dbg BurnDamage BURN_DAMAGE

	applyeffect ent_laststruckbyme effects/dot_fire 20 MY_OWNER BURN_DAMAGE spellcasting.fire
}

{ [override] lighten //param1 = fire dmg

	setvard LOCK_BURN_DAMAGE PARAM1
}
