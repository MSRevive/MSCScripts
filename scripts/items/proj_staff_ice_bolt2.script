//tempt at guided
{
	const MODEL_HANDS      	none
	const MODEL_WORLD      	weapons/projectiles.mdl
	const MODEL_BODY_OFS 7
	const ARROW_BODY_OFS 7
	const SOUND_HITWALL1		weapons/axemetal1.wav
	const SOUND_HITWALL2		weapons/axemetal1.wav
	const SOUND_BURN		magic/ice_powerup.wav
	const ARROW_SOLIDIFY_ON_WALL	0  	//base_arrow: Turns solid when a wall is hit
	const ARROW_BREAK_CHANCE	100%	//base_arrow: Chance of breaking upon hit
	const ITEM_NAME		watermana
	const PROJ_DAMAGE_TYPE	cold
	const PROJ_DAMAGESTAT		spellcasting.ice //this does not determin xp stat
	const PROJ_ANIM_IDLE		idle_icebolt
	const PROJ_MOTIONBLUR 0

	const PROJ_DAMAGE		0
	const PROJ_STICK_DURATION	0  //Don't stick in enemies
	const PROJ_COLLIDEHITBOX	0
}

#include items/proj_arrow_base

{ arrow_spawn

	name 		a|Guided Ice Bolt
	desc 		A sharp bolt of ice
	weight 		0.1
	size 		1
	value		0
	gravity		0.0
	groupable    	25
}

{ game_tossprojectile
	dbg game_tossprojectile
	callevent 0.1 pick_target
}

{ pick_target
	dbg pick_target
	//race human
	//local TARG_LIST $get_insphere(enemy,1024,game.monster.origin)
	callexternal ent_expowner ext_sphere_token_byrange enemy 768 $get(ent_me,origin)
	setvard TARG_LIST $get(ent_expowner,scriptvar,'PLR_SCAN_TOKEN')
	dbg pick_target list TARG_LIST
	if TARG_LIST isnot none
	
	calleventloop $get_token_amt(TARG_LIST) pick_target_loop

	dbg pick_target $get(MY_TARG,name)
	if ( !$get(MY_TARG,isplayer) )
	{
		setvard TARG_HALF_HEIGHT $get(MY_TARG,height)
		multiply TARG_HALF_HEIGHT 0.5
	}
	else
	{
		setvard TARG_HALF_HEIGHT 0
	}
	setvard IS_ACTIVE 1
	callevent 0.1 orient_on_target
}

{ pick_target_loop

	local L_TARG $get_token(TARG_LIST,game.script.iteration)
	local L_POS $get(L_TARG,origin)
	
	if $get_traceline($get(ent_me,origin),L_POS,worldonly) equals L_POS
	if ( $get(L_TARG,isplayer) )
	{
		if ( $get(ent_owner,isplayer) )
		{
			if ( !game.pvp ) exitevent
		}
	}
	if $get(L_TARG,isalive)
	if $get_takedmg(L_TARG,cold) //Can take damage from cold
	
	setvard MY_TARG L_TARG
	breakloop
}

{ orient_on_target
	if IS_ACTIVE
	if $get(MY_TARG,isalive)
	callevent 0.25 orient_on_target
	local TARG_ORG $get(MY_TARG,origin)
	vectoradd TARG_ORG z TARG_HALF_HEIGHT
	local MY_ORG $get(ent_me,origin)
	local ANG_TO_TARG $angles3d(MY_ORG,TARG_ORG)
	vectorset ANG_TO_TARG x $neg($vec.x(ANG_TO_TARG))
	//dbg orient_on_target ANG_TO_TARG
	setprop ent_me velocity $relvel(ANG_TO_TARG,$vec(0,300,0))
	setprop ent_me movedir ANG_TO_TARG
}

{ game_projectile_landed
	setvard IS_ACTIVE 0
}

{ game_projectile_hitnpc

	setvard IS_ACTIVE 0

	local MY_OWNER $get(ent_expowner,id)
	local OWNER_ISPLAYER $get(MY_OWNER,isplayer)
	local ENT_HIT PARAM1

	if ( OWNER_ISPLAYER == 1 )
	{
		if !game.pvp
		if $get(ENT_HIT,isplayer)
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if $get(MY_OWNER,relationship,ENT_HIT) equals enemy

	xdodamage ENT_HIT direct $get(MY_OWNER,skill.spellcasting.ice) 100% MY_OWNER ent_me spellcasting.ice cold

	local EFFECT_DURATION $get(MY_OWNER,skill.spellcasting.ice)
	local DOT_ICE $get(MY_OWNER,skill.spellcasting.ice)
	multiply EFFECT_DURATION 0.5
	multiply DOT_ICE 0.5
	capvar EFFECT_DURATION 3 5

	applyeffect ENT_HIT effects/effect_frostbite_dmg EFFECT_DURATION MY_OWNER DOT_ICE
}