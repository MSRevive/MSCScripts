{
	callevent validation_check
}

{ game_removefromowner

	callevent validation_check
}

{ validation_check

	if ( game.central )
	{
		local L_ME $get(ent_owner,steamid)
		if ( G_DEVSTAFF_OWNERS !contains L_ME )
		{
			deleteme
		}
	}
}

//----------------------------------------------

//Left mouse button phys guns objects around
//Right mouse button teleports the player in the direction they are looking

//Holding use and clicking right mouse button will copy the target and overwrite the lmb to paste it
//Trying to copy no target clears the selection and returns the lmb to normal control

//Holding use and clicking left mouse will open a menu with more options

{
	//anims
	const ANIM_USE 6
	setvar LAST_ANIM 0
}

{ 
	setvar BEAM_TYPE 0 //0 = physgun; 1 = remover; 2 = afflicter;
	setvar BEAM_TARGET none
	
	setvar MENU_ID none
}

#include items/base_item_extras

#include items/dev_staff/physgun
#include items/dev_staff/copypaste
#include items/dev_staff/remover
#include items/dev_staff/afflicter
#include items/dev_staff/damager

{ game_spawn

	name "Jester's Staff"
	desc "For the real clowns who spend days troubleshooting simple problems."

	weight 0
	size 5
	value 0
	sethudsprite hand item
	sethudsprite trade 21

	setmodel weapons/p_weapons1.mdl
	setmodelbody 0 20
	
	setviewmodel viewmodels/v_1hblunts.mdl

	sethand both
}

{ bweapon_effect_activate

	bplayermessage ent_owner "Hold 'e' then push mouse1 to access the menu."
	bplayermessage ent_owner "Mouse2 instead to copy your target. Left click will be replaced with paste."
	bplayermessage ent_owner "Clear the clipboard (copy nothing) to restore other left click function."
	
	callevent 0.1 submodel_me_baby

	if MENU_ID equals none
	createnpc items/dev_staff/menus/main '(0,0,0)' $get(ent_owner,id) $get(ent_me,id)
	setvard MENU_ID $get(ent_lastcreated,id)
}

{ bweapon_effect_remove
	//callevent 0.1 wmodel_me_baby
	
	deleteent MENU_ID
	setvard MENU_ID none
}

{ game_pickup

	playviewanim 0
}

{ game_attack1 //Paste npc

	if ( $get(ent_owner,keydown,use) )
	{
		callexternal MENU_ID ext_menu_main
	}
	else if ( NPC_CLIPBOARD isnot none )
	{
		callevent paste_target
	}
}

{ game_attack1_down

	if ( !$get(ent_owner,keydown,use) )
	{
		if ( NPC_CLIPBOARD equals none )
		{
			if ( BEAM_TYPE == 0 ) callevent try_physgun
			if ( BEAM_TYPE == 1 ) callevent try_remove
			if ( BEAM_TYPE == 2 ) callevent try_applyeffect
			if ( BEAM_TYPE == 3 ) callevent try_damage
		}
	}
}

{ game_attack2

	if ( $get(ent_owner,keydown,use) )
	{
		callevent copy_target
	}
	else //No keydown
	{
		if ( AM_NOCLIPPING )
		{
			setvard AM_NOCLIPPING 0
			setprop ent_owner movetype 3
			setprop ent_owner solid 2
		}
		else
		{
			setvard AM_NOCLIPPING 1
			setprop ent_owner movetype 8
			setprop ent_owner solid 0
		}
	}
}

{ [server] find_beam_target

	local L_START $get(ent_owner,eyepos)
	
	local L_END $relpos($get(ent_owner,viewangles),$vec(0,10000,0))
	vectoradd L_END L_START
	
	setvard BEAM_TARGET none
	local L_TARGET $get_traceline(L_START,L_END,ent,$get(ent_owner,id))
	if ( $get(L_TARGET,exists) )
	{
		if ( $get(L_TARGET,isalive) )
		{
			setvard BEAM_TARGET L_TARGET
		}
	}
}

{ game_attack1_down //anims
	callevent try_anim
}

{ try_anim

	if ( $math(subtract,game.time,LAST_ANIM) > 0.8 )
	{
		playviewanim ANIM_USE
		setvard LAST_ANIM game.time
	}
}

{ set_beam_type
	setvard BEAM_TYPE PARAM1
}

{ submodel_me_baby

	setviewmodelprop ent_me submodel 0 6
}

{ wmodel_me_baby

	//setmodelprop ent_me submodel 0 6
}