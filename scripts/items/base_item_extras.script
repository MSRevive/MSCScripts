
//ITEM_RESERVE_FOR_STRONGEST 1 - this item can only be picked up by the player with the most damage points when it first drops into the world
//PICKUP_ALLOW_LIST - array stores players allowed to pick up item (if this array exists, item pickup is restricted code side)
//
//QUEST_ITEM_CAT - quest data slot to store quest item in
//QUEST_ITEM_ID - quest data to store
//- if QUEST_ITEM_ID already exists in QUEST_ITEM_CAT it will not be added again
//- You can use this quest data to determine if the player has EVER had said item

//======= Reserve items for strongest player
{
	const BWEAPON_BASE_ANIM_SPEED 1.0
}

#include items/item_debug

{ game_spawn
	callevent 0.1 vanish_item //Item owner is propogated 0.1 seconds after player is put in world
}

{ [server] vanish_item

	local L_OWNER $get(ent_me,owner) //Can't use ent_owner, since item hasn't been placed in players hands yet
	if ( $get(L_OWNER,exists) )
	{
		setorigin ent_me $vec(20000,20000,20000)
	}
}

{ game_fall
	if game.serverside
	if ITEM_RESERVE_FOR_STRONGEST
	callevent bitem_reserve_for_strongest
}

{ bitem_reserve_for_strongest
	setvard ITEM_RESERVE_FOR_STRONGEST 1
	array.create PICKUP_ALLOW_LIST
	callevent 0.1 bitem_get_strongest
}

{ game_restricted //<player_who_tried_to_pick_me_up>
	if game.serverside
	if ITEM_RESERVE_FOR_STRONGEST
	dbg game_restricted
	local OUT_MSG "This trophy is reserved for "
	local ITEM_RESERVER $get_array(PICKUP_ALLOW_LIST,0)
	stradd OUT_MSG $get(ITEM_RESERVER,name)
	infomsg PARAM1 "Item Damagepoint Restricted" OUT_MSG
}

{ bitem_get_strongest

	if game.serverside

	if ( $get(ent_owner,isalive) )
	{
		array.erase PICKUP_ALLOW_LIST //already owned, don't restrict anymore
	}
	else
	{
		callexternal GAME_MASTER gm_find_strongest_reset
		local WINNING_PLAYER $get(GAME_MASTER,scriptvar,'THE_CHOSEN_ONE')
		array.add PICKUP_ALLOW_LIST WINNING_PLAYER
		dbg bitem_get_strongest $get(WINNING_PLAYER,name)
	}
}

//====== Quest Flagged Items
{ game_deploy

	if game.serverside

	if ( QUEST_ITEM_CAT isnot 'QUEST_ITEM_CAT' )
	{
		local QUEST_CAT_DATA $get_quest_data(ent_owner,QUEST_ITEM_CAT)

		if ( QUEST_CAT_DATA contains QUEST_ITEM_ID ) local DO_NOT_ADD 1
		if !DO_NOT_ADD

		if ( QUEST_CAT_DATA equals 0 )
		{
			quest set ent_owner QUEST_ITEM_CAT QUEST_ITEM_ID
		}
		else
		{
			token.add QUEST_CAT_DATA QUEST_ITEM_ID
			quest set ent_owner QUEST_ITEM_CAT QUEST_CAT_DATA
		}
	}
}

//============= Wielded
//include for weapons/items that have an effect only while weilded
{ [server] ext_activate_items
	if game.serverside
	if PARAM1 equals $get(ent_owner,id)
	if $get(ent_owner,scriptvar,'PLR_IN_WORLD')
	if ( $get(ent_me,id) equals $get(ent_owner,scriptvar,'PLR_LEFT_HAND') ) local BEW_IS_WEILDED 1
	if ( $get(ent_me,id) equals $get(ent_owner,scriptvar,'PLR_RIGHT_HAND') ) local BEW_IS_WEILDED 1
	if BEW_IS_WEILDED
	callevent bweapon_effect_activate
}

{ [server] game_deploy
	
	if $get(ent_owner,scriptvar,'PLR_IN_WORLD')
	callevent bweapon_effect_activate

	callevent 0.01 bweapon_fixprops
}

{ bweapon_fixprops
	//restore normal viewmodel props in case was set funky by last weapon
	setviewmodelprop ent_me rendermode 0
	setviewmodelprop ent_me renderamt 255
}

{ game_wear
	//if ( game.clientside ) dbg *** cl_game_wear
	if ( game.serverside ) callevent bweapon_effect_remove
}
{ game_putinpack //this isn't working (SEP2009 - comment only)
	cancelattack
	//if ( game.clientside ) dbg *** cl_game_putinpack
	if game.serverside
	callevent bweapon_effect_remove game_putinpack
}
{ game_remove
	//if ( game.clientside ) dbg *** cl_game_remove
	if game.serverside
	callevent bweapon_effect_remove game_remove
}
{ game_sheath
	if game.serverside
	callevent bweapon_effect_remove game_sheath
}
{ game_drop
	cancelattack
	//if ( game.clientside ) dbg *** cl_game_drop
	if game.serverside
	callevent bweapon_effect_remove game_drop
}

{ game_deleted
	dbg game_deleted
	//fuxing the wrong way with this causes problems due to the universal item list that deletes itself
	cancelattack
	if game.serverside
	if ( $get(ent_me,inhand) ) local L_REM_EFFECTS 1
	if ( $get(ent_me,is_worn) ) local L_REM_EFFECTS 1
	if L_REM_EFFECTS
	callevent bweapon_effect_remove game_deleted
}
//============= Setrender
{ ext_render_items //<owner> <rendermode> <renderamt>
	if PARAM1 equals $get(ent_owner,id)
	setprop ent_me rendermode PARAM2
	setprop ent_me renderamt PARAM3
}

{ ext_playowneranim
	//dbg playowneranim PARAM1 PARAM2 PARAM3 PARAM4
	//playowneranim	//<once|hold|critical|break> <AnimName> [LegAnimName] [TimeHoldLegAnim] - Play anim on the player model
	playowneranim PARAM1 PARAM2 PARAM3 PARAM4
}

{ item_banked
	//dbg item_banked
	callevent game_putinpack
}

{ ext_item_swift_blade //<base_speed|remove> <base_stike_delay>

	dbg ext_item_swift_blade PARAM1 PARAM2
	setvard ITEM_SWIFT_BLADE 1
	if ( PARAM1 isnot remove )
	{
		setvard ITEM_BASE_SPEED PARAM1
		setvard ITEM_BASE_STRIKE PARAM2
	}
	else
	{
		setvard ITEM_SWIFT_BLADE 0
		setviewmodelprop ent_me animspeed 1.0
		attackprop ent_me 0 delay.end ITEM_BASE_SPEED
		attackprop ent_me 0 delay.strike ITEM_BASE_STRIKE
	}
}

{ ext_viewanim_test
	dbg ext_viewanim_test
	setviewmodelprop ent_me animspeed 5.0
}

{ game_deploy
	if IS_CONTAINER
	if !IS_RESERVED
	setvard IS_RESERVED 1
	array.create PICKUP_ALLOW_LIST
	array.add PICKUP_ALLOW_LIST $get(ent_owner,id)
}

{ game_fall
	if IS_CONTAINER
	if !IS_RESERVED
	setvard IS_RESERVED 1
	array.create PICKUP_ALLOW_LIST
	array.add PICKUP_ALLOW_LIST $get(ent_owner,id)
}

{ game_restricted
	if IS_CONTAINER
	if IS_RESERVED
	local OUT_MSG "This container is reserved for "
	local ITEM_RESERVER $get_array(PICKUP_ALLOW_LIST,0)
	stradd OUT_MSG $get(ITEM_RESERVER,name)
	infomsg PARAM1 "Item Restricted" OUT_MSG
}

{ bweapon_effect_activate
}

{ bweapon_effect_remove //When item is removed from players hands
	
	local L_SET_ID 0
	local L_HAND -1
	
	local L_ID $get(ent_owner,scriptvar,'PLR_LEFT_HAND') //Get if item is left hand
	if ( L_ID equals $get(ent_me,id) )
	{
		if ( $get(L_ID,hand_index) equals $get(ent_me,hand_index) ) local L_HAND $get(L_ID,hand_index)
	}
	
	local L_ID $get(ent_owner,scriptvar,'PLR_RIGHT_HAND')  //Get if item is Right hand
	if ( L_ID equals $get(ent_me,id) )
	{
		if ( $get(L_ID,hand_index) equals $get(ent_me,hand_index) ) local L_HAND $get(L_ID,hand_index)
	}
	
	if L_HAND >= 0 //If item is still wielded then it will have a hand value
	if L_HAND <= 3 //This is a valid range of hand values

	callexternal ent_owner ext_set_hand_id L_HAND 0 //remove hand value
}