//Rune shield
//Immortal, chance of frostbiting opponents


{
	const NOPUSH_CHANCE 75%

	const PARRY_MULTI 2.0
	const SHIELD_BASE_PARRY 25

	const MODEL_VIEW       	viewmodels/v_shields.mdl
	const MODEL_VIEW_IDX	2
	//const MODEL_VIEW armor/shields/runeshield_rview.mdl

	const SHIELD_REPORT_HITS 1

  const MODEL_BODY_OFS		69 //8
  const MELEE_ENERGY		15
  const MELEE_ACCURACY   	90%
  const BLOCK_CHANCE_UP		100	//%Chance of blocking damage while up
  const DMG_BLOCK_UP		0.35	//%Damage player takes when blocks (0.0=none 1.0=all)
  const BLOCK_CHANCE_DOWN	30	//%Chance of blocking all damage while down
  const SHIELD_MAXHEALTH	infinite
  const SHIELD_IMMORTAL		1
  setvard SHIELD_HEALTH		infinite
  const SOUND_BLOCK      	doors/doorstop5.wav
  const EFFECT_RANGE		150	//would use MELE_RANGE - but fraid might due funkiness
  precache SOUND_BLOCK
}

#include items/shields_base

{  shield_spawn

   name 	a|Rune Shield
   desc 	This rune shield is cold to the touch
   weight 	40
   size 	45
   value	5000
   quality      2000
   sethudsprite hand ironshield
   sethudsprite trade runeshield
}


{ game_wear

  playermessage You sling a Rune shield over your shoulder.
}

{ shield_hit //1: Attacker  2: Inflictor  3: dmg  4: dmg type

	if $get(PARAM1,isalive)
	if !$get(PARAM2,is_projectile)

	local MY_OWNER $get(ent_owner,id)
	local THE_ATTACKER $get(PARAM1,id)
	if THE_ATTACKER isnot $get(ent_owner,id)	//don't freeze self shielding from effects

	local OWNER_ORG $get(MY_OWNER,origin)
	local ATTACKER_ORG $get(THE_ATTACKER,origin)

	if $dist(OWNER_ORG,ATTACKER_ORG) < EFFECT_RANGE

	if( !game.item.attacking )
	{
		local FROST_CHANCE 25
	}
	else
	{
		local FROST_CHANCE 5
	}

	local FROST_ROLL $rand(1,FROST_CHANCE)
	dbg frost_roll FROST_ROLL / FROST_CHANCE

	if ( FROST_ROLL == 1 ) 
	{
		playermessage "The rune shield's magic has frozen your enemy!"
		playsound 2 const.snd.fullvol debris/beamstart14.wav
		applyeffect THE_ATTACKER effects/dot_cold 10 $get(ent_owner,id) $randf(5,15) none
	}
}