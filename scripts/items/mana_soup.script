//New Sylphiel's Soup

//Still full HP & Mana heal upon slurp
//Regen is still 3 minutes, but it's for both mana and health
//Regen starts at 10 per tick for the first 30 seconds, then 5 per tick for the next 30 seconds and then 2 per tick for the last 2 minutes
//(Numbers still subject to change in case it either gives too much HP or MP)

{
	const ANIM_LIFT1 21
	const ANIM_IDLE1 21
	const MODEL_VIEW viewmodels/v_martialarts.mdl
	const ITEM_MODEL_VIEW_IDX 8
	const MODEL_HANDS misc/p_misc.mdl
	const MODEL_WORLD misc/p_misc.mdl
	const MODEL_BODY_OFS 69
	const MODEL_BODY_FLOOR 71
	const ANIM_WORLD_IDLE apple_floor_idle
	const ANIM_HAND_IDLE idle
	const OWNER_ANIM_IDLE aim_soup
	const OWNER_ANIM_USE aim_soup

	const ANIM_USE 20
	const USE_DELAY 2.0

	const SOUND_USE magic/slurp.wav

	const NO_IDLE 1
}

#include items/base_item

{ game_spawn

	name "a|Sylphiel's Soup"
	desc "Sylphiel's Applemead Soup provides temporary health and mana regen."
	weight 1
	size 1
	value 200
	sethand both
	sethudsprite hand 88
	sethudsprite trade 88
}

{ game_fall
 	setmodelbody 0 MODEL_BODY_FLOOR
	playanim WANIM_FLOOR
	playanim ANIM_WORLD_IDLE
}

{ game_deploy
	setanimext soup

	setviewmodel MODEL_VIEW
	setmodel MODEL_HANDS
	playanim ANIM_HAND_IDLE
 	setmodelbody 0 MODEL_BODY_OFS

	if game.serverside
	playowneranim once OWNER_ANIM_IDLE global.anim.combatwalk 3
	setvard NEXT_USE game.time
	add NEXT_USE 1.0

	setvard DO_OWNER_IDLES 1
	callevent 2.0 item_idle
}

{ game_attack1
	if game.serverside
	if !ITEM_USED
	if game.time > NEXT_USE
	setvard ITEM_USED 1
	splayviewanim ent_me ANIM_USE
	callevent USE_DELAY use_item 
}

{ use_item
	callexternal ent_owner ext_svplaysound_kiss 1 10 SOUND_USE 0.8 100
	playowneranim once OWNER_ANIM_USE global.anim.combatwalk 3
	callevent use_effect	
}

{ use_effect
	callevent 2.0 use_complete
	
	local L_POOPY 0
	if ( $rand(0,99) == 0 ) local L_POOPY 1
	
	if ( !$get(ent_owner,haseffect,'soup') )
	{
		applyeffect ent_owner "effects/soup" 180 L_POOPY //If it doesn't have the effect, apply it
	}
	else 
	{
		removeeffect ent_owner 'soup' //<target> <effect_id> //Otherwise flag for deletion and apply it again on the end step.
		setvard APPLY_LATER 1
	}
	
	givehp ent_owner $math(subtract,$get(ent_owner,maxhp),$get(ent_owner,hp)) 
	givemp ent_owner $math(subtract,$get(ent_owner,maxmp),$get(ent_owner,mp))
}

{ use_complete

	local L_POOPY 0
	if ( $rand(0,99) == 0 ) local L_POOPY 1
	
	gplayermessage ent_owner "All your health and mana has been restored."
	gplayermessage ent_owner "Three minutes of health and mana regen."

	if ( APPLY_LATER ) applyeffect ent_owner "effects/soup" 180 L_POOPY //If it doesn't have the effect, apply it

	removescript
	deleteent ent_me
}

{ fake_precache
	svplaysound 0 0 SOUND_USE
}

{ bweapon_effect_remove
	setvard DO_OWNER_IDLES 0
}

{ item_idle
	if DO_OWNER_IDLES
	if ( !$get(ent_me,inhand) )
	{
		setvard DO_OWNER_IDLES 0
	}
	else
	{
		playowneranim once OWNER_ANIM_IDLE global.anim.combatwalk 3
		callevent 1.0 item_idle
	}
}