//Thothie - Increasing heal formula and mana use in an attempt to cut down on 'rejuv lag'
{
  const SOUND_SHOOT      	magic/heal_strike.wav

  const MELEE_RANGE		384
  const MELEE_HITCHANCE 	100%
  const MELEE_ATK_DURATION 	1

  setvar SPELL_SKILL_REQUIRED	5
  const SPELL_FIZZLE_TIME	25
  const SPELL_CAST_SUCCESS	95%
  const SPELL_PREPARE_TIME	2
  const SPELL_DAMAGE_TYPE	divination
  const SPELL_ENERGYDRAIN	10
  const SPELL_MPDRAIN		5
  const SPELL_STAT		spellcasting.divination

  const EFFECT_MAXDURATION 	20
  const EFFECT_MINDURATION 	10
  const EFFECT_DURATION_STAT	$get(ent_owner,stat.concentration.ratio)
  const EFFECT_DURATION_FORMULA $get_skill_ratio(EFFECT_DURATION_STAT,EFFECT_MINDURATION,EFFECT_MAXDURATION,inversed)
  const HEAL_MAX	 	75	//old formula 10000
  const HEAL_MIN 		30	//old formula 5
  const HEAL_SKILL		$get(ent_owner,skill.spellcasting.divination.ratio)
  
  const LOOP_SOUND		player/heartbeat_noloop.wav
  const LOOP_CHANNEL		const.sound.item 
  precache LOOP_SOUND

  const SPELL_SCRIPT		effects/effect_rejuv2
  precachefile 			SPELL_SCRIPT
}

#include items/magic_hand_base

{  spell_spawn

   name 		Rejuvination
   desc 		Replenish yours or an ally's health over time

   setvard FAN_LOOP 0

   callevent passive_regen
}

{  spell_casted	//PARAM1: "none"/"npc"/"world"   2: Attack end Position   3: Entity hit (if any)

	local DEMON_ON $get(ent_owner,scriptvar,'DEMON_BLOOD')
	if ( DEMON_ON )
	{
		playermessage You cannot use divine magic while under the influence of Demon Blood!
		callevent spell_end
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	local L_CASTER $get(ent_owner,id) 
	local L_SPELL_TARGET $get(ent_owner,id)
	
	local L_HEAL_AMT $get(ent_owner,skill.spellcasting.divination)
	add L_HEAL_AMT HEAL_MIN
	
	if( PARAM1 equals 'npc' )
	{
		if ( $get(ent_owner,relationship,PARAM3) equals 'ally' ) local L_SPELL_TARGET PARAM3
	}
	if ( $get(PARAM3,isplayer) ) local L_SPELL_TARGET PARAM3
	if ( $get(PARAM3,scriptvar,IAM_PLAYER) ) local L_SPELL_TARGET PARAM3

	if ( L_CASTER isnot L_SPELL_TARGET )
	{
		local L_TEN_PERCENT $get(L_SPELL_TARGET,maxhp)
		multiply L_TEN_PERCENT 0.1
		add L_HEAL_AMT L_TEN_PERCENT
		local L_HEAL_AMT $int(L_HEAL_AMT) // $int() now so rounding doesn't confuse when reporting damage points vs healed
		
		//give damage point if other player or critical npc
		if ( $get(L_SPELL_TARGET,isplayer) ) local L_ADD_BONUS 1
		if ( $get(L_SPELL_TARGET,scriptvar,'NPC_CRITICAL') ) local L_ADD_BONUS 1
		if ( L_ADD_BONUS )
		{
			if $get(L_SPELL_TARGET,hp) < $get(L_SPELL_TARGET,maxhp)
			callexternal ent_owner add_dmg_points $math(multiply,L_HEAL_AMT,5)
			local L_GAVE_DMG_POINTS 1
		}
	}
	else
	{
		multiply L_HEAL_AMT 0.5
	}
	////dbg HealAmt Final HEAL_AMT
	
	svplaysound game.sound.voice 10 SOUND_SHOOT

	//applyeffect SPELL_TARGET SPELL_SCRIPT HEAL_AMT DIV_SKILL $get(ent_owner,id)
	callevent heal_target L_SPELL_TARGET L_HEAL_AMT L_GAVE_DMG_POINTS
	if ( L_SPELL_TARGET isnot L_CASTER ) callexternal SPELL_TARGET display_health
}

{ heal_target //<target> <amt> <gave_dmgpts:0|1>

	local L_HEAL_TGT PARAM1
	local L_HEAL_AMT PARAM2
	local L_DP PARAM3
	local L_CASTER_ID $get(ent_owner,id)

	local L_MAX_HEALTH $get(L_HEAL_TGT,maxhp)
	local L_CUR_HEALTH $get(L_HEAL_TGT,hp)

	if ( L_HEAL_TGT isnot L_CASTER_ID ) local L_HEALING_OTHER 1

	if ( L_CUR_HEALTH < L_MAX_HEALTH )
	{
		givehp L_HEAL_TGT L_HEAL_AMT
		local L_HEALED 1
		if ( L_HEALING_OTHER )
		{
			gplayermessage L_CASTER_ID You heal $get(L_HEAL_TGT,name) for $int(L_HEAL_AMT) hp
			gplayermessage L_HEAL_TGT $get(L_CASTER_ID,name) heals you for $int(L_HEAL_AMT) hp
			if ( L_DP )
			{
				if game.players > 1
				gplayermessage L_CASTER_ID (Credited $int($math(multiply,L_HEAL_AMT,5)) damage points.)
			}
		}
		else
		{
			gplayermessage L_CASTER_ID You heal yourself for $int(L_HEAL_AMT) hp
		}
	}
	else
	{
		if ( L_HEALING_OTHER )
		{
			gplayermessage L_CASTER_ID $get(L_HEAL_TGT,name) is at maximum health
		}
		else
		{
			gplayermessage L_CASTER_ID You are at maximum health
		}
	}

	if L_HEALED

	effect glow L_HEAL_TGT (0,255,0) 256 1 1
}

{ prepare_success_done
		//volume 5
		//playsound const.sound.item 5 UP_SOUND
		setvard FAN_LOOP 1
		//callevent 2 loop_sound
}

{ passive_regen

	repeatdelay 0.5

	if FAN_LOOP >= 1

	add FAN_LOOP 1
	if ( FAN_LOOP == 3 )
	{
        playsound LOOP_CHANNEL 10 LOOP_SOUND
		effect glow ent_owner (0,255,0) 64 0.5 0.5
		setvard FAN_LOOP 1 
	}

	local MY_MAX_HEALTH $get(ent_owner,maxhp)
	local MY_CUR_HEALTH $get(ent_owner,hp)

	local MY_SKILL $get(ent_owner,skill.spellcasting.divination)
	local MY_PASSIVE_RATE MY_SKILL
	multiply MY_PASSIVE_RATE 0.1
	add MY_PASSIVE_RATE 4

	if ( MY_CUR_HEALTH < MY_MAX_HEALTH ) givehp MY_PASSIVE_RATE

	callexternal $get(ent_owner,id) display_health
}

{ game_takedamage ////PARAM1=attacker  PARAM2=inflictor? PARAM3=dmg PARAM4=dmg_type - use returndata to alter incoming damage

	if PARAM4 isnot target //dont cancel heal for target tracking damage
	if PARAM3 > 0.01
	setvard BITCH_SLAPPED 1
	callevent spell_end
}

{ [override] spell_end

	if ( BITCH_SLAPPED ) playermessage You cannot rejuvenate while being attacked!
	if ( !BITCH_SLAPPED ) playermessage The spell's duration ends.
	callevent end_spell
	//playviewanim	ANIM_IDLE1
}

{ fake_precache
	svplaysound 0 0 SOUND_SHOOT
}

{ end_spell
	//remove any cl effects here
	deleteme
}
