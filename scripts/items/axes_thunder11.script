{
	const BASE_LEVEL_REQ 15
	const MELEE_DMG 200

	const CUSTOM_REGISTER_CHARGE1 1

	const MODEL_VIEW viewmodels/v_2haxes.mdl
	const MODEL_VIEW_IDX 3
	const MODEL_HANDS weapons/p_weapons2.mdl
	const MODEL_WORLD weapons/p_weapons2.mdl
	const SOUND_SWIPE weapons/swingsmall.wav
	const MODEL_BODY_OFS 121
}

#include items/axes_battleaxe

{  weapon_spawn

   name  a|Thunderaxe
   desc  An axe enchanted with lightning magics
   weight  80
   size  15
   value 800
   sethudsprite hand 121
   sethudsprite trade 121
}

{ game_deploy
	if game.serverside
	setvard GAME_PVP game.pvp
}

{ register_charge1


   local reg.attack.type		strike-land
   local reg.attack.keys		+attack1
   local reg.attack.range		MELEE_RANGE
   local reg.attack.dmg			MELEE_DMG
   local reg.attack.dmg.range		MELEE_DMG_RANGE
   local reg.attack.dmg.type		MELEE_DMG_TYPE
   local reg.attack.energydrain		MELEE_ENERGY
   local reg.attack.stat		MELEE_STAT
   local reg.attack.hitchance		MELEE_ACCURACY
   local reg.attack.priority		0
   local reg.attack.delay.strike	MELEE_DMG_DELAY
   local reg.attack.delay.end		MELEE_ATK_DURATION
   local reg.attack.ofs.startpos	MELEE_STARTPOS
   local reg.attack.ofs.aimang		MELEE_AIMANGLE
   local reg.attack.callback		melee
   local reg.attack.noise		MELEE_NOISE

   local reg.attack.priority	1
   local reg.attack.keys	-attack1
   local reg.attack.callback	special_01
   multiply reg.attack.dmg	2
   local reg.attack.chargeamt	100%
   local reg.attack.reqskill	2

	if ( BASE_LEVEL_REQ > reg.attack.reqskill ) add reg.attack.reqskill BASE_LEVEL_REQ

   registerattack


	  local reg.attack.type		strike-land
	local reg.attack.noautoaim		1
   local reg.attack.keys		-attack1
   local reg.attack.range		4096
   local reg.attack.dmg			0
   local reg.attack.dmg.range		0
   local reg.attack.dmg.type		slash
   local reg.attack.energydrain		2
   local reg.attack.stat		axehandling
   local reg.attack.hitchance		100%
   local reg.attack.priority		3
   local reg.attack.delay.strike	0.2
   local reg.attack.delay.end		0.9
   local reg.attack.ofs.startpos	MELEE_STARTPOS
   local reg.attack.ofs.aimang		MELEE_AIMANGLE
   local reg.attack.callback		axethrow
   local reg.attack.noise		MELEE_NOISE
   local reg.attack.chargeamt	400%
   local reg.attack.reqskill	18
	registerattack
}

{ special_02
	dbg **************** special_02
}

//{ game_deploy
//
//	local reg.effect.name	axe_stuff
//	local reg.effect.flags	nostack
//	local reg.effect.script	$currentscript
//	registereffect
//}

{ axethrow_start

	setvard AXE_RESTORED 0
	sethand	undroppable
	setmodel none
	setworldmodel none
	setviewmodel viewmodels/v_martialarts.mdl
	playviewanim  4
	if ( game.clientside )
	{
		//dbg axethrow_start restoring axe in 3.0
		//callevent 3.0 restore_axe_cl //nvm, server can force this now
	}
	else
	{
		setviewmodelprop ent_me submodel $get(ent_owner,scriptvar,'PLR_HAND_SET') 0
		svplaysound 0 8  $get(ent_owner,scriptvar,'PLR_SOUND_JAB2')
	}
}

{ axethrow_strike	//PARAM1: "none"/"npc"/"world"   2: Attack end Position   3: Entity hit (if any)

	playowneranim critical bow_release
//	if ( game.clientside ) 
//	{
//		dbg axethrow_strike restoring axe in 3.0
//		callevent 3.0 restore_axe_cl
//	}

	if game.serverside
	local END_TARGET PARAM2
	local MY_VIEW $get(ent_owner,viewangles)
	vectoradd END_TARGET $relpos(MY_VIEW,$vec(0,128,0))

	if ( $get(PARAM3,isalive) )
	{
		if ( $get(PARAM3,isplayer) )
		{
			if GAME_PVP < 1
			local NO_EFFECT 1
		}
		if !NO_EFFECT
		dbg applying shock
		local L_BURN_DAMAGE $get(ent_owner,skill.spellcasting.lightning)
		divide L_BURN_DAMAGE 2
		add L_BURN_DAMAGE $randf(1,3)
		if ( L_BURN_DAMAGE < 5 ) local L_BURN_DAMAGE 5
		applyeffect PARAM3 effects/dot_lightning 5 $get(ent_owner,id) L_BURN_DAMAGE axehandling
	}

//	if ( !$get(END_TARGET,isalive) )
//	{
//		local END_TARGET $get(ent_owner,origin)
//		local MY_VIEW $get(ent_owner,viewangles)
//		vectoradd END_TARGET $relpos(MY_VIEW,$vec(0,1024,0))
//	}
//	if ( $get(END_TARGET,isalive) ) local END_TARGET $get(END_TARGET,origin)

	local DMG_AXE $get(ent_owner,skill.axehandling)
	multiply DMG_AXE 0.75
	createnpc monsters/summon/sorc_axe $get(ent_owner,origin) $get(ent_owner,id) END_TARGET DMG_AXE $get(ent_me,id) axehandling

	applyeffect ent_owner effects/effect_templock
}

{ restore_axe_cl
	setviewmodel MODEL_VIEW
	setmodel MODEL_WORLD
	setworldmodel MODEL_WORLD
	if !AXE_RESTORED
	playviewanim  ANIM_LIFT1
}

{ catch_axe //called external from sorc_axe

	dbg Caught Axe
	//if ( game.clientside ) callevent restore_axe_cl //this is probably never client side as it is an external call
	if ( game.serverside )
	{
		//callexternal ent_owner ext_force_itemevent $get(ent_me,id) restore_axe_cl
		callclitemevent ent_me restore_axe_cl
		setviewmodelprop ent_me submodel $get(ent_owner,scriptvar,'PLR_HAND_SET') MODEL_VIEW_IDX
	}

	if ( game.serverside ) setviewmodelprop ent_me submodel $get(ent_owner,scriptvar,'PLR_HAND_SET') MODEL_VIEW_IDX

	callexternal ent_owner ext_end_templock
	setvard THROWING_AXE 0
	sethand	both
}

{ game_dodamage //1: Attack Hit (1/0)  2: Entity Attack hit  3:  Start Position  4:  End Position

	if PARAM1
	if $rand(1,5) == 1
	local L_BURN_DAMAGE $get(ent_owner,skill.spellcasting.lightning)
	divide L_BURN_DAMAGE 2
	add L_BURN_DAMAGE $randf(1,3)
	if ( L_BURN_DAMAGE < 5 ) local L_BURN_DAMAGE 5
	applyeffect PARAM2 effects/dot_lightning 5 $get(ent_owner,id) L_BURN_DAMAGE axehandling
}


{ melee_start
	setviewmodel MODEL_VIEW
	setmodel MODEL_WORLD
	setworldmodel MODEL_WORLD
	setvard AXE_RESTORED 1
}