//Flame Jet CL effects from proj_arrow_spiral by Thothie
#scope client

{
	const GLOW_RAD 96
	const GLOW_COLOR (255,128,64)
	const MAX_DURATION 2.0
}

{ client_activate //<model_idx> <trail_sprite> [max_duration] [scale]

	setcallback render enable

	setvard MY_OWNER PARAM1
	setvard PASS_SPRITE PARAM2

	if ( PARAM4 startswith PARAM )
	{
		setvard SPRITE_SCALE 0.75
	}
	else
	{
		setvard SPRITE_SCALE PARAM4
	}

	setvard FX_ACTIVE 1

	cleffect light new $getcl(MY_OWNER,origin) GLOW_RAD GLOW_COLOR 0.25
	setvard CL_LIGHT_ID game.script.last_light_id

	setvard ROT_COUNT 0
	callevent fx_loop
	if ( PARAM3 startswith 'PARAM' )
	{
		callevent MAX_DURATION end_fx
	}
	else
	{
		callevent PARAM3 end_fx
	}
}

{ fx_loop
	if FX_ACTIVE
	callevent 0.05 fx_loop
	local SPRITE_POS $getcl(MY_OWNER,origin)
	cleffect tempent sprite  PASS_SPRITE SPRITE_POS setup_sprite
}

{ end_fx
	if FX_ACTIVE
	setvard FX_ACTIVE 0
	callevent 1.0 remove_me
}

{ remove_me
	removescript
}

{ game_prerender

	if FX_ACTIVE
	local L_POS $getcl(MY_OWNER,origin)
	if $getcl(MY_OWNER,exists)
	cleffect light CL_LIGHT_ID L_POS GLOW_RAD GLOW_COLOR 0.25
}

{ setup_sprite
	cleffect tempent set_current_prop death_delay 0.5
	cleffect tempent set_current_prop rendercolor (0,255,0)
	cleffect tempent set_current_prop scale SPRITE_SCALE
	cleffect tempent set_current_prop gravity    0.5
	cleffect tempent set_current_prop collide    none
	cleffect tempent set_current_prop framerate	30
	cleffect tempent set_current_prop frames	19
}