{
	const BASE_LEVEL_REQ 30

	const FIRE_WAVE_MP 40

	const DEMON_CHARGES 6

	const DEMON_DURATION 40.0

	const MELEE_RANGE 90
	const MELEE_DMG_DELAY  0.5
	const MELEE_ATK_DURATION 1.1
	const DEMON_DMG_DELAY  0.25
	const DEMON_ATK_DURATION 0.7
	const MELEE_ENERGY 2
	const MELEE_DMG 450
	const DEMON_DMG 800
	const MELEE_DMG_RANGE 40
	const MELEE_ACCURACY 80%
	const MELEE_PARRY_AUGMENT 20%
	const MELEE_DMG_TYPE dark

	const MODEL_VIEW viewmodels/v_2hblunts.mdl
	const MODEL_VIEW_IDX 7

	const MODEL_WORLD weapons/p_weapons3.mdl
	const MODEL_BODY_OFS 71
	const ANIM_PREFIX standard
}

#include items/blunt_mithral

{  weapon_spawn

	name  a|Demon Bludgeon Hammer
	desc  A massive Bludgeon Demon hammer powered by an infernal soul
	weight  80
	size  10
	value 6000
	sethudsprite hand hammer
	sethudsprite trade 144

	setvard M_ATTACK 1
}

{ game_deploy
	setvard GAME_PVP game.pvp
}

{ game_dodamage //PARAM1=hit:0|1 PARAM2=ent_hit PARAM3=(start) PARAM4=(end) PARAM5=DmgType PARAM6=DmgAmt
	if PARAM1
	if ( $get(PARAM2,isplayer) )
	{
		if !GAME_PVP
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	local DOT_BURN $get(ent_owner,skill.spellcasting.fire)
	multiply DOT_BURN 0.5
	applyeffect PARAM2 effects/effect_burn 5.0 $get(ent_owner,id) DOT_BURN
}

{ special_02_strike

	if game.serverside

	if PARAM1 equals world

	local ATTACK_END_POS PARAM2

	if $vec.z(ATTACK_END_POS) equals $get_ground_height(ATTACK_END_POS)

	if $get(ent_owner,skill.spellcasting.fire) >= 25

	if ( $get(ent_owner,mp) < FIRE_WAVE_MP ) dplayermessage ent_owner Demon Bludgeon Hammer: Insufficient MP for Fire Wave

	if $get(ent_owner,mp) >= FIRE_WAVE_MP

	givemp ent_owner $neg(FIRE_WAVE_MP)

	callevent do_fire_wave $get(ent_owner,origin)
}


{ do_fire_wave
	playsound 0 7 magic/flame_loop_start.wav

	setvard FIRE_WAVE_START_POS PARAM1
	setvard FIRE_WAVE_YAW $get(ent_owner,angles.yaw)
	clientevent new all effects/sfx_fire_wave FIRE_WAVE_START_POS FIRE_WAVE_YAW

	local FIRE_WAVE_SCAN_POS FIRE_WAVE_START_POS
	vectoradd FIRE_WAVE_SCAN_POS $relpos($vec(0,FIRE_WAVE_YAW,0),$vec(0,128,32))

	callexternal ent_owner ext_sphere_token enemy 1024 FIRE_WAVE_SCAN_POS
	setvard FIRE_WAVE_TARGS $get(ent_owner,scriptvar,'PLR_SCAN_TOKEN')

	if FIRE_WAVE_TARGS isnot none
	calleventloop $get_token_amt(FIRE_WAVE_TARGS) fire_wave_affect_targs
}

{ fire_wave_affect_targs

	local CUR_TARG $get_token(FIRE_WAVE_TARGS,game.script.iteration)

	if ( $get(CUR_TARG,isplayer) )
	{
		if !GAME_PVP
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	local OWNER_ORG FIRE_WAVE_START_POS
	local OWNER_ANG $vec(0,FIRE_WAVE_YAW,0)
	local TARG_ORG $get(CUR_TARG,origin)

	dbg fire_wave_affect_targs $within_cone2D(TARG_ORG,OWNER_ORG,OWNER_ANG,100) $get(CUR_TARG,name)

	if $within_cone2D(TARG_ORG,OWNER_ORG,OWNER_ANG,100)

	local TRACE_START FIRE_WAVE_START_POS
	local TRACE_END TARG_ORG
	local TRACE_LINE $get_traceline(TRACE_START,TRACE_END,worldonly)
	if TRACE_LINE equals TRACE_END

	local DOT_BURN $get(ent_owner,skill.spellcasting.fire)
	applyeffect CUR_TARG effects/effect_burn 5.0 $get(ent_owner,id) DOT_BURN
}