 // Spawn properties
{ spawn

   hp	30
   gold	50    
   name	Ferrin
   width	32
   height	72
   race	human
   roam	0
   setmodel	npc/human1.mdl
   invincible	1
   setmodelbody	0 2
   setvar	RETRIEVE 0

   catchspeech say_hi hi hello hail greet

   catchspeech say_dangerous dangerous
   catchspeech say_orcs orcs
   catchspeech say_blackhand blackhand
   catchspeech say_keep keep
   catchspeech say_homage homage caretaker
   catchspeech say_picture  picture
   catchspeech say_retrieve  retrieve
}
//Ramble, bamble...
{ say_hi

  saytext Ahh, another traveller? Please feel free to rest, if you like. It's [dangerous] outside these days.
}
{ say_dangerous

  saytext Well, it's getting so that I can't even go into the [keep] to pay my respects! That, and [orcs] are ravaging the land.
}

{ say_orcs

  saytext These terrible [Blackhand] Orcs have been tearing through the countryside lately. They leave me alone because they're afraid of the keep.
}
{ say_blackhand

  saytext It's a new tribe of Orcs that call themselves the Blackhand. They obey some Orcish Shaman named 'The Blackhand' and wage war upon humans.
}
{ say_keep

  saytext The Keledros Keep, of course. I'm the last in a long line of [caretakers] for this bellhouse. We used to serve the Captain Marshall of Keledros.
}
{ say_homage

  saytext I used to go into the keep every seventh day of the week...to pay my respects.
  calleventtimed 4 say_homage2
}
{ say_homage2

  saytext There is a picture of my Great Grandfather Luc where I would place flowers...
  calleventtimed say_homage3
}
{ say_homage3

  saytext Now the place is so infested with evil I can't even get the [picture] out
}
{ say_picture

  saytext Will you go into the keep and retrive my picture for me? I would be extremely grateful to you! Please say you will go [retrieve] it?
}

{ say_retrieve

  if RETRIEVE equals 0
  setvar RETRIEVE 1

  saytext Thank you so much! Here, you'll need this key to get into the room where it's stored!
  offer ent_lastspoke item_wpkey
}
//When you've finally retrieved the picture.
{ give_picture

  recvoffer accept
  saytext A picture of my Great Grandfather Luc! Thank you so much adventurer! I regret I have little to offer, but take this!
  offer PARAM1 item_storageroomkey
} 

{ game_menu_getoptions
	local reg.mitem.title	"Say Hello"
	local reg.mitem.type	say

	local l.say $rand(2,4)
	if( l.say == 1 ) local reg.mitem.data 'Hello'
	else if( l.say == 2 )	local reg.mitem.data 'Hi'
	else if( l.say == 3 )	local reg.mitem.data "Hail" 
	else if( l.say == 4 )	local reg.mitem.data 'Greetings!'
	menuitem.register

	if( $item_exists(PARAM1,item_picashlborn) )
	{
		local reg.mitem.title 	"Return picture"
		local reg.mitem.type 	payment
		local reg.mitem.data 	item_picashlborn
		local reg.mitem.callback give_picture
		menuitem.register
	}
}