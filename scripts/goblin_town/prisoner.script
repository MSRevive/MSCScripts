// Spawn properties
// Prisoner Quest in Goblin Town
// Husband from Gate City was travelling between towns selling
// his goods. He was captured by goblins and held prisoner.
// Wife wants you to find him. When found, he gives you a letter
// to deliver to her stating he is fine and will be home soon.
// Wife gives reward when the letter is delivered.
{
	const NO_JOB 1
	const NO_RUMOR 1
}

{ game_precache

	precache npc/human1.mdl
}

#include monsters/base_chat

{ spawn

   hp 30/30    //Start with 30 out of 30 max hit points
   gold 50    
   name "Almund the Traveling Merchant"
   //fov 120
   width 32
   height 72
   race human
   roam 0      //Don't go roaming around
   setmodel npc/human1.mdl
   invincible  1
   setmodelbody 1 2
   setvar QUEST_GOBLINPRISONER 0
   const HEARING_RANGE 175

   catchspeech say_hi hi hello hail greet hey

   catchspeech say_no no nope

   catchspeech say_yes yes yup yea

}

{ say_hi

	callevent playerdist
	if ( distance <= HEARING_RANGE )
   	{
		if ( QUEST_GOBLINPRISONER == 0 )
		{
			saytext Are you here to rescue me?
		}
		if ( QUEST_GOBLINPRISONER == 1 )
		{
			saytext I can find my way home now
		}
	}
}

{ say_no

	callevent playerdist
	if ( distance <= HEARING_RANGE )
   	{
		if ( QUEST_GOBLINPRISONER == 0 )
		{
			saytext I guess I am doomed to rot in this cage...
		}
		if ( QUEST_GOBLINPRISONER == 1 )
		{
			saytext I can find my way home now
		}
	}
}

{ say_yes

	callevent playerdist
	if ( distance <= HEARING_RANGE )
   	{
		if ( QUEST_GOBLINPRISONER == 1 )
		{
			saytext "I can find my way home now."
		}
		if ( QUEST_GOBLINPRISONER == 0 )
		{
			setvar QUEST_GOBLINPRISONER 1
			saytext "Thank you! My family must be worried about me!"
			calleventtimed 3 ending
		}
	}
}

{ ending
	saytext "I was captured by these goblins while traveling between towns."
	calleventtimed 3 ending2
}

{ ending2
	saytext "They stole all of my goods, but that doesn't matter."
	calleventtimed 3 reward
}

{ reward
	saytext "Please take this letter to my wife, Kendra, in Gate City."
	offer ent_lastspoke item_letter_almund
}

{ playerdist
	setvar PLAYER $get(ent_lastspoke,id)
	setvar PLAYER_O $get(PLAYER,origin)
	setvar NPC $get(ent_me,id)
	setvar NPC_O $get(NPC,origin)
	setvar distance $dist(NPC_O,PLAYER_O)
	////debugprint distance
}