//Thothie - Moved to New AI 8/Sep/2006 10:21
//Lord of Deralia
{
	setvard DROP_GOLD $rand(10,50)
	const SOUND_STRUCK1 body/armour1.wav
	const SOUND_STRUCK2 body/armour2.wav
	const SOUND_STRUCK3 body/armour3.wav
	const SOUND_WARCRY voices/human/male_guard_dismiss.wav
	const SOUND_ATTACK weapons/swingsmall.wav
	const SOUND_PAIN voices/human/male_hit1.wav
	const SOUND_IDLE voices/human/male_idle2.wav
	const SOUND_DEATH voices/human/male_die.wav
	setvar ANIM_IDLE idle1
	setvar ANIM_RUN run
	setvar ANIM_WALK walk
	setvard ANIM_ATTACK swordswing1_L
	setvard ANIM_DEATH dieforward
	setvard ATTACK_MOVERANGE 45
	setvard ATTACK_RANGE 65
	setvard ATTACK_HITRANGE 150
	const ATTACK_HITCHANCE 90%
	const ATTACK_DAMAGE 45
	const ATTACK_STUNCHANCE 50%
	const FLEE_HEALTH 15   //Used by base_monster
	const FLEE_CHANCE 5%   //Used by base_monster
	setvard NORMAL_MENU 1

	setvard JOBS_DONE 0

	const BG_NO_GO_HOME 1
	const BG_ROAM 1

	const BG_MAX_HEAR_CIV 32000

	setvard NO_STEP_ADJ 1

	const NO_CHAT 1

	//Precache
	precache SOUND_DEATH //external

	//catchspeech say_hail hi hullo greet yo hai hello //Giving it the "standard" catchspeeches
	catchspeech say_hi hail hi j0 yo waddup wassup gweets greet
	catchspeech say_job job work
	catchspeech say_rumor rumour rumor
}

#include monsters/base_monster_new
#include monsters/base_guard_friendly_new

{ npc_spawn

	name_unique knight_lord

	menu.autoopen 1

	hp   800
	gold   DROP_GOLD
	width   32
	height   72
	race   hguard
	name   a|Lord of the land
	roam   1
	hearingsensitivity   10
	setvard NPC_GIVE_EXP   100
	takedmg all .4
	movespeed 1.0

	blood red

	setmodel   npc/guard2.mdl
	setmodelbody   1 0
	setidleanim   ANIM_IDLE
	setmoveanim   ANIM_WALK
	//setactionanim   swordswing1_L

	giveitem ring_light2
}

{ say_hail

	local L_RAND $rand(0,1)

	if ( L_RAND == 0 )
	{
		saytext "Well met, adventurer."
	}
	else if ( L_RAND == 1 )
	{
		saytext "How do you do?"
	}
}

{ say_rumor

	local L_RAND $rand(0,2)

	if ( L_RAND == 0 )
	{
		saytext "Cathlain seems to be losing guards in the sewers... perhaps he could use some help."
	}
	else if ( L_RAND == 1 )
	{
		saytext "Master Proffund likes gambling too much. Hopefully he doesn't get in trouble with the wrong people."
	}
	else if ( L_RAND == 2 )
	{
		saytext "I recently stored one of Thordac's weapons in Galat's Wondrous Chest of Storage, but it was gone when I went to retrieve it..."
		saytext "Guilda wasn't very helpful when trying to figure out where it went."
	}
}

{ say_job

	if ( !JOBS_DONE )
	{
		saytext "I have been looking for a sneaky pick pocket, who has been troubling the townsfolk."
		saytext "If you spot him, do try to apprehend him, as I am also quite busy with other matters."
		saytext "Do not worry, for you will be generously rewarded for your actions."
	}

	if JOBS_DONE
	saytext "I am afraid that, at the moment, I've no other tasks for good samaritans such as yourself."
}

{ say_jail

	roam 0
	setmovedest $get_by_name(thief)
	setmoveanim ANIM_IDLE
	saytext "You shall be taken away and locked behind bars, thief!"
	local QUEST_COMPLETER $get(APPREHENDER,name)
	saytext QUEST_COMPLETER ", you have done well."
	saytext "Please accept this ring as a token of my gratitude."
	saytext "May it bring light in even the darkest of dungeons and caves."
	callexternal $get_by_name(thief) quest_done
	setvard JOBS_DONE 1
	setvard NORMAL_MENU 0
	menu.open APPREHENDER
	callevent 10 resume_roam
}

{ offer_ring
	offer APPREHENDER ring_light2
	setvard NORMAL_MENU 1
	setvard GAVE_RING 1
}

{ game_menu_cancel

	local L_RAND $rand(0,1)

	if ( L_RAND == 0 )
	{
		saytext "Very well then."
	}
	else if ( L_RAND == 1 )
	{
		saytext "As you were."
	}

	setvard NORMAL_MENU 1
}

{ game_menu_getoptions

	if ( NORMAL_MENU == 1 )
	{
		local reg.mitem.title "Hail"
		local reg.mitem.type callback
		local reg.mitem.callback say_hail
		menuitem.register

		local reg.mitem.title "Ask about Rumors"
		local reg.mitem.type callback
		local reg.mitem.callback say_rumor
		menuitem.register

		local reg.mitem.title "Ask about Jobs"
		local reg.mitem.type callback
		local reg.mitem.callback say_job
		menuitem.register
	}

	if ( JOBS_DONE == 1 )
	{
		if !GAVE_RING
		local reg.mitem.title "Accept"
		local reg.mitem.type callback
		local reg.mitem.callback offer_ring
		menuitem.register
	}


}

{ resume_roam
	roam 1
	setmoveanim ANIM_WALK
}

//Idling
{
	repeatdelay 10
	if !IS_HUNTING
	volume 5
	playsound 0 10 SOUND_IDLE
}

{ baseguard_tobattle //called from base_guard_friendly

	playsound 0 10 SOUND_WARCRY
}

{ attack_1

	playsound 0 10 SOUND_ATTACK
	callevent npcatk_dodamage NPCATK_TARGET ATTACK_HITRANGE ATTACK_DAMAGE ATTACK_HITCHANCE
}

{ game_dodamage //1: Attack Hit (1/0)  2: Entity Attack hit  3:  Start Origin  4:  End Origin

	if PARAM1
	if ( $rand(1,100) <= ATTACK_STUNCHANCE ) applyeffect ent_laststruckbyme effects/effect_stun 3 0
}

{ npc_struck

	playrandomsound 0 8 SOUND_STRUCK1 SOUND_STRUCK2 SOUND_STRUCK3 SOUND_STRUCK1 SOUND_PAIN
}