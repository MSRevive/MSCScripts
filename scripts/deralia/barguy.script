// Bar Patron
{ game_spawn

	hp 25
	gold 30
	name Mosor
	width 32
	height 72
	race human
	roam 0      	//Don't go roaming around
	setmodel npc/human1.mdl
	invincible 1
	setmodelbody 0 $rand(0,2)
	setmodelbody 1 2
	
	//basechat
	setvar CANCHAT 1
	
	setvar HAT_QUEST 0
	setvar ATTACK_RANGE 90
	setvar ATTACK1_DAMAGE 5
	setvar ATTACK_PERCENTAGE 60%
	setvar CURRENT_THIEF ÅPNULLÅP//A player can't pick this name

	setvar SOUND_IDLE1 voices/human/male_idle4.wav
	setvar SOUND_IDLE2 voices/human/male_idle5.wav
	setvar SOUND_IDLE3 voices/human/male_idle6.wav

	setvard REQ_QUEST_NOTDONE 1

	catchspeech say_hi hi hello hail greet

	catchspeech say_apple apple

	catchspeech respond_no no

	catchspeech say_job help yes sure try give have buy

	catchspeech say_rumor rumours news happenings rumour rumor
}
   //Idling

#include monsters/base_chat

{

	repeatdelay 35
	playrandomsound 2 5 SOUND_IDLE1 SOUND_IDLE2 SOUND_IDLE3
}

{
	repeatdelay 60
	saytext "Give me another pint!"
}

{ say_hi

	saytext "What do you 'ant?"
}

{ say_rumor

	saytext Gossip?
	if ( REQ_QUEST_NOTDONE ) callevent 1 helpeh
	else callevent 1 say_a_really_important

}

{ helpeh
	if !SAID_HELP
	saytext "Bah, away with you, unless you can [help] me!"
	setvard SAID_HELP 1
}

{ respond_no

	if SAID_HELP

	saytext "Then begone!"
	setvard SAID_HELP 0
	setvard HAT_QUEST 0
}

{ say_job

	//if SAID_HELP equals 1
	if !HAT_QUEST

	playanim once converse1
	saytext "Well, a few days ago I came home after a night like this, quite... happy."
	calleventtimed 2 say_thief2
}

{ say_thief2

	saytext "I walked through the door into my house only to find that everything was missing!"
	calleventtimed 2 say_thief3
}

{ say_thief3

	saytext "Me gold, me weapons, even me hat! I must have me hat! I don't know what to do!"
	calleventtimed 2 say_thief4
}

{ say_thief4

	playanim once pondering
	saytext "Maybe you can get me hat back for me! I'll reward ye! Well!"
	setvard HAT_QUEST 1
}

//Rumours
{ say_a_really_important
	playanim once pondering
	saytext "No rumors around here... only truths." //oops, not that important.
}

{ give_hat

	setvard QUEST_WINNER PARAM1
	recvoffer accept
	saytext MY HAT!
	calleventtimed 5 hat_2 
} 

{ hat_2

	saytext Thank you so much! Heres your reward!
	offer QUEST_WINNER gold 25
	setvard REQ_QUEST_NOTDONE 0
}

{ game_menu_getoptions

	if REQ_QUEST_NOTDONE
	if( $item_exists(PARAM1,item_hat) )
	{
		local reg.mitem.title 	"Return hat"
		local reg.mitem.type 	payment
		local reg.mitem.data 	item_hat
		local reg.mitem.callback give_hat
		menuitem.register
	}
	
	if ( HAT_QUEST )
	{
		local reg.mitem.title 	"No"
		local reg.mitem.type 	callback
		local reg.mitem.callback respond_no
		menuitem.register
	}
	else if ( SAID_HELP )
	{
		local reg.mitem.title 	"Help"
		local reg.mitem.type 	callback
		local reg.mitem.callback say_job
		menuitem.register
	}	
}