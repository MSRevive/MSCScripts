// *** Sitting human script ***
//
{
	const NO_JOB 1
	const NO_RUMOR 1
}

#include monsters/base_npc
#include monsters/base_chat

{ npc_spawn

	name_unique	Proffund

	hp		1
	//fov		180
	width		32
	height		72
	race		human
	name		Master Proffund
	roam		0
	setmodel	npc/human1.mdl
	setmodelbody	0 $rand(0,2)
	setmodelbody	1 $rand(0,5)
	setmoveanim	walk
	invincible	1

	catchspeech say_hi hail hello hi greet
	catchspeech say_deed deed
	catchspeech say_slinker slinker
	catchspeech say_yes yes sure ok yea
	catchspeech say_no no never naw

	setvar DEED 0
	setvar SLINKER 0
	setvar OFFER 0
	setvar OVER 2
	setvar GIVEREWARD 0
}

{ say_hi

	if( GIVEREWARD equals 0 )
	{
		saytext Yes, what do you want?
	}
	if( GIVEREWARD equals 1 )
	{
		saytext Nice work, here's your reward!
		offer ent_lastspoke gold 25
		offer ent_lastspoke scroll_summon_rat
		setvar GIVEREWARD 0
	}
}

{ say_deed

	if OVER equals 0
	if( SLINKER equals 0 )
	{
		setvar DEED 1
		saytext A property deed? What nonsense is this? ... who sent you?
	}
	else
	{
		saytext Oh really? A deed to one of my farm properties, I suppose. Well, he'll have to try harder than you, if he's planning on 
		saytext intimidating me into giving him a property deed!
		calleventtimed 3 say_helpme
	}
}

{ say_slinker

	if OVER equals 0
	if( DEED equals 0 )
	{
		setvar SLINKER 1
		saytext S-slinker? Shh, be a little quieter! My reputation, you understand.. What does he want?
	}
	else
	{
		saytext S-slinker? Shh, be a little quieter! Well, he'll have to try harder than you, if he's planning on intimidating me into giving him a property deed!
		calleventtimed 3 say_helpme
	}
}

{ say_helpme

	saytext However, maybe you can help me out. I'd like Slinker, shall we say, removed from the picture. Will you help me by, uhm, taking care of him?
	setvar OFFER 1
}

{ say_yes

	if OVER equals 0
	if OFFER equals 1
	saytext Excellent. When you return, I shall reward you...
	callexternal $get_by_name(Slinker) mortal
	setvar OVER 1
}

{ say_no

	if OVER equals 0
	if OFFER equals 1
	saytext Are..are you sure? I mean... please don't hurt me. Here, here! Take it!
	offer ent_lastspoke item_deed
	setvar OFFER 0
	setvar OVER 1
}

{ slinker_dead

	setvar GIVEREWARD 1
}

{ quest_start

	Debugprint Started!
	setvar OVER 0
}