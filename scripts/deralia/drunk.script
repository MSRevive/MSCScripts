//Drunkard Dridmar

{
	const CHAT_AUTO_HAIL 1
	
	const CHAT_USE_CONV_ANIMS 0 //Do not use chat animations, makes him stand up.
	
	const CHAT_AUTO_FACE 0 //do not face speaker
	const CHAT_FACE_ON_USE 0
}


#include monsters/base_npc
#include monsters/base_civilian
#include monsters/base_chat_array

{ npc_spawn

	name "Drunkard"
	hp 25
	
	width 32
	height 32
	race human
	roam 0

	setmodel npc/human1.mdl

	setidleanim sitidle

	setvard SAID_HI 0
	setvard CAUGHT 0
	setvard DRUNKARD_QUEST 0
	setvard QUEST_DONE 0
}

{ game_menu_getoptions

	if ( CAUGHT == 0 )
	{ 
		local reg.mitem.title "Pickpocket"
		local reg.mitem.type callback
		local reg.mitem.callback say_rob
		menuitem.register
	}

	if ( SAID_HI == 1 )
	{
		local reg.mitem.title "Drink"
		local reg.mitem.type callback
		local reg.mitem.callback say_drink
		menuitem.register
	}		

	if ( DRUNKARD_QUEST == 1 )
	{
		if ( $item_exists(PARAM1,drink_mead) )
		{
			local reg.mitem.title "Give Mead"
			local reg.mitem.type payment
			local reg.mitem.data drink_mead
			local reg.mitem.callback give_mead
			menuitem.register
		}
	}
}

{ say_rob

	setvard ROB_ATTEMPT $rand(1,3)

	if ( ROB_ATTEMPT == 1 ) callevent rob_fail
	else callevent rob_pass
}

{ rob_pass 

	setvard Thief $get(ent_lastused,id)
	offer Thief gold 3
	setvard CAUGHT 1
}

{ rob_fail

	saytext Get your hands out of my pockets!
	setvard CAUGHT 1
	callevent bchat_mouth_move
}

{ say_hi

	if ( QUEST_DONE )
	{
		saytext "Thanks for *hic* your help, Adventurer."
		callevent chat_move_mouth 2
		setvard DRUNKARD_QUEST 0
	}
	else
	{
		saytext "*hic* Can you believe they cut me off. I really need another [drink]."
		setvard SAID_HI 1
		callevent chat_move_mouth 2.5
	}
}

{ say_drink

	callevent chat_now "Can you go inside the Inn and get me *hic*" 1.8
	callevent chat_now "I will give you some gold for your troubles." 2.0
	callevent chat_now "I would you to get me *hic* of mead." 2.0
	callevent chat_move_mouth 6
	
	setvard DRUNKARD_QUEST 1
}

{ give_mead

	saytext "Cheers! Thanks for your *hic* help adventurer."
	callevent chat_move_mouth 1.5
	
	offer PARAM1 gold 10
	setvard QUEST_DONE 1
	setvard SAID_HI 0
	
	usetrigger RenderMug
}