// Dorfgan the Smith


{
   eventname spawn

   hp 350/350
   gold 25
   name        Dorfgan
   //ov 120
   width 32
   height 72
   race human
   roam 0      //Don't go roaming around
   setmodel    npc/blacksmith.mdl
   setactionanim beatdoor

   setvar SOUND_PAIN player/chesthit1.wav
   setvar SOUND_PAIN2 player/armhit1.wav
   setvar ATTACK1_DAMAGE 3
   setvard ATTACK_RANGE 90

   setvar STORENAME dorfgans_blacksmith
   setvar SAY_SO 0
   setvar EXPLAINING_QUEST 0
   setvar QUEST_1 0
   setvar QUEST_2 0
   setvar OFFER_ITEMS 1
   setvar CANCHAT 1
   setvar CAN_SCREAM 1
   setvar CAN_RUN 1
   setvar SEE_ENEMY 0
   setvard SELL_WEAPON_LEVEL 3,6,9,12

   callevent createmystore   

   catchspeech say_hi hi
   catchspeech say_hi hello
   catchspeech say_hi hail
   catchspeech say_hi greet

   catchspeech say_quest attack
   catchspeech say_quest orc

}

{
  eventname say_hi

  saytext Adventurer!  We're under [attack]!
  calleventtimed 3 say_hi2test
}

{
  eventname say_hi2test

  var SAY_SO equals 0

  saytext I'm Dorfgan, the blacksmith

  callevent setsayso
}

{
  eventname satsayso
  setvar SAY_SO 1
}

{
  eventname say_quest

  var EXPLAINING_QUEST equals 0

  saytext Our village is being ransacked by those blasted Orcs!
  setvar EXPLAINING_QUEST 1
  calleventtimed 2 say_quest_2
}
{
  eventname say_quest_2

  saytext Mercenaries should be coming shortly to help... that is, if I can forge weapons for them.
  calleventtimed 3 say_quest_3
}
{
  eventname say_quest_3

  saytext But I've lost my hammer and can't forge anything right now.
  calleventtimed 2 say_quest_4
}
{
  eventname say_quest_4

  saytext I've lost it somewhere in the caves in the southern plains... without it, I'm afraid this village won't last long...
  setvar EXPLAINING_QUEST 0
}
//Delivering Note from Erkold, if hammer has been returned
{
  eventname recvoffer_item_erkoldsnote

  if QUEST_1 equals 0

  DLLFunc recvoffer refuse
  saytext I need my hammer to finish it. Perhaps if you found it for me...
}

{
  eventname recvoffer_item_erkoldsnote

  if QUEST_1 equals 1
  if QUEST_2 equals 0

  DLLFunc recvoffer accept
  saytext I see... well, it's all done and repaired. You'll need two hands for this one, it's heavy!
  setvar QUEST_2 1
  calleventtimed 4 givearmor
}
{
  eventname givearmor

  offer ent_lastgave armor_mongol
  offer ent_lastgave armor_helm_mongol
}
{
  eventname recvoffer_blunt_rustyhammer2

  if QUEST_1 equals 0

  DLLFunc recvoffer accept
  saytext That's my hammer! Thank you very much for bringing it back!
  setvar QUEST_1 1
  calleventtimed 4 recvhammer_2
}

{
  eventname recvhammer_2

  playanim critical pull_needle
  saytext It will be some time before the mercenaries will arrive. Please take this gold as a reward!
  calleventtimed 30 callmercs
  calleventtimed 2 recvhammer_3
}
{
  eventname recvhammer_3
  
  offer ent_lastgave gold 100
}
{
  eventname callmercs

  usetrigger spawnmercs
}
////////////////////////////////////////////////////////////
   //Attack
{
   repeatdelay 0.5

   see enemy ATTACK_RANGE

   
   setvar OFFER_ITEMS 0
   calleventtimed 5 resetoffer
   setmovedest ent_lastseen ATTACK_RANGE
   DLLFunc attack anim
}

{
   // Deal damage
   eventname attack_1

   dodamage ent_lastseen ATTACK_RANGE ATTACK1_DAMAGE 50%
}

{
   //Hit by someone
   eventname struck
   
   volume 10
   playrandomsound 2 SOUND_PAIN SOUND_PAIN2 none none
   playanim once raflinch

   callevent flee
}
{
   eventname resetoffer

   setvar OFFER_ITEMS 1   
}
///////////////////////////////////////////////////////////
// 

{
   eventname playerused

   var OFFER_ITEMS equals 1

   offerstore STORENAME buysell trade
}
{
   eventname trade_success

   var CANCHAT equals 1

   saytext  We're low on supplies, so I don't have much to offer...
   setvar CANCHAT 0
   calleventtimed 10 resetchat
}
{
   eventname resetchat

   setvar CANCHAT 1
}

///////////////////////////////////////////////////////////
// The blacksmith doesn't have much to offer since the village is being attacked and low on supplies

{
   eventname createmystore

   createstore STORENAME
   
   //           |Store name|  |Item name|  |Quantity| |cost|
   addstoreitem  STORENAME     swords_shortsword   1         100
   addstoreitem  STORENAME     swords_bastardsword   1         100
   addstoreitem  STORENAME     axes_doubleaxe   2         100
   addstoreitem  STORENAME     shields_ironshield   2         100
   callevent addrandomitems
}
{
   eventname addrandomitems
   if $rand(1,2) == 1
   addstoreitem  STORENAME     swords_longsword   	 1          200
}
{
   eventname addrandomitems
   if $rand(1,2) == 1
   addstoreitem  STORENAME     swords_scimitar   	 1          100
}

{
   eventname addrandomitems
   if $rand(1,2) == 1
   addstoreitem  STORENAME     axes_battleaxe   	 1          200
}
// -- Attack thieves --

{
  eventname robbed

  saytext Damn thief!
}
// -------------------------------------------------------------
//                      Flee from monsters
// -------------------------------------------------------------
{
   repeatdelay 0.2

   see enemy 300
   if SEE_ENEMY equals 0

   callevent flee
   callevent shiver
   callevent scream
}
{
   eventname flee
   if CAN_RUN equals 1

   setmoveanim run1
   setmovedest flee ent_lastseen 128
   setvar SEE_ENEMY 1
   setvar CAN_RUN 1
   calleventtimed $rand(0,4) resetflee
}
{
   eventname shiver
   
   playanim once crouch_idle3

   if CAN_SCREAM equals 1
   volume 8
   playsound 1 player/fallpain4.wav
   say *[100]
   calleventtimed 2 flee
}

{
   eventname stopmoving

   if SEE_ENEMY equals 1

   setvar SEE_ENEMY 0
}
{
   eventname scream

   if CAN_SCREAM equals 1

   volume 8
   playrandomsound 2 scientist/scream07.wav scientist/scream10.wav scientist/scream12.wav scientist/scream13.wav
   setvar CAN_SCREAM 0
   say *[200]
   calleventtimed $rand(2,6) resetscream
}
{
   eventname resetscream

   setvar CAN_SCREAM 1
}
{
   eventname resetflee

   setvar CAN_RUN 1
}

{
   eventname struck

   volume 8
   playsound 1 player/fallpain4.wav
   callevent flee
}
{
   eventname parry

   playanim once flinch
   callevent flee
}