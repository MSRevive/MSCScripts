//Thothie - Provided for backwards compatability with MS 1.35
//- Guess who's back :D
//- He can be given Infernal Fire Damage or Invulnerability (toggle) via ms_npcscript send:
//- warboss_godon - invincible
//- warboss_godoff - vulnerable
//- warboss_infernal - fire damage +30-60
//- warboss_normal - removes fire damage
//- Note name changes with mode, if you want to have both modes on, consider which you'd rather have

{
   setvard DROP_GOLD 1			//Gold Drops
   setvard DROP_GOLD_AMT $rand(50,250)	//
   setvard NPC_GIVE_EXP 200

   setvard ANIM_ATTACK battleaxe_swing1_L //Attack animation
   setvard FLINCH_CHANCE 35%

   setvard DROP_ITEM1 axes_greataxe
   setvard DROP_ITEM1_CHANCE 100%
   setvar INFERNAL 0

   const ATTACK_ACCURACY 80%
   const ATTACK_DMG_LOW 5
   const ATTACK_DMG_HIGH 150
}

//Manually porting orc_melee include for safty
{
   setvard MOVE_RANGE 64
   setvard ATTACK_RANGE 150
   setvard ATTACK_HITRANGE 225
   const ORC_SHIELD 0
}


   // Deal damage
   // Called from the animation on the attack frame
{  swing_axe

	callevent baseorc_yell

   	local L_DMG $randf(ATTACK_DMG_LOW,ATTACK_DMG_HIGH)
   	dodamage ent_lastseen ATTACK_HITRANGE L_DMG ATTACK_ACCURACY

	if ( INFERNAL == 1 ) applyeffect ent_laststruckbyme effects/dot_fire 5 ent_me $rand(30,60)
}

{ swing_sword

	callevent swing_axe
}

#include monsters/orc_base

{  orc_spawn

   	hp 1250
	//ov 360
	width 32
	height 60
	name the|Ghost Graznux the Warboss
   	hearingsensitivity 8
   	setstat parry 30
	setstat swordsmanship 10 25 20
	takedmg all .8
	invincible 0
	rendermode	5		//thothie experiments
	renderamt	255

	setprop ent_me rendermode 5
	setprop ent_me renderamt 200

   	setmodelbody	0 2		//Body 0=naked 2=leather 3=plate 4=green-hide
   	setmodelbody	1 3		//Head 0=cap_shorthorn 1=cap_nohorn 2=buffo_skin 3=cheif_ramhorns 4=green_hood
   	setmodelbody	2 5		//Weapon 0=fist 1=axe 2=bow_weak 3=bow_strng 4=sword 5=bloodyaxe 6=sword&sheild
}

{ warboss_godoff

 name Graznux the Warboss
 invincible 0
 saytextrange 1024
 saytext What have you done to me!?
}

