#scope server
{ [server] validate_spawn

	local LASTMAP_IN PARAM1
	local LASTMAP_IN $lcase(LASTMAP_IN)

	//force players to travel maps in known gauntlet series
	//at least the first player on the map must come from one of the valid maps
	//other players may still join

	//this script only runs on players until one makes a valid spawn

	setvard THIS_MAP $lcase(game.map.name)

	//problem with this running before map fully loaded
	if ( !FIRST_RUN ) 
	{
		setvard FIRST_RUN 1
		local FIRST_EXIT 1
	}
	if !FIRST_EXIT

	setvard GAUNTLET_MAP 0

	if THIS_MAP isnot ''


	//dbg Validating spawn from LASTMAP_IN to THIS_MAP

	//======================================== DEFINE GAUNTLET MAPS

	//Nashalrath
	if ( THIS_MAP equals nashalrath ) 
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 daragoth
		setvard VALID_START_MAP daragoth
	}

	//The Wall
	if ( THIS_MAP equals the_wall2 ) 
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 the_wall
		setvard VALID_START_MAP the_wall
	}

	//COTBG
	if ( THIS_MAP equals highlands_msc ) 
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 lowlands
		setvard GAUNTLET_VALID2 lostcastle_msc
		setvard VALID_START_MAP lowlands
	}
	if ( THIS_MAP equals lostcastle_msc )
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 highlands_msc
		setvard GAUNTLET_VALID2 skycastle
		setvard GAUNTLET_VALID3 Skycastle //this bit redundant thanks to $lcase
		setvard START_MAP lowlands
		setvard VALID_START_MAP lowlands
	}
	if ( THIS_MAP equals lostcastle_msc )
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 highlands_msc
		setvard GAUNTLET_VALID2 Skycastle //this bit redundant thanks to $lcase
		setvard GAUNTLET_VALID3 skycastle
		setvard VALID_START_MAP lowlands
	}
	if ( THIS_MAP equals skycastle )
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 lostcastle_msc
		setvard VALID_START_MAP lowlands
	}
	if ( THIS_MAP equals orcplace2_beta )
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 lostcastle_msc
		setvard VALID_START_MAP lowlands
	}

	//World Walker, set 1
	if ( THIS_MAP equals ww2b )
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 ww1
		setvard VALID_START_MAP ww1
	}
	if ( THIS_MAP equals ww3d )
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 ww2b
		setvard VALID_START_MAP ww1
	}

	if ( THIS_MAP equals ww3 )
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 ww2
		setvard VALID_START_MAP ww1
	}

	//keledros ruins
	if ( THIS_MAP equals keledrosruins )
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 keledrosprelude2keledrosruins
		setvard GAUNTLET_VALID2 keledrosprelude2
		setvard VALID_START_MAP keledrosprelude2
		//LAST_MAP comes out: keledrosprelude2keledrosruins ?
	}

	//old_helena
	if ( THIS_MAP equals old_helena )
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 helena
		setvard VALID_START_MAP helena
		//LAST_MAP comes out: keledrosprelude2keledrosruins ?
	}

	//lodagond
	if ( THIS_MAP equals lodagond-2 )
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 lodagond-1
		setvard VALID_START_MAP lodagond-1
	}
	if ( THIS_MAP equals lodagond-3 )
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 lodagond-2
		setvard VALID_START_MAP lodagond-1
	}

	if ( THIS_MAP equals lodagond-4 )
	{
		setvard GAUNTLET_MAP 1
		setvard GAUNTLET_VALID1 lodagond-3
		setvard VALID_START_MAP lodagond-1
	}

	if ( GAUNTLET_MAP ) 
	{
		//dbg ********** GAUNTLET MAP - Spawning is restricted! **********
		setvarg VALID_SPAWN 0
		if ( $get_map_legit(ent_me) equals 0 ) setvard INVALID_SPAWN 1
		if ( LASTMAP_IN equals THIS_MAP )
		{
			if ( $get_quest_data(ent_me,mv) equals THIS_MAP )
			{
				setvard INVALID_SPAWN 1
			}
			else
			{
				setvard INVALID_SPAWN 0
			}
		}
	}


	//======================================== 


	if ( G_DEVELOPER_MODE )
	{
		setvard INVALID_SPAWN 0
		setvard GAUNTLET_MAP 0
	}

	if ( GAUNTLET_MAP )
	{
		if !G_VALID_SPAWN
		setvard INVALID_SPAWN 1
		//this is no good, cuz you can just warp from map to map
//		if ( LASTMAP_IN equals GAUNTLET_VALID1 ) setvard INVALID_SPAWN 0
//		if ( LASTMAP_IN equals GAUNTLET_VALID2 ) setvard INVALID_SPAWN 0
//		if ( LASTMAP_IN equals GAUNTLET_VALID3 ) setvard INVALID_SPAWN 0
//		if ( LASTMAP_IN equals GAUNTLET_VALID4 ) setvard INVALID_SPAWN 0

		//pull last map player was on from quest data
		//if ( !INVALID_SPAWN ) //dbg Traveler from LASTMAP_IN heading to QUEST_MAP
		//if ( INVALID_SPAWN ) //dbg Lastmap valid failed, attempting QUEST_MAP vs THIS_MAP (or valid connectors)
		local QUEST_MAP $get_quest_data(ent_me,m) //single letter quest to save on character size
		local QUEST_MAP $lcase(QUEST_MAP)
		if ( QUEST_MAP equals THIS_MAP ) setvard INVALID_SPAWN 0
		//this is no good, as map becomes valid if you spawn inside transition
//		if ( QUEST_MAP equals GAUNTLET_VALID1 ) setvard INVALID_SPAWN 0
//		if ( QUEST_MAP equals GAUNTLET_VALID2 ) setvard INVALID_SPAWN 0
//		if ( QUEST_MAP equals GAUNTLET_VALID3 ) setvard INVALID_SPAWN 0
//		if ( QUEST_MAP equals GAUNTLET_VALID4 ) setvard INVALID_SPAWN 0

		//Legit check claims valid if you repeat the changelevel to the same map, so removing this
		//if ( PARAM2 equals 1 ) 
		//{
		//	//dbg Legit check says map is valid.
		//	setvard INVALID_SPAWN 0
		//}

		if ( INVALID_SPAWN )
		{
			setvard VALID_MAP1 GAUNTLET_VALID1
			if ( GAUNTLET_VALID2 isnot 'GAUNTLET_VALID2' ) setvard VALID_MAP2 GAUNTLET_VALID2
			if ( GAUNTLET_VALID3 isnot 'GAUNTLET_VALID3' ) setvard VALID_MAP3 GAUNTLET_VALID3
			if ( GAUNTLET_VALID4 isnot 'GAUNTLET_VALID4' ) setvard VALID_MAP2 GAUNTLET_VALID4
			setvard MSG_COUNTER 0
			callevent build_valid_list
			callevent gauntlet_invalid_Loop
			callevent 300.0 reset_server
			local EXIT_SUB 1
		}
	}
	if !EXIT_SUB
	if !INVALID_SPAWN

	//dbg Spawn valid, no more validation checks.
	setvarg G_VALID_SPAWN 1
	if ( GAUNTLET_MAP ) quest set ent_me mv THIS_MAP
}

{ gauntlet_invalid_Loop

	//make this an effect

	//dbg Checking against G_VALID_SPAWN 

	if ( G_DEVELOPER_MODE ) setvarg G_VALID_SPAWN 1

	if ( G_VALID_SPAWN == 1 )
	{
		infomsg ent_me "Redeemed" A player who traveled to this map legally has joined.
		quest set ent_me mv $lcase(game.map.name)
		setorigin ent_me PLR_PRECHEAT_POS
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	applyeffect ent_me effects/cheater_freeze 4.9

	setorigin ent_me (-20000,-20000,-20000)
	effect screenfade ent_me 0.9 5.0 (10,10,10) 255 noblend

	add MSG_COUNTER 1
	if ( MSG_COUNTER == 1 ) 
	{
		playermessage ent_me Gauntlet or Boss Map: you cannot warp here. You must travel from one of these maps:
		infomsg ent_me "Gauntlet or Boss Map" You have reached this map illegally, you will not be allowed to spawn here.
	}
	if ( MSG_COUNTER == 2 ) 
	{
		setvard MSG_COUNTER 0
		playermessage ent_me connected maps: VALID_LIST
		if ( VALID_START_MAP equals lowlands ) infomsg ent_me "Curse of the Bear Gods Series" The first map in this series is Lowlands, you may begin there.
		if ( VALID_START_MAP equals lodagond-1 ) infomsg ent_me "Lodagond Series" The first map in this series is Lodagond-1, you may begin there.
		if ( VALID_START_MAP equals ww1 ) infomsg ent_me "World Walker I Series" The first map in this series is WW1, you may begin there.
		if ( THIS_MAP equals keledrosruins ) infomsg ent_me "Keledros Ruins" You must reach this map from Keledrosprelude2

		if ( !STARTED_VOTE ) callevent 5.0 start_vote
	}

	if G_VALID_SPAWN == 0 
	callevent 5.0 gauntlet_invalid_Loop
}

{ build_valid_list

	setvard VALID_LIST VALID_MAP1

	if ( VALID_LIST isnot 'VALID_LIST' )
	{
		local CONNECTOR ", "
		if VALID_MAP2 isnot 'VALID_MAP2'
		stradd VALID_LIST CONNECTOR
		stradd VALID_LIST VALID_MAP2
		if VALID_MAP3 isnot 'VALID_MAP3'
		stradd VALID_LIST CONNECTOR
		stradd VALID_LIST VALID_MAP3
		if VALID_MAP4 isnot 'VALID_MAP4'
		stradd VALID_LIST CONNECTOR
		stradd VALID_LIST VALID_MAP4
	}
	if THIS_MAP isnot keledrosruins
	stradd VALID_LIST " (start at: "
	stradd VALID_LIST VALID_START_MAP
	stradd VALID_LIST ")"
}

{ start_vote

	if !G_VALID_SPAWN
	setvard STARTED_VOTE 1
	setvard VOTE_TRIG touch_trans_
	local VOTE_SUFFIX VALID_START_MAP
	stradd VOTE_TRIG VOTE_SUFFIX
	usetrigger VOTE_TRIG
	callevent 60.0 reset_vote
}

{ reset_vote
	//vote again every 65 secs
	setvard STARTED_VOTE 0
}

{ reset_server

	if !G_VALID_SPAWN
	//five minutes, either no one is here, or no one has figured it out - reset server via amx
	usetrigger force_map_edana
	callevent 10.0 manual_changelevel
}

{ manual_changelevel

	if !G_VALID_SPAWN
	//this work?
	callexternal GAME_MASTER gm_manual_map_change edana
}
