//elemental resistance system, again, again.
//- item sends the element and % resistance, and take a unique name for register/unregistering its effect
//- similarly, titles and negative effects should be registerable this way

#scope server

{
	if ( !$get_array_exists(PLR_RESIST_NAMES) ) //will be -1 if the array doesnt exist. if it doesnt, create them.
	{
		array.create PLR_RESIST_NAMES
		array.create PLR_RESIST_TYPES
		array.create PLR_RESIST_AMTS
		
		array.create PLR_RESIST_WEAPON_IDS
		array.create PLR_RESIST_WEAPON_TAGS
	}
	
	setvard PLR_RESIST_ELEMENTS "fire;lightning;cold;earth;poison;acid;holy;dark;magic;slash;blunt;pierce;all"
	setvard PLR_RESIST_VALUES "1.0;1.0;1.0;1.0;1.0;1.0;1.0;1.0;1.0;1.0;1.0;1.0;1.0"

	setvard PLR_CHECK_WEAPON_RESIST 0

	const PLR_RESIST_RESET "1.0;1.0;1.0;1.0;1.0;1.0;1.0;1.0;1.0;1.0;1.0;1.0;1.0"
}

{ ext_register_element //<name> <element|remove> <% resistance>

	local RESIST_NAME PARAM1 //unique name of the registered element
	local RESIST_TYPE PARAM2 //element type or "remove" to remove it
	local RESIST_AMT PARAM3 //% resistance
	
	local FIND_NAME $get_arrayfind(PLR_RESIST_NAMES,RESIST_NAME)
	
	//infomsg all "ext_register_element" RESIST_NAME RESIST_TYPE RESIST_AMT FIND_NAME

	if ( RESIST_TYPE equals remove )
	{
		//remove a registered resistance
		if ( FIND_NAME == -1 )
		{
			infomsg all "Element not registered:" RESIST_NAME
		}
		else
		{
			array.del PLR_RESIST_NAMES FIND_NAME
			array.del PLR_RESIST_TYPES FIND_NAME
			array.del PLR_RESIST_AMTS FIND_NAME
			callevent update_resistances
		}
	}
	else
	{
		multiply RESIST_AMT 0.01 //% to float

		if ( FIND_NAME != -1 ) //existing name?
		{
			//adjust the value of an existing array
			local OLD_VALUE $get_arrayfind(PLR_RESIST_AMTS,FIND_NAME)
			if ( OLD_VALUE != RESIST_AMT )
			{
				array.set PLR_RESIST_AMTS FIND_NAME RESIST_AMT //JAN2010_15 - AUG! Bug here, fixed (forgot FIND_NAME)
			}
		}
		else
		{
			//register a new elemental resistance
			array.add PLR_RESIST_NAMES RESIST_NAME
			array.add PLR_RESIST_TYPES RESIST_TYPE
			array.add PLR_RESIST_AMTS RESIST_AMT
			//dbg ext_register_element add RESIST_TYPE RESIST_AMT - PLR_RESIST_AMTS
		}

		callevent update_resistances
	}
}

{ update_resistances

	setvard PLR_RESIST_UPDATE_FLAG 0
	setvard OLD_RESISTANCES PLR_RESIST_VALUES //for notifying of changes
	setvard PLR_RESIST_VALUES PLR_RESIST_RESET //reset all resists to 1.0
	calleventloop $get_array_amt(PLR_RESIST_NAMES) cat_resistances //adjust resistances based on registered resist names
	calleventloop $get_token_amt(PLR_RESIST_VALUES) apply_resistances //actualy set takedmg levels, and notify of differences
}

{ cat_resistances //adjust resistances based on registered resist names

	local CUR_IDX game.script.iteration
	local ITEM_RESIST_TYPE $get_array(PLR_RESIST_TYPES,CUR_IDX) //registered item/effect's element resist type
	local ITEM_RESIST_AMT $get_array(PLR_RESIST_AMTS,CUR_IDX) //registered item/effect's element resist amount
	local ITEM_RESIST_AMT $neg(ITEM_RESIST_AMT) //we're actually subtracting from 1 (75% resistance, = 1 - 0.75 = 0.25 )
	local CUR_RESIST_IDX $get_find_token(PLR_RESIST_ELEMENTS,ITEM_RESIST_TYPE) //find index of item's element resist type on player
	local CUR_RESIST_AMT $get_token(PLR_RESIST_VALUES,CUR_RESIST_IDX) //player's current resistance level

	//dbg cat_resistances # CUR_IDX it_type ITEM_RESIST_TYPE it_amt ITEM_RESIST_AMT cur_rst CUR_RESIST_AMT

	add CUR_RESIST_AMT ITEM_RESIST_AMT
	if ( CUR_RESIST_AMT < 0 ) local CUR_RESIST_AMT 0
	token.set PLR_RESIST_VALUES CUR_RESIST_IDX CUR_RESIST_AMT
}

{ apply_resistances

	local CUR_IDX game.script.iteration
	local CUR_RESIST_TYPE $get_token(PLR_RESIST_ELEMENTS,CUR_IDX)
	local CUR_RESIST_AMT $get_token(PLR_RESIST_VALUES,CUR_IDX)
	takedmg CUR_RESIST_TYPE CUR_RESIST_AMT

	local OLD_RESIST_AMT $get_token(OLD_RESISTANCES,CUR_IDX)

	if ( CUR_RESIST_AMT isnot OLD_RESIST_AMT )
	{
		//resistance has changed, notify player
		//convert to %
		multiply CUR_RESIST_AMT 100
		subtract CUR_RESIST_AMT 100
		local CUR_RESIST_AMT $neg(CUR_RESIST_AMT)
		local CUR_RESIST_AMT $int(CUR_RESIST_AMT)
		stradd CUR_RESIST_AMT "%"
		if ( !ELM_REGISTER_SILENT )
		{
			gplayermessage ent_me "Your resistance to" CUR_RESIST_TYPE "is now" CUR_RESIST_AMT
		}
		setvard ELM_REGISTER_SILENT 0
	}
}

{ ext_register_weapon //<weapon_id> <tag> <element|remove> [%amt]

	local WEAPON_ID PARAM1
	local OUT_TAG PARAM2
	local OUT_ELM PARAM3
	local OUT_AMT PARAM4

	if ( PARAM3 isnot remove )
	{
		if ( $get_arrayfind(PLR_RESIST_WEAPON_IDS,PARAM1) == -1 )
		{
			callevent ext_register_element OUT_TAG OUT_ELM OUT_AMT
			array.add PLR_RESIST_WEAPON_IDS WEAPON_ID
			array.add PLR_RESIST_WEAPON_TAGS OUT_TAG
		}
	}
	else
	{
		callevent ext_register_element OUT_TAG remove
		local WEAPON_IDX $get_arrayfind(PLR_RESIST_WEAPON_IDS,PARAM1)
		array.del PLR_RESIST_WEAPON_IDS WEAPON_IDX
		array.del PLR_RESIST_WEAPON_TAGS WEAPON_IDX
	}

	if ( $len(PLR_RESIST_WEAPON_IDS) >= 3 )
	{
		if !PLR_CHECK_WEAPON_RESIST
		setvard PLR_CHECK_WEAPON_RESIST 1
		callevent check_weapons_loop
	}
	else
	{
		setvard PLR_CHECK_WEAPON_RESIST 0
	}
}

{ check_weapons_loop

	if PLR_CHECK_WEAPON_RESIST
	callevent 5.0 check_weapons_loop
	if PLR_IN_WORLD
	if $get(ent_me,isalive)
	local N_RESIST_WEAPONS $get_array_amt(PLR_RESIST_WEAPON_IDS)
	if ( N_RESIST_WEAPONS > 0 )	calleventloop $get_array_amt(PLR_RESIST_WEAPON_IDS) check_weapons
	if ( $len(PLR_RESIST_WEAPON_IDS) < 3 ) setvard PLR_CHECK_WEAPON_RESIST 0
}

{ check_weapons

	local CUR_WEAPON $get_array(PLR_RESIST_WEAPON_IDS,game.script.iteration)
	if ( CUR_WEAPON equals PLR_LEFT_HAND ) local NO_REMOVE 1
	if ( CUR_WEAPON equals PLR_RIGHT_HAND ) local NO_REMOVE 1
	if !NO_REMOVE
	callevent ext_register_element $get_array(PLR_RESIST_WEAPON_TAGS,CUR_WEAPON) remove
	array.del PLR_RESIST_WEAPON_IDS CUR_WEAPON
	array.del PLR_RESIST_WEAPON_TAGS CUR_WEAPON
}

{ game_death //Removes temporary resistances after player dies.

	callevent ext_register_element firep remove
	callevent ext_register_element coldp remove
	callevent ext_register_element ligip remove
	callevent ext_register_element firip remove
	callevent ext_register_element colip remove
	callevent ext_register_element poiip remove
}

