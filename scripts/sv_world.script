//Manages the game world
//
//Useful for controlling ongoing global events
//
//Thothie - this is now used for central map configs

//include weather/sv_weather_control
#include sv_world_commands
#include sv_world_time
#include sv_world_vote

#include world/player_vote
#include world/help

#include secondary_globals

{
	setvarg MAPS_HIDDEN "challs;keledrosruins;nashalrath;rmines;fmines;underpath;undercliffs"
	setvarg MAPS_MAZE "goblintown"
	setvarg MAPS_GAUNTLET "highlands_msc;lostcastle_msc;skycastle;orcplace2_beta;ww2b;ww3d;old_helena;lodagond-2;lodagond-3;lodagond-4;nashalrath;the_wall2"
	setvarg MAPS_GAUNTLET_START "lowlands;lodagond-1;ww1;the_wall;old_helena;nashalrath"

	setvarg MAPS_UNCONNECTEDS 2
	setvarg MAPS_UNCONNECTED1 "ms_quest;char_recover;guildmaster;cleicert;foutpost;lodagond-1;island1;lostcaverns;ms_underworldv2;orc_arena;pvp_archery;pvp_arena;unrest;unrest2;unrest2_beta1;ww1;pvp_canyons;canyons;ocean_crossing;smugglers_cove;isles_dread1;"
	setvarg MAPS_UNCONNECTED2 "kfortress;gertenheld_cave;islesofdread2_old;the_wall;catacombs;bloodshrine;ms_soccer;shender_east;nightmare_edana;m2_quest;gertenhell"

	//^ also note that global array in \globals.script

	setvarg G_NOT_ON_FN "test_scripts"

	//reset globals
	//NOV2009_07 - Thothie, moving this up here to prevent overriding of map_startup files
	setvarg global.map.weather "clear;clear;clear" //FEB2009_17 - default to clear weather
	setvarg G_OVERRIDE_WEATHER_CODE 0 //NOV2009_07 - Weather issues
	setvarg G_NO_STEP_ADJ 0 //no step size adjustment for monsters on this map when set 1
	setvarg G_MAP_NAME 'G_MAP_NAME' //map file name
	setvarg G_MAP_DESC 'G_MAP_DESC' //map description
	setvarg G_MAP_DIFF 'G_MAP_DIFF' //map difficulty 
	setvarg G_WARN_HP 'G_WARN_HP' //hit point warning level
	setvarg G_HELP_ON 0 //help mode on
	setvarg G_CRITICAL_NPCS '' //n of critical NPCs on siege map
	setvarg G_SIEGE_MAP 'G_SIEGE_MAP' //map in siege mode
	setvarg G_FORCE_SPAWN_WEATHER 'G_FORCE_SPAWN_WEATHER' //??
	setvarg G_EXP_MULTI 1.0 //global experience multiplier - set to 1.0 to enable XP multi for dmgmulti/hpmulti monsters
	setvarg G_NO_DROP 0 //no drop treasure
	setvarg G_UNDAMAEL_VULNERABLE 0 //undamael vulnerability flag when quest done proper
	setvarg G_SHAD_PRESENT 0 //shaddahr present to convert orcs to zombies?
	setvarg G_SORC_NEXT_TELE 0 //AUG2009 - time to next allowed sorc teleport
	setvarg G_MUMMY_NEXT_REBIRTH 0 //SEP2009 - next time a mummy pinned by a dead comerade can rebirth
	setvarg G_SKELE_NEXT_REBIRTH 0 //SEP2009_14 - next time a skeleton (archer) pinned by a dead comerade can rebirth
	setvarg G_GUARDIAN_CHARGER 'G_GUARDIAN_CHARGER' //NOV2009_07 - mark position for guardian charger (see monsters/guardian_iron [_charger])
	setvarg G_SORC_TELE_POINTS 0 //DEC2009_10 - sorc teleporter system - number of teleporters
	setvarg G_SORC_CHIEF_PRESENT 0 //JAN2010_01 - sorc chief present flag for sorc_palace
	setvarg G_TELF_ESCORTS 0 //JAN2010_01 - torkie escort counter for the_wall
	setvarg G_ONE_SHOT 0 //JAN2010_10 - shad palace talk counter
	setvarg G_WEATHER_LOCK 0 //JAN2010 - allow weather to be locked (monster blackness, for instance)
	setvarg G_NPC_COMBAT_MAP 0 //FEB2010_06 - used to flag a map with heavy NPC vs NPC fighting - causes all NPCs to cycle full when in combat with one another, as if their opponents were players
	setvarg G_TELF_LEADER_COUNTER 0 //FEB2010_10 - for the_wall
	setvarg G_CURRENT_WEATHER 0 //NOV2010_28 holds current weather type
	setvarg G_TRACK_DEATHS 0 //DEC2010_03 track monster deaths for challenges
	setvarg G_TRACK_DEATHS_TRIGGER 0 //level at which to trigger next stage of challenge
	setvarg G_TRACK_DEATHS_EVENT 0 //GM event to call when achievement level reached
	setvarg G_SERVER_LOCKED 0 //votelock flag
	setvarg G_SADJ_DEATHS 0 //self-adjusting death tracker (#killed)
	setvarg G_SADJ_LEVELS 0 //self-adjusting death tracker (total levels)
	setvarg G_DARKEN_BLOOM 1 //darken bloom level, adjusted by map/area
	setvarg G_GAVE_TOME1 0 //treasure tracker
	setvarg G_GAVE_TOME2 0 //treasure tracker
	setvarg G_GAVE_TOME3 0 //treasure tracker
	setvarg G_GAVE_TOME4 0 //treasure tracker
	setvarg G_GAVE_TOME5 0 //treasure tracker
	setvarg G_GAVE_TOME6 0 //treasure tracker
	setvarg G_GAVE_TOME7 0 //treasure tracker
	setvarg G_GAVE_ARTI1 0 //treasure tracker
	setvarg G_LAST_GABE_TARGET 0 //for the gabe gag
	setvarg G_APRIL_FOOLS_MODE 0 //ditto
	setvarg G_DEVELOPER_MODE 0 //reset dev mode, just in case
	setvarg G_TRACK_HP 0 //set to 1 track average player HP on the server with each kill
	setvarg G_TRACK_KILLS 0
	setvarg G_LAST_VICTORY 0 //for victory sounds, so not all monsters make the same chear at once
	setvarg G_NPC_SUMMON_COUNT 0 //tracks summons for some npcs
	setvarg G_FLAMES_EAGLES 0 //tracks eagles on phlames map (stops vines from spawning while they are about)
	setvarg G_GENERAL_USE1 0
	setvarg G_GENERAL_USE2 0
	setvarg G_GENERAL_USE3 0
	setvarg G_ALERT_CYCLE 0 //stops some monsters from using the same alert anim/sound at same time
	setvarg G_SPECIAL_COMMANDS 0 //special commands for special maps
	setvarg G_CHEST_TRACKER 0 //for number of self-adjust chests found on a map
}

{ game_spawn

	//xmass mode
	if ( game.time.month == 12 )
	{
		//Thothie JAN2010_11 - Extending Xmass mode to all of December
		setvarg G_CHRISTMAS_MODE 1
	}

	//set global map settings based on world_spawn entity
	if ( game.map.title isnot 0 )
	{
		if game.map.title isnot none
		if ( G_MAP_NAME equals 'G_MAP_NAME' ) setvarg G_MAP_NAME game.map.title
	}
	if ( game.map.desc isnot 0 )
	{
		if game.map.desc isnot none
		if ( G_MAP_DESC equals 'G_MAP_DESC' ) setvarg G_MAP_DESC game.map.desc
	}
	if ( game.map.hpwarn isnot 0 ) setvarg G_WARN_HP game.map.hpwarn
	if ( game.map.weather isnot 0 )
	{
		//FEB2009_17 (also 21) - attempts to fix the last of the sticky weather bugs
		if ( G_OVERRIDE_WEATHER_CODE isnot '0' ) local OVERRIDE_WEATHER 1
		if ( G_OVERRIDE_WEATHER_CODE equals 'G_OVERRIDE_WEATHER_CODE' ) local OVERRIDE_WEATHER 0
		if ( OVERRIDE_WEATHER )
		{
			setvarg global.map.weather G_OVERRIDE_WEATHER_CODE
		}
		else
		{
			setvarg global.map.weather game.map.weather
		}
	}

	//reset game master
	setvarg G_MAP_UPTIME 0
	setvarg G_NGAME_MASTERS 0
	setvarg GAME_MASTER 0
	local GAME_MASTER_CLEANUP $get_by_name(game_master)
	local GAME_MASTER_CLEANUP_ID $get(GAME_MASTER_CLEANUP,id)
	if ( $get(GAME_MASTER_CLEANUP_ID,exists) ) deleteent GAME_MASTER_CLEANUP_ID

	//causes crash here
	//if ( !$get(GAME_MASTER,exists) ) createnpc game_master $vec(20000,-10000,-20000)
	

	//reload globals
	callevent load_globals

	//===================================== HANDLE INDIVIDUAL MAP SETTINGS [BEGIN]
	//(in some cases easier than going through each map_startup.cfg)
	local L_MAP_NAME $lcase(game.map.name)

	setvarg G_EXP_MULTI 1.0 //not currently used by any maps, but available

	if ( L_MAP_NAME equals cleicert ) setvarg G_DARKEN_BLOOM 6
	if ( L_MAP_NAME equals oceancrossing ) setvarg G_DARKEN_BLOOM 6
	if ( L_MAP_NAME equals ms_snow ) setvarg G_DARKEN_BLOOM 20
	if ( L_MAP_NAME equals char_recover ) setvarg G_DARKEN_BLOOM 6
	if ( L_MAP_NAME equals catacombs ) setvarg G_DARKEN_BLOOM 0
	if ( L_MAP_NAME equals sfor ) setvarg G_DARKEN_BLOOM 0
	if ( L_MAP_NAME equals orc_for ) setvarg G_DARKEN_BLOOM 0
	if ( L_MAP_NAME equals calruin2 ) setvarg G_DARKEN_BLOOM 0
	if ( L_MAP_NAME equals bcastle ) setvarg G_DARKEN_BLOOM 0
	if ( L_MAP_NAME equals nightmare_thornlands ) setvarg G_DARKEN_BLOOM 6
	if ( L_MAP_NAME equals dragooncaves ) setvarg G_DARKEN_BLOOM 0
	if ( L_MAP_NAME equals the_keep ) setvarg G_DARKEN_BLOOM 0

	if ( L_MAP_NAME equals deraliasewers ) setvarg G_TRACK_HP 1
	

	if ( L_MAP_NAME equals ms_underworldv2 )
	{
		setvarg G_FORCE_SPAWN_WEATHER 'fog_red'
	}

	if ( L_MAP_NAME equals old_helena )
	{
		setvarg G_NO_DROP 1
		setvarg G_EXP_MULTI 1
		setvarg G_SIEGE_MAP 1 //1 in 3 monsters set to ignore players
	}

	if ( L_MAP_NAME startswith 'pvp' ) callevent 5.0 set_pvp

	if ( L_MAP_NAME equals 'ms_soccer' )
	{
		setvarg G_SPECIAL_COMMANDS 1
		//callevent 5.0 set_pvp
	}

	if ( L_MAP_NAME equals 'nashalrath' )
	{
		setvarg G_DARKEN_BLOOM 0
	}

	if ( L_MAP_NAME equals 'rmines' )
	{
		setvarg G_TRACK_DEATHS 1
		setvarg G_TRACK_DEATHS_TRIGGER 200
		setvarg G_TRACK_DEATHS_EVENT gm_map_rmines_killspawns
	}

	if ( L_MAP_NAME equals 'fmines' )
	{
		setvarg G_TRACK_DEATHS 1
		setvarg G_TRACK_DEATHS_TRIGGER 200
		setvarg G_TRACK_DEATHS_EVENT gm_map_fmines_killspawns
	}

	//===================================== HANDLE INDIVIDUAL MAP SETTINGS [END]
}

{ set_pvp
	dbg set_pvp 1
	setcvar ms_pklevel 1
	setpvp 1
}

// Called when map ends

{ game_end

	//this does not fire
	//errors in code
	//MSGameEnd --> Call MSGlobals::EndMap
	//MSGlobals::NewMap --> MSGlobals::GameScript already allocated!
	//dbg sv_world game_end game.map.name PARAM1 PARAM2 PARAM3 PARAM4
}

// Called when a player joins

{ game_playerjoin //PARAM1=PlayerID

	//set global HP
	//if ms_difficulty is set to 1, difficulty is determined by total HP of players on server
	//every 400 hp's ramps difficulty by 1 by default
	//on maps that have an HP warning, every HP_WARNING hp will ramp the diff by one
	local HP_TICK G_WARN_HP
	if ( G_WARN_HP == 0 ) local HP_TICK 400
	local TOTAL_HP game.players.totalhp //check hard code
	divide TOTAL_HP HP_TICK
	setvarg G_HP_RAMP $int(TOTAL_HP)
	dbg $get(PARAM1,name) joined, HP total is now TOTAL_HP ramp G_HP_RAMP

	//code side ramping
	//set ramplevel here
	//use ramplevel to multiply hp under the hp command
	//and set dmgmulti if == 0
}

{ game_playerleave

	dbg sv_world game_playerleave $get(PARAM1,name)
	callexternal all player_left $get(PARAM1,id)

	if ( G_SERVER_LOCKED )
	{
		if game.players == 4 //will become 3 after this player leave finishes, i think
		setvarg G_SERVER_LOCKED 0
		local MSG_DESC $get(PARAM1,name)
		stradd MSG_DESC " has left the server."
		infomsg all "SERVER IS NO LONGER LOCKED" MSG_DESC

		local SV_CMD "sv_password "
		stradd SV_CMD $quote()
		stradd SV_CMD $quote()
		servercmd SV_CMD
	}
}

//(Not in use)
//{ game_singletouch
//	//sparimenting
//	////dbg game_singletouch(sv_world) $get(PARAM1,name) PARAM2 PARAM3 PARAM4
//}
