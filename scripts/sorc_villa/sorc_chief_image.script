#scope server
{
	const CHAT_USE_CONV_ANIMS 0
	setvard CHAT_MENU_ON 0
	const CHAT_AUTO_FACE 0
	const CHAT_NO_CLOSE_MOUTH 1
}

#include monsters/base_chat_array

{ [shared] game_spawn

	name Runegahr the Warchief
	race orc
	nopush 1

	invincible 1

	setmodel monsters/sorc.mdl
	setmodelbody 0 2 //body: 0=bare 1=bloody_shirt 2=gold_armor 3=archer
	setmodelbody 1 3 //head: 0=hat_horns 1=hat_spiked 2=lizardskin_horned 3=gold_chieften 4=lizard_cap
	setmodelbody 2 0 //weapon: 0=unarmed 1=axe 2=bow 3=steel_bow 4=sword 5=great_axe 6=sword_sheild 8=yellow_axe 9=blooddrinker
	setstat parry 150
	width 32
  	height 96

	setidleanim idle1
	setmoveanim idle1

	setprop ent_me rendermode 5
	setprop ent_me renderamt 0

	roam 0

	saytextrange 1024
}

{ game_dynamically_created

	setvard POT_GUY_ID $get(PARAM1,id)
	callexternal POT_GUY_ID ext_face_chief
	callevent 0.01 tele_in
}

{ tele_in
	clientevent new all effects/sfx_repulse_burst $get(ent_me,origin) 64 1.0
	playsound 1 10 weather/Storm_exclamation.wav
	setmovedest POT_GUY_ID 9999
	setvard FADE_COUNT 0
	callevent fade_in

	//bchat array causing odd stuff here
	//callevent chat_add_text rant_off "Easkhar! You sniveling swine! These are MY guests, and you WILL treat them as such!" 3.0 swordswing1_L berate_done1
	//callevent chat_add_text done_here "My apologies, this unenlightened thakwaw will aid you now, should you wish it." 3.0 nod_yes tele_out1

	callevent 0.5 do_rant
}

{ fade_in
	if ( FADE_COUNT <= 255 )
	{
		add FADE_COUNT 25
		setprop ent_me renderamt FADE_COUNT
		callevent 0.1 fade_in
	}
	else
	{
		setprop ent_me renderamt 255
		setprop ent_me rendermode 1
	}
}

{ do_rant
	setmovedest POT_GUY_ID 9999
	callevent 1.0 do_rant2
}

{ do_rant2
	setmovedest POT_GUY_ID 9999
	callevent chat_now "Easkhar! You sniveling swine! These are MY guests, and you WILL treat them as such!" 3.0 swordswing1_L
	callevent 3.0 get_response
}

{ get_response
	callexternal POT_GUY_ID ext_chief_done_berating
	callevent 8.0 says_sorries
}

{ says_sorries
	setvard NEAREST_PLAYER $get_insphere(player,512)
	setmovedest NEAREST_PLAYER 9999

	callevent chat_now "My apologies, this unenlightened thakwaw will aid you now, should you wish it." 3.0 nod_yes
	callevent 3.0 tele_out1
}

{ tele_out1
	clientevent new all effects/sfx_repulse_burst $get(ent_me,origin) 64 1.0
	setprop ent_me rendermode 5
	callevent fade_out
	callevent 3.5 tele_out_done
}

{ tele_out_done
	dbg tele_out2 sending event to $get(POT_GUY_ID,name)
	callexternal POT_GUY_ID ext_chief_gone
}

{ fade_out
	if ( FADE_COUNT > 0 )
	{
		subtract FADE_COUNT 25
		setprop ent_me renderamt FADE_COUNT
		callevent 0.1 fade_out
	}
	else
	{
		setprop ent_me renderamt 0
		callevent 5.0 remove_script
	}
}

{ remove_script
	deleteent ent_me
}