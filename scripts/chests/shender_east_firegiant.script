#scope server

{
	const BC_ARTIFACTS_ONE_OF_EACH 1
	setvard BC_FARTIFACTS "blunt_bf;blunt_bt;blunt_northmaul972"
}

#include chests/base_treasurechest_array

{ treasure_spawn
	name "Fire Giant Chest"
	add G_GAVE_TOME7 1 //using this as global end-chest tracker for this map
}

{ chest_add_quality_items

	if ( !FOUND_STRONGEST )
	{
		callevent find_strongest_player
		setvard FOUND_STRONGEST 1
		local SHOW_STRONGEST 0
		if ( G_DEVELOPER_MODE == 1 ) local SHOW_STRONGEST 1
		if ( game.playersnb > 1 ) local SHOW_STRONGEST 1
		if SHOW_STRONGEST
		local OUT_STR $get(ent_me,name)
		strconc OUT_STR " finds " $get(BC_STRONGEST_PLAYER,name) " to be the most worthy player."
		infomsg all "STRONGEST PLAYER" OUT_STR
	}

	if ( CHEST_USER equals BC_STRONGEST_PLAYER )
	{
		local ARTI_CHANCE game.playersnb
		multiply ARTI_CHANCE 10
		callevent add_artifact ARTI_CHANCE
		if ( $rand(1,16) <= game.playersnb ) addstoreitem STORENAME blunt_fs 1 0
		if ( $rand(1,16) <= game.playersnb ) addstoreitem STORENAME shields_f 1 0
	}

	callevent add_epic_item
	calleventloop game.playersnb add_epics
	callevent add_great_item
	callevent add_great_item
	callevent add_great_item
	calleventloop game.playersnb add_gold_by_hp

	if ( game.players.totalhp > 2000 ) callevent add_epic_item //meaner spiders
}

{ add_epics
	callevent add_epic_item
}