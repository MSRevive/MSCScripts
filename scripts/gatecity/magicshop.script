//magic shop

{
	const SOUND_DEATH none			//base_npc

	//base_vendor
	setvar STORE_CLOSED 0
   	const STORE_NAME gatecity_magicshop
   	const STORE_TRIGGERTEXT "store trade buy sell purchase sale offer"
	const STORE_SELLMENU 1
	const SELL_RATIO 0.75
	const NO_JOB 1
	const NO_RUMOR 1
	const NO_HAIL 1

	setvard VEND_NEWBIE 1
	setvard VEND_WEAPONS 1
	setvard VEND_CONTAINERS 1
	setvard VEND_ARMORER 0

	const NPC_REACTS 1
}

#include monsters/base_npc
#include monsters/base_chat
#include monsters/base_npc_vendor
#include monsters/base_npc_vendor_confirm

// Spawn properties
{ game_spawn

	name_unique	Galan
	hp	25
	gold	25
	name	Galan the Mage
	fov	30
	width	32
	height	72
	race	human
	roam	0
	setmodel	npc/balancepriest1.mdl
	invincible	1

	setvard	JOB 0
	setvard	CANCHAT 1
	setvard	STORE_CLOSED 0

	catchspeech say_hi hi hello hail greet hey
   	catchspeech say_job job work money gold
   	catchspeech say_rumor rumour news happenings rumor

	callevent createmystore
}

///////////////////////////////////////////////////////////

// Greeting players

{ npcreact_targetsighted		//Respond to seeing player
  	
	if( !STORE_CLOSED )
	{
		if( $get(PARAM1,dist) <= 90 )
		{
			saytext Can I get you some potions?	
		}
	}
}

{ say_hi

	if( !STORE_CLOSED )
	{
		if( $get(ent_lastspoke,dist) <= 90 )
		{
			saytext Would you like a potion or a new spell?
		}
	}
}

{ say_job

	saytext Until the Undermountains is open again, I have no need to hire you.

}

{ say_rumor

	saytext Goblins have been hanging around the city lately.

}

{
   eventname vendor_addstoreitems

   //           |Store name|   |Item name|	       |Quantity|	|cost| |Sell ratio|
   addstoreitem  STORE_NAME    health_mpotion		$rand(25,30)	 115 0.2
   addstoreitem  STORE_NAME    health_lpotion		$rand(25,30)	 115 0.2
   if( $rand(1,5) == 1 )
   {
   addstoreitem  STORE_NAME    health_spotion   	$rand(1,4)	 120 0.2
   }
   addstoreitem  STORE_NAME    mana_mpotion		$rand(5,10)	 120 0.2
   addstoreitem  STORE_NAME    scroll_fire_dart	 	$rand(1,3)  	 135
   addstoreitem  STORE_NAME    scroll_lightning_weak	$rand(1,3)  	 135
   addstoreitem  STORE_NAME    scroll2_fire_dart	$rand(1,3)  	 135
   addstoreitem  STORE_NAME    scroll2_lightning_weak	$rand(1,3)  	 135
   addstoreitem  STORE_NAME    sheath_spellbook		$rand(1,3)  	 135
   addstoreitem  STORE_NAME    scroll2_glow		$rand(1,2)  	 200
   addstoreitem  STORE_NAME    scroll_glow		$rand(3,5)  	 200
	//if ( $rand(1,2) == 1 ) addstoreitem	 STORE_NAME		mana_protection 1 80 0.1
	//if ( $rand(1,10) == 1 ) addstoreitem	 STORE_NAME		mana_gprotection 1 110 0.1
	//if ( $rand(1,10) == 1 ) addstoreitem	 STORE_NAME		mana_speed 1 150 0.1
   	if ( $rand(1,5) == 1 )
	{
		addstoreitem STORE_NAME mana_resist_fire 1 150 0.1
		//addstoreitem STORE_NAME mana_immune_poison 1 150 0.1
	}
	else if ( $rand(1,15) == 1 )
	{
		addstoreitem STORE_NAME mana_resist_cold 1 150 0.1
		//addstoreitem STORE_NAME mana_immune_fire 1 150 0.1
	}

}

{  trade_success

   if CANCHAT == 1

   playsound game.sound.voice game.sound.maxvol npc/goods.wav
   say [.34] [.24] [.35] [.40]
   setvard CANCHAT 0
   callevent 10 resetchat
}

{
  eventname resetchat
  setvard CANCHAT 1
}

{ vendor_say_closed

   saytext Sorry, I'm closed. I will reopen at seven in the morning.
}