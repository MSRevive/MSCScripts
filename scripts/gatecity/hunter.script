//Hunting Supplies merchant

{
	const SOUND_DEATH none			//base_npc

	//base_vendor
	setvar STORE_CLOSED 0
   	const STORE_NAME gatecity_huntery
   	setvar STORE_TRIGGERTEXT "store trade buy sell purchase sale offer"
	const STORE_SELLMENU 1
	const SELL_RATIO 0.75
   	setvard SELL_WEAPON_LEVEL 3,6

	setvard VEND_NEWBIE 1
	setvard VEND_WEAPONS 1
	setvard VEND_CONTAINERS 1
	setvard VEND_ARMORER 0

	const NO_CHAT 1
	const NPC_REACTS 1
}

#include monsters/base_npc
#include monsters/base_chat
#include monsters/base_npc_vendor
#include monsters/base_npc_vendor_confirm

// Spawn properties
{ game_spawn

	name_unique	Durwyn
	hp	25
	gold	25
	name	Durwyn the Hunter
	fov	30
	width	32
	height	72
	race	human
	roam	0
	setmodel	npc/human1.mdl
	invincible	1

	setmodelbody 2 0

	setvard	JOB 0
	setvard	CANCHAT 1
	setvard	STORE_CLOSED 0

	catchspeech say_hi hi hello hail greet hey
   	catchspeech say_job job work money gold
   	catchspeech say_rumor rumour news happenings rumor

	callevent createmystore
}

///////////////////////////////////////////////////////////

// Greeting players

{ npcreact_targetsighted		//Respond to seeing player
  	
	if( !STORE_CLOSED )
	{
		if( $get(PARAM1,dist) <= 90 )
		{
			saytext Get your hunting supplies!	
		}
	}
}

{ say_hi

	if( !STORE_CLOSED )
	{
		if( $get(ent_lastspoke,dist) <= 90 )
		{
			saytext Would you like a fine bow? Or perhaps a quiver?
		}
	}
}

{ say_job

	saytext I have everything I need right now.

}

{ say_rumor

	saytext If you want some information about this area, speak with the miner in the tavern.
}

{
   eventname vendor_addstoreitems

   //           |Store name|   |Item name|	       |Quantity|	|cost| |Sell ratio|
   addstoreitem  STORE_NAME     proj_arrow_wooden 	600		100	0	60
   addstoreitem  STORE_NAME     proj_arrow_fire 	600		100	0	60
   addstoreitem  STORE_NAME     proj_arrow_broadhead	600		100	0	60
   addstoreitem STORE_NAME proj_arrow_holy 60 200 0.1 30
   callevent silver_tipped

   addstoreitem  STORE_NAME     pack_quiver		$rand(1,3)	100	SELL_RATIO

   addstoreitem  STORE_NAME     bows_orcbow   	 	$rand(1,3)	100	SELL_RATIO
   addstoreitem  STORE_NAME     bows_treebow 		$rand(1,3)	100	SELL_RATIO
   addstoreitem  STORE_NAME     bows_shortbow		$rand(1,2)	115	SELL_RATIO

   addstoreitem  STORE_NAME     smallarms_knife 	$rand(1,5)	100	SELL_RATIO
   addstoreitem  STORE_NAME     smallarms_dagger 	$rand(1,5)	100	SELL_RATIO

	addstoreitem  STORE_NAME     polearms_sp	$rand(0,2)  	100	SELL_RATIO
	addstoreitem  STORE_NAME     polearms_tri	$rand(0,1)  	100	SELL_RATIO


   addstoreitem  STORE_NAME     skin_ratpelt	 	0		130	1.1
   addstoreitem  STORE_NAME     skin_boar		0		130	1.1
   addstoreitem  STORE_NAME     skin_boar_heavy	 	0		130	1.1
   addstoreitem  STORE_NAME     skin_bear	 	0		130	1.1

   addstoreitem STORE_NAME proj_bolt_fire 50 300 0 25

}

{
  eventname silver_tipped

   if( $rand(1,5) == 1 )
   {
   addstoreitem  STORE_NAME     proj_arrow_silvertipped 300		135	0	30
   }

}

{  trade_success

   if CANCHAT == 1

   playsound game.sound.voice game.sound.maxvol npc/goods.wav
   say [.34] [.24] [.35] [.40]
   setvard CANCHAT 0
   callevent 10 resetchat
}

{
  eventname resetchat
  setvard CANCHAT 1
}

{ vendor_say_closed

   saytext Sorry, I'm closed. I will reopen at seven in the morning.
}