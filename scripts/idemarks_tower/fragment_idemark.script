{
	const CHAT_AUTO_HAIL 1
	const CHAT_USE_CONV_ANIMS 0
	const CHAT_FACE_ON_USE 0
	const CHAT_AUTO_FACE 0
}

#include monsters/base_chat_array

{ game_spawn

	name "a fragment of|Idemark"
	name_unique idemark
	
	hp 100
	nopush 1
	
	saytextrange 1000
	
	invincible 1
	race beloved
	
	setmodel npc/femhuman2.mdl
	width 32
	height 72
	
	setprop ent_me rendermode 5
	setprop ent_me renderfx 3
	setprop ent_me renderamt 225

	//playanim hold 71
	setidleanim sitting2
	setmoveanim walk
	
	catchspeech say_gems gem lens
	catchspeech say_ring ring
	
	menu.autoopen 1
	
	setvard CHAPEL_WALK_POINT $get_by_name('chapel_target2')
}

{ game_menu_getoptions

	if CHAT_MENU_ON
	
	local L_QUEST $get_quest_data(PARAM1,'manaring')
	local L_ITEM $item_exists(PARAM1,'item_ring_ryza')
	
	infomsg all L_QUEST L_ITEM
	
	if L_QUEST equals '0'
	if L_ITEM
	
	local L_GEM_COUNT 0
	add L_GEM_COUNT $item_exists(PARAM1,'item_ring_ryza_gem1')
	add L_GEM_COUNT $item_exists(PARAM1,'item_ring_ryza_gem2')
	add L_GEM_COUNT $item_exists(PARAM1,'item_ring_ryza_gem3')
	add L_GEM_COUNT $item_exists(PARAM1,'ring_light2')
	
	if ( L_GEM_COUNT == 0 )
	{
		local reg.mitem.title	"Show Ryza's Trinket"
		local reg.mitem.type	callback
		local reg.mitem.callback say_ring_nogems
		menuitem.register
	}
	else if ( L_GEM_COUNT < 4 )
	{
		local reg.mitem.title	"Show Ryza's Artifacts"
		local reg.mitem.type	callback
		local reg.mitem.callback say_not_enough_gem
		menuitem.register
	}
	else
	{
		local reg.mitem.title	"Give Ryza's Artifacts"
		local reg.mitem.type	callback
		local reg.mitem.callback fix_ring_and_return
		menuitem.register
	}
	
	
} 

{ say_hi
	if !CHAT_BUSY
	if CHAT_MENU_ON
	callevent chat_now "Who goes there? Are you of the Lost?" 3 none none
	callevent chat_now "I have retreated to the confines of this chapel, where I have just enough power to keep their leader sealed in another plane of reality." 7 none none
	callevent chat_now "You must seek out one of my guard, should they still exist among us. They have each sworn an oath to me, serving me even in death. They can help you." 9 none none
}

{ say_ring_nogems
	if !CHAT_BUSY
	if CHAT_MENU_ON
	callevent chat_now "Is that?... no. Not here. It wouldn't make sense."
	callevent chat_now "Please tell me the magical bindings of Atholo have not failed! Unless..."
	callevent chat_now "You've defeated him? It has been centuries since I devised the ensorcelled device."
	callevent chat_now "To allow a great power to remain on your plane whose sole purpose was to bind Atholo."
	callevent chat_now "Ryza was a devoted supporter and believed in the strength of humanity. Her sacrifice will echo in eternity. I am forever grateful of her divine act to preserve this plane."
	callevent chat_now "The device, however, seems to be missing its three focusing [lenses], as well as the empowering body to which it is attuned."
	callevent chat_now "If you are able to find such and return them to me, I may be able to scrounge up the arcane power of this tower to restore this artifact to its former glory."
}

{ say_not_enough_gem
	if !CHAT_BUSY
	if CHAT_MENU_ON
	callevent chat_now "It seems you are on the right track, seeker. However, the device, the ring, and its focusing gems are useless without each other."
	callevent chat_now "Return to me with the [ring] and all three [gems] and I will attempt to restore its power for you."
}

{ say_gems
	if !CHAT_BUSY
	if CHAT_MENU_ON
	callevent chat_now "It seems one has made its way into the cursed coffers of a crystal-protected bandit's stronghold, nestled deep in a lush grove."
	callevent chat_now "You may need to find yet more crystals, however."
	callevent chat_now "Another was given to a powerful crystalline wizard who was so apt at the arcane, they could split their very being into elemental fragments."
	callevent chat_now "The final jewel has recessed deep under the cliffs, to the possession of a dimensional arachnid."
	callevent chat_now "The base of the trinket was once a gift to Felewyn's devout followers to light their way during dark times."
}

{ say_ring
	callevent say_gems
}

{ fix_ring_and_return
	if !CHAT_BUSY
	if CHAT_MENU_ON
	
	local L_GEM_COUNT 0
	add L_GEM_COUNT $item_exists(PARAM1,'item_ring_ryza')
	add L_GEM_COUNT $item_exists(PARAM1,'item_ring_ryza_gem1')
	add L_GEM_COUNT $item_exists(PARAM1,'item_ring_ryza_gem2')
	add L_GEM_COUNT $item_exists(PARAM1,'item_ring_ryza_gem3')
	add L_GEM_COUNT $item_exists(PARAM1,'ring_light2')
	
	if ( L_GEM_COUNT == 5 )
	{
		add L_GEM_COUNT $item_exists(PARAM1,'item_ring_ryza',remove)
		add L_GEM_COUNT $item_exists(PARAM1,'item_ring_ryza_gem1',remove)
		add L_GEM_COUNT $item_exists(PARAM1,'item_ring_ryza_gem2',remove)
		add L_GEM_COUNT $item_exists(PARAM1,'item_ring_ryza_gem3',remove) //doesn't add? but will remove them.
		add L_GEM_COUNT $item_exists(PARAM1,'ring_light2',remove)

		setvard MANARING_TARGET PARAM1
		
		callevent chat_now "By Felewyn's Light. You've done it! The awe-inspiring power of this magical construct shall resonate once again."
		callevent chat_now "Give me a moment, as I attempt to restore the ring to its former glory."
		callevent chat_now "Just need to recall the ancient incantations..." none none make_ring
	}
}

{ give_manaring

	quest set MANARING_TARGET manaring 1
	offer MANARING_TARGET item_ring_mana
	setvard MANARING_TARGET 'MANARING_TARGET'
}

{ make_ring

	playsound 0 10 magic/energy1_loud.wav

	if ( !RING_STEP )
	{
		playanim critical sitting3
		playsound 0 10 magic/shock_noloop.wav
		clientevent new all effects/sfx_light_fade $get(ent_me,svbonepos,19) '(255,0,0)' 200 0
	}
	else if ( RING_STEP == 1 ) clientevent new all effects/sfx_light_fade $get(ent_me,svbonepos,19) '(255,0,255)' 200 0
	else if ( RING_STEP == 2 ) clientevent new all effects/sfx_light_fade $get(ent_me,svbonepos,19) '(0,255,0)' 200 0
	else if ( RING_STEP == 3 )
	{
		clientevent new all effects/sfx_light_fade $get(ent_me,svbonepos,19) '(255,255,255)' 200 0
		setvard RING_STEP 0
		callevent done_ring
		exitevent
	}
	
	add RING_STEP 1
	callevent 2.4 make_ring
}

{ done_ring
	if $get(MANARING_TARGET,exists) //Chat can end early if someone starts the maps end sequence. Just check that the ring wasnt given away already
	
	callevent chat_now "...there it is. A wonder to behold. An ancient artifact born anew. Its latent energies manifest once more! The arcane shall now flow through you readily." none none !give_manaring
	callevent chat_now "Thank you, adventurer, for restoring this great power. For being a champion of its rebirth. Its mana stones shall restore your magical powers, just as you have done for it."
	callevent chat_now "May Felewyn forever be by your side."
}

{ ext_chapel_start

	callevent chat_clear_que //Stop talking
	
	if ( $get(MANARING_TARGET,exists) ) //Someone was in the middle of making a ring
	{
		callevent give_manaring //Just give it to them. We don't need to finish speaking.
	}

	saytextrange 2000
	
	//local L_ORIGIN $get(ent_me,origin)
	//vectoradd L_ORIGIN $relpos($get(ent_me,angles),$vec(0,25,25))
	//setorigin ent_me L_ORIGIN
	
	setidleanim idle1
	
	setmovedest CHAPEL_WALK_POINT 5
	playanim critical sitstand
  
	setvard ISHMEEA_ID $get_by_name('ishmeea')
	setvard CHAT_MENU_ON 0
}

{ game_reached_dest

	local L_ORIGIN $get(CHAPEL_WALK_POINT,origin)
	vectorset L_ORIGIN z $get(ent_me,origin.z)
	
	setorigin ent_me L_ORIGIN
	setangle face $get(CHAPEL_WALK_POINT,angles)
}

{ ext_chapel_speech

	if ( !CHAPEL_CHAT_STEP ) callevent chat_now "Ishme'Ea. No, I played a small part in all this. I am just as much a prisoner in this chapel as the monstrosity that I restrain." 8 none ext_chapel_speech 
	if ( CHAPEL_CHAT_STEP == 1 ) callevent chat_now "Yourself and Leofing were essential parts of the series of protective rituals that I have cast in and around the tower." 6 none ext_chapel_speech 
	if ( CHAPEL_CHAT_STEP == 2 ) callevent chat_now "I also counted on your curiosity and eagerness to help the world. In hopes that you would return with the knowledge to aid us." 7 none do_ishmeea_line 
	if ( CHAPEL_CHAT_STEP == 3 ) callevent chat_now "Goodbye, Ishme'Ea. I will not survive the ritual. It was an honor to serve as Apostle to Pathos." 6  none ext_chapel_speech 
	if ( CHAPEL_CHAT_STEP == 4 )
	{
		callevent chat_now "And an honor to be your first teacher. Benevolent visitor, may your steel strike true, and your magic flow freely." 7 none do_ishmeea_line 
		callevent 10 ded
	}

	add CHAPEL_CHAT_STEP 1
}

{ do_ishmeea_line 

	callexternal ISHMEEA_ID ext_chapel_start
}

{ ded

	deleteent ent_me fade
}

