{
	const CHAT_NEVER_INTERRUPT 1
	const CHAT_AUTO_HAIL 1
	
	setvar COLLECTED_ITEMS 0
}

#include monsters/base_chat_array

{ game_spawn

	name "The Wizard|Ishme'Ea"
	name_unique ishmeea
	
	invincible 1
	race beloved
	
	setmodel npc/elf_f_warrior.mdl
	width 32
	height 72
	
	setmodelbody 1 7 //no body #7?
	setprop ent_me skin 1
	
	playanim hold 3
	
	menu.autoopen 1
	saytextrange 1000
	
	callevent create_conversations
}

{ create_conversations

	catchspeech say_met_leofing leofing
	catchspeech say_gave_items give item

	//say_hi
	callevent chat_add_text say_hi "WHAT was that? I heard you crashing down here like a bag of hammers. Do you not understand the situation here?" 5.0 none flag_said_hi
	callevent chat_add_text say_hi "*sigh* Pardon me, it's been so long since I had proper visitors that I forget myself down here, sometimes."
	callevent chat_add_text say_hi "Something at the tower emits a powerful aura, it speaks to me in my dreams. In nightmares that I wouldn't wish on Lor Malgoriand himself."
	callevent chat_add_text say_hi "Do try and keep the noise down, darling."
	
	//say_hi_alt -alt is for looping after initial intro. It would be odd to have the npc to keep remarking the noise the player is supposedly making
	callevent chat_add_text say_hi_alt "It's been so long since I had proper visitors that I forget myself down here, sometimes."
	callevent chat_add_text say_hi_alt "Something at the tower emits a powerful aura, it speaks to me in my dreams. In nightmares that I wouldn't wish on Lor Malgoriand himself."
	callevent chat_add_text say_hi_alt "Do try and keep the noise down, darling."
	
	//say_met_leofing
	callevent chat_add_text say_met_leofing "Leofing? Alive? Well, in a manner of speaking. Good to know he has not changed in a few hundred years."
	callevent chat_add_text say_met_leofing "You see, I am studying the phenomenon here at Idemark's Tower. It is highly unusual, to say the least, that the lands themselves fight the Lost."
	callevent chat_add_text say_met_leofing "These purple crystals are the most reactive samples that I have found in all of Daragoth, by far!"
	callevent chat_add_text say_met_leofing "The crystal rods are remnants of the great ballistae bolts that rained down here at the start of the Age of War."
	callevent chat_add_text say_met_leofing "The megalithic ammunition must have been cursed by The Lost to spread their influence upon strategic locations."
	callevent chat_add_text say_met_leofing "I have been busy in the years since, traveling to different Elvish enclaves to further my arcane knowledge."
	callevent chat_add_text say_met_leofing "Arcane scholars I studied with at our capital city, Kray Eldorad, claim that this energy can be re-cast."
	callevent chat_add_text say_met_leofing "It could be used as a conduit to purge The Lost from this area, allowing me to enter the Tower proper."
	callevent chat_add_text say_met_leofing "A few ingredients are required for this. We will need the remains from a powerful leader of The Lost to counteract its influence."
	callevent chat_add_text say_met_leofing "The remains of Leofing himself are needed, his unwavering devotion will power the connection."
	callevent chat_add_text say_met_leofing "And finally a sample of the purple crystal itself, the physical connection needed to bridge my mind to the ether." 6.0 none flag_said_leofing
	
	//say_met_leofing_alt
	callevent chat_add_text say_met_leofing_alt "I can use the energy here to purge The Lost from this area, allowing me to enter the Tower."
	callevent chat_add_text say_met_leofing_alt "A few ingredients are required for this."
	callevent chat_add_text say_met_leofing_alt "We will need the remains from a powerful leader of The Lost to counteract its influence."
	callevent chat_add_text say_met_leofing_alt "The remains of Leofing himself are needed, his unwavering devotion will power the connection."
	callevent chat_add_text say_met_leofing_alt "And finally a sample of the purple crystal itself, the physical connection needed to bridge my mind to the ether."
	
	//say_gave_items
	callevent chat_add_text say_gave_items "Incredible. To play a part in the fates, however minor, is beyond our wildest dreams. Thank you, Leofing, for this last gift." 7.0 none !flag_gave_items
	callevent chat_add_text say_gave_items "You know not the depths of your power, I would not be surprised if your path is guided by Pathos themself."
	callevent chat_add_text say_gave_items "Meet me in the chapel. It can be found high up on the mighty stone walls, near the front gate." 4.8 none tele_out
	
	//say_chapel_start
	//else if ( CHAPEL_CHAT_STEP == 1 ) "Idemark... all this time, it was you? Of course, there was no other explanation for all of this."
	
	//say_boss_died
	callevent chat_add_text say_boss_died "All of Daragoth should be cheering your name right now, hero."
	callevent chat_add_text say_boss_died "A victory of this scale against The Lost has not happened in a long, long time."
	callevent chat_add_text say_boss_died "The Idemark you met in this chapel is a fragment of herself, a pool of magic that could only react to certain stimuli."
	callevent chat_add_text say_boss_died "Being in her study for so long, I can tell you that Idemark has left many more surprises for us inside that Tower."
	callevent chat_add_text say_boss_died "Rest your muscles and mind for now, adventurer, you will need your strength for what lies ahead."
}

{ game_menu_getoptions

	if SAID_HI
	if ( MET_LEOFING )
	{
		local reg.mitem.title 	"Met Leofing"
		local reg.mitem.type 	callback
		local reg.mitem.callback say_met_leofing
		menuitem.register
	}
	
	if ( COLLECTED_ITEMS == 3 )
	{
		local reg.mitem.title 	"Give items"
		local reg.mitem.type 	callback
		local reg.mitem.callback say_gave_items
		menuitem.register
	}
}

{ say_hi
	
	if ( !CHAT_BUSY ) 
	if CHAT_MENU_ON
	
	if ( !SAID_HI ) callevent chat_start_sequence say_hi
	else callevent chat_start_sequence say_hi_alt
}

{ flag_said_hi
	setvard SAID_HI 1
	if ( MET_LEOFING ) setvard CHAT_MENU_ON 0
}

{ say_met_leofing

	if ( !SAID_LEOFING ) callevent chat_start_sequence say_met_leofing
	else callevent chat_start_sequence say_met_leofing_alt
}

{ flag_said_leofing

	usetrigger cv_met
	setvard SAID_LEOFING 1
}

{ say_gave_items //teleports away after
	if COLLECTED_ITEMS == 3
	callevent chat_start_sequence say_gave_items
}

{ flag_gave_items

	setvard MET_LEOFING 0
	setvard COLLECTED_ITEMS 0
	
	setvard IDEMARK_ID $get_by_name('idemark')
}

{ ext_chapel_start
	if ( !CHAPEL_CHAT_STEP ) callevent chat_now "Idemark... all this time, it was you? Of course, there was no other explanation for all of this." 5.0 none ext_chapel_start 
	else if ( CHAPEL_CHAT_STEP == 1 ) callevent chat_now "The years that passed, the thousands of allies slain by the hordes of The Lost, the chosen Apostles now fallen." 6.8 none ext_chapel_start 
	else if ( CHAPEL_CHAT_STEP == 2 ) callevent chat_now "And your inner sanctum was the only one standing intact?" 2.8 none do_idemark_line
	else if ( CHAPEL_CHAT_STEP == 3 ) callevent chat_now "I am beyond impressed, in all my travels this type of magic has never been proven successful." 4.5 none ext_chapel_start
	else if ( CHAPEL_CHAT_STEP == 4 ) callevent chat_now "The power of ritual. A type of magic that can be practiced by the young, the old; the sick, the healthy; the stupid, and gifted alike." 7.8 none ext_chapel_start
	else if ( CHAPEL_CHAT_STEP == 5 ) callevent chat_now "To ritualize a mass sacrifice like this is unprecedented, even Leofing was unaware, I believe he was just acting in devotion to you." 6.8 none ext_chapel_start
	else if ( CHAPEL_CHAT_STEP == 6 ) callevent chat_now "One last powerful ritual is what's needed to drain the pus from this festering wound that The Lost occupy." 5.5 none ext_chapel_start
	else if ( CHAPEL_CHAT_STEP == 7 ) callevent chat_now "Warrior of Fate, when you are ready, draw the staff from the pedestal to make this evil manifest itself!" 6.0 none do_idemark_line
	else if ( CHAPEL_CHAT_STEP == 8 ) callevent chat_now "Oh, Idemark. Even in your diminished form you make time for pleasantries. I carry your spirit with me everywhere I go." 4.0 none ext_chapel_start
	else if ( CHAPEL_CHAT_STEP == 9 ) callevent chat_now "Goodbye, dear friend and mentor."

	add CHAPEL_CHAT_STEP 1
}

{ do_idemark_line
	callexternal IDEMARK_ID ext_chapel_speech
}

{ ext_boss_died
	callevent chat_start_sequence say_boss_died
}

{ tele_out

	local L_TARGET $get_by_name('chapel_target1')
	setorigin ent_me $get(L_TARGET,origin)
}

{ ext_met_leofing
	setvard MET_LEOFING 1
	if ( SAID_HI ) setvard CHAT_MENU_ON 0
}

{ ext_collected
	add COLLECTED_ITEMS 1
}

{ ext_tele_triggered
	infomsg all tele_ishmeea "triggered"
}