#scope server
{
	//base_npc
	const SOUND_DEATH none
	setvard ANIM_DEATH diesimple

	//base_vendor
	setvar STORE_NAME rons_shop
   	setvar STORE_TRIGGERTEXT "store trade buy sell purchase sale offer"
	setvar SAY_SO 0

	const NO_JOB 1
	const NO_RUMOR 1

	setvard NO_SPAWN_STUCK_CHECK 1

	setvard VENDOR_NOT_ON_USE 1
	setvard NPC_NO_PLAYER_DMG 1
}

#include monsters/base_npc_vendor

{ [shared] game_spawn

	hp		100 //30 is too low. Changed to 100   
	name	"Captain Ron"
	width	32
	height	72
	race	human
	roam	0      //Don't go roaming around
	setmodel npc/human1.mdl
	setmodelbody 1 1
	nopush	1

	takedmg holy 0

	catchspeech say_hi hi hello hail greet j0 wassup
	menu.autoopen 1
}

{ vendor_addstoreitems
   
	//           |Store name|  |Item name|  	|Quantity| |cost%|   |Sell ratio|	|Bundle amt|
	addstoreitem STORE_NAME health_apple 15 100
	addstoreitem STORE_NAME health_mpotion 20 100 0
	addstoreitem STORE_NAME mana_mpotion 20 100 0
	addstoreitem STORE_NAME drink_mead 20 100
	addstoreitem STORE_NAME drink_ale 20 100
	addstoreitem STORE_NAME drink_wine 20 100
}

{ say_hi
	if !SET_DESTINATION
	if !MENU_OPEN
	menu.open $get(ent_lastspoke,id)
}

{ [server] game_menu_getoptions

	setvard MENU_OPEN 1

	if ( !SET_DESTINATION )
	{
		playanim once converse1

		if ( !GAVE_INTRO ) 
		{
			saytext "Ahoy there! Welcome to Captain Ron's pleasure cruise slash high seas adventure! Where be ye travelin' lad?"
			setvard GAVE_INTRO 1
		}

		local reg.mitem.title 	"To Deralia"
		local reg.mitem.type 	callback
		local reg.mitem.callback set_sail
		local reg.mitem.data	deralia
		menuitem.register

		local reg.mitem.title 	"To Port Ara"
		local reg.mitem.type 	callback
		local reg.mitem.callback set_sail
		local reg.mitem.data	ara
		menuitem.register

		local reg.mitem.title 	"To The Isles of Dread"
		local reg.mitem.type 	callback
		local reg.mitem.callback set_sail
		local reg.mitem.data	isles
		menuitem.register

		local reg.mitem.title 	"To The Tundra"
		local reg.mitem.type 	callback
		local reg.mitem.callback set_sail
		local reg.mitem.data	tundra
		menuitem.register
	}
}

{ set_sail
	if !SET_DESTINATION
	setvard SET_DESTINATION 1
	setvard VENDOR_NOT_ON_USE 0
	local TRIGGER_OUT PARAM2
	stradd TRIGGER_OUT _start
	usetrigger TRIGGER_OUT
	playanim once converse2

	if ( PARAM2 equals 'deralia' ) saytext "Ah yes, Deralia, jewel of Daragoth, ne're a safer port there be. Off we go!"
	if ( PARAM2 equals 'ara' ) saytext "Hmm... I think I saw some orcs rowin' towards Ara. Don't be too surprised if we catch up with 'em."
	if ( PARAM2 equals 'isles' ) saytext "Isles of Dread? I'd charge extra for that, were I not already on the King's commission."
	if ( PARAM2 equals 'tundra' ) saytext "Brrrr... cold up there. Alright - just mind the orca when you step off!"
}

{ game_menu_cancel
	menu.autoopen 1
	setvard MENU_OPEN 0
}

{ game_damaged //PARAM1=attacker PARAM2=dmg PARAM3=dmg_type Param4=AccuracyRoll

	if ( $get(PARAM1,isplayer) ) //Dont take dmg from players
	{
		setdmg dmg 0
		setdmg hit 0
		return 0
	}
}