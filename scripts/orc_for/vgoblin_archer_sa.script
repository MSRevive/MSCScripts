#scope server

{
	const NPC_BASE_EXP 75
	const NPC_CAP_EXP 1000

	const SOUND_BOW weapons/bow/bow.wav

	const GOB_JUMPER 0
	const GOB_CHARGER 0

	const DMG_BOW 20
	const DMG_KICK 10

	const DOT_POISON 5

	const KICK_RANGE 64
	const KICK_HITCHANCE 90

	const FREQ_KICK 10.0

	setvard CAN_FIREBALL 0

	setvard NPC_RANGED 1

	setvard ANIM_ATTACK shootorcbow
}

#include orc_for/tiers2
#include orc_for/goblin_base

{ [shared] goblin_spawn

	name a|Vile Goblin Needler
	race goblin
	blood red
	hp 125


	setmodel monsters/goblin_new.mdl
	width 24
	height 50

	setprop ent_me skin 2 //0-norm 1-blood 2-vile

	setmodelbody 0 1 //body: 0-Bare 1-Quiver&Strap
	setmodelbody 1 2 //head: 0-Bare 1-Horns 2-Animal 3-Chief 4-Rag
	setmodelbody 2 2 //weapon: 0-Unarmed 1-Axe 2-wbow 3-sbow 4-sword 5-rAxe 6-sword&shield 7-club 8-knife
	setmodelbody 3 0 //arrow: On/off
	
	roam 1

	hearingsensitivity 2

	takedmg poison 0.0
	takedmg lightning 1.25

	setidleanim ANIM_IDLE
	setmoveanim ANIM_WALK

	callevent 0.01 gob_extras
}

{ gob_extras

	//these are not setting right for some damned reason
	setvard MOVE_RANGE 2000
	setvard ATTACK_RANGE 2000
	setvard ATTACK_HITRANGE 2000

	setvard DROP_GOLD_AMT $rand(30,50)
}

{ grab_arrow
	setmodelbody 3 1
}

{ shoot_arrow

	local TARGET_DIST $get(ent_lastseen,range)
	local FINAL_TARGET $get(ent_lastseen,origin)
	vectoradd FINAL_TARGET $relpos($vec(0,0,0),$vec(0,0,TARGET_DIST))
	divide TARGET_DIST 100
	setangle add_view.pitch TARGET_DIST

	tossprojectile "proj_arrow_npc_dyn" $relpos(0,0,7) none 900 DMG_BOW 2 none
	callexternal ent_lastprojectile ext_lighten 0.4 1 (0,255,0)

	setmodelbody 3 0

	playsound 2 SOUND_BOW
}

{ game_death
	setmodelbody 3 0
}

{ gob_hunt
	if $get(NPCATK_TARGET,isalive)
	if $get(NPCATK_TARGET,range) < KICK_RANGE
	if game.time > NEXT_KICK
	setvard NEXT_KICK game.time
	playanim break
	setmodelbody 3 0
	add NEXT_KICK FREQ_KICK
	setvard ANIM_ATTACK ANIM_KICK
}

{ kick_land
	dodamage NPCATK_TARGET KICK_RANGE DMG_KICK KICK_HITCHANCE blunt
	setvard KICK_ATTACK 1
	setvard ANIM_ATTACK ANIM_BOW
	callevent npcatk_flee NPCATK_TARGET 1024 3.0
}

{ npcatk_flee
	movespeed 2.0
}

{ npcatk_stopflee
	movespeed 1.0
}

{ game_dodamage

	if ( KICK_ATTACK )
	{
		if PARAM1
		if $get(PARAM2,range) < KICK_RANGE
		applyeffect PARAM2 effects/debuff_stun 5 $get(ent_me,id)
		if ( NPC_ADJ_LEVEL > 2 ) addvelocity PARAM2 $relvel(0,600,110)
	}
	setvard KICK_ATTACK 0
}

{ ext_arrow_hit
	if $get(ent_me,relationship,PARAM2) equals enemy

	if ( NPC_ADJ_LEVEL > 2 )
	{
		addvelocity PARAM2 $relvel(0,600,110)
	}

	applyeffect PARAM2 effects/dot_poison 5.0 $get(ent_me,id) DOT_POISON
}