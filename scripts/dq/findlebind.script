// Spawn properties
#scope server
{
	const ANIM_DIEING c1a4_wounded_idle
	setvard ANIM_DEATH_SPEECH c1a4_dying_speech 
	const ANIM_DEAD dead_sitting
	const SOUND_COUGH scientist/cough.wav
	const SOUND_DEATH scientist/sci_die1.wav
	precache SOUND_DEATH

	setvar BRAP_DELAY 3.5 //Chat delay is a const in an included script, so I changed it. -greatguys1 SEP2021

	setvard NO_JOB 1
	setvard NO_RUMOR 1
}

#include monsters/base_npc
#include monsters/base_chat

{ npc_spawn

	name_unique npc_findlebind

	hp 25
	gold 25
	name Findlebind Goodheart
	//fov 30
	width 32
	height 72
	race beloved
	roam 0      //Don't go roaming around
	setmodel    npc/elf1.mdl
	invincible  1
	setvar      CANCHAT 1
	hearingsensitivity 6

	catchspeech say_hi hail hi hello j0 wassup wazzup greet job gweets
	catchspeech say_rumor door melanion mine rumor
	catchspeech say_job job what yes sure yah
	catchspeech say_priest priest
	catchspeech say_elf elves elf
}

{ resetchat

	setvar CANCHAT 1
	calleventtimed 10 resetchat
}

{ say_hi

	if !RECIEVED_CLAW

	if ( PARAM1 equals 'PARAM1' ) setmovedest $get(ent_lastspoke,origin) 9999

	playanim once converse1
	saytext Blessings to ye, adventurer! Would ye care to assist me with a bit of a problem?
	say [.3] [.3] [.3] [.2] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/greeting.wav
	setvard BRAP_DELAY 5.1
	calleventtimed BRAP_DELAY say_hi2
}

{ say_hi2

	playanim once castspell
	saytext "There's this large bear out there. I can't hardly go outside, and he's scared off all the good hunting!"
	say [.3] [.3] [.3] [.2] [.1] [.3]
	usetrigger make_bear
	playsound 0 10 voices/bloodrose/findlebind/theres_this_large_bear.wav
	setvard BRAP_DELAY 5.7
	calleventtimed BRAP_DELAY say_hi3
}

{ say_hi3
	playanim once fear1
	saytext Aye! A kodiak, believe it or not! Bring me proof of its death, and I shall reward ye!
	say [.3] [.3] [.3] [.2] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/aye_a_kodiak.wav
	setvard GAVE_JOB 1
}

{ gave_claw

	setvard QUEST_WINNER PARAM1
	setvard RECIEVED_CLAW 1
	setvard NO_HAIL 1
	playanim once pondering
	saytext "Wow! You actually killed it! ...and here I feared you'd only keep its belly full..."
	say [.1] [.2] [.3] [.2] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/you_actually_killed_it.wav
	setvard BRAP_DELAY 4.8
	callevent BRAP_DELAY gave_claw2
}

{ gave_claw2

	saytext "I suppose you've earned a reward for that..."
	say [.3] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/i_suppose_youve.wav
	setvard BRAP_DELAY 2.6
	callevent BRAP_DELAY gave_claw3
}

{ gave_claw3

	//local REWARD_TYPE $rand(1,100)

	//if ( REWARD_TYPE <= 10 )
	//{
		//healscroll
	//	saytext I can't use this thing, but you look like ya know more than I.
	//	offer QUEST_WINNER scroll2_acid_xolt
	//}

	saytext "Here... It's not as if I get to town often enough to spend this."
	say [.3] [.1] [.3] [.2] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/heres_a_reward.wav
	setvard BRAP_DELAY 3.4
	callevent BRAP_DELAY offer_quest2
	offer QUEST_WINNER gold 50
}

{ say_rumor

	if ( PARAM1 equals 'PARAM1' ) setmovedest $get(ent_lastspoke,origin) 9999

	saytext That mine door has been sealed, though it once lead to the ruins of Melanion, our ancient elven capital.
	playanim once lean
	say [.3] [.1] [.3] [.2] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/that_mine_door_is_sealed.wav
	setvard BRAP_DELAY 6.0
	callevent BRAP_DELAY say_door2
}

{ say_door2

	saytext Kray Eldorad was later built upon its ruins... Those poor fools... 
	say [.3] [.1] [.3] [.2] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/kray_eldorad.wav
	setvard BRAP_DELAY 4.1
	callevent BRAP_DELAY say_door3
}

{ say_door3

	playanim once panic
	saytext Beneath that veneer of elven paradise, lurk monsters that you cannot even imagine!
	say [.3] [.1] [.3] [.2] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/beneath_that_veneer.wav
	setvard BRAP_DELAY 6.0
	callevent BRAP_DELAY say_door4
}

{ say_door4
	saytext "That's why *I* live HERE! At least here, the horrors don't hide under your feet."
	playsound 0 10 voices/bloodrose/findlebind/thats_why_i_live_here.wav
	//setvard BRAP_DELAY 5.5
	say [.3] [.1] [.3] [.2] [.1] [.3]
}

{ offer_quest2

	playanim once lean
	saytext "I... Have another job you could do for me... But it won't be nearly so easy."
	say [.1] [.2] [.3] [.1] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/i_have_another_job.wav
	setvard BRAP_DELAY 5.5
	setvard QUEST2_AVAILABLE 1
	//setvard NO_JOB 0
}

{ say_job

	if ( PARAM1 equals 'PARAM1' ) setmovedest $get(ent_lastspoke,origin) 9999

	if ( !QUEST2_AVAILABLE )
	{
		saytext "That bear won't know what hit it, thanks!"
		playsound 0 10 voices/bloodrose/findlebind/that_bear_wont_know.wav
	}

	if QUEST2_AVAILABLE

	setvard QUEST_PLAYER PARAM1
	if ( QUEST_PLAYER equals 'PARAM1' ) setvard QUEST_PLAYER $get(ent_lastspoke,id)

	if ( $item_exists(QUEST_PLAYER,key_red) )
	{
		saytext Ah, I see you have the key, now you just have to find his lair!
		say [.3] [.1] [.3] [.2] [.1] [.3]
		playsound 0 10 voices/bloodrose/findlebind/ah_you_have_the_key.wav
		setvard BRAP_DELAY 3.5
		callevent BRAP_DELAY say_lair2
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	saytext "Well, there's this necromancer that's been causing all this pandemonium in these parts."
	say [.3] [.1] [.3] [.2] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/theres_this_necromancer.wav
	setvard BRAP_DELAY 5.2
	callevent BRAP_DELAY say_job2
}

{ say_lair2
	saytext "There's a sinister door, up to the north, I'm not sure, but that maybe it."
	say [.1] [.2] [.3] [.1] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/the_sinister_looking_door.wav
}


{ say_job2
	saytext "Rather, it's his apprentice that does all the dirty work, Lord of Snakes... Slithar, I believe."
	say [.1] [.2] [.3] [.1] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/rather_its_his_apprentice.wav
	setvard BRAP_DELAY 6.5
	callevent BRAP_DELAY say_job3
}

{ say_job3
	saytext "Bring me proof that he has been slain, and I'll grant you something a lot better than pocket change."
	say [.3] [.1] [.3] [.2] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/bring_me_proof.wav
	setvard BRAP_DELAY 5.2
	callevent BRAP_DELAY say_job4
}

{ say_job4

	if ( QUEST_PLAYER equals 'PARAM1' ) setvard QUEST_PLAYER $get(ent_lastspoke,id)

	if ( $get(QUEST_PLAYER,maxhp) >= 200 )
	{
		playanim once give_shot
		usetrigger key_chest	
		//createitem key_red $relpos(0,40,32)
		setvard MENTIONED_PRIESTESS 1
		saytext I once had a key that, if what the [priestess] said was true, would get you into his lair.
		say [.1] [.2] [.3] [.1] [.1] [.3]
		playsound 0 10 voices/bloodrose/findlebind/i_once_had_a_key.wav
		setvard BRAP_DELAY 5.3
		callevent BRAP_DELAY say_key1
	}

	if $get(QUEST_PLAYER,maxhp) < 200
	playanim once no
	saytext "I warn ye though, ye do not look nearly prepared for this, he'd very likely mean be the end of you."
	say [.1] [.2] [.3] [.1] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/player_underprepared.wav
	setvard BRAP_DELAY 5.2
	callevent BRAP_DELAY say_job5
}

{ say_key1
	saytext Sadly, I left it behind in a wardrobe, when I had to make a hasty move from the old village.
	say [.3] [.2] [.1] [.1] [.2] [.3]
	playsound 0 10 voices/bloodrose/findlebind/sadly_i_left_it_behind.wav
	setvard BRAP_DELAY 4.7
	callevent BRAP_DELAY say_key3 //too chatty, skipping ahead abit
}

{ say_key2
	saytext "Finding the key is nothing you can't handle, to be sure, after slaying that bear. But the Snake Lord..."
	say [.1] [.2] [.3] [.1] [.1] [.2]
	playsound 0 10 voices/bloodrose/findlebind/finding_the_key.wav
	setvard BRAP_DELAY 6.2
	callevent BRAP_DELAY say_key3
}

{ say_key3
	saytext "He's turned all the people who were once there into his minions!"
	say [.1] [.2] [.3] [.1]
	playsound 0 10 voices/bloodrose/findlebind/he_turned_all_the_people.wav
	setvard BRAP_DELAY 3.6
	callevent BRAP_DELAY say_key4
}

{ say_key4
	saytext No matter... You may retain anything else you happen to find in that old dresser as well, should you find it of value.
	say [.1] [.2] [.3] [.1] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/no_matter_you_can.wav
}

{ say_job5
	playanim once retina
	saytext "I think you should train some more first. I'll grant you the key to his lair when you appear ready."
	say [.1] [.2] [.3] [.1] [.1] [.3] [.1] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/player_underprepared2.wav
}

{ say_priest

	if QUEST2_AVAILABLE
	if ( PARAM1 equals 'PARAM1' ) setmovedest $get(ent_lastspoke,origin) 9999
	playanim once yes
	saytext A priestess of Felewyn came here a few years back...
	say [.1] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/a_priestess_of_felewyn.wav
	setvard BRAP_DELAY 3.2
	callevent BRAP_DELAY say_priest2
}

{ say_priest2

	saytext She said she was too weak to defeat the Snake Lord and his master, but...
	say [.2] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/she_said_she_was.wav
	setvard BRAP_DELAY 4.2
	callevent BRAP_DELAY say_priest3
}

{ say_priest3

	saytext She also said that I should give that red key to anyone I thought strong enough to try.
	say [.1] [.1] [.3] [.1] [.1] [.3]
	playsound 0 10 voices/bloodrose/findlebind/she_said_i_should_give.wav
}

{ slithar_died

	menu.autoopen 0
	setvard I_R_DIEING 1
	setvard NO_RUMOR 1
	setvard NO_JOB 1
	setvard NO_HAIL 1
	setidleanim ANIM_DIEING
	setmoveanim ANIM_DIEING
	playanim once ANIM_DIEING
	callevent 0.1 scan_for_worthy
}

{ game_heardsound
	if ( I_R_DIEING )
	{
		if !FOUND_WORTHY
		if $get(ent_lastheard,range) < 256
		if $get(ent_lastheard,isplayer)
		setvard TARGET_PLAYER $get(ent_lastheard,id)
		callevent found_player
	}
	if !I_R_DIEING
	setmovedest $get(ent_lastheard,origin) 9999
}

{ scan_for_worthy

	if !FOUND_WORTHY
	if $get(ent_lastseen,range) < 256
	if ( $get(ent_lastseen,isplayer) )
	{
		setvard TARGET_PLAYER $get(ent_lastseen,id)
		callevent found_player
	}
	callevent 0.2 scan_for_worthy
}

{ found_player

	if ( $get(TARGET_PLAYER,maxhp) >= 300 )
	{
		playsound 0 10 SOUND_COUGH
		playanim critical ANIM_DEATH_SPEECH
		setvard FOUND_WORTHY 1
		usetrigger findlebind_died
		saytextrange 2048
		saytext *cough* Good! I see you defeated the snake lord! *gasp*
		say [.1] [.1] [.3]
		playsound 0 10 voices/bloodrose/findlebind/you_killed_the_snake_lord.wav
		setvard BRAP_DELAY 6.8
		callevent BRAP_DELAY found_player2
	}
}

{ found_player2

	saytextrange 2048
	saytext Sadly, his master did not take kindly to that. *cough*
	say [.2] [.1] [.4]
	playsound 0 10 voices/bloodrose/findlebind/his_master_didnt_like_that.wav
	setvard BRAP_DELAY 6.2
	callevent BRAP_DELAY found_player3
}

{ found_player3

	saytextrange 2048
	saytext He said, to give you... This...
	say [.1] [.1] [.3] [.1] [.1] [.3] [.1] [.1] [.4]
	createitem item_necro_note $relpos(20,40,32)
	createitem key_green $relpos(20,40,32)
	setidleanim ANIM_DEAD
	setmoveanim ANIM_DEAD
	//playsound 0 10 SOUND_DEATH
	playsound 0 10 voices/bloodrose/findlebind/he_told_me_to_give.wav
	callevent 8.0 death_rattle
	callevent 15.0 npc_fade_away
}

{ death_rattle
	playsound 0 10 voices/bloodrose/findlebind/death_rattle.wav
}

{ say_elf

	setvard SAYSTEP_ELF 0	
	callevent elf_joke
}

{ elf_joke

	if ( PARAM1 equals 'PARAM1' ) setmovedest $get(ent_lastspoke,origin) 9999

	add SAYSTEP_ELF 1
	say [.1] [.2] [.3] [.2] [.2] [.3] [.2]
	if ( SAYSTEP_ELF == 1 )
	{
		saytext "Yes, it is true, you don't see many of us these days."
		playsound 0 10 voices/bloodrose/findlebind/you_dont_see_many_elves.wav
		setvard BRAP_DELAY 3.6
		
	}
	if ( SAYSTEP_ELF == 2 )
	{
		saytext "We've been hiding in this mess."
		playsound 0 10 voices/bloodrose/findlebind/were_hiding_in_this_mess.wav
		setvard BRAP_DELAY 3.0
	}
	if ( SAYSTEP_ELF == 3 )
	{
		saytext Bad magic, I say.
		playsound 0 10 voices/bloodrose/findlebind/bad_magic_i_say.wav
		setvard BRAP_DELAY 1.7
	}
	if ( SAYSTEP_ELF < 3 )
	{
		callevent BRAP_DELAY elf_joke
	}
	else
	{
		setvard SAYSTEP_ELF 0
	}
}

{ game_menu_getoptions

	if !I_R_DIEING

	setmovedest $get(PARAM1,origin) 9999

	if ( !RECIEVED_CLAW )
	{
		if BEAR_DEAD
		if $item_exists(PARAM1,item_bearclaw)
		local reg.mitem.title	"Offer the Bear Claw"
		local reg.mitem.type	payment	//_silent means don't say what's missing (also crashes)
		local reg.mitem.data	item_bearclaw
		local reg.mitem.callback gave_claw
		menuitem.register
	}

	if ( !BEAR_DEAD )
	{
		if !QUEST2_AVAILABLE
		if ( GAVE_JOB )
		{
			local reg.mitem.title	"Jobs"
			local reg.mitem.type	callback
			local reg.mitem.callback say_job
			menuitem.register
		}
	}

	if ( QUEST2_AVAILABLE )
	{
		if ( $item_exists(QUEST_PLAYER,key_red) ) local L_HAS_KEY 1
		if ( $item_exists(PARAM1,key_red) ) local L_HAS_KEY 1
		if ( !L_HAS_KEY )
		{
			local reg.mitem.title	"Another job?"
		}
		else
		{
			local reg.mitem.title	"Where is Slithar?"
		}
		local reg.mitem.type	callback
		local reg.mitem.callback say_job
		menuitem.register

		if MENTIONED_PRIESTESS
		local reg.mitem.title	"A preistess?"
		local reg.mitem.type	callback
		local reg.mitem.callback say_priest
		menuitem.register
	}

	local reg.mitem.title	"What's that large gate?"
	local reg.mitem.type	callback
	local reg.mitem.callback say_rumor
	menuitem.register

	local reg.mitem.title	"You're an elf!"
	local reg.mitem.type	callback
	local reg.mitem.callback elf_joke
	menuitem.register
}

{ da_bear_died //called externally from Kodiak

	//dbg Recieved bear death notice
	setvard BEAR_DEAD 1
}