{
	const NO_JOB 1
	const NO_RUMOR 1
	const CHAT_DELAY 3.0
}

#include monsters/base_npc
#include monsters/base_chat

{ npc_spawn
	name Tao'l
	hp 1
	invincible 1
	width 32
	height 72
	race beloved
	setmodel npc/human1.mdl
	setmodelbody 0 1
	setmodelbody 1 0
	roam 0
	setmoveanim idle1
	hearingsensitivity 10
	saytextrange 1024

	catchspeech say_hi hi hail yo waddup greetings hello j0
}

{ game_heardsound

	local LAST_HEARD $get(ent_lastheard,id)

	if $get(LAST_HEARD,isplayer)

	if $cansee(LAST_HEARD)

	if $get(LAST_HEARD,range) < 200
	if ( CONGRATS_MSG )
	{
		callevent face_speaker LAST_HEARD
		if !DID_CONGRATS
		callevent say_hi
	}
	if !SAID_HI
	callevent face_speaker LAST_HEARD
	callevent say_hi
}


{ say_hi

	if ( $get(PARAM1,isplayer) ) callevent face_speaker $get(PARAM1,id)
	if ( $get(ent_lastspoke,isplayer) ) callevent face_speaker $get(ent_lastspoke,id)	

	setvard SAID_HI 1

	if ( CONGRATS_MSG )
	{
		setvard DID_CONGRATS 1
		playanim critical eye_wipe
		setvard CHAT_STEPS 2
		setvard CHAT_STEP 0
		setvard CHAT_STEP1 "Thank you for bringing us the key."
		setvard CHAT_STEP2 "If you find something of interest in the cellar, feel free to have that too!"
		callevent chat_loop
		callevent 2.0 fix_anims
	}

	if !CONGRATS_MSG

	if !BUSY_CHATTING
	if ( $get(PARAM1,isplayer) ) callevent face_speaker $get(PARAM1,id)
	if ( $get(ent_lastspoke,isplayer) ) callevent face_speaker $get(ent_lastspoke,id)	

	playanim critical converse1
	setvard CHAT_STEPS 7
	setvard CHAT_STEP 0
	setvard BUSY_CHATTING 1
	setvard CHAT_STEP1 "Thank you so much for killing the orcs!"
	setvard CHAT_STEP2 "Take anything you want from my chest as a reward."
	setvard CHAT_STEP3 "If you want to continue your journey, you can travel to the forest behind the rocks but be careful..."
	setvard CHAT_STEP4 "...there might be more orcs near the town and in the forest too!"
	setvard CHAT_STEP5 "Oh, also, the town cellar door is locked and we don't have the key."
	setvard CHAT_STEP6 "I think it was left in the shed near the field when the orcs attacked..."
	setvard CHAT_STEP7 "Please, see if you can find it for us."
	callevent chat_loop
	callevent 2.0 fix_anims
}

{ dungeon //ms_npcsript
	setvard CONGRATS_MSG 1
	local NEARBY_PLAYER $get_insphere(player,512)
	if ( NEARBY_PLAYER isnot 0 )
	{
		callevent face_speaker NEARBY_PLAYER
		callevent say_hi
	}
}

{ fix_anims
	//ms_npcscript seems to be fubar'ing my anims, reset after call
	setmoveanim idle1
	setidleanim idle1
	playanim once idle1
}