{ game_spawn //Generate dmgpoint code

	//Generate a code at the start of a new map. Do not generate a new one in the middle of a gauntlet.
	//But do generate one if the server crashed and the code is lost.
	
	local L_MAP_NAME $lcase(game.map.name)
	
	local L_GENERATE_CODE 1
	if ( $get_find_token(MAPS_GAUNTLET,L_MAP_NAME) > -1 ) //In the middle of a gauntlet?
	{
		if ( G_DM_CODE ) //Code still exists? I havent crashed?
		{
			local L_GENERATE_CODE 0
		}
	}
	
	//First rand does not include a zero so that the code can never evaluate to 0
	if ( L_GENERATE_CODE ) setvarg G_DM_CODE $rand(1,9) $rand(0,9) $rand(0,9) $rand(0,9) $rand(0,9) $rand(0,9)
}

{ gm_find_strongest_player

	dbg gm_find_strongest_player

	setvard STRONGEST_IDX 0
	setvard STRONGEST_STAT_LEVEL 0
	if ( STRONGEST_PLAYER_LIST equals 'STRONGEST_PLAYER_LIST' ) getplayersnb STRONGEST_PLAYER_LIST
	calleventloop $get_token_amt(STRONGEST_PLAYER_LIST) find_strongest_player_loop
	setvard THE_CHOSEN_ONE $get_token(STRONGEST_PLAYER_LIST,STRONGEST_IDX)
	setvard THE_CHOSEN_ONE_IDX STRONGEST_IDX
	token.del PLAYER_LIST STRONGEST_IDX
	//callevent inform_player //SEP2009 event not used (probably hold over from arti-chest)
}

{ gm_find_strongest_reset
	setvard STRONGEST_PLAYER_LIST 'STRONGEST_PLAYER_LIST'
	callevent gm_find_strongest_player
}

{ find_strongest_player_loop

	local CUR_IDX game.script.iteration
	local CUR_PLAYER $get_token(STRONGEST_PLAYER_LIST,CUR_IDX)
	
	callexternal CUR_PLAYER ext_get_dmgpoints
	local PLAYER_STAT $get(CUR_PLAYER,scriptvar,'EXT_DMGPOINTS') 

	if ( PLAYER_STAT > STRONGEST_STAT_LEVEL )
	{
		setvard STRONGEST_STAT_LEVEL PLAYER_STAT
		setvard STRONGEST_IDX CUR_IDX
	}
}